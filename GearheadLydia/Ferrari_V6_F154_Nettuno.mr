import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

public node maserati_nettuno_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 8100 * units.rpm;
    input limiter_duration: 0.04;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0 / 6.0) * cycle)
            .connect_wire(wires.wire6, (0.75 / 6.0) * cycle)
            .connect_wire(wires.wire3, (2.0 / 6.0) * cycle)
            .connect_wire(wires.wire4, (2.75 / 6.0) * cycle)
            .connect_wire(wires.wire2, (4.0 / 6.0) * cycle)
            .connect_wire(wires.wire5, (4.75 / 6.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
}

public node maserati_nettuno_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 210 * units.deg,
            gamma: 1.0,
            lift: 11.5 * units.mm,
            steps: 100
        );
}

public node maserati_nettuno_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 210 * units.deg,
            gamma: 1.0,
            lift: 11.5 * units.mm,
            steps: 100
        );
}

public node maserati_nettuno_camshaft_builder {
    input in_lobe_profile: maserati_nettuno_lobe_profile_int();
    input ex_lobe_profile: maserati_nettuno_lobe_profile_exh();
    input lobe_separation: 100.0 * units.deg;
    input intake_lobe_center: 100.0 * units.deg;
    input exhaust_lobe_center: 100.0 * units.deg;
    input advance_in: 10.0 * units.deg;
    input advance_ex: 10.0 * units.deg;
    input base_radius: 15 * units.mm;

    output intake_cam_L: _intake_cam_L;
    output exhaust_cam_L: _exhaust_cam_L;
    output intake_cam_R: _intake_cam_R;
    output exhaust_cam_R: _exhaust_cam_R;

    camshaft_parameters params(
        advance: advance_in,
        base_radius: base_radius
    )

    camshaft _intake_cam_L(params, lobe_profile: in_lobe_profile, advance: advance_in)
    camshaft _exhaust_cam_L(params, lobe_profile: ex_lobe_profile, advance: advance_ex)

    camshaft _intake_cam_R(params, lobe_profile: in_lobe_profile, advance: advance_in)
    camshaft _exhaust_cam_R(params, lobe_profile: ex_lobe_profile, advance: advance_ex)


    label rot30(30 * units.deg)
    label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_L
        .add_lobe(rot360 + intake_lobe_center + (0 * rot120))
        .add_lobe(rot360 + intake_lobe_center + (4 * rot120))
        .add_lobe(rot360 + intake_lobe_center + (2 * rot120))

    _intake_cam_R
        .add_lobe(rot360 + intake_lobe_center + (1 * rot120) - rot30)
        .add_lobe(rot360 + intake_lobe_center + (5 * rot120) - rot30)
        .add_lobe(rot360 + intake_lobe_center + (3 * rot120) - rot30)

    _exhaust_cam_L
        .add_lobe(rot360 - exhaust_lobe_center + (0 * rot120))
        .add_lobe(rot360 - exhaust_lobe_center + (4 * rot120))
        .add_lobe(rot360 - exhaust_lobe_center + (2 * rot120))

    _exhaust_cam_R
        .add_lobe(rot360 - exhaust_lobe_center + (1 * rot120) - rot30)
        .add_lobe(rot360 - exhaust_lobe_center + (5 * rot120) - rot30)
        .add_lobe(rot360 - exhaust_lobe_center + (3 * rot120) - rot30)
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.mm, k_28inH2O(flow))
}

public node maserati_nettuno_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 50.0 * units.cc; // guess
    input flip_display: false;
    
    // guesses - modern DOHC heads
    input flow_intake: 295.0;
    input flow_exhaust: 255.0;
    input lift_max: 12.0 * units.mm;
    alias output __out: head;

    function intake_flow(lift_max / 5.0)
    intake_flow
        .add_sample(0.0 * lift_max, k_28inH2O(0.00 * flow_intake))
        .add_sample(0.2 * lift_max, k_28inH2O(0.32 * flow_intake))
        .add_sample(0.4 * lift_max, k_28inH2O(0.63 * flow_intake))
        .add_sample(0.6 * lift_max, k_28inH2O(0.83 * flow_intake))
        .add_sample(0.8 * lift_max, k_28inH2O(0.95 * flow_intake))
        .add_sample(1.0 * lift_max, k_28inH2O(1.00 * flow_intake))
        .add_sample(1.2 * lift_max, k_28inH2O(1.02 * flow_intake))

    function exhaust_flow(lift_max / 5.0)
    exhaust_flow
        .add_sample(0.0 * lift_max, k_28inH2O(0.00 * flow_exhaust))
        .add_sample(0.2 * lift_max, k_28inH2O(0.32 * flow_exhaust))
        .add_sample(0.4 * lift_max, k_28inH2O(0.63 * flow_exhaust))
        .add_sample(0.6 * lift_max, k_28inH2O(0.83 * flow_exhaust))
        .add_sample(0.8 * lift_max, k_28inH2O(0.95 * flow_exhaust))
        .add_sample(1.0 * lift_max, k_28inH2O(1.00 * flow_exhaust))
        .add_sample(1.2 * lift_max, k_28inH2O(1.02 * flow_exhaust))

        
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.0 * units.inch) * 3 * units.inch,
        intake_runner_cross_section_area: circle_area(1.0 * units.inch),
        exhaust_runner_volume: circle_area(0.7 * units.inch) * 2 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.7 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}

// https://www.roadandtrack.com/new-cars/car-technology/a33078911/maserati-nettuno-v6-part-sharing/
public node maserati_nettuno_engine {
    alias output __out: engine;

    wires wires()

    label bore(88.0 * units.mm)
    label stroke(82.0 * units.mm)
    label rod_length(145 * units.mm) // guess
    label compression_height(25 * units.mm) // guess
    label piston_dome(0.0 * units.cc)
    label chamber_volume(35.0 * units.cc) // guess

    label crank_mass(65 * units.lb)
    label rod_mass(550 * units.g)
    label piston_mass(400 * units.g)
    label flywheel_mass(15 * units.lb)
    label flywheel_radius(5 * units.inch)

    label compression_ratio(11.0)
    label deck_clearance((((bore * bore / 4.0 * constants.pi * stroke)/(compression_ratio-1.0)) - chamber_volume + piston_dome) / (bore * bore / 4.0 * constants.pi))

    engine engine(
        name: "Maserati Nettuno",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 1200 * units.rpm,
        redline: 8000 * units.rpm,
        fuel: fuel(max_burning_efficiency: 1.12), // free power to compensate for not having turbos
        throttle_gamma: 1.0
    )

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 15 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia: crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 135.0 * units.deg // 45 degrees CCW from vertical
    )

    rod_journal rj1(angle:  0  * units.deg)
    rod_journal rj2(angle: 120 * units.deg)
    rod_journal rj3(angle: 240 * units.deg)

    c0
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)


    piston_parameters piston_params(
        mass: piston_mass,
        blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: stroke / 2 + rod_length + compression_height + deck_clearance
    )

    intake intake(
        plenum_volume: 2.0 * units.L,
        plenum_cross_section_area: 100.0 * units.cm2,
        intake_flow_rate: k_carb(800),
        idle_flow_rate: k_carb(0.0001),
        idle_throttle_plate_position: 0.998,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(250),
        runner_length: 8.5 * units.inch,
        velocity_decay: 0.1
    )
    
    label exhaust_radius((4.0 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(800),
        primary_tube_length: 10.0 * units.inch,
        primary_flow_rate: k_carb(250),
        velocity_decay: 1.0, //0.5
        collector_cross_section_area: circle_area(exhaust_radius) * 2,
        length: 500 * units.mm,
        volume: 500 * units.mm * circle_area(exhaust_radius) * 2
    )

    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.0005, impulse_response: ir_lib.default_0)
    
    label pipe_length_1(4 * units.inch)
    label pipe_length_2(0 * units.inch)
    label pipe_length_3(2 * units.inch)
    
    label exhaust_stagger(2 * units.inch)

    cylinder_bank bL(bank_params, angle: 45.0 * units.deg) // L (first)
    bL
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire1,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_1 + exhaust_stagger,
            sound_attenuation: 1.0
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire2,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_2 + exhaust_stagger,
            sound_attenuation: 0.8
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire3,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_3 + exhaust_stagger,
            sound_attenuation: 1.0
        )

    cylinder_bank bR(bank_params, angle: -45.0 * units.deg) // R (second)
    bR
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire6,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_1,
            sound_attenuation: 1.0
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire5,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_2,
            sound_attenuation: 0.8
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire4,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_3,
            sound_attenuation: 1.0
        )

    engine
        .add_cylinder_bank(bL)
        .add_cylinder_bank(bR)

    engine.add_crankshaft(c0)

    maserati_nettuno_camshaft_builder camshaft()

    bL.set_cylinder_head (
        maserati_nettuno_head(
            intake_camshaft: camshaft.intake_cam_L,
            exhaust_camshaft: camshaft.exhaust_cam_L,
            chamber_volume: chamber_volume,
            flip_display: true
        )
    )
    bR.set_cylinder_head (
        maserati_nettuno_head(
            intake_camshaft: camshaft.intake_cam_R,
            exhaust_camshaft: camshaft.exhaust_cam_R,
            chamber_volume: chamber_volume,
            flip_display: false
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 80 * units.deg)
        .add_sample(1000 * units.rpm, 85 * units.deg)
        .add_sample(2000 * units.rpm, 87 * units.deg)
        .add_sample(3000 * units.rpm, 87 * units.deg)
        .add_sample(4000 * units.rpm, 85 * units.deg)
        .add_sample(5000 * units.rpm, 80 * units.deg)
        .add_sample(6000 * units.rpm, 77 * units.deg)
        .add_sample(7000 * units.rpm, 77 * units.deg)

    engine.add_ignition_module(
        maserati_nettuno_distributor(
            wires: wires,
            timing_curve: timing_curve
        )
    )
}

// 2017 Alfa Romeo Giulia Quadrifoglio (952)
public node alfa_giulia_qv_952_vehicle {
    alias output __out:
        vehicle(
            mass: 1675 * units.kg, // for Euro manual; US spec auto: 1715
            drag_coefficient: 0.32,
            cross_sectional_area: 24.693 * (units.foot * units.foot),
            diff_ratio: 3.09,
            tire_radius: 326.5 * units.mm, // P285/30R19
            rolling_resistance: 1675 * units.kg * 9.81 * (285.0/275.0) * 0.01
        );
}

public node zf_8hp75_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 1200 * units.lb_ft // actual: 550 lbft / 750 Nm
        )
        .add_gear(5.00)
        .add_gear(3.20)
        .add_gear(2.14)
        .add_gear(1.72)
        .add_gear(1.31)
        .add_gear(1.00)
        .add_gear(0.82)
        .add_gear(0.64);
}

public node zf_gs6_53bz_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 900 * units.lb_ft
        )
        .add_gear(4.055)
        .add_gear(2.369)
        .add_gear(1.592)
        .add_gear(1.192)
        .add_gear(1.000)
        .add_gear(0.872);
}

public node main {
    set_engine(maserati_nettuno_engine())
    set_vehicle(alfa_giulia_qv_952_vehicle())
    set_transmission(zf_gs6_53bz_transmission())
}

main()