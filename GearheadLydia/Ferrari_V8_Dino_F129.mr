import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

// firing order: 15374826
// in standard:  27638145

public node ferrari_f129_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 8800 * units.rpm;
    input limiter_duration: 0.08;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0 / 8.0) * cycle)
            .connect_wire(wires.wire5, (1.0 / 8.0) * cycle)
            .connect_wire(wires.wire3, (2.0 / 8.0) * cycle)
            .connect_wire(wires.wire7, (3.0 / 8.0) * cycle)
            .connect_wire(wires.wire4, (4.0 / 8.0) * cycle)
            .connect_wire(wires.wire8, (5.0 / 8.0) * cycle)
            .connect_wire(wires.wire2, (6.0 / 8.0) * cycle)
            .connect_wire(wires.wire6, (7.0 / 8.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
    output wire7: ignition_wire();
    output wire8: ignition_wire();
}

public node ferrari_f129_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 235 * units.deg,
            gamma: 1.0,
            lift: 11.0 * units.mm,
            steps: 100
        );
}

public node ferrari_f129_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 235 * units.deg,
            gamma: 1.0,
            lift: 10.0 * units.mm,
            steps: 100
        );
}

public node ferrari_f129_camshaft_builder {
    input in_lobe_profile: ferrari_f129_lobe_profile_int();
    input ex_lobe_profile: ferrari_f129_lobe_profile_exh();
    input intake_lobe_profile: in_lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input intake_lobe_center: 115.0 * units.deg;
    input exhaust_lobe_center: 115.0 * units.deg;
    input advance: 15.0 * units.deg;
    input base_radius: 15 * units.mm;

    output intake_cam_L: _intake_cam_L;
    output intake_cam_R: _intake_cam_R;
    output exhaust_cam_L: _exhaust_cam_L;
    output exhaust_cam_R: _exhaust_cam_R;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_L(params, lobe_profile: intake_lobe_profile)
    camshaft _intake_cam_R(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_L(params, lobe_profile: exhaust_lobe_profile)
    camshaft _exhaust_cam_R(params, lobe_profile: exhaust_lobe_profile)

    label rot90(90 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_R
        .add_lobe(rot360 + intake_lobe_center + 5 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 3 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 7 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 1 * rot90)

    _intake_cam_L
        .add_lobe(rot360 + intake_lobe_center + 0 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 6 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 2 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 4 * rot90)

    _exhaust_cam_R
        .add_lobe(rot360 - exhaust_lobe_center + 5 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 7 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot90)

    _exhaust_cam_L
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 6 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 4 * rot90)
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.thou, k_28inH2O(flow))
}

public node ferrari_f129_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 30.0 * units.cc; // guess
    input flip_display: false;
    
    alias output __out: head;

// http://www.users.interport.net/s/r/srweiss/tablehdc.htm#FERRARI

    function intake_flow(50 * units.thou)
    intake_flow
        .add_flow_sample(  0,   0)
        .add_flow_sample( 50,  44)
        .add_flow_sample(100,  87)
        .add_flow_sample(150, 127)
        .add_flow_sample(200, 164)
        .add_flow_sample(250, 192)
        .add_flow_sample(300, 204)
        .add_flow_sample(350, 213)
        .add_flow_sample(400, 219)
        .add_flow_sample(450, 224)
        .add_flow_sample(500, 227)
        .add_flow_sample(550, 229)

    function exhaust_flow(50 * units.thou)
    exhaust_flow
        .add_flow_sample(  0,   0)
        .add_flow_sample( 50,  28)
        .add_flow_sample(100,  58)
        .add_flow_sample(150,  89)
        .add_flow_sample(200, 114)
        .add_flow_sample(250, 128)
        .add_flow_sample(300, 138)
        .add_flow_sample(350, 147)
        .add_flow_sample(400, 152)
        .add_flow_sample(450, 156)
        .add_flow_sample(500, 157)
        .add_flow_sample(550, 162)

    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(26.5 * units.mm / 2.0) * 3 * (4 * units.inch),  // valve diameter: 26.5mm x 3
        intake_runner_cross_section_area: circle_area(26.5 * units.mm / 2.0) * 3,
        exhaust_runner_volume: circle_area(28.5 * units.mm / 2.0) * 2 * (4 * units.inch), // valve diameter: 28.5mm x 2
        exhaust_runner_cross_section_area: circle_area(28.5 * units.mm / 2.0) * 2,

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}

public node ferrari_f129_engine {
    alias output __out: engine;

    wires wires()

    engine engine(
        name: "Ferrari F129",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 1200 * units.rpm,
        redline: 8500 * units.rpm, // 8700
        throttle_gamma: 1.0
    )

    label bore(85.0 * units.mm)
    label stroke(77.0 * units.mm)
    label rod_length(137.0 * units.mm)
    label rod_mass(621.5 * units.g)
    label piston_mass(400 * units.g)
    label compression_height(29.00 * units.mm)
    label piston_dome(-1.2 * units.cc) // for 11.2:1 compression
    label deck_height((204.2 + 1.5) * units.mm) // real, apparently
    label crank_mass(41 * units.lb)
    label flywheel_mass(20 * units.lb)
    label flywheel_radius(6 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 20 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia:
            crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 135.0 * units.deg // 45 degrees
    )

    rod_journal rj1(angle:  0  * units.deg)
    rod_journal rj2(angle: 180 * units.deg)
    rod_journal rj3(angle: 180 * units.deg)
    rod_journal rj4(angle:  0  * units.deg)

    c0
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)

    // piston specs are guesses
    piston_parameters piston_params(
        mass: piston_mass,
        blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0 * units.mm,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    intake intakeL(
        plenum_volume: 2.0 * units.L, // actually about accurate
        plenum_cross_section_area: 100.0 * units.cm2,
        intake_flow_rate: k_carb(500),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.998,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(275.0),
        runner_length: 6.5 * units.inch,
        velocity_decay: 0.1
    )

    intake intakeR(
        plenum_volume: 2.0 * units.L, // actually about accurate
        plenum_cross_section_area: 100.0 * units.cm2,
        intake_flow_rate: k_carb(500),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.998,
        throttle_gamma: 2.0,
        runner_flow_rate: k_carb(275.0),
        runner_length: 6.5 * units.inch,
        velocity_decay: 0.1
    )

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(900.0),
        primary_tube_length: 12.0 * units.inch,
        primary_flow_rate: k_carb(400.0),
        velocity_decay: 0.2,
        volume: 3.0 * units.L,
        length: 500 * units.mm
    )

    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.002, impulse_response: ir_lib.default_0)
    
    label pipe_length_1( 0 * units.mm)
    label pipe_length_2(11 * units.mm)
    label pipe_length_3(22 * units.mm)
    label pipe_length_4(33 * units.mm)
    
    label exhaust_stagger(25 * units.mm)
    
    label exh_vol_L(0.9)
    label exh_vol_R(1.0)

    cylinder_bank bL(bank_params, angle: 45.0 * units.deg) // L (first)
    bL
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire1,
            intake: intakeL,
            exhaust_system: exhaust,
            primary_length: pipe_length_1 + exhaust_stagger,
            sound_attenuation: exh_vol_L * 0.85
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire2,
            intake: intakeL,
            exhaust_system: exhaust,
            primary_length: pipe_length_2 + exhaust_stagger,
            sound_attenuation: exh_vol_L * 0.9
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire3,
            intake: intakeL,
            exhaust_system: exhaust,
            primary_length: pipe_length_3 + exhaust_stagger,
            sound_attenuation: exh_vol_L * 0.95
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire4,
            intake: intakeL,
            exhaust_system: exhaust,
            primary_length: pipe_length_4 + exhaust_stagger,
            sound_attenuation: exh_vol_L * 1.0
        )

    cylinder_bank bR(bank_params, angle: -45 * units.deg) // R (second)
    bR
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire8,
            intake: intakeR,
            exhaust_system: exhaust,
            primary_length: pipe_length_1,
            sound_attenuation: exh_vol_R * 0.85
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire7,
            intake: intakeR,
            exhaust_system: exhaust,
            primary_length: pipe_length_2,
            sound_attenuation: exh_vol_R * 0.9
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire6,
            intake: intakeR,
            exhaust_system: exhaust,
            primary_length: pipe_length_3,
            sound_attenuation: exh_vol_R * 0.95
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire5,
            intake: intakeR,
            exhaust_system: exhaust,
            primary_length: pipe_length_4,
            sound_attenuation: exh_vol_R * 1.0
        )

    engine
        .add_cylinder_bank(bL)
        .add_cylinder_bank(bR)

    engine.add_crankshaft(c0)

    ferrari_f129_camshaft_builder camshaft(
        in_lobe_profile: ferrari_f129_lobe_profile_int(),
        ex_lobe_profile: ferrari_f129_lobe_profile_exh()
    )

    bL.set_cylinder_head (
        ferrari_f129_head(
            intake_camshaft: camshaft.intake_cam_L,
            exhaust_camshaft: camshaft.exhaust_cam_L,
            flip_display: true
        )
    )
    bR.set_cylinder_head (
        ferrari_f129_head(
            intake_camshaft: camshaft.intake_cam_R,
            exhaust_camshaft: camshaft.exhaust_cam_R,
            flip_display: false
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 20 * units.deg)
        .add_sample(1000 * units.rpm, 43 * units.deg)
        .add_sample(2000 * units.rpm, 46 * units.deg)
        .add_sample(3000 * units.rpm, 48 * units.deg)
        .add_sample(4000 * units.rpm, 48 * units.deg)
        .add_sample(5000 * units.rpm, 47 * units.deg)
        .add_sample(6000 * units.rpm, 46 * units.deg)
        .add_sample(7000 * units.rpm, 44 * units.deg)
        .add_sample(8000 * units.rpm, 42 * units.deg)
        .add_sample(9000 * units.rpm, 42 * units.deg)

    engine.add_ignition_module(
        ferrari_f129_distributor(
            wires: wires,
            timing_curve: timing_curve
        )
    )
}

// 1995 Ferrari F355 Berlinetta (Type F129)
public node ferrari_f355_vehicle {
    alias output __out:
        vehicle(
            mass: 1448 * units.kg,
            drag_coefficient: 0.33,
            cross_sectional_area: 1.85 * (units.m * units.m),
            diff_ratio: 4.34,
            tire_radius: 334 * units.mm, // 265/40ZR18
            rolling_resistance: 520 * units.N
        );
}

public node ferrari_f355_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 1000 * units.lb_ft
        )
        .add_gear(3.07)
        .add_gear(2.158)
        .add_gear(1.609)
        .add_gear(1.269)
        .add_gear(1.034)
        .add_gear(0.838);
}

public node main {
    set_engine(ferrari_f129_engine())
    set_vehicle(ferrari_f355_vehicle())
    set_transmission(ferrari_f355_transmission())
}

main()