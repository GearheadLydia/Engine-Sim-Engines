import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

// firing order: 18436572

public node cadillac_bigblock_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 4600 * units.rpm;
    input limiter_duration: 0.12;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0 / 8.0) * cycle)
            .connect_wire(wires.wire8, (1.0 / 8.0) * cycle)
            .connect_wire(wires.wire4, (2.0 / 8.0) * cycle)
            .connect_wire(wires.wire3, (3.0 / 8.0) * cycle)
            .connect_wire(wires.wire6, (4.0 / 8.0) * cycle)
            .connect_wire(wires.wire5, (5.0 / 8.0) * cycle)
            .connect_wire(wires.wire7, (6.0 / 8.0) * cycle)
            .connect_wire(wires.wire2, (7.0 / 8.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
    output wire7: ignition_wire();
    output wire8: ignition_wire();
}

// https://www.cadillacforums.com/threads/stock-cam-specs.24121/
public node cadillac_bigblock_500_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 202 * units.deg,
            gamma: 1.0,
            lift: 454 * units.thou,
            steps: 100
        );
}

public node cadillac_bigblock_500_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 198 * units.deg,
            gamma: 1.0,
            lift: 440 * units.thou,
            steps: 100
        );
}

public node cadillac_bigblock_camshaft_builder {
    input in_lobe_profile: cadillac_bigblock_500_lobe_profile_int();
    input ex_lobe_profile: cadillac_bigblock_500_lobe_profile_exh();
    input intake_lobe_profile: in_lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input lobe_separation: 115.5 * units.deg;
    input intake_lobe_center: lobe_separation;
    input exhaust_lobe_center: lobe_separation;
    input advance: -4.0 * units.deg;
    input base_radius: 1.5 / 2 * units.inch; // guess

    output intake_cam_R: _intake_cam_R;
    output exhaust_cam_R: _exhaust_cam_R;
    output intake_cam_L: _intake_cam_L;
    output exhaust_cam_L: _exhaust_cam_L;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_R(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_R(params, lobe_profile: exhaust_lobe_profile)

    camshaft _intake_cam_L(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_L(params, lobe_profile: exhaust_lobe_profile)


    label rot30(30 * units.deg)
    label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_R
        .add_lobe(rot360 + intake_lobe_center + 0 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 3 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 5 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 6 * rot90)

    _intake_cam_L
        .add_lobe(rot360 + intake_lobe_center + 7 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 2 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 4 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 1 * rot90)

    _exhaust_cam_R
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 5 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 6 * rot90)

    _exhaust_cam_L
        .add_lobe(rot360 - exhaust_lobe_center + 7 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 4 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot90)
}

public node cadillac_bigblock_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 76.0 * units.cc; // "small" is 76, "large" is 126
    input flip_display: false;
    
    alias output __out: head;
    
    // http://www.users.interport.net/s/r/srweiss/tablehdc.htm#Cadillac
    // assuming 902 casting number
    function intake_flow(100 * units.thou)
    intake_flow
        .add_flow_sample(  0,   0)
        .add_flow_sample(100,  83)
        .add_flow_sample(200, 134)
        .add_flow_sample(300, 180)
        .add_flow_sample(400, 215)
        .add_flow_sample(500, 248)
        .add_flow_sample(600, 263)
        .add_flow_sample(700, 277)

    function exhaust_flow(100 * units.thou)
    exhaust_flow
        .add_flow_sample(  0,   0)
        .add_flow_sample(100,  73)
        .add_flow_sample(200, 107)
        .add_flow_sample(300, 140)
        .add_flow_sample(400, 164)
        .add_flow_sample(500, 181)
        .add_flow_sample(600, 191)
        .add_flow_sample(700, 196)
        
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.2 * units.inch) * 4 * units.inch,
        intake_runner_cross_section_area: circle_area(1.2 * units.inch),
        exhaust_runner_volume: circle_area(0.75 * units.inch) * 4 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.75 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}

// https://cadillacmagazine.com/forums/threads/which-heads-would-you-want-on-a-060-over-500-76cc-or-120cc.6545/
// https://cadillacmagazine.com/forums/threads/heads-pistons-and-compression.17788/
public node cadillac_bigblock_500_engine {
    alias output __out: engine;

    wires wires()

    engine engine(
        name: "Cadillac Big Block",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 700 * units.rpm,
        redline: 4500 * units.rpm,
        throttle_gamma: 1.0
    )

    // various sources
    label bore(4.300 * units.inch)
    label bore_spacing(5.00 * units.inch)
    label stroke(4.304 * units.inch)
    label rod_length(6.75 * units.inch)
    label rod_mass(800 * units.g)
    label compression_height(1.882 * units.inch)
    label piston_mass(600 * units.g)
    label piston_dome(-38.00 * units.cc) // basically exact; 9.0:1 CR
    label deck_height((10.8125 * units.inch) + (32 * units.thou))
    label crank_mass(90 * units.lb)
    label flywheel_mass(15 * units.lb)
    label flywheel_radius(8 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 20 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia:
            crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 45.0 * units.deg // 45 degrees back from right side
    )

    rod_journal rj1(angle:   0 * units.deg)
    rod_journal rj2(angle: 270 * units.deg)
    rod_journal rj3(angle:  90 * units.deg)
    rod_journal rj4(angle: 180 * units.deg)

    c0
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)


    piston_parameters piston_params(
        mass: piston_mass,
        blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0 * units.mm,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    intake intake(
        plenum_volume: 2.0 * units.L,
        plenum_cross_section_area: 100.0 * units.cm2,
        intake_flow_rate: k_carb(1250.0),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.997,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(400.0),
        runner_length: 8.0 * units.inch,
        velocity_decay: 0.1
    )
    
    label exhaust_radius((3.0 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(1000.0),
        primary_tube_length: 10.0 * units.inch,
        primary_flow_rate: k_carb(400.0),
        velocity_decay: 1.0, //0.5
        collector_cross_section_area: circle_area(exhaust_radius) * 2,
        length: 3000 * units.mm,
        volume: 3000 * units.mm * circle_area(exhaust_radius) * 2
    )

    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.05, impulse_response: ir_lib.default_0)
    
    label pipe_length_1(bore_spacing * 3.0)
    label pipe_length_2(bore_spacing * 2.0)
    label pipe_length_3(bore_spacing * 1.0)
    label pipe_length_4(bore_spacing * 0.0)
    label pipe_volume_1(1.0)
    label pipe_volume_2(1.0)
    label pipe_volume_3(1.0)
    label pipe_volume_4(1.0)
    
    label exhaust_stagger(bore_spacing * 0.2)

    cylinder_bank bR(bank_params, angle: -45.0 * units.deg) // R (first)
    bR
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire1,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_1 + exhaust_stagger,
            sound_attenuation: pipe_volume_1
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire3,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_2 + exhaust_stagger,
            sound_attenuation: pipe_volume_2
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire5,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_3 + exhaust_stagger,
            sound_attenuation: pipe_volume_3
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire7,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_4 + exhaust_stagger,
            sound_attenuation: pipe_volume_4
        )

    cylinder_bank bL(bank_params, angle: 45.0 * units.deg) // L (second)
    bL
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire2,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_1,
            sound_attenuation: pipe_volume_1
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire4,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_2,
            sound_attenuation: pipe_volume_2
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire6,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_3,
            sound_attenuation: pipe_volume_3
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire8,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_4,
            sound_attenuation: pipe_volume_4
        )

    engine
        .add_cylinder_bank(bR)
        .add_cylinder_bank(bL)

    engine.add_crankshaft(c0)

    cadillac_bigblock_camshaft_builder camshaft(
        in_lobe_profile: cadillac_bigblock_500_lobe_profile_int(),
        ex_lobe_profile: cadillac_bigblock_500_lobe_profile_exh()
    )

    bL.set_cylinder_head (
        cadillac_bigblock_head(
            intake_camshaft: camshaft.intake_cam_L,
            exhaust_camshaft: camshaft.exhaust_cam_L,
            flip_display: true
        )
    )
    bR.set_cylinder_head (
        cadillac_bigblock_head(
            intake_camshaft: camshaft.intake_cam_R,
            exhaust_camshaft: camshaft.exhaust_cam_R,
            flip_display: false
        )
    )

    function timing_curve_gross(1000 * units.rpm)
    timing_curve_gross
        .add_sample(0000 * units.rpm, 20 * units.deg)
        .add_sample(1000 * units.rpm, 28 * units.deg)
        .add_sample(2000 * units.rpm, 32 * units.deg)
        .add_sample(3000 * units.rpm, 36 * units.deg)
        .add_sample(4000 * units.rpm, 39 * units.deg)
        .add_sample(5000 * units.rpm, 30 * units.deg)

    function timing_curve_net(1000 * units.rpm)
    timing_curve_net
        .add_sample(0000 * units.rpm, 0 * units.deg)
        .add_sample(1000 * units.rpm, 8 * units.deg)
        .add_sample(2000 * units.rpm, 12 * units.deg)
        .add_sample(3000 * units.rpm, 16 * units.deg)
        .add_sample(4000 * units.rpm, 19 * units.deg)
        .add_sample(5000 * units.rpm, 20 * units.deg)

    engine.add_ignition_module(
        cadillac_bigblock_distributor(
            wires: wires,
            timing_curve: timing_curve_net
        )
    )
}

// 1970 Cadillac Fleetwood Eldorado
public node cadillac_fleetwood_eldorado_vehicle {
    alias output __out:
        vehicle(
            mass: 4718 * units.lb,
            drag_coefficient: 0.55,
            cross_sectional_area: 2.3 * (units.m) * (units.m),
            diff_ratio: 3.07,
            tire_radius: 372 * units.mm, // based on L78-15
            rolling_resistance: 4718 * units.lb * 9.81 * (225.0/275.0) * 0.01
        );
}

public node gm_turbo_hydramatic_thm_425_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 800 * units.lb_ft // uprated probably
        )
        .add_gear(2.48)
        .add_gear(1.48)
        .add_gear(1.00);
}

public node main {
    set_engine(cadillac_bigblock_500_engine())
    set_vehicle(cadillac_fleetwood_eldorado_vehicle())
    set_transmission(gm_turbo_hydramatic_thm_425_transmission())
}

main()