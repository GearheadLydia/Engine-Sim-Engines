import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

public node toyota_4age_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 11500 * units.rpm;
	input limiter_duration: 0.05;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0/4.0) * cycle)
            .connect_wire(wires.wire3, (1.0/4.0) * cycle)
			.connect_wire(wires.wire4, (2.0/4.0) * cycle)
			.connect_wire(wires.wire2, (3.0/4.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
}

private node add_sym_sample {
    input angle;
    input lift;
    input this;
    alias output __out: this;

    this.add_sample(angle * units.deg, lift * units.thou)
    this.add_sample(-angle * units.deg, lift * units.thou)
}

public node toyota_4age_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 288 * units.deg,
            gamma: 1.0,
            lift: 10.2 * units.mm,
            steps: 100
        );
}

public node toyota_4age_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 294 * units.deg,
            gamma: 1.0,
            lift: 10.2 * units.mm,
            steps: 100
        );
}

public node toyota_4age_camshaft_builder {
    input lobe_profile: toyota_4age_lobe_profile_int();
	input ex_lobe_profile: toyota_4age_lobe_profile_exh();
    input intake_lobe_profile: lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input lobe_separation: 105.0 * units.deg;
    input intake_lobe_center: 105.0 * units.deg;
    input exhaust_lobe_center: 105.0 * units.deg;
    input advance: 10.0 * units.deg;
    input base_radius: 16.0 * units.mm;

    output intake_cam_0: _intake_cam_0;
    output exhaust_cam_0: _exhaust_cam_0;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_0(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_0(params, lobe_profile: exhaust_lobe_profile)


    label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_0
        .add_lobe(rot360 + intake_lobe_center + 0 * rot180)
		.add_lobe(rot360 + intake_lobe_center + 3 * rot180)
		.add_lobe(rot360 + intake_lobe_center + 1 * rot180)
		.add_lobe(rot360 + intake_lobe_center + 2 * rot180)

	_exhaust_cam_0
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot180)
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot180)
		.add_lobe(rot360 - exhaust_lobe_center + 1 * rot180)
		.add_lobe(rot360 - exhaust_lobe_center + 2 * rot180)
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.mm, k_28inH2O(flow))
}

public node toyota_4age_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 37.8 * units.cc; // estimates range from 36.0 to 38.5
    input flip_display: false;
	
	input flow_attenuation_in: 1.0;
	input flow_attenuation_ex: 1.0;
    input lift_scale: 1.0;
    alias output __out: head;

    function intake_flow(1.0 * units.mm)
    intake_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_in)
        .add_flow_sample( 1 * lift_scale,  30 * flow_attenuation_in)
        .add_flow_sample( 2 * lift_scale,  60 * flow_attenuation_in)
        .add_flow_sample( 3 * lift_scale,  90 * flow_attenuation_in)
        .add_flow_sample( 4 * lift_scale, 125 * flow_attenuation_in)
        .add_flow_sample( 5 * lift_scale, 160 * flow_attenuation_in)
        .add_flow_sample( 6 * lift_scale, 195 * flow_attenuation_in)
        .add_flow_sample( 7 * lift_scale, 210 * flow_attenuation_in)
        .add_flow_sample( 8 * lift_scale, 235 * flow_attenuation_in)
        .add_flow_sample( 9 * lift_scale, 270 * flow_attenuation_in)
        .add_flow_sample(10 * lift_scale, 305 * flow_attenuation_in)
        .add_flow_sample(11 * lift_scale, 320 * flow_attenuation_in)
        .add_flow_sample(12 * lift_scale, 335 * flow_attenuation_in)
        .add_flow_sample(13 * lift_scale, 335 * flow_attenuation_in)
        .add_flow_sample(14 * lift_scale, 335 * flow_attenuation_in)

    function exhaust_flow(1.0 * units.mm)
    exhaust_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_ex)
        .add_flow_sample( 1 * lift_scale,  30 * flow_attenuation_ex)
        .add_flow_sample( 2 * lift_scale,  55 * flow_attenuation_ex)
        .add_flow_sample( 3 * lift_scale,  85 * flow_attenuation_ex)
        .add_flow_sample( 4 * lift_scale, 115 * flow_attenuation_ex)
        .add_flow_sample( 5 * lift_scale, 140 * flow_attenuation_ex)
        .add_flow_sample( 6 * lift_scale, 160 * flow_attenuation_ex)
        .add_flow_sample( 7 * lift_scale, 180 * flow_attenuation_ex)
        .add_flow_sample( 8 * lift_scale, 205 * flow_attenuation_ex)
        .add_flow_sample( 9 * lift_scale, 220 * flow_attenuation_ex)
        .add_flow_sample(10 * lift_scale, 240 * flow_attenuation_ex)
        .add_flow_sample(11 * lift_scale, 260 * flow_attenuation_ex)
        .add_flow_sample(12 * lift_scale, 280 * flow_attenuation_ex)
        .add_flow_sample(13 * lift_scale, 280 * flow_attenuation_ex)
        .add_flow_sample(14 * lift_scale, 280 * flow_attenuation_ex)

		
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.05 * units.inch) * 3 * units.inch,
        intake_runner_cross_section_area: circle_area(1.05 * units.inch),
        exhaust_runner_volume: circle_area(0.6 * units.inch) * 6 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.6 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}

public node toyota_4age_20v_race {
    alias output __out: engine;

    engine engine(
        name: "Toyota 4A-GE 20V Gr.A",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 1200 * units.rpm,
        redline: 11000 * units.rpm,
		hf_gain: 0.05,
		jitter: 0.55,
        throttle_gamma: 2.0
    )
	
	label lean_angle(0 * units.deg)

    label bore(81.0 * units.mm)
	label bore_spacing(87.5 * units.mm)
    label stroke(77.0 * units.mm)
    label rod_length(122.0 * units.mm)
    label rod_mass(350 * units.g)
	label piston_mass(347 * units.g)
    label compression_height(30.50 * units.mm)
	label deck_height((191.0 + 0.8) * units.mm)
	label piston_dome(2.5 * units.cc) // calculated from 11.0:1 compression ratio
    label crank_mass(4.0 * units.kg)
    label flywheel_mass(5.0 * units.kg)
    label flywheel_radius(5.0 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 15 * units.kg, radius: 8.0 * units.cm)
    )

    wires wires()

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 10.0 * units.lb_ft,
        moment_of_inertia: crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 90.0 * units.deg + lean_angle
    )

    rod_journal rj0(angle:  0  * units.deg)
    rod_journal rj1(angle: 180 * units.deg)
    rod_journal rj2(angle: 180 * units.deg)
    rod_journal rj3(angle:  0  * units.deg)
	
    c0
        .add_rod_journal(rj0)
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)

    piston_parameters piston_params(
        mass: piston_mass,
        //blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    intake intake(
        plenum_volume: 1.0 * units.L,
        plenum_cross_section_area: 50.0 * units.cm2,
        intake_flow_rate: k_carb(600.0),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.993,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(400.0),
        runner_length: 3.0 * units.inch,
        velocity_decay: 0.1
    )
	
	label exhaust_radius((2.5 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(450.0),
        primary_tube_length: 12.0 * units.inch,
        primary_flow_rate: k_carb(250.0),
        velocity_decay: 1.0, //0.5
		collector_cross_section_area: circle_area(exhaust_radius),
		length: 2500 * units.mm,
		volume: 2500 * units.mm * circle_area(exhaust_radius)
    )
	
    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.003, impulse_response: ir_lib.default_0)
 
    cylinder_bank b0(bank_params, angle: 0 * units.deg + lean_angle)
    b0
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            ignition_wire: wires.wire1,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: 1.2 * units.inch,
			sound_attenuation: 1.0
        )
		.add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire2,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: 0.6 * units.inch,
			sound_attenuation: 1.0
        )
		.add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire3,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: 0.0 * units.inch,
			sound_attenuation: 1.0
        )
		.add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire4,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: 0.6 * units.inch,
			sound_attenuation: 1.0
        )

    engine
        .add_cylinder_bank(b0)

    engine.add_crankshaft(c0)

    toyota_4age_camshaft_builder camshaft(
	    lobe_profile: toyota_4age_lobe_profile_int(),
		ex_lobe_profile: toyota_4age_lobe_profile_exh()
	)

    b0.set_cylinder_head (
        toyota_4age_head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0,
			flip_display: false
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample( 0000 * units.rpm, 20 * units.deg)
        .add_sample( 1000 * units.rpm, 50 * units.deg)
        .add_sample( 2000 * units.rpm, 60 * units.deg)
        .add_sample( 3000 * units.rpm, 60 * units.deg)
        .add_sample( 4000 * units.rpm, 60 * units.deg)
        .add_sample( 5000 * units.rpm, 60 * units.deg)
        .add_sample( 6000 * units.rpm, 60 * units.deg)
        .add_sample( 7000 * units.rpm, 60 * units.deg)
        .add_sample( 8000 * units.rpm, 60 * units.deg)
        .add_sample( 9000 * units.rpm, 60 * units.deg)
        .add_sample(10000 * units.rpm, 60 * units.deg)
        .add_sample(11000 * units.rpm, 60 * units.deg)

    engine.add_ignition_module(
        toyota_4age_distributor(
            wires: wires,
            timing_curve: timing_curve
        ))
}

// http://www.aeu86.org/technical/ae86-specifications/
// tweaked to be Initial D-like
// 1984 Toyota Sprinter Trueno Hatchback (AE86)
public node toyota_corolla_ae86_id_vehicle {
    alias output __out:
        vehicle(
            mass: 970 * units.kg,
            drag_coefficient: 0.35,
            cross_sectional_area: 1.87 * (units.m * units.m), // estimate
            diff_ratio: 5.200, // "rally gearing"
            tire_radius: 297.0 * units.mm, // 195/55R15 (guess)
            rolling_resistance: 460 * units.N
        );
}

public node toyota_t50_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 500 * units.lb_ft
        )
        .add_gear(3.587)
        .add_gear(2.022)
        .add_gear(1.384)
        .add_gear(1.000)
        .add_gear(0.861);
}

public node main {
	set_engine(toyota_4age_20v_race())
    set_vehicle(toyota_corolla_ae86_id_vehicle())
    set_transmission(toyota_t50_transmission())
}

main()