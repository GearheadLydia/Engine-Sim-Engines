import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

// firing order: 18627345
// in GM layout: 54368721

public node ferrari_f136_r_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 7550 * units.rpm;
    input limiter_duration: 0.09;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0 / 8.0) * cycle)
            .connect_wire(wires.wire8, (1.0 / 8.0) * cycle)
            .connect_wire(wires.wire6, (2.0 / 8.0) * cycle)
            .connect_wire(wires.wire2, (3.0 / 8.0) * cycle)
            .connect_wire(wires.wire7, (4.0 / 8.0) * cycle)
            .connect_wire(wires.wire3, (5.0 / 8.0) * cycle)
            .connect_wire(wires.wire4, (6.0 / 8.0) * cycle)
            .connect_wire(wires.wire5, (7.0 / 8.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
    output wire7: ignition_wire();
    output wire8: ignition_wire();
}

public node ferrari_f136_r_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 250 * units.deg,
            gamma: 1.0,
            lift: 10.0 * units.mm,
            steps: 100
        );
}

public node ferrari_f136_r_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 245 * units.deg,
            gamma: 1.0,
            lift: 10.0 * units.mm,
            steps: 100
        );
}

public node ferrari_f136_r_camshaft_builder {
    input in_lobe_profile: ferrari_f136_r_lobe_profile_int();
    input ex_lobe_profile: ferrari_f136_r_lobe_profile_exh();
    input intake_lobe_profile: in_lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input intake_lobe_center: 110.0 * units.deg;
    input exhaust_lobe_center: 110.0 * units.deg;
    input advance: 0.0 * units.deg;
    input base_radius: 15 * units.mm;

    output intake_cam_0: _intake_cam_0;
    output intake_cam_1: _intake_cam_1;
    output exhaust_cam_0: _exhaust_cam_0;
    output exhaust_cam_1: _exhaust_cam_1;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_0(params, lobe_profile: intake_lobe_profile)
    camshaft _intake_cam_1(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_0(params, lobe_profile: exhaust_lobe_profile)
    camshaft _exhaust_cam_1(params, lobe_profile: exhaust_lobe_profile)

    label rot90(90 * units.deg)
    label rot360(360 * units.deg)

    // firing order: 18627345
    _intake_cam_0
        .add_lobe(rot360 + intake_lobe_center + 0 * rot90) // #1
        .add_lobe(rot360 + intake_lobe_center + 3 * rot90) // #2
        .add_lobe(rot360 + intake_lobe_center + 5 * rot90) // #3
        .add_lobe(rot360 + intake_lobe_center + 6 * rot90) // #4

    _intake_cam_1
        .add_lobe(rot360 + intake_lobe_center + 1 * rot90) // #8
        .add_lobe(rot360 + intake_lobe_center + 4 * rot90) // #7
        .add_lobe(rot360 + intake_lobe_center + 2 * rot90) // #6
        .add_lobe(rot360 + intake_lobe_center + 7 * rot90) // #5

    _exhaust_cam_0
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 5 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 6 * rot90)

    _exhaust_cam_1
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 4 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 7 * rot90)
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.mm, k_28inH2O(flow))
}

public node ferrari_f136_r_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 30.0 * units.cc;
    input flip_display: false;
    
    input flow_attenuation_in: 1.0;
    input flow_attenuation_ex: 1.0;
    input lift_scale: 1.1;
    alias output __out: head;

    function intake_flow(1.0 * units.mm)
    intake_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_in)
        .add_flow_sample( 1 * lift_scale,  30 * flow_attenuation_in)
        .add_flow_sample( 2 * lift_scale,  60 * flow_attenuation_in)
        .add_flow_sample( 3 * lift_scale,  90 * flow_attenuation_in)
        .add_flow_sample( 4 * lift_scale, 125 * flow_attenuation_in)
        .add_flow_sample( 5 * lift_scale, 160 * flow_attenuation_in)
        .add_flow_sample( 6 * lift_scale, 195 * flow_attenuation_in)
        .add_flow_sample( 7 * lift_scale, 210 * flow_attenuation_in)
        .add_flow_sample( 8 * lift_scale, 235 * flow_attenuation_in)
        .add_flow_sample( 9 * lift_scale, 270 * flow_attenuation_in)
        .add_flow_sample(10 * lift_scale, 305 * flow_attenuation_in)
        .add_flow_sample(11 * lift_scale, 320 * flow_attenuation_in)
        .add_flow_sample(12 * lift_scale, 335 * flow_attenuation_in)
        .add_flow_sample(13 * lift_scale, 335 * flow_attenuation_in)
        .add_flow_sample(14 * lift_scale, 335 * flow_attenuation_in)

    function exhaust_flow(1.0 * units.mm)
    exhaust_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_ex)
        .add_flow_sample( 1 * lift_scale,  30 * flow_attenuation_ex)
        .add_flow_sample( 2 * lift_scale,  55 * flow_attenuation_ex)
        .add_flow_sample( 3 * lift_scale,  85 * flow_attenuation_ex)
        .add_flow_sample( 4 * lift_scale, 115 * flow_attenuation_ex)
        .add_flow_sample( 5 * lift_scale, 140 * flow_attenuation_ex)
        .add_flow_sample( 6 * lift_scale, 160 * flow_attenuation_ex)
        .add_flow_sample( 7 * lift_scale, 180 * flow_attenuation_ex)
        .add_flow_sample( 8 * lift_scale, 205 * flow_attenuation_ex)
        .add_flow_sample( 9 * lift_scale, 220 * flow_attenuation_ex)
        .add_flow_sample(10 * lift_scale, 240 * flow_attenuation_ex)
        .add_flow_sample(11 * lift_scale, 260 * flow_attenuation_ex)
        .add_flow_sample(12 * lift_scale, 280 * flow_attenuation_ex)
        .add_flow_sample(13 * lift_scale, 280 * flow_attenuation_ex)
        .add_flow_sample(14 * lift_scale, 280 * flow_attenuation_ex)

    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.2 * units.inch) * 4 * units.inch,
        intake_runner_cross_section_area: circle_area(1.2 * units.inch),
        exhaust_runner_volume: circle_area(0.7 * units.inch) * 4 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.7 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}

public node ferrari_f136_r_engine {
    alias output __out: engine;

    wires wires()

    engine engine(
        name: "Maserati F136 R",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 1200 * units.rpm,
        redline: 7500 * units.rpm,
        throttle_gamma: 1.0
    )

    label bore(92.0 * units.mm)
    label stroke(79.8 * units.mm)
    label rod_length(140.98 * units.mm) // borrowed from 4.7, probably wrong
    label rod_mass(550 * units.g)
    label piston_mass(400 * units.g)
    label compression_height(28.6 * units.mm) // complete guess, similar total height as F136E
    label piston_dome(-2.4 * units.cc) // for 11.1:1 CR
    label deck_height((211.25 + 1.25) * units.mm)
    label crank_mass(85 * units.lb)
    label flywheel_mass(15 * units.lb)
    label flywheel_radius(6 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 20 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia:
            crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 135.0 * units.deg // 45 degrees
    )

    rod_journal rj1(angle:   0 * units.deg)
    rod_journal rj2(angle: 270 * units.deg)
    rod_journal rj3(angle:  90 * units.deg)
    rod_journal rj4(angle: 180 * units.deg)

    c0
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)

    // piston specs are guesses
    piston_parameters piston_params(
        mass: piston_mass,
        //blowby: 0,
        compression_height: compression_height,
        wrist_pin_position: 0 * units.mm,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    intake intake(
        plenum_volume: 2.5 * units.L,
        plenum_cross_section_area: 200.0 * units.cm2,
        intake_flow_rate: k_carb(600),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.9975,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(400.0),
        runner_length: 9.0 * units.inch,
        velocity_decay: 0.1
    )
    
    label exhaust_radius((4.0 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(600.0),
        primary_tube_length: 10.0 * units.inch,
        primary_flow_rate: k_carb(450.0),
        velocity_decay: 1.0, //0.5
        collector_cross_section_area: circle_area(exhaust_radius),
        length: 2400 * units.mm,
        volume: 2400 * units.mm * circle_area(exhaust_radius)
    )

    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.5, impulse_response: ir_lib.default_0)

    label exhaust_stagger(60 * units.mm)

    cylinder_bank b0(bank_params, angle: 45.0 * units.deg) // L (first)
    b0
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            intake: intake,
            exhaust_system: exhaust,
            ignition_wire: wires.wire1,
            primary_length: (60 * units.mm) + exhaust_stagger,
            sound_attenuation: 0.7
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            intake: intake,
            exhaust_system: exhaust,
            ignition_wire: wires.wire2,
            primary_length: (40 * units.mm) + exhaust_stagger,
            sound_attenuation: 0.7
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            intake: intake,
            exhaust_system: exhaust,
            ignition_wire: wires.wire3,
            primary_length: (0 * units.mm) + exhaust_stagger
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            intake: intake,
            exhaust_system: exhaust,
            ignition_wire: wires.wire4,
            primary_length: (0 * units.mm) + exhaust_stagger
        )

    cylinder_bank b1(bank_params, angle: -45 * units.deg) // R (second)
    b1
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            intake: intake,
            exhaust_system: exhaust,
            ignition_wire: wires.wire8,
            primary_length: (60 * units.mm)
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            intake: intake,
            exhaust_system: exhaust,
            ignition_wire: wires.wire7,
            primary_length: (20 * units.mm),
            sound_attenuation: 0.7
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            intake: intake,
            exhaust_system: exhaust,
            ignition_wire: wires.wire6,
            primary_length: (00 * units.mm),
            sound_attenuation: 0.7
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            intake: intake,
            exhaust_system: exhaust,
            ignition_wire: wires.wire5,
            primary_length: (40 * units.mm)
        )

    engine
        .add_cylinder_bank(b0)
        .add_cylinder_bank(b1)

    engine.add_crankshaft(c0)

    ferrari_f136_r_camshaft_builder camshaft(
        in_lobe_profile: ferrari_f136_r_lobe_profile_int(),
        ex_lobe_profile: ferrari_f136_r_lobe_profile_exh()
    )

    b0.set_cylinder_head (
        ferrari_f136_r_head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0,
            flip_display: true
        )
    )
    b1.set_cylinder_head (
        ferrari_f136_r_head(
            intake_camshaft: camshaft.intake_cam_1,
            exhaust_camshaft: camshaft.exhaust_cam_1,
            flip_display: false
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 80 * units.deg)
        .add_sample(1000 * units.rpm, 87 * units.deg)
        .add_sample(2000 * units.rpm, 87 * units.deg)
        .add_sample(3000 * units.rpm, 87 * units.deg)
        .add_sample(4000 * units.rpm, 86 * units.deg)
        .add_sample(5000 * units.rpm, 84 * units.deg)
        .add_sample(6000 * units.rpm, 82 * units.deg)
        .add_sample(7000 * units.rpm, 80 * units.deg)
        .add_sample(8000 * units.rpm, 79 * units.deg)

    engine.add_ignition_module(
        ferrari_f136_r_distributor(
            wires: wires,
            timing_curve: timing_curve
        )
    )
}

// 2004 Maserati Coupe GT 4200
public node maserati_coupe_gt_vehicle {
    alias output __out:
        vehicle(
            mass: 1668 * units.kg,
            drag_coefficient: 0.36,
            cross_sectional_area: 1.97 * (units.m * units.m), // estimated
            diff_ratio: 3.7,
            tire_radius: 321.5 * units.mm, // 265/35ZR18
            rolling_resistance: 1668 * units.kg * 9.81 * (265.0/275.0) * 0.01
        );
}

public node maserati_coupe_gt_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 700 * units.lb_ft
        )
        .add_gear(3.286)
        .add_gear(2.158)
        .add_gear(1.609)
        .add_gear(1.269)
        .add_gear(1.034)
        .add_gear(0.848);
}

public node main {
    set_engine(ferrari_f136_r_engine())
    set_vehicle(maserati_coupe_gt_vehicle())
    set_transmission(maserati_coupe_gt_transmission())
}

main()