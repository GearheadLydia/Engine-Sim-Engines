import "engine_sim.mr"

// almost all specs are guesses

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

public node tvr_speed_six_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 7550 * units.rpm;
    input limiter_duration: 0.09;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0/6.0) * cycle)
            .connect_wire(wires.wire5, (1.0/6.0) * cycle)
            .connect_wire(wires.wire3, (2.0/6.0) * cycle)
            .connect_wire(wires.wire6, (3.0/6.0) * cycle)
            .connect_wire(wires.wire2, (4.0/6.0) * cycle)
            .connect_wire(wires.wire4, (5.0/6.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
}

// https://www.pistonheads.com/gassing/topic.asp?h=0&f=152&t=1210549
public node tvr_speed_six_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 268 * units.deg,
            gamma: 1.0,
            lift: 10.16 * units.mm,
            steps: 100
        );
}

public node tvr_speed_six_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 268 * units.deg,
            gamma: 1.0,
            lift: 10.16 * units.mm,
            steps: 100
        );
}

public node tvr_speed_six_camshaft_builder {
    input lobe_profile: tvr_speed_six_lobe_profile_int();
    input ex_lobe_profile: tvr_speed_six_lobe_profile_exh();
    input intake_lobe_profile: lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input lobe_separation: 106.0 * units.deg;
    input intake_lobe_center: 113.5 * units.deg;
    input exhaust_lobe_center: 107.5 * units.deg;
    input advance: 0.0 * units.deg;
    input base_radius: 16.0 * units.mm;

    output intake_cam_0: _intake_cam_0;
    output exhaust_cam_0: _exhaust_cam_0;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_0(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_0(params, lobe_profile: exhaust_lobe_profile)


    label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_0
        .add_lobe(rot360 + intake_lobe_center + 0 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 4 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 2 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 5 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 1 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 3 * rot120)

    _exhaust_cam_0
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 4 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 5 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot120)
}

public node tvr_speed_six_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 44.0 * units.cc; // unknown; guess (makes compression ratio come out right)
    input flip_display: false;
    
    input flow_intake: 280.0;
    input flow_exhaust: 250.0;
    input lift_max: 10.0 * units.mm;
    alias output __out: head;

    function intake_flow(lift_max / 5.0)
    intake_flow
        .add_sample(0.0 * lift_max, k_28inH2O(0.00 * flow_intake))
        .add_sample(0.2 * lift_max, k_28inH2O(0.32 * flow_intake))
        .add_sample(0.4 * lift_max, k_28inH2O(0.63 * flow_intake))
        .add_sample(0.6 * lift_max, k_28inH2O(0.83 * flow_intake))
        .add_sample(0.8 * lift_max, k_28inH2O(0.95 * flow_intake))
        .add_sample(1.0 * lift_max, k_28inH2O(1.00 * flow_intake))
        .add_sample(1.2 * lift_max, k_28inH2O(1.02 * flow_intake))

    function exhaust_flow(lift_max / 5.0)
    exhaust_flow
        .add_sample(0.0 * lift_max, k_28inH2O(0.00 * flow_exhaust))
        .add_sample(0.2 * lift_max, k_28inH2O(0.32 * flow_exhaust))
        .add_sample(0.4 * lift_max, k_28inH2O(0.63 * flow_exhaust))
        .add_sample(0.6 * lift_max, k_28inH2O(0.83 * flow_exhaust))
        .add_sample(0.8 * lift_max, k_28inH2O(0.95 * flow_exhaust))
        .add_sample(1.0 * lift_max, k_28inH2O(1.00 * flow_exhaust))
        .add_sample(1.2 * lift_max, k_28inH2O(1.02 * flow_exhaust))

        
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.1 * units.inch) * 6 * units.inch,
        intake_runner_cross_section_area: circle_area(1.1 * units.inch),
        exhaust_runner_volume: circle_area(0.85 * units.inch) * 4 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.85 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}

// http://www.rndengineering.co.uk/downloads/SPEED6%20SPREADS.pdf
public node speedsix {
    alias output __out: engine;

    engine engine(
        name: "TVR Speed Six",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 1500 * units.rpm,
        redline: 7500 * units.rpm,
        throttle_gamma: 1.0,
        hf_gain: 0.05,
        jitter: 0.6
    )
    
    label lean_angle(-30 * units.deg)

    label bore(96.0 * units.mm)
    label stroke(92.0 * units.mm)
    label rod_length(144.5 * units.mm)
    label rod_mass(450 * units.g)
    label piston_mass(347 * units.g)
    label compression_height(30.5 * units.mm)
    label deck_height((220.0 + 1.5) * units.mm) // deck height estimated from a drawing and the known stroke measurement
    label piston_dome(-20.0 * units.cc) // makes compression come out right
    label crank_mass(65 * units.lb) // from a manual
    label flywheel_mass(8.5 * units.kg) // 4.3kg flywheel, plus unknown clutch
    label flywheel_radius(6.5 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 15 * units.kg, radius: 8.0 * units.cm)
    )

    wires wires()

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia: crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 90.0 * units.deg + lean_angle
    )

    rod_journal rj1(angle:  0  * units.deg)
    rod_journal rj2(angle: 120 * units.deg)
    rod_journal rj3(angle: 240 * units.deg)
    rod_journal rj4(angle: 240 * units.deg)
    rod_journal rj5(angle: 120 * units.deg)
    rod_journal rj6(angle:  0  * units.deg)
    
    c0
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)
        .add_rod_journal(rj5)
        .add_rod_journal(rj6)

    piston_parameters piston_params(
        mass: piston_mass,
        blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )
    
    intake intake(
        plenum_volume: 2 * units.L,
        plenum_cross_section_area: 100 * units.cm2,
        intake_flow_rate: k_carb(1000),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.9977,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(300),
        runner_length: 8 * units.inch,
        velocity_decay: 0.1
    )
    
    label exhaust_radius((4.0 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(900),
        primary_tube_length: 12 * units.inch,
        primary_flow_rate: k_carb(500),
        velocity_decay: 1.0, //0.5
        collector_cross_section_area: circle_area(exhaust_radius),
        length: 2000 * units.mm,
        volume: (1800 * units.mm * circle_area(exhaust_radius)) + (200 * units.mm * circle_area(exhaust_radius) * 2)
    )

    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.04, impulse_response: ir_lib.default_0)
    
    label pipe_length_1(35 * units.mm)
    label pipe_length_2(00 * units.mm)
    label pipe_length_3(20 * units.mm)
    label pipe_length_4(85 * units.mm)
    label pipe_length_5(50 * units.mm)
    label pipe_length_6(70 * units.mm)
 
    cylinder_bank b0(bank_params, angle: 0 * units.deg + lean_angle)
    b0
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire1,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_1
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire2,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_2
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire3,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_3
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire4,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_4
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj5,
            ignition_wire: wires.wire5,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_5
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj6,
            ignition_wire: wires.wire6,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_6
        )

    engine
        .add_cylinder_bank(b0)

    engine.add_crankshaft(c0)

    tvr_speed_six_camshaft_builder camshaft(
        lobe_profile: tvr_speed_six_lobe_profile_int(),
        ex_lobe_profile: tvr_speed_six_lobe_profile_exh()
    )

    b0.set_cylinder_head (
        tvr_speed_six_head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0,
            flip_display: false
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 50 * units.deg)
        .add_sample(1000 * units.rpm, 65 * units.deg)
        .add_sample(2000 * units.rpm, 70 * units.deg)
        .add_sample(3000 * units.rpm, 70 * units.deg)
        .add_sample(4000 * units.rpm, 70 * units.deg)
        .add_sample(5000 * units.rpm, 70 * units.deg)
        .add_sample(6000 * units.rpm, 70 * units.deg)
        .add_sample(7000 * units.rpm, 70 * units.deg)
        .add_sample(8000 * units.rpm, 70 * units.deg)

    engine.add_ignition_module(
        tvr_speed_six_distributor(
            wires: wires,
            timing_curve: timing_curve
        ))
}

// 2007 TVR Sagaris
public node tvr_sagaris_vehicle {
    alias output __out:
        vehicle(
            mass: 1078 * units.kg,
            drag_coefficient: 0.34, // guesstimate
            cross_sectional_area: 1.85 * (units.m * units.m), // guesstimate
            diff_ratio: 3.73,
            tire_radius: 327.5 * units.mm, // 255/35ZR18
            rolling_resistance: 500 * units.N
        );
}

public node tvr_sagaris_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 800 * units.lb_ft
        )
        .add_gear(2.95)
        .add_gear(1.95)
        .add_gear(1.34)
        .add_gear(1.00)
        .add_gear(0.80);
}

public node main {
    set_engine(speedsix())
    set_vehicle(tvr_sagaris_vehicle())
    set_transmission(tvr_sagaris_transmission())
}

main()