import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

// firing order: 13726548
// in standard:  26543187

public node ford_modular_3v46_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 6250 * units.rpm;
    input limiter_duration: 0.1;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0 / 8.0) * cycle)
            .connect_wire(wires.wire3, (1.0 / 8.0) * cycle)
            .connect_wire(wires.wire7, (2.0 / 8.0) * cycle)
            .connect_wire(wires.wire2, (3.0 / 8.0) * cycle)
            .connect_wire(wires.wire6, (4.0 / 8.0) * cycle)
            .connect_wire(wires.wire5, (5.0 / 8.0) * cycle)
            .connect_wire(wires.wire4, (6.0 / 8.0) * cycle)
            .connect_wire(wires.wire8, (7.0 / 8.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
    output wire7: ignition_wire();
    output wire8: ignition_wire();
}

public node ford_modular_3v46_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 195 * units.deg,
            gamma: 1.0,
            lift: 11.15 * units.mm,
            steps: 100
        );
}

public node ford_modular_3v46_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 221 * units.deg,
            gamma: 1.0,
            lift: 11.07 * units.mm,
            steps: 100
        );
}

public node ford_modular_3v46_camshaft_builder {
    input in_lobe_profile: ford_modular_3v46_lobe_profile_int();
    input ex_lobe_profile: ford_modular_3v46_lobe_profile_exh();
    input intake_lobe_profile: in_lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input lobe_separation: 117.5 * units.deg;
    input intake_lobe_center: lobe_separation;
    input exhaust_lobe_center: lobe_separation;
    input advance: 0.0 * units.deg;
    input base_radius: 15 * units.mm;

    output intake_cam_L: _intake_cam_L;
    output exhaust_cam_L: _exhaust_cam_L;
    output intake_cam_R: _intake_cam_R;
    output exhaust_cam_R: _exhaust_cam_R;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_L(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_L(params, lobe_profile: exhaust_lobe_profile)

    camshaft _intake_cam_R(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_R(params, lobe_profile: exhaust_lobe_profile)


    label rot30(30 * units.deg)
    label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_L
        .add_lobe(rot360 + intake_lobe_center + 0 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 3 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 1 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 6 * rot90)

    _intake_cam_R
        .add_lobe(rot360 + intake_lobe_center + 5 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 4 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 2 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 7 * rot90)

    _exhaust_cam_L
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 6 * rot90)

    _exhaust_cam_R
        .add_lobe(rot360 - exhaust_lobe_center + 5 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 4 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 7 * rot90)
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.thou, k_28inH2O(flow))
}

public node ford_modular_3v46_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 49.8 * units.cc;
    input flip_display: false;
    
    alias output __out: head;
    
    // http://www.users.interport.net/s/r/srweiss/tablehdc.htm#Ford
    function intake_flow(100 * units.thou)
    intake_flow
        .add_flow_sample(  0,   0)
        .add_flow_sample(100,  84)
        .add_flow_sample(200, 145)
        .add_flow_sample(300, 208)
        .add_flow_sample(400, 217)
        .add_flow_sample(500, 223)
        .add_flow_sample(600, 227)

    function exhaust_flow(100 * units.thou)
    exhaust_flow
        .add_flow_sample(  0,   0)
        .add_flow_sample(100,  54)
        .add_flow_sample(200, 100)
        .add_flow_sample(300, 125)
        .add_flow_sample(400, 138)
        .add_flow_sample(500, 148)
        .add_flow_sample(600, 154)

        
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.2 * units.inch) * 5 * units.inch,
        intake_runner_cross_section_area: circle_area(1.2 * units.inch),
        exhaust_runner_volume: circle_area(0.7 * units.inch) * 12 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.7 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}


public node ford_modular_v8_3v46_engine {
    alias output __out: engine;

    wires wires()

    engine engine(
        name: "Ford Modular 3V",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 900 * units.rpm,
        redline: 6000 * units.rpm,
        throttle_gamma: 1.0
        //jitter: 0.3,
        //hf_gain: 0.005
    )

    // https://fordauthority.com/fmc/ford-motor-company-engines/ford-modular-family/ford-4-6l-engine/
    label bore(90.22 * units.mm)
    label bore_spacing(100.0 * units.mm)
    label stroke(90.0 * units.mm)
    label rod_length(150.7 * units.mm)
    label rod_mass(618 * units.g) // 4v number?
    label compression_height(31.00 * units.mm)
    label piston_mass(361 * units.g) // 4v number?
    label piston_dome(-9.0 * units.cc) // had "-9.7" before; not sure where that came from; correct 9.8:1 compression ratio with this
    label deck_height((227.0 + 0.762) * units.mm)
    label crank_mass(54.3 * units.lb)
    label flywheel_mass(25 * units.lb)
    label flywheel_radius(6 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 20 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia:
            crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 135.0 * units.deg // -45 degrees
    )

    rod_journal rj1(angle:   0 * units.deg)
    rod_journal rj2(angle: 270 * units.deg)
    rod_journal rj3(angle:  90 * units.deg)
    rod_journal rj4(angle: 180 * units.deg)

    c0
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)


    piston_parameters piston_params(
        mass: piston_mass,
        blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0 * units.mm,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    intake intake(
        plenum_volume: 2.0 * units.L,
        plenum_cross_section_area: 100.0 * units.cm2,
        intake_flow_rate: k_carb(600.0),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.996,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(450.0),
        runner_length: 6.5 * units.inch,
        velocity_decay: 0.1
    )

    label exhaust_radius((4.0 * units.inch)/2)
    
    label exhaust_length(2400 * units.mm)
    label exhaust_stagger(0.3 * bore_spacing)

    exhaust_system_parameters es_params_L(
        outlet_flow_rate: k_carb(350.0),
        primary_tube_length: 10.0 * units.inch,
        primary_flow_rate: k_carb(350.0),
        velocity_decay: 1.0, //0.5
        collector_cross_section_area: circle_area(exhaust_radius),
        length: (exhaust_length + exhaust_stagger),
        volume: (exhaust_length + exhaust_stagger) * circle_area(exhaust_radius)
    )

    exhaust_system_parameters es_params_R(
        outlet_flow_rate: k_carb(350.0),
        primary_tube_length: 10.0 * units.inch,
        primary_flow_rate: k_carb(350.0),
        velocity_decay: 1.0, //0.5
        collector_cross_section_area: circle_area(exhaust_radius),
        length: exhaust_length,
        volume: exhaust_length * circle_area(exhaust_radius)
    )

    exhaust_system exhaustL(es_params_L, audio_volume: 1.0 * 0.08, impulse_response: ir_lib.default_0)
    exhaust_system exhaustR(es_params_R, audio_volume: 1.0 * 0.08, impulse_response: ir_lib.default_0)

    label pipe_length_1(2.4 * bore_spacing)
    label pipe_length_2(1.6 * bore_spacing)
    label pipe_length_3(0.8 * bore_spacing)
    label pipe_length_4(0.0 * bore_spacing)

    cylinder_bank bL(bank_params, angle: 45.0 * units.deg, display_depth: 0.6) // L (first)
    bL
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire1,
            intake: intake,
            exhaust_system: exhaustL,
            primary_length: pipe_length_1
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire2,
            intake: intake,
            exhaust_system: exhaustL,
            primary_length: pipe_length_2
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire3,
            intake: intake,
            exhaust_system: exhaustL,
            primary_length: pipe_length_3
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire4,
            intake: intake,
            exhaust_system: exhaustL,
            primary_length: pipe_length_4
        )

    cylinder_bank bR(bank_params, angle: -45.0 * units.deg, display_depth: 0.6) // R (second)
    bR
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire5,
            intake: intake,
            exhaust_system: exhaustR,
            primary_length: pipe_length_1
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire6,
            intake: intake,
            exhaust_system: exhaustR,
            primary_length: pipe_length_2
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire7,
            intake: intake,
            exhaust_system: exhaustR,
            primary_length: pipe_length_3
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire8,
            intake: intake,
            exhaust_system: exhaustR,
            primary_length: pipe_length_4
        )

    engine
        .add_cylinder_bank(bL)
        .add_cylinder_bank(bR)

    engine.add_crankshaft(c0)

    ford_modular_3v46_camshaft_builder camshaft(
        in_lobe_profile: ford_modular_3v46_lobe_profile_int(),
        ex_lobe_profile: ford_modular_3v46_lobe_profile_exh()
    )

    bL.set_cylinder_head (
        ford_modular_3v46_head(
            intake_camshaft: camshaft.intake_cam_L,
            exhaust_camshaft: camshaft.exhaust_cam_L,
            flip_display: true
        )
    )
    bR.set_cylinder_head (
        ford_modular_3v46_head(
            intake_camshaft: camshaft.intake_cam_R,
            exhaust_camshaft: camshaft.exhaust_cam_R,
            flip_display: false
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 19 * units.deg)
        .add_sample(1000 * units.rpm, 24 * units.deg)
        .add_sample(2000 * units.rpm, 28 * units.deg)
        .add_sample(3000 * units.rpm, 32 * units.deg)
        .add_sample(4000 * units.rpm, 35 * units.deg)
        .add_sample(5000 * units.rpm, 37 * units.deg)
        .add_sample(6000 * units.rpm, 39 * units.deg)
        .add_sample(7000 * units.rpm, 39 * units.deg)

    engine.add_ignition_module(
        ford_modular_3v46_distributor(
            wires: wires,
            timing_curve: timing_curve
        )
    )
}

// 2005 Ford Mustang GT (S197)
public node ford_mustang_gt_s197_vehicle {
    alias output __out:
        vehicle(
            mass: 3483 * units.lb,
            drag_coefficient: 0.35,
            cross_sectional_area: 2.19 * (units.m * units.m),
            diff_ratio: 3.55,
            tire_radius: 345.5 * units.mm, // 235/55ZR17
            rolling_resistance: 500 * units.N
        );
}

public node tremec_tr3650_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 550 * units.lb_ft
        )
        .add_gear(3.38)
        .add_gear(2.00)
        .add_gear(1.32)
        .add_gear(1.00)
        .add_gear(0.67);
}

public node main {
    set_engine(ford_modular_v8_3v46_engine())
    set_vehicle(ford_mustang_gt_s197_vehicle())
    set_transmission(tremec_tr3650_transmission())
}

main()