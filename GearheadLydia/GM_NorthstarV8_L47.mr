import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

// firing order: 12734568
// in GM normal: 21843657

public node cadillac_northstar_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 6200 * units.rpm;
    input limiter_duration: 0.08;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0 / 8.0) * cycle)
            .connect_wire(wires.wire2, (1.0 / 8.0) * cycle)
            .connect_wire(wires.wire7, (2.0 / 8.0) * cycle)
            .connect_wire(wires.wire3, (3.0 / 8.0) * cycle)
            .connect_wire(wires.wire4, (4.0 / 8.0) * cycle)
            .connect_wire(wires.wire5, (5.0 / 8.0) * cycle)
            .connect_wire(wires.wire6, (6.0 / 8.0) * cycle)
            .connect_wire(wires.wire8, (7.0 / 8.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
    output wire7: ignition_wire();
    output wire8: ignition_wire();
}

// borrowing same cams as LH2; specs seem to match
// https://aurorah.proboards.com/thread/11021
public node cadillac_northstar_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 242.4 * units.deg,
            gamma: 1.0,
            lift: 10.5 * units.mm,
            steps: 100
        );
}

public node cadillac_northstar_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 236.3 * units.deg,
            gamma: 1.0,
            lift: 10.0 * units.mm,
            steps: 100
        );
}

public node cadillac_northstar_camshaft_builder {
    input in_lobe_profile: cadillac_northstar_lobe_profile_int();
    input ex_lobe_profile: cadillac_northstar_lobe_profile_exh();
    input intake_lobe_profile: in_lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input intake_lobe_center: 115.6 * units.deg;
    input exhaust_lobe_center: 104.35 * units.deg;
    input advance: 0.0 * units.deg;
    input base_radius: 15 * units.mm;

    output intake_cam_L: _intake_cam_L;
    output intake_cam_R: _intake_cam_R;
    output exhaust_cam_L: _exhaust_cam_L;
    output exhaust_cam_R: _exhaust_cam_R;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_L(params, lobe_profile: intake_lobe_profile)
    camshaft _intake_cam_R(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_L(params, lobe_profile: exhaust_lobe_profile)
    camshaft _exhaust_cam_R(params, lobe_profile: exhaust_lobe_profile)

    label rot90(90 * units.deg)
    label rot360(360 * units.deg)

    // firing order: 12734568
    _intake_cam_L
        .add_lobe(rot360 + intake_lobe_center + 0 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 3 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 5 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 2 * rot90)

    _intake_cam_R
        .add_lobe(rot360 + intake_lobe_center + 1 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 4 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 6 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 7 * rot90)

    _exhaust_cam_L
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 5 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot90)

    _exhaust_cam_R
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 4 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 6 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 7 * rot90)
}

public node cadillac_northstar_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 48.6 * units.cc;
    input flip_display: false;
    
    alias output __out: head;
    
    // http://www.users.interport.net/s/r/srweiss/tablehdc.htm#Cadillac
    function intake_flow(50 * units.thou)
    intake_flow
        .add_flow_sample(  0,   0)
        .add_flow_sample( 50,  26)
        .add_flow_sample(100,  75)
        .add_flow_sample(150, 111)
        .add_flow_sample(200, 144)
        .add_flow_sample(250, 171)
        .add_flow_sample(300, 193)
        .add_flow_sample(350, 207)
        .add_flow_sample(400, 212)
        .add_flow_sample(450, 215)
        .add_flow_sample(500, 217)

    function exhaust_flow(50 * units.thou)
    exhaust_flow
        .add_flow_sample(  0,   0)
        .add_flow_sample( 50,  30)
        .add_flow_sample(100,  68)
        .add_flow_sample(150,  94)
        .add_flow_sample(200, 109)
        .add_flow_sample(250, 119)
        .add_flow_sample(300, 126)
        .add_flow_sample(350, 131)
        .add_flow_sample(400, 134)
        .add_flow_sample(450, 138)
        .add_flow_sample(500, 142)

    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.0 * units.inch) * 4 * units.inch,
        intake_runner_cross_section_area: circle_area(1.0 * units.inch),
        exhaust_runner_volume: circle_area(0.6 * units.inch) * 4 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.6 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}

// most specs borrowed from LD8
// https://www.fiero.nl/forum/Archives/Archive-000001/HTML/20090219-2-080565.html

public node oldsmobile_northstar_l47_engine {
    alias output __out: engine;

    wires wires()

    engine engine(
        name: "Oldsmobile Northstar L47",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 1200 * units.rpm,
        redline: 6000 * units.rpm,
        throttle_gamma: 1.0
    )

    label bore(87.0 * units.mm)
    label bore_spacing(101.6 * units.mm)
    label stroke(84.0 * units.mm)
    label rod_length(151.0 * units.mm)
    label rod_mass(600 * units.g) // guess
    label piston_mass(400 * units.g) // guess
    label compression_height(31.5 * units.mm) // some sources say 32.0, some sources say 31.5
    label piston_dome(3.0 * units.cc) // for 10.3:1 CR
    label deck_height((224.55 + 1.3) * units.mm)
    label crank_mass(85 * units.lb)
    label flywheel_mass(20 * units.lb)
    label flywheel_radius(6 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 20 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia:
            crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 135.0 * units.deg // +45 degrees
    )

    rod_journal rj1(angle:   0 * units.deg)
    rod_journal rj2(angle: 270 * units.deg)
    rod_journal rj3(angle:  90 * units.deg)
    rod_journal rj4(angle: 180 * units.deg)

    c0
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)

    // piston specs are guesses
    piston_parameters piston_params(
        mass: piston_mass,
        blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0 * units.mm,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    intake intake(
        plenum_volume: 2.0 * units.L,
        plenum_cross_section_area: 100.0 * units.cm2,
        intake_flow_rate: k_carb(450.0),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.9965,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(200.0),
        runner_length: 9.0 * units.inch,
        velocity_decay: 0.1
    )

    label exhaust_radius((3.5 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(400.0),
        primary_tube_length: 10.0 * units.inch,
        primary_flow_rate: k_carb(150.0),
        velocity_decay: 1.0, //0.5
        collector_cross_section_area: circle_area(exhaust_radius) * 2,
        length: 2700 * units.mm,
        volume: (2250 * units.mm * circle_area(exhaust_radius)) + (450 * units.mm * circle_area(exhaust_radius) * 2)
    )

    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.05, impulse_response: ir_lib.default_0)

    label pipe_length_1(2.7 * bore_spacing)
    label pipe_length_2(1.8 * bore_spacing)
    label pipe_length_3(0.9 * bore_spacing)
    label pipe_length_4(0.0 * bore_spacing)
    
    label exhaust_stagger(10 * units.inch)

    cylinder_bank bL(bank_params, angle: 45 * units.deg) // L (second, rear)
    bL
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire1,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_1
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire3,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_2
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire5,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_3
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire7,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_4
        )

    cylinder_bank bR(bank_params, angle: -45.0 * units.deg) // R (first, front)
    bR
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire2,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_1 + exhaust_stagger
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire4,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_2 + exhaust_stagger
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire6,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_3 + exhaust_stagger
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire8,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_4 + exhaust_stagger
        )

    engine
        .add_cylinder_bank(bL)
        .add_cylinder_bank(bR)

    engine.add_crankshaft(c0)

    cadillac_northstar_camshaft_builder camshaft(
        in_lobe_profile: cadillac_northstar_lobe_profile_int(),
        ex_lobe_profile: cadillac_northstar_lobe_profile_exh()
    )

    bL.set_cylinder_head (
        cadillac_northstar_head(
            intake_camshaft: camshaft.intake_cam_L,
            exhaust_camshaft: camshaft.exhaust_cam_L,
            flip_display: true
        )
    )
    bR.set_cylinder_head (
        cadillac_northstar_head(
            intake_camshaft: camshaft.intake_cam_R,
            exhaust_camshaft: camshaft.exhaust_cam_R,
            flip_display: false
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 46 * units.deg)
        .add_sample(1000 * units.rpm, 56 * units.deg)
        .add_sample(2000 * units.rpm, 56 * units.deg)
        .add_sample(3000 * units.rpm, 56 * units.deg)
        .add_sample(4000 * units.rpm, 56 * units.deg)
        .add_sample(5000 * units.rpm, 56 * units.deg)
        .add_sample(6000 * units.rpm, 56 * units.deg)
        .add_sample(7000 * units.rpm, 56 * units.deg)

    engine.add_ignition_module(
        cadillac_northstar_distributor(
            wires: wires,
            timing_curve: timing_curve
        )
    )
}

// 1995 Oldsmobile Aurora V8
public node oldsmobile_aurora_v8_1995_vehicle {
    alias output __out:
        vehicle(
            mass: 3966 * units.lb,
            drag_coefficient: 0.32,
            cross_sectional_area: 2.21 * (units.m * units.m),
            diff_ratio: 3.71,
            tire_radius: 344 * units.mm, // P235/60R16
            rolling_resistance: 3966 * units.lb * 9.81 * (235.0/275.0) * 0.01
        );
}

// 2001 Oldsmobile Aurora V8
public node oldsmobile_aurora_v8_2001_vehicle {
    alias output __out:
        vehicle(
            mass: 3803 * units.lb,
            drag_coefficient: 0.32,
            cross_sectional_area: 2.21 * (units.m * units.m),
            diff_ratio: 3.71,
            tire_radius: 345.5 * units.mm, // 235/55R17H
            rolling_resistance: 3803 * units.lb * 9.81 * (235.0/275.0) * 0.01
        );
}

public node gm_4t80e_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 500 * units.lb_ft
        )
        .add_gear(2.960)
        .add_gear(1.626)
        .add_gear(1.000)
        .add_gear(0.681);
}

public node main {
    set_engine(oldsmobile_northstar_l47_engine())
    set_vehicle(oldsmobile_aurora_v8_2001_vehicle())
    set_transmission(gm_4t80e_transmission())
}

main()