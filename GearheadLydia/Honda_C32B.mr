import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

public node honda_c32b_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 8250 * units.rpm;
    input limiter_duration: 0.07;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0/6.0) * cycle)
            .connect_wire(wires.wire4, (1.0/6.0) * cycle)
            .connect_wire(wires.wire2, (2.0/6.0) * cycle)
            .connect_wire(wires.wire5, (3.0/6.0) * cycle)
            .connect_wire(wires.wire3, (4.0/6.0) * cycle)
            .connect_wire(wires.wire6, (5.0/6.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
}

// camshaft specs via TODA Racing catalog (probably not stock)
// lift via http://www.speedhunters.com/2019/07/project-nsx-dreams-power-part-one/
// centers are guesses based on https://www.nsxprime.com/threads/camshaft-info-and-gurus-please.174161/
// advance just set to 0 as a guess / default

// advertised specs:
// primary non-VTEC intake:  233 / 8.7
// primary non-VTEC exhaust: 238 / 7.7
// secondary non-VTEC intake:  238 / 8.3
// secondary non-VTEC exhaust: 243 / 8.6
// VTEC intake:  285 / 10.2
// VTEC exhaust: 280 /  9.5

public node honda_c32b_lobe_profile_int_lo {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 207.5 * units.deg, // average of 197.5 and 217.5
            gamma: 1.0,
            lift: 8.5 * units.mm,
            steps: 100
        );
}

public node honda_c32b_lobe_profile_exh_lo {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 209.4 * units.deg, // average of 199.1 and 219.6
            gamma: 1.0,
            lift: 8.2 * units.mm,
            steps: 100
        );
}

public node honda_c32b_lobe_profile_int_hi {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 248.0 * units.deg,
            gamma: 1.0,
            lift: 10.2 * units.mm,
            steps: 100
        );
}

public node honda_c32b_lobe_profile_exh_hi {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 243.4 * units.deg,
            gamma: 1.0,
            lift: 9.5 * units.mm,
            steps: 100
        );
}

public node honda_c32b_camshaft_builder {
    input in_lobe_profile_lo: honda_c32b_lobe_profile_int_lo();
    input ex_lobe_profile_lo: honda_c32b_lobe_profile_exh_lo();
    input in_lobe_profile_hi: honda_c32b_lobe_profile_int_hi();
    input ex_lobe_profile_hi: honda_c32b_lobe_profile_exh_hi();
    input intake_lobe_center_lo: 100.0 * units.deg;  // fudged to improve cam crossover
    input exhaust_lobe_center_lo: 100.0 * units.deg; // fudged to improve cam crossover
    input intake_lobe_center_hi: 106.75 * units.deg;
    input exhaust_lobe_center_hi: 104.0 * units.deg;
    input advance_in_lo: 0.0 * units.deg;
    input advance_ex_lo: 0.0 * units.deg;
    input advance_in_hi: 0.0 * units.deg;
    input advance_ex_hi: 0.0 * units.deg;
    input base_radius: 15 * units.mm;

    output intake_cam_0_lo: _intake_cam_0_lo;
    output exhaust_cam_0_lo: _exhaust_cam_0_lo;
    output intake_cam_1_lo: _intake_cam_1_lo;
    output exhaust_cam_1_lo: _exhaust_cam_1_lo;

    output intake_cam_0_hi: _intake_cam_0_hi;
    output exhaust_cam_0_hi: _exhaust_cam_0_hi;
    output intake_cam_1_hi: _intake_cam_1_hi;
    output exhaust_cam_1_hi: _exhaust_cam_1_hi;

    camshaft_parameters params(
        advance: advance_in_lo,
        base_radius: base_radius
    )

    camshaft _intake_cam_0_lo(params, lobe_profile: in_lobe_profile_lo, advance: advance_in_lo)
    camshaft _exhaust_cam_0_lo(params, lobe_profile: ex_lobe_profile_lo, advance: advance_ex_lo)

    camshaft _intake_cam_1_lo(params, lobe_profile: in_lobe_profile_lo, advance: advance_in_lo)
    camshaft _exhaust_cam_1_lo(params, lobe_profile: ex_lobe_profile_lo, advance: advance_ex_lo)

    camshaft _intake_cam_0_hi(params, lobe_profile: in_lobe_profile_hi, advance: advance_in_hi)
    camshaft _exhaust_cam_0_hi(params, lobe_profile: ex_lobe_profile_hi, advance: advance_ex_hi)

    camshaft _intake_cam_1_hi(params, lobe_profile: in_lobe_profile_hi, advance: advance_in_hi)
    camshaft _exhaust_cam_1_hi(params, lobe_profile: ex_lobe_profile_hi, advance: advance_ex_hi)


    label rot30(30 * units.deg)
    label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_0_lo
        .add_lobe(rot360 + intake_lobe_center_lo + (0 * rot120))
        .add_lobe(rot360 + intake_lobe_center_lo + (2 * rot120))
        .add_lobe(rot360 + intake_lobe_center_lo + (4 * rot120))

    _exhaust_cam_0_lo
        .add_lobe(rot360 - exhaust_lobe_center_lo + (0 * rot120))
        .add_lobe(rot360 - exhaust_lobe_center_lo + (2 * rot120))
        .add_lobe(rot360 - exhaust_lobe_center_lo + (4 * rot120))

    _intake_cam_1_lo
        .add_lobe(rot360 + intake_lobe_center_lo + (1 * rot120))
        .add_lobe(rot360 + intake_lobe_center_lo + (3 * rot120))
        .add_lobe(rot360 + intake_lobe_center_lo + (5 * rot120))

    _exhaust_cam_1_lo
        .add_lobe(rot360 - exhaust_lobe_center_lo + (1 * rot120))
        .add_lobe(rot360 - exhaust_lobe_center_lo + (3 * rot120))
        .add_lobe(rot360 - exhaust_lobe_center_lo + (5 * rot120))

    _intake_cam_0_hi
        .add_lobe(rot360 + intake_lobe_center_hi + (0 * rot120))
        .add_lobe(rot360 + intake_lobe_center_hi + (2 * rot120))
        .add_lobe(rot360 + intake_lobe_center_hi + (4 * rot120))

    _exhaust_cam_0_hi
        .add_lobe(rot360 - exhaust_lobe_center_hi + (0 * rot120))
        .add_lobe(rot360 - exhaust_lobe_center_hi + (2 * rot120))
        .add_lobe(rot360 - exhaust_lobe_center_hi + (4 * rot120))

    _intake_cam_1_hi
        .add_lobe(rot360 + intake_lobe_center_hi + (1 * rot120))
        .add_lobe(rot360 + intake_lobe_center_hi + (3 * rot120))
        .add_lobe(rot360 + intake_lobe_center_hi + (5 * rot120))

    _exhaust_cam_1_hi
        .add_lobe(rot360 - exhaust_lobe_center_hi + (1 * rot120))
        .add_lobe(rot360 - exhaust_lobe_center_hi + (3 * rot120))
        .add_lobe(rot360 - exhaust_lobe_center_hi + (5 * rot120))
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.mm, k_28inH2O(flow))
}

public node honda_c32b_head {
    input cam_in_lo;
    input cam_ex_lo;
    input cam_in_hi;
    input cam_ex_hi;
    input chamber_volume: 51.0 * units.cc; // not sure where I got this spec
    input flip_display: false;
    
    input flow_attenuation: 1.0;
    input lift_scale: 1.1;
    alias output __out: head;

    function intake_flow(1.0 * units.mm)
    intake_flow
        .add_flow_sample(0 * lift_scale, 0 * flow_attenuation)
        .add_flow_sample(1 * lift_scale, 30 * flow_attenuation)
        .add_flow_sample(2 * lift_scale, 60 * flow_attenuation)
        .add_flow_sample(3 * lift_scale, 90 * flow_attenuation)
        .add_flow_sample(4 * lift_scale, 125 * flow_attenuation)
        .add_flow_sample(5 * lift_scale, 160 * flow_attenuation)
        .add_flow_sample(6 * lift_scale, 195 * flow_attenuation)
        .add_flow_sample(7 * lift_scale, 210 * flow_attenuation)
        .add_flow_sample(8 * lift_scale, 235 * flow_attenuation)
        .add_flow_sample(9 * lift_scale, 270 * flow_attenuation)
        .add_flow_sample(10 * lift_scale, 305 * flow_attenuation)
        .add_flow_sample(11 * lift_scale, 320 * flow_attenuation)
        .add_flow_sample(12 * lift_scale, 335 * flow_attenuation)

    function exhaust_flow(1.0 * units.mm)
    exhaust_flow
        .add_flow_sample(0 * lift_scale, 0 * flow_attenuation)
        .add_flow_sample(1 * lift_scale, 30 * flow_attenuation)
        .add_flow_sample(2 * lift_scale, 55 * flow_attenuation)
        .add_flow_sample(3 * lift_scale, 85 * flow_attenuation)
        .add_flow_sample(4 * lift_scale, 115 * flow_attenuation)
        .add_flow_sample(5 * lift_scale, 140 * flow_attenuation)
        .add_flow_sample(6 * lift_scale, 160 * flow_attenuation)
        .add_flow_sample(7 * lift_scale, 180 * flow_attenuation)
        .add_flow_sample(8 * lift_scale, 205 * flow_attenuation)
        .add_flow_sample(9 * lift_scale, 220 * flow_attenuation)
        .add_flow_sample(10 * lift_scale, 240 * flow_attenuation)
        .add_flow_sample(11 * lift_scale, 260 * flow_attenuation)
        .add_flow_sample(12 * lift_scale, 280 * flow_attenuation)

        
    generic_cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.0 * units.inch) * 1.2 * units.inch,
        intake_runner_cross_section_area: circle_area(1.0 * units.inch),
        exhaust_runner_volume: circle_area(0.75 * units.inch) * 1.2 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.75 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        valvetrain: vtec_valvetrain(
            vtec_intake_camshaft: cam_in_hi,
            vtec_exhaust_camshaft: cam_ex_hi,
            intake_camshaft: cam_in_lo,
            exhaust_camshaft: cam_ex_lo,
            min_rpm: 5800 * units.rpm
        ),
        flip_display: flip_display
    )
}


public node honda_c32b_engine {
    alias output __out: engine;

    wires wires()

    label bore(93.0 * units.mm)
    label bore_spacing(100.0 * units.mm)
    label stroke(78.0 * units.mm)
    label rod_length(152.5 * units.mm)
    label rod_mass(400 * units.g) // guesstimate based on aftermarket chromoly rods (~550g) - stock rods are titanium
    label piston_mass(400 * units.g)
    label compression_height(30.00 * units.mm)
    label piston_dome(-1.5 * units.cc) // for 10.2:1 CR
    label deck_height((221.5 + 0.75) * units.mm)
    label crank_mass(45 * units.lb)
    label flywheel_mass(25 * units.lb)
    label flywheel_radius(5 * units.inch)

    engine engine(
        name: "Honda C32B",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 1200 * units.rpm,
        redline: 8000 * units.rpm,
        throttle_gamma: 1.0
    )

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 15 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia: crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 135.0 * units.deg
    )

    rod_journal rj0(angle:   0 * units.deg)
    rod_journal rj1(angle:  30 * units.deg)
    rod_journal rj2(angle: 240 * units.deg)
    rod_journal rj3(angle: 270 * units.deg)
    rod_journal rj4(angle: 120 * units.deg)
    rod_journal rj5(angle: 150 * units.deg)

    c0
        .add_rod_journal(rj0)
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)
        .add_rod_journal(rj5)


    piston_parameters piston_params(
        mass: piston_mass,
        //blowby: 0,
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    intake intake(
        plenum_volume: 0.5 * units.L,
        plenum_cross_section_area: 50.0 * units.cm2,
        intake_flow_rate: k_carb(500.0),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.9972,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(250.0),
        runner_length: 4.5 * units.inch,
        velocity_decay: 0.1
    )

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(600.0),
        primary_tube_length: 6.0 * units.inch,
        primary_flow_rate: k_carb(240.0),
        velocity_decay: 0.6, //0.5
        volume: 5.0 * units.L,
        length: 800 * units.mm
    )

    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.005, impulse_response: ir_lib.default_0)
    
    label pipe_length_1(00 * units.mm)
    label pipe_length_2(03 * units.mm)
    label pipe_length_3(10 * units.mm)
    
    label exhaust_stagger(5 * units.inch)

    cylinder_bank b0(bank_params, angle: 45.0 * units.deg) // L (first)
    b0
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            ignition_wire: wires.wire1,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_1,
            sound_attenuation: 0.7 * 0.9
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire2,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_2,
            sound_attenuation: 0.85 * 0.9
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.2)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire3,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_3,
            sound_attenuation: 1.0 * 0.9
        )

    cylinder_bank b1(bank_params, angle: -45.0 * units.deg) // R (second)
    b1
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire4,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_1 + exhaust_stagger,
            sound_attenuation: 0.7
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire5,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_2 + exhaust_stagger,
            sound_attenuation: 0.85
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.2)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj5,
            ignition_wire: wires.wire6,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_3 + exhaust_stagger,
            sound_attenuation: 1.0
        )

    engine
        .add_cylinder_bank(b0)
        .add_cylinder_bank(b1)

    engine.add_crankshaft(c0)

    honda_c32b_camshaft_builder camshaft()

    b0.set_cylinder_head (
        honda_c32b_head(
            cam_in_lo: camshaft.intake_cam_0_lo,
            cam_in_hi: camshaft.intake_cam_0_hi,
            cam_ex_lo: camshaft.exhaust_cam_0_lo,
            cam_ex_hi: camshaft.exhaust_cam_0_hi,
            flip_display: true
        )
    )
    b1.set_cylinder_head (
        honda_c32b_head(
            cam_in_lo: camshaft.intake_cam_1_lo,
            cam_in_hi: camshaft.intake_cam_1_hi,
            cam_ex_lo: camshaft.exhaust_cam_1_lo,
            cam_ex_hi: camshaft.exhaust_cam_1_hi,
            flip_display: false
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 35 * units.deg)
        .add_sample(1000 * units.rpm, 40 * units.deg)
        .add_sample(2000 * units.rpm, 43 * units.deg)
        .add_sample(3000 * units.rpm, 46 * units.deg)
        .add_sample(4000 * units.rpm, 49 * units.deg)
        .add_sample(5000 * units.rpm, 52 * units.deg)
        .add_sample(6000 * units.rpm, 54 * units.deg)
        .add_sample(7000 * units.rpm, 55 * units.deg)
        .add_sample(8000 * units.rpm, 56 * units.deg)
        .add_sample(9000 * units.rpm, 56 * units.deg)

    engine.add_ignition_module(
        honda_c32b_distributor(
            wires: wires,
            timing_curve: timing_curve
        )
    )
}

// 1999 Acura NSX Zanardi Edition
public node honda_nsx_na2_vehicle {
    alias output __out:
        vehicle(
            mass: 3015 * units.lb,
            drag_coefficient: 0.32,
            cross_sectional_area: 1.78 * (units.m * units.m),
            diff_ratio: 4.062,
            tire_radius: 314 * units.mm, // 245/40ZR17
            rolling_resistance: 3015 * units.lb * 9.81 * (245.0/275.0) * 0.01
        );
}

public node honda_nsx_na2_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 500 * units.lb_ft
        )
        .add_gear(3.066)
        .add_gear(1.956)
        .add_gear(1.428)
        .add_gear(1.125)
        .add_gear(0.914)
        .add_gear(0.717);
}

public node main {
    set_engine(honda_c32b_engine())
    set_vehicle(honda_nsx_na2_vehicle())
    set_transmission(honda_nsx_na2_transmission())
}

main()