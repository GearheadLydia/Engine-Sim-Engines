import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

public node audi_v6_ccba_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 7200 * units.rpm; // real, reportedly
    input limiter_duration: 0.06;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0 / 6.0) * cycle)
            .connect_wire(wires.wire4, (1.0 / 6.0) * cycle)
            .connect_wire(wires.wire3, (2.0 / 6.0) * cycle)
            .connect_wire(wires.wire6, (3.0 / 6.0) * cycle)
            .connect_wire(wires.wire2, (4.0 / 6.0) * cycle)
            .connect_wire(wires.wire5, (5.0 / 6.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
}

// https://www.034motorsport.com/034motorsport-camshaft-kit-for-b8-audi-s4-3-0-tfsi.html
public node audi_v6_ccba_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 227 * units.deg, // 252 at 10 thou; 201 at 100 thou
            gamma: 1.0,
            lift: 10.05 * units.mm,
            steps: 100
        );
}

public node audi_v6_ccba_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 234 * units.deg, // 257 at 10 thou; 210 at 100 thou
            gamma: 1.0,
            lift: 10.0 * units.mm,
            steps: 100
        );
}

public node audi_v6_ccba_camshaft_builder {
    input in_lobe_profile: audi_v6_ccba_lobe_profile_int();
    input ex_lobe_profile: audi_v6_ccba_lobe_profile_exh();
    input intake_lobe_center: 128.0 * units.deg;
    input exhaust_lobe_center: 114.0 * units.deg;
    input advance_in: 0.0 * units.deg;
    input advance_ex: 0.0 * units.deg;
    input base_radius: 15 * units.mm;

    output intake_cam_0: _intake_cam_0;
    output exhaust_cam_0: _exhaust_cam_0;
    output intake_cam_1: _intake_cam_1;
    output exhaust_cam_1: _exhaust_cam_1;

    camshaft_parameters params(
        advance: advance_in,
        base_radius: base_radius
    )

    camshaft _intake_cam_0(params, lobe_profile: in_lobe_profile, advance: advance_in)
    camshaft _exhaust_cam_0(params, lobe_profile: ex_lobe_profile, advance: advance_ex)

    camshaft _intake_cam_1(params, lobe_profile: in_lobe_profile, advance: advance_in)
    camshaft _exhaust_cam_1(params, lobe_profile: ex_lobe_profile, advance: advance_ex)


    label rot30(30 * units.deg)
    label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_0
        .add_lobe(rot360 + intake_lobe_center + (1 * rot120))
        .add_lobe(rot360 + intake_lobe_center + (5 * rot120))
        .add_lobe(rot360 + intake_lobe_center + (3 * rot120))

    _exhaust_cam_0
        .add_lobe(rot360 - exhaust_lobe_center + (1 * rot120))
        .add_lobe(rot360 - exhaust_lobe_center + (5 * rot120))
        .add_lobe(rot360 - exhaust_lobe_center + (3 * rot120))

    _intake_cam_1
        .add_lobe(rot360 + intake_lobe_center + (0 * rot120))
        .add_lobe(rot360 + intake_lobe_center + (4 * rot120))
        .add_lobe(rot360 + intake_lobe_center + (2 * rot120))

    _exhaust_cam_1
        .add_lobe(rot360 - exhaust_lobe_center + (0 * rot120))
        .add_lobe(rot360 - exhaust_lobe_center + (4 * rot120))
        .add_lobe(rot360 - exhaust_lobe_center + (2 * rot120))
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.mm, k_28inH2O(flow))
}

// cylinder head: 44cc
// https://forum.rs246.com/viewtopic.php?t=60970
public node audi_v6_ccba_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 44.0 * units.cc;
    input flip_display: false;
    
    input flow_attenuation_in: 1.0;
    input flow_attenuation_ex: 1.0;
    input lift_scale: 1.0;
    alias output __out: head;

    function intake_flow(1.0 * units.mm)
    intake_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_in)
        .add_flow_sample( 1 * lift_scale,  30 * flow_attenuation_in)
        .add_flow_sample( 2 * lift_scale,  60 * flow_attenuation_in)
        .add_flow_sample( 3 * lift_scale,  90 * flow_attenuation_in)
        .add_flow_sample( 4 * lift_scale, 125 * flow_attenuation_in)
        .add_flow_sample( 5 * lift_scale, 160 * flow_attenuation_in)
        .add_flow_sample( 6 * lift_scale, 195 * flow_attenuation_in)
        .add_flow_sample( 7 * lift_scale, 210 * flow_attenuation_in)
        .add_flow_sample( 8 * lift_scale, 235 * flow_attenuation_in)
        .add_flow_sample( 9 * lift_scale, 270 * flow_attenuation_in)
        .add_flow_sample(10 * lift_scale, 305 * flow_attenuation_in)
        .add_flow_sample(11 * lift_scale, 320 * flow_attenuation_in)
        .add_flow_sample(12 * lift_scale, 335 * flow_attenuation_in)
        .add_flow_sample(13 * lift_scale, 335 * flow_attenuation_in)
        .add_flow_sample(14 * lift_scale, 335 * flow_attenuation_in)

    function exhaust_flow(1.0 * units.mm)
    exhaust_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_ex)
        .add_flow_sample( 1 * lift_scale,  30 * flow_attenuation_ex)
        .add_flow_sample( 2 * lift_scale,  55 * flow_attenuation_ex)
        .add_flow_sample( 3 * lift_scale,  85 * flow_attenuation_ex)
        .add_flow_sample( 4 * lift_scale, 115 * flow_attenuation_ex)
        .add_flow_sample( 5 * lift_scale, 140 * flow_attenuation_ex)
        .add_flow_sample( 6 * lift_scale, 160 * flow_attenuation_ex)
        .add_flow_sample( 7 * lift_scale, 180 * flow_attenuation_ex)
        .add_flow_sample( 8 * lift_scale, 205 * flow_attenuation_ex)
        .add_flow_sample( 9 * lift_scale, 220 * flow_attenuation_ex)
        .add_flow_sample(10 * lift_scale, 240 * flow_attenuation_ex)
        .add_flow_sample(11 * lift_scale, 260 * flow_attenuation_ex)
        .add_flow_sample(12 * lift_scale, 280 * flow_attenuation_ex)
        .add_flow_sample(13 * lift_scale, 280 * flow_attenuation_ex)
        .add_flow_sample(14 * lift_scale, 280 * flow_attenuation_ex)

        
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.1 * units.inch) * 3 * units.inch,
        intake_runner_cross_section_area: circle_area(1.1 * units.inch),
        exhaust_runner_volume: circle_area(0.7 * units.inch) * 3 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.7 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}


public node audi_v6_30tfsi_ccba_engine {
    alias output __out: engine;

    engine engine(
        name: "Audi V6 3.0T CCBA",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 1000 * units.rpm,
        redline: 7000 * units.rpm,
        fuel: fuel(max_burning_efficiency: 1.03), // free power to compensate for unavailable supercharger
        throttle_gamma: 1.0
    )

    wires wires()

    label bore(84.5 * units.mm)
    label stroke(89.0 * units.mm)
    label rod_length(152.7 * units.mm)
    label rod_mass(550 * units.g)
    label piston_mass(400 * units.g)
    label compression_height(30.8 * units.mm)
    label piston_dome(-1.2 * units.cc) // for 10.3:1 CR
    label deck_height((228.0 + 1.5) * units.mm) // gasket is a guess
    label crank_mass(55 * units.lb)
    label flywheel_mass(20 * units.lb)
    label flywheel_radius(5 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 15 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia: crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 135.0 * units.deg
    )

    rod_journal rj1(angle:   0 * units.deg)
    rod_journal rj2(angle:  30 * units.deg)
    rod_journal rj3(angle: 120 * units.deg)
    rod_journal rj4(angle: 150 * units.deg)
    rod_journal rj5(angle: 240 * units.deg)
    rod_journal rj6(angle: 270 * units.deg)

    c0
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)
        .add_rod_journal(rj5)
        .add_rod_journal(rj6)


    piston_parameters piston_params(
        mass: piston_mass,
        //blowby: 0,
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    intake intake(
        plenum_volume: 2.0 * units.L,
        plenum_cross_section_area: 100.0 * units.cm2,
        intake_flow_rate: k_carb(450.0),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.9957,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(300.0),
        runner_length: 2.5 * units.inch,
        velocity_decay: 0.1
    )
    
    label exhaust_radius((3.5 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(450.0),
        primary_tube_length: 5.0 * units.inch,
        primary_flow_rate: k_carb(280.0),
        velocity_decay: 1.0, //0.5
        collector_cross_section_area: circle_area(exhaust_radius) * 2,
        length: 2700 * units.mm,
        volume: (2000 * units.mm * circle_area(exhaust_radius)) + (700 * units.mm * circle_area(exhaust_radius) * 2)
    )

    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.1, impulse_response: ir_lib.default_0)

    label pipe_length_1(70 * units.mm)
    label pipe_length_2(35 * units.mm)
    label pipe_length_3(0 * units.mm)

    label exhaust_stagger(20 * units.mm)

    cylinder_bank b1(bank_params, angle: 45.0 * units.deg) // L (second)
    b1
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire1,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_1,
            sound_attenuation: 1.0
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire2,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_2,
            sound_attenuation: 1.0
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.5)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj5,
            ignition_wire: wires.wire3,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_3,
            sound_attenuation: 1.0
        )

    cylinder_bank b0(bank_params, angle: -45.0 * units.deg) // R (first)
    b0
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire4,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_1 + exhaust_stagger,
            sound_attenuation: 1.0
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire5,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_2 + exhaust_stagger,
            sound_attenuation: 1.0
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.5)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj6,
            ignition_wire: wires.wire6,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_3 + exhaust_stagger,
            sound_attenuation: 1.0
        )

    engine
        .add_cylinder_bank(b1)
        .add_cylinder_bank(b0)

    engine.add_crankshaft(c0)

    audi_v6_ccba_camshaft_builder camshaft()

    b0.set_cylinder_head (
        audi_v6_ccba_head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0,
            flip_display: false
        )
    )
    b1.set_cylinder_head (
        audi_v6_ccba_head(
            intake_camshaft: camshaft.intake_cam_1,
            exhaust_camshaft: camshaft.exhaust_cam_1,
            flip_display: true
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 65 * units.deg)
        .add_sample(1000 * units.rpm, 75 * units.deg)
        .add_sample(2000 * units.rpm, 85 * units.deg)
        .add_sample(3000 * units.rpm, 85 * units.deg)
        .add_sample(4000 * units.rpm, 73 * units.deg)
        .add_sample(5000 * units.rpm, 61 * units.deg)
        .add_sample(6000 * units.rpm, 49 * units.deg)
        .add_sample(7000 * units.rpm, 38 * units.deg)
        .add_sample(8000 * units.rpm, 38 * units.deg)

    engine.add_ignition_module(
        audi_v6_ccba_distributor(
            wires: wires,
            timing_curve: timing_curve
        )
    )
}

// 2010 Audi S4 (B8)
public node audi_s4_b8_vehicle {
    alias output __out:
        vehicle(
            mass: 1660 * units.kg,
            drag_coefficient: 0.282,
            cross_sectional_area: 2.2 * (units.m * units.m),
            diff_ratio: 3.682,
            tire_radius: 326.5 * units.mm, // 245/40ZR18
            rolling_resistance: 1660 * units.kg * 9.81 * (225.0/275.0) * 0.01
        );
}

// not going to bother adding the S5 because it's nearly identical

public node audi_s4_b8_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 700 * units.lb_ft
        )
        .add_gear(3.667)
        .add_gear(2.158)
        .add_gear(1.520)
        .add_gear(1.133)
        .add_gear(0.919)
        .add_gear(0.778);
}

public node main {
    set_engine(audi_v6_30tfsi_ccba_engine())
    set_vehicle(audi_s4_b8_vehicle())
    set_transmission(audi_s4_b8_transmission())
}

main()