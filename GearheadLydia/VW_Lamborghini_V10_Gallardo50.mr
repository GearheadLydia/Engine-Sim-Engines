import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

// firing order:  1-6-5-10-2-7-3-8-4-9
// in standard: 2-1-10-9-4-3-6-5-8-7

public node lamborghini_gallardo_v10_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 9000 * units.rpm;
	input limiter_duration: 0.05;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire01, (0.0/10.0) * cycle)
            .connect_wire(wires.wire06, (1.0/10.0) * cycle)
			.connect_wire(wires.wire05, (2.0/10.0) * cycle)
            .connect_wire(wires.wire10, (3.0/10.0) * cycle)
            .connect_wire(wires.wire02, (4.0/10.0) * cycle)
			.connect_wire(wires.wire07, (5.0/10.0) * cycle)
            .connect_wire(wires.wire03, (6.0/10.0) * cycle)
            .connect_wire(wires.wire08, (7.0/10.0) * cycle)
			.connect_wire(wires.wire04, (8.0/10.0) * cycle)
            .connect_wire(wires.wire09, (9.0/10.0) * cycle);
}

private node wires {
    output wire01: ignition_wire();
    output wire02: ignition_wire();
    output wire03: ignition_wire();
    output wire04: ignition_wire();
    output wire05: ignition_wire();
    output wire06: ignition_wire();
    output wire07: ignition_wire();
    output wire08: ignition_wire();
    output wire09: ignition_wire();
    output wire10: ignition_wire();
}

public node lamborghini_gallardo_v10_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 250 * units.deg,
            gamma: 1.0,
            lift: 12.5 * units.mm,
            steps: 100
        );
}

public node lamborghini_gallardo_v10_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 250 * units.deg,
            gamma: 1.0,
            lift: 12.5 * units.mm,
            steps: 100
        );
}

public node lamborghini_gallardo_v10_camshaft_builder {
    input in_lobe_profile: lamborghini_gallardo_v10_lobe_profile_int();
	input ex_lobe_profile: lamborghini_gallardo_v10_lobe_profile_exh();
    input intake_lobe_profile: in_lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input lobe_separation: 110.0 * units.deg;
    input intake_lobe_center: lobe_separation;
    input exhaust_lobe_center: lobe_separation;
    input advance: 0.0 * units.deg;
    input base_radius: 15 * units.mm;

    output intake_cam_0: _intake_cam_0;
    output exhaust_cam_0: _exhaust_cam_0;
    output intake_cam_1: _intake_cam_1;
    output exhaust_cam_1: _exhaust_cam_1;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_0(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_0(params, lobe_profile: exhaust_lobe_profile)

    camshaft _intake_cam_1(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_1(params, lobe_profile: exhaust_lobe_profile)


	label rot18(18 * units.deg)
	label rot30(30 * units.deg)
	label rot54(54 * units.deg)
	label rot60(60 * units.deg)
	label rot72(72 * units.deg)
    label rot90(90 * units.deg)
	label rot120(120 * units.deg)
    label rot360(360 * units.deg)
	
    _intake_cam_0
        .add_lobe(rot360 + intake_lobe_center + 0 * rot72) //  #1
        .add_lobe(rot360 + intake_lobe_center + 4 * rot72) //  #2
        .add_lobe(rot360 + intake_lobe_center + 6 * rot72) //  #3
        .add_lobe(rot360 + intake_lobe_center + 8 * rot72) //  #4
        .add_lobe(rot360 + intake_lobe_center + 2 * rot72) //  #5

    _intake_cam_1
        .add_lobe(rot360 + intake_lobe_center + 1 * rot72) //  #6
        .add_lobe(rot360 + intake_lobe_center + 5 * rot72) //  #7
        .add_lobe(rot360 + intake_lobe_center + 7 * rot72) //  #8
        .add_lobe(rot360 + intake_lobe_center + 9 * rot72) //  #9
        .add_lobe(rot360 + intake_lobe_center + 3 * rot72) // #10

    _exhaust_cam_0
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot72)
        .add_lobe(rot360 - exhaust_lobe_center + 4 * rot72)
        .add_lobe(rot360 - exhaust_lobe_center + 6 * rot72)
        .add_lobe(rot360 - exhaust_lobe_center + 8 * rot72)
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot72)

    _exhaust_cam_1
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot72)
        .add_lobe(rot360 - exhaust_lobe_center + 5 * rot72)
        .add_lobe(rot360 - exhaust_lobe_center + 7 * rot72)
        .add_lobe(rot360 - exhaust_lobe_center + 9 * rot72)
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot72)
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.thou, k_28inH2O(flow))
}

public node lamborghini_gallardo_v10_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 46.0 * units.cc;
    input flip_display: false;
	
	input flow_attenuation_in: 1.0;
	input flow_attenuation_ex: 1.0;
    input lift_scale: 50.0;
    alias output __out: head;

    function intake_flow(1.0 * units.mm)
    intake_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_in)
        .add_flow_sample( 1 * lift_scale,  30 * flow_attenuation_in)
        .add_flow_sample( 2 * lift_scale,  60 * flow_attenuation_in)
        .add_flow_sample( 3 * lift_scale,  90 * flow_attenuation_in)
        .add_flow_sample( 4 * lift_scale, 125 * flow_attenuation_in)
        .add_flow_sample( 5 * lift_scale, 160 * flow_attenuation_in)
        .add_flow_sample( 6 * lift_scale, 195 * flow_attenuation_in)
        .add_flow_sample( 7 * lift_scale, 210 * flow_attenuation_in)
        .add_flow_sample( 8 * lift_scale, 235 * flow_attenuation_in)
        .add_flow_sample( 9 * lift_scale, 270 * flow_attenuation_in)
        .add_flow_sample(10 * lift_scale, 305 * flow_attenuation_in)
        .add_flow_sample(11 * lift_scale, 320 * flow_attenuation_in)
        .add_flow_sample(12 * lift_scale, 335 * flow_attenuation_in)
        .add_flow_sample(13 * lift_scale, 335 * flow_attenuation_in)
        .add_flow_sample(14 * lift_scale, 335 * flow_attenuation_in)

    function exhaust_flow(1.0 * units.mm)
    exhaust_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_ex)
        .add_flow_sample( 1 * lift_scale,  30 * flow_attenuation_ex)
        .add_flow_sample( 2 * lift_scale,  55 * flow_attenuation_ex)
        .add_flow_sample( 3 * lift_scale,  85 * flow_attenuation_ex)
        .add_flow_sample( 4 * lift_scale, 115 * flow_attenuation_ex)
        .add_flow_sample( 5 * lift_scale, 140 * flow_attenuation_ex)
        .add_flow_sample( 6 * lift_scale, 160 * flow_attenuation_ex)
        .add_flow_sample( 7 * lift_scale, 180 * flow_attenuation_ex)
        .add_flow_sample( 8 * lift_scale, 205 * flow_attenuation_ex)
        .add_flow_sample( 9 * lift_scale, 220 * flow_attenuation_ex)
        .add_flow_sample(10 * lift_scale, 240 * flow_attenuation_ex)
        .add_flow_sample(11 * lift_scale, 260 * flow_attenuation_ex)
        .add_flow_sample(12 * lift_scale, 280 * flow_attenuation_ex)
        .add_flow_sample(13 * lift_scale, 280 * flow_attenuation_ex)
        .add_flow_sample(14 * lift_scale, 280 * flow_attenuation_ex)

		
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.4 * units.inch) * 9 * units.inch,
        intake_runner_cross_section_area: circle_area(1.4 * units.inch),
        exhaust_runner_volume: circle_area(0.85 * units.inch) * 12 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.85 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}


public node gallardo_v10 {
    alias output __out: engine;

    wires wires()

    label bore(82.5 * units.mm)
    label stroke(92.8 * units.mm)
    label rod_length(154.0 * units.mm)
    label rod_mass(434 * units.g)
	label piston_mass(290 * units.g)
    label compression_height(27.6 * units.mm)
	label piston_dome(3.7 * units.cc)
	label deck_height(228.5 * units.mm)
    label crank_mass(75 * units.lb)
    label flywheel_mass(20 * units.lb)
    label flywheel_radius(6 * units.inch)

    engine engine(
        name: "Lamborghini V10 Even Fire",
        starter_torque: 150 * units.lb_ft,
		starter_speed: 900 * units.rpm,
        redline: 8800 * units.rpm,
        fuel: fuel(
            max_turbulence_effect: 4.0,
            burning_efficiency_randomness: 0.2,
            max_burning_efficiency: 0.85),
        throttle_gamma: 1.0,
		simulation_frequency: 7500
    )

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 15 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 10.0 * units.lb_ft,
        moment_of_inertia: crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 135.0 * units.deg // +45 degrees
    )

    rod_journal rj0(angle:  0  * units.deg)
    rod_journal rj1(angle: -18  * units.deg)
    rod_journal rj2(angle: 288 * units.deg)
    rod_journal rj3(angle: 270 * units.deg)
    rod_journal rj4(angle: 72 * units.deg)
    rod_journal rj5(angle: 54 * units.deg)
	rod_journal rj6(angle: 216 * units.deg)
	rod_journal rj7(angle: 198 * units.deg)
    rod_journal rj8(angle: 144 * units.deg)
    rod_journal rj9(angle: 126 * units.deg)

    c0
        .add_rod_journal(rj0)
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)
        .add_rod_journal(rj5)
        .add_rod_journal(rj6)
        .add_rod_journal(rj7)
        .add_rod_journal(rj8)
        .add_rod_journal(rj9)


    piston_parameters piston_params(
        mass: piston_mass,
        blowby: 0,
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

	intake intakeL(
		plenum_volume: 2.0 * units.L,
		plenum_cross_section_area: 150.0 * units.cm2,
		intake_flow_rate: k_carb(450.0),
		idle_flow_rate: k_carb(0.1),
		idle_throttle_plate_position: 0.9935,
		throttle_gamma: 2.0,
		runner_flow_rate: k_carb(450.0),
		runner_length: 12.0 * units.inch,
		velocity_decay: 0.1
	)

	intake intakeR(
		plenum_volume: 2.0 * units.L,
		plenum_cross_section_area: 150.0 * units.cm2,
		intake_flow_rate: k_carb(450.0),
		idle_flow_rate: k_carb(0.1),
		idle_throttle_plate_position: 0.9935,
		throttle_gamma: 2.0,
		runner_flow_rate: k_carb(450.0),
		runner_length: 12.0 * units.inch,
		velocity_decay: 0.1
	)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(1000.0),
        primary_tube_length: 20.0 * units.inch,
        primary_flow_rate: k_carb(450.0),
        velocity_decay: 1.0, //0.5
        volume: 50.0 * units.L
    )

    exhaust_system exhaust(
		es_params,
        length: 500 * units.mm,
		audio_volume: 1.0 * 0.01,
		impulse_response: ir_lib.default_0
	)
	
	label pipe_length_1(3.0 * units.inch)
	label pipe_length_2(2.5 * units.inch)
	label pipe_length_3(1.0 * units.inch)
	label pipe_length_4(0.5 * units.inch)
	label pipe_length_5(0.0 * units.inch)
	
	label exhaust_stagger(10 * units.mm)
	
	label exh_vol_L1(0.8)
	label exh_vol_L3(1.0)
	label exh_vol_R1(0.64)
	label exh_vol_R3(0.8)

    cylinder_bank b0(bank_params, angle: 45.0 * units.deg) // L (first)
    b0
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.5)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            ignition_wire: wires.wire01,
            intake: intakeL,
            exhaust_system: exhaust,
			primary_length: pipe_length_1 + exhaust_stagger,
			sound_attenuation: exh_vol_L1
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.3)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire02,
            intake: intakeL,
            exhaust_system: exhaust,
			primary_length: pipe_length_2 + exhaust_stagger,
			sound_attenuation: exh_vol_L1
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire03,
            intake: intakeL,
            exhaust_system: exhaust,
			primary_length: pipe_length_3 + exhaust_stagger,
			sound_attenuation: exh_vol_L3
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.3)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj6,
            ignition_wire: wires.wire04,
            intake: intakeL,
            exhaust_system: exhaust,
			primary_length: pipe_length_4 + exhaust_stagger,
			sound_attenuation: exh_vol_L3
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.5)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj8,
            ignition_wire: wires.wire05,
            intake: intakeL,
            exhaust_system: exhaust,
			primary_length: pipe_length_5 + exhaust_stagger,
			sound_attenuation: exh_vol_L3
        )

    cylinder_bank b1(bank_params, angle: -45.0 * units.deg) // R (second)
    b1
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.5)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire06,
            intake: intakeR,
            exhaust_system: exhaust,
			primary_length: pipe_length_1,
			sound_attenuation: exh_vol_R1
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.3)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire07,
            intake: intakeR,
            exhaust_system: exhaust,
			primary_length: pipe_length_2,
			sound_attenuation: exh_vol_R1
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj5,
            ignition_wire: wires.wire08,
            intake: intakeR,
            exhaust_system: exhaust,
			primary_length: pipe_length_3,
			sound_attenuation: exh_vol_R3
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.3)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj7,
            ignition_wire: wires.wire09,
            intake: intakeR,
            exhaust_system: exhaust,
			primary_length: pipe_length_4,
			sound_attenuation: exh_vol_R3
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.5)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj9,
            ignition_wire: wires.wire10,
            intake: intakeR,
            exhaust_system: exhaust,
			primary_length: pipe_length_5,
			sound_attenuation: exh_vol_R3
        )

    engine
        .add_cylinder_bank(b0)
        .add_cylinder_bank(b1)

    engine.add_crankshaft(c0)

    lamborghini_gallardo_v10_camshaft_builder camshaft(
	    in_lobe_profile: lamborghini_gallardo_v10_lobe_profile_int(),
		ex_lobe_profile: lamborghini_gallardo_v10_lobe_profile_exh()
	)

    b0.set_cylinder_head (
        lamborghini_gallardo_v10_head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0,
            flip_display: true
        )
    )
    b1.set_cylinder_head (
        lamborghini_gallardo_v10_head(
            intake_camshaft: camshaft.intake_cam_1,
            exhaust_camshaft: camshaft.exhaust_cam_1,
            flip_display: false
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000  * units.rpm, 15 * units.deg)
        .add_sample(1000  * units.rpm, 21 * units.deg)
        .add_sample(2000  * units.rpm, 26 * units.deg)
        .add_sample(3000  * units.rpm, 33 * units.deg)
        .add_sample(4000  * units.rpm, 40 * units.deg)
        .add_sample(5000  * units.rpm, 45 * units.deg)
        .add_sample(6000  * units.rpm, 48 * units.deg)
		.add_sample(7000  * units.rpm, 50 * units.deg)
		.add_sample(8000  * units.rpm, 50 * units.deg)
		.add_sample(9000  * units.rpm, 50 * units.deg)
		.add_sample(10000 * units.rpm, 50 * units.deg)

    engine.add_ignition_module(
        lamborghini_gallardo_v10_distributor(
            wires: wires,
            timing_curve: timing_curve,
            rev_limit: 9000 * units.rpm
        )
    )
}

// 2004 Lamborghini Gallardo
public node lamborghini_gallardo_vehicle {
    alias output __out:
        vehicle(
            mass: 3153 * units.lb,
            drag_coefficient: 0.36,
            cross_sectional_area: 1.9 * (units.m * units.m),
            diff_ratio: 3.60,
            tire_radius: 330.2 * units.mm, // 295/35ZR20
            rolling_resistance: 525 * units.N
        );
}

public node lamborghini_gallardo_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 700 * units.lb_ft
        )
        .add_gear(2.563)
        .add_gear(1.850)
        .add_gear(1.423)
        .add_gear(1.138)
        .add_gear(0.939)
        .add_gear(0.806);
}

public node main {
    set_engine(gallardo_v10())
    set_vehicle(lamborghini_gallardo_vehicle())
    set_transmission(lamborghini_gallardo_transmission())
}
