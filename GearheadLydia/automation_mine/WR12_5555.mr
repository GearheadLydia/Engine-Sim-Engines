import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

label bank_angle_major(75 * units.deg / 2)
label bank_angle_minor(10 * units.deg / 2)

label bank_timing_offset(bank_angle_major * 2 - (60 * units.deg))

// firing order: 1 12 5 8 3 10 6 7 2 11 4 9

public node wr12_5555_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 7200 * units.rpm;
    input limiter_duration: 0.06;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire01,  (0.0/12.0) * cycle)
            .connect_wire(wires.wire12,  (1.0/12.0) * cycle + bank_timing_offset)
            .connect_wire(wires.wire05,  (2.0/12.0) * cycle)
            .connect_wire(wires.wire08,  (3.0/12.0) * cycle + bank_timing_offset)
            .connect_wire(wires.wire03,  (4.0/12.0) * cycle)
            .connect_wire(wires.wire10,  (5.0/12.0) * cycle + bank_timing_offset)
            .connect_wire(wires.wire06,  (6.0/12.0) * cycle)
            .connect_wire(wires.wire07,  (7.0/12.0) * cycle + bank_timing_offset)
            .connect_wire(wires.wire02,  (8.0/12.0) * cycle)
            .connect_wire(wires.wire11,  (9.0/12.0) * cycle + bank_timing_offset)
            .connect_wire(wires.wire04, (10.0/12.0) * cycle)
            .connect_wire(wires.wire09, (11.0/12.0) * cycle + bank_timing_offset);
}

private node wires {
    output wire01: ignition_wire();
    output wire02: ignition_wire();
    output wire03: ignition_wire();
    output wire04: ignition_wire();
    output wire05: ignition_wire();
    output wire06: ignition_wire();
    output wire07: ignition_wire();
    output wire08: ignition_wire();
    output wire09: ignition_wire();
    output wire10: ignition_wire();
    output wire11: ignition_wire();
    output wire12: ignition_wire();
}

// cams copied from VR6 R32

public node wr12_5555_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 272 * units.deg,
            gamma: 1.5,
            lift: 9.85 * units.mm,
            steps: 100
        );
}

public node wr12_5555_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 258 * units.deg,
            gamma: 1.5,
            lift: 9.90 * units.mm,
            steps: 100
        );
}

public node wr12_5555_camshaft_builder {
    input in_lobe_profile: wr12_5555_lobe_profile_int();
    input ex_lobe_profile: wr12_5555_lobe_profile_exh();
    input intake_lobe_profile: in_lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input lobe_separation: 86.5 * units.deg;
    input intake_lobe_center: 110.0  * units.deg;
    input exhaust_lobe_center: 110.0 * units.deg;
    input advance: 19.0 * units.deg;
    input base_radius: 17 * units.mm;

    output intake_cam_LO: _intake_cam_LO;
    output exhaust_cam_LO: _exhaust_cam_LO;
    output intake_cam_LI: _intake_cam_LI;
    output exhaust_cam_LI: _exhaust_cam_LI;
    output intake_cam_RI: _intake_cam_RI;
    output exhaust_cam_RI: _exhaust_cam_RI;
    output intake_cam_RO: _intake_cam_RO;
    output exhaust_cam_RO: _exhaust_cam_RO;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_LO(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_LO(params, lobe_profile: exhaust_lobe_profile)

    camshaft _intake_cam_LI(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_LI(params, lobe_profile: exhaust_lobe_profile)

    camshaft _intake_cam_RI(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_RI(params, lobe_profile: exhaust_lobe_profile)

    camshaft _intake_cam_RO(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_RO(params, lobe_profile: exhaust_lobe_profile)


    // firing order: 1 12 5 8 3 10 6 7 2 11 4 9

    label rot30(30 * units.deg)
    label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_LO
        .add_lobe(rot360 + intake_lobe_center + ( 0 * rot60)) //  #1
        .add_lobe(rot360 + intake_lobe_center + ( 4 * rot60)) //  #3
        .add_lobe(rot360 + intake_lobe_center + ( 2 * rot60)) //  #5

    _intake_cam_LI
        .add_lobe(rot360 + intake_lobe_center + ( 8 * rot60)) //  #2
        .add_lobe(rot360 + intake_lobe_center + (10 * rot60)) //  #4
        .add_lobe(rot360 + intake_lobe_center + ( 6 * rot60)) //  #6

    _intake_cam_RI
        .add_lobe(rot360 + intake_lobe_center + ( 7 * rot60) + bank_timing_offset) //  #7
        .add_lobe(rot360 + intake_lobe_center + (11 * rot60) + bank_timing_offset) //  #9
        .add_lobe(rot360 + intake_lobe_center + ( 9 * rot60) + bank_timing_offset) // #11

    _intake_cam_RO
        .add_lobe(rot360 + intake_lobe_center + ( 3 * rot60) + bank_timing_offset) //  #8
        .add_lobe(rot360 + intake_lobe_center + ( 5 * rot60) + bank_timing_offset) // #10
        .add_lobe(rot360 + intake_lobe_center + ( 1 * rot60) + bank_timing_offset) // #12

    _exhaust_cam_LO
        .add_lobe(rot360 - exhaust_lobe_center + ( 0 * rot60)) //  #1
        .add_lobe(rot360 - exhaust_lobe_center + ( 4 * rot60)) //  #3
        .add_lobe(rot360 - exhaust_lobe_center + ( 2 * rot60)) //  #5

    _exhaust_cam_LI
        .add_lobe(rot360 - exhaust_lobe_center + ( 8 * rot60)) //  #2
        .add_lobe(rot360 - exhaust_lobe_center + (10 * rot60)) //  #4
        .add_lobe(rot360 - exhaust_lobe_center + ( 6 * rot60)) //  #6

    _exhaust_cam_RI
        .add_lobe(rot360 - exhaust_lobe_center + ( 7 * rot60) + bank_timing_offset) //  #7
        .add_lobe(rot360 - exhaust_lobe_center + (11 * rot60) + bank_timing_offset) //  #9
        .add_lobe(rot360 - exhaust_lobe_center + ( 9 * rot60) + bank_timing_offset) // #11

    _exhaust_cam_RO
        .add_lobe(rot360 - exhaust_lobe_center + ( 3 * rot60) + bank_timing_offset) //  #8
        .add_lobe(rot360 - exhaust_lobe_center + ( 5 * rot60) + bank_timing_offset) // #10
        .add_lobe(rot360 - exhaust_lobe_center + ( 1 * rot60) + bank_timing_offset) // #12
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.thou, k_28inH2O(flow))
}

public node wr12_5555_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 45.0 * units.cc;
    input intake_runner_volume: circle_area(1.1 * units.inch) * 2 * units.inch;
    input intake_runner_cross_section_area: circle_area(1.1 * units.inch);
    input exhaust_runner_volume: circle_area(0.85 * units.inch) * 2 * units.inch;
    input exhaust_runner_cross_section_area: circle_area(0.85 * units.inch);
    input flip_display: false;
    
    input flow_attenuation: 1.0;
    input lift_scale: 50.0;
    alias output __out: head;

    function intake_flow(50 * units.thou)
    intake_flow
        .add_flow_sample(0,    0  * flow_attenuation)
        .add_flow_sample(50,   25 * flow_attenuation)
        .add_flow_sample(100,  76 * flow_attenuation)
        .add_flow_sample(150, 100 * flow_attenuation)
        .add_flow_sample(200, 146 * flow_attenuation)
        .add_flow_sample(250, 175 * flow_attenuation)
        .add_flow_sample(300, 212 * flow_attenuation)
        .add_flow_sample(350, 230 * flow_attenuation)
        .add_flow_sample(400, 255 * flow_attenuation)
        .add_flow_sample(450, 275 * flow_attenuation)
        .add_flow_sample(500, 294 * flow_attenuation)
        .add_flow_sample(550, 300 * flow_attenuation)
        .add_flow_sample(600, 314 * flow_attenuation)
        .add_flow_sample(650, 314 * flow_attenuation)
        .add_flow_sample(700, 314 * flow_attenuation)

    function exhaust_flow(50 * units.thou)
    exhaust_flow
        .add_flow_sample(0,    0  * flow_attenuation)
        .add_flow_sample(50,   25 * flow_attenuation)
        .add_flow_sample(100,  70 * flow_attenuation)
        .add_flow_sample(150, 100 * flow_attenuation)
        .add_flow_sample(200, 132 * flow_attenuation)
        .add_flow_sample(250, 140 * flow_attenuation)
        .add_flow_sample(300, 156 * flow_attenuation)
        .add_flow_sample(350, 170 * flow_attenuation)
        .add_flow_sample(400, 181 * flow_attenuation)
        .add_flow_sample(450, 191 * flow_attenuation)
        .add_flow_sample(500, 207 * flow_attenuation)
        .add_flow_sample(550, 214 * flow_attenuation)
        .add_flow_sample(600, 228 * flow_attenuation)
        .add_flow_sample(650, 228 * flow_attenuation)
        .add_flow_sample(700, 228 * flow_attenuation)

        
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: intake_runner_volume,
        intake_runner_cross_section_area: intake_runner_cross_section_area,
        exhaust_runner_volume: exhaust_runner_volume,
        exhaust_runner_cross_section_area: exhaust_runner_cross_section_area,

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}


public node wr12_5555_engine {
    alias output __out: engine;

    wires wires()

    label bore(83.85 * units.mm)
    label bore_spacing(92.0 * units.mm)
    label stroke(83.85 * units.mm)
    label rod_length(170.0 * units.mm)
    label compression_height(28.0 * units.mm)
    label piston_dome(0.0 * units.cc)
    label chamber_volume(35.0 * units.cc)
    
    label crank_mass(35 * units.kg)
    label rod_mass(550 * units.g)
    label piston_mass(480 * units.g)
    label flywheel_mass(15 * units.lb)
    label flywheel_radius(6 * units.inch)
    
    label compression_ratio(12.0)
    label deck_clearance((((bore * bore / 4.0 * constants.pi * stroke)/(compression_ratio-1.0)) - chamber_volume + piston_dome) / (bore * bore / 4.0 * constants.pi))

    engine engine(
        name: "WR12 ''5555''",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 1200 * units.rpm,
        redline: 7000 * units.rpm,
        throttle_gamma: 1.0,
        simulation_frequency: 10000
    )

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 15 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia: crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: (bank_angle_major) + (90 * units.deg)
    )

    // 6 deg additional offset - supported by trigonometry
    label crank_pin_offset(bank_angle_minor + 6.0 * units.deg)
    rod_journal rj1(angle: (  0 * units.deg) + crank_pin_offset)
    rod_journal rj2(angle: (120 * units.deg) - crank_pin_offset)
    rod_journal rj3(angle: (240 * units.deg) + crank_pin_offset)
    rod_journal rj4(angle: (240 * units.deg) - crank_pin_offset)
    rod_journal rj5(angle: (120 * units.deg) + crank_pin_offset)
    rod_journal rj6(angle: (  0 * units.deg) - crank_pin_offset)

    c0
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)
        .add_rod_journal(rj5)
        .add_rod_journal(rj6)


    piston_parameters piston_params(
        mass: piston_mass,
        blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: stroke / 2 + rod_length + compression_height + deck_clearance
    )

    intake intake_L(
        plenum_volume: 2.0 * units.L,
        plenum_cross_section_area: 100.0 * units.cm2,
        intake_flow_rate: k_carb(900.0),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.9987,
        throttle_gamma: 2.0,
        runner_flow_rate: k_carb(800.0),
        runner_length: 7.0 * units.inch,
        velocity_decay: 0.1
    )

    intake intake_R(
        plenum_volume: 2.0 * units.L,
        plenum_cross_section_area: 100.0 * units.cm2,
        intake_flow_rate: k_carb(900.0),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.9987,
        throttle_gamma: 2.0,
        runner_flow_rate: k_carb(600.0),
        runner_length: 7.0 * units.inch,
        velocity_decay: 0.1
    )

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(1600.0),
        primary_tube_length: 9.0 * units.inch,
        primary_flow_rate: k_carb(550.0),
        velocity_decay: 1.0, //0.5
        volume: 4.0 * units.L,
        length: 500 * units.mm
    )
    exhaust_system exhaust(es_params, audio_volume:  1.0*0.006, impulse_response: ir_lib.default_0)
    
    // random, geometrically defensible offset value
    label bore_center_offset(25.0 * units.mm / 2)
    // these values do NOT auto-update when bank_angle_major or bank_angle_minor is changed!!
    label position_xO_offset(bore_center_offset *  0.73727733681012404138429339498232) // cos(42.5)
    label position_yO_offset(bore_center_offset * -0.67559020761566024434833935367435) // sin(42.5)
    label position_xI_offset(bore_center_offset * -0.84339144581288570127285680582757) // cos(32.5)
    label position_yI_offset(bore_center_offset *  0.53729960834682383184078554626771) // sin(32.5)
    
    label exhaust_length_stagger_LR(95 * units.mm)
    label exhaust_bore_spacing(20 * units.mm)

    cylinder_bank bLO(bank_params, angle:  bank_angle_major + bank_angle_minor, position_x: -position_xO_offset, position_y: position_yO_offset) // LO (first)
    bLO
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire01,
            intake: intake_L,
            exhaust_system: exhaust,
            primary_length: exhaust_bore_spacing * 4 + exhaust_length_stagger_LR
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire03,
            intake: intake_L,
            exhaust_system: exhaust,
            primary_length: exhaust_bore_spacing * 2 + exhaust_length_stagger_LR
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj5,
            ignition_wire: wires.wire05,
            intake: intake_L,
            exhaust_system: exhaust,
            primary_length: exhaust_bore_spacing * 0 + exhaust_length_stagger_LR
        )

    cylinder_bank bRI(bank_params, angle: -bank_angle_major + bank_angle_minor, position_x: position_xI_offset, position_y: position_yI_offset) // RI (second)
    bRI
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire07,
            intake: intake_R,
            exhaust_system: exhaust,
            primary_length: (exhaust_bore_spacing * 4)
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire09,
            intake: intake_R,
            exhaust_system: exhaust,
            primary_length: (exhaust_bore_spacing * 2)
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj5,
            ignition_wire: wires.wire11,
            intake: intake_R,
            exhaust_system: exhaust,
            primary_length: (exhaust_bore_spacing * 0)
        )
    
    cylinder_bank bLI(bank_params, angle:  bank_angle_major - bank_angle_minor, position_x: -position_xI_offset, position_y: position_yI_offset) // LI (third)
    bLI
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire02,
            intake: intake_L,
            exhaust_system: exhaust,
            primary_length: (exhaust_bore_spacing * 5) + exhaust_length_stagger_LR
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire04,
            intake: intake_L,
            exhaust_system: exhaust,
            primary_length: (exhaust_bore_spacing * 3) + exhaust_length_stagger_LR
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj6,
            ignition_wire: wires.wire06,
            intake: intake_L,
            exhaust_system: exhaust,
            primary_length: (exhaust_bore_spacing * 1) + exhaust_length_stagger_LR
        )

    cylinder_bank bRO(bank_params, angle: -bank_angle_major - bank_angle_minor, position_x: position_xO_offset, position_y: position_yO_offset) // RO (fourth)
    bRO
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire08,
            intake: intake_R,
            exhaust_system: exhaust,
            primary_length: exhaust_bore_spacing * 5
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire10,
            intake: intake_R,
            exhaust_system: exhaust,
            primary_length: exhaust_bore_spacing * 3
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj6,
            ignition_wire: wires.wire12,
            intake: intake_R,
            exhaust_system: exhaust,
            primary_length: exhaust_bore_spacing * 1
        )

    engine
        .add_cylinder_bank(bLO)
        .add_cylinder_bank(bRI)
        .add_cylinder_bank(bLI)
        .add_cylinder_bank(bRO)

    engine.add_crankshaft(c0)

    wr12_5555_camshaft_builder camshaft(
        in_lobe_profile: wr12_5555_lobe_profile_int(),
        ex_lobe_profile: wr12_5555_lobe_profile_exh()
    )

    label intake_runner_radius(1.2 * units.inch)
    label exhaust_runner_radius(0.85 * units.inch)
    
    label runner_length_shrt(2.75 * units.inch)
    label runner_length_long(4.25 * units.inch)

    bLO.set_cylinder_head (
        wr12_5555_head(
            intake_camshaft: camshaft.intake_cam_LO,
            exhaust_camshaft: camshaft.exhaust_cam_LO,
            intake_runner_volume: circle_area(intake_runner_radius) * runner_length_long,
            intake_runner_cross_section_area: circle_area(intake_runner_radius),
            exhaust_runner_volume: circle_area(exhaust_runner_radius) * runner_length_shrt,
            exhaust_runner_cross_section_area: circle_area(exhaust_runner_radius),
            chamber_volume: chamber_volume,
            flip_display: true
        )
    )
    bRI.set_cylinder_head (
        wr12_5555_head(
            intake_camshaft: camshaft.intake_cam_RI,
            exhaust_camshaft: camshaft.exhaust_cam_RI,
            intake_runner_volume: circle_area(intake_runner_radius) * runner_length_shrt,
            intake_runner_cross_section_area: circle_area(intake_runner_radius),
            exhaust_runner_volume: circle_area(exhaust_runner_radius) * runner_length_long,
            exhaust_runner_cross_section_area: circle_area(exhaust_runner_radius),
            chamber_volume: chamber_volume,
            flip_display: false
        )
    )
    bLI.set_cylinder_head (
        wr12_5555_head(
            intake_camshaft: camshaft.intake_cam_LI,
            exhaust_camshaft: camshaft.exhaust_cam_LI,
            intake_runner_volume: circle_area(intake_runner_radius) * runner_length_shrt,
            intake_runner_cross_section_area: circle_area(intake_runner_radius),
            exhaust_runner_volume: circle_area(exhaust_runner_radius) * runner_length_long,
            exhaust_runner_cross_section_area: circle_area(exhaust_runner_radius),
            chamber_volume: chamber_volume,
            flip_display: true
        )
    )
    bRO.set_cylinder_head (
        wr12_5555_head(
            intake_camshaft: camshaft.intake_cam_RO,
            exhaust_camshaft: camshaft.exhaust_cam_RO,
            intake_runner_volume: circle_area(intake_runner_radius) * runner_length_long,
            intake_runner_cross_section_area: circle_area(intake_runner_radius),
            exhaust_runner_volume: circle_area(exhaust_runner_radius) * runner_length_shrt,
            exhaust_runner_cross_section_area: circle_area(exhaust_runner_radius),
            chamber_volume: chamber_volume,
            flip_display: false
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 25 * units.deg)
        .add_sample(1000 * units.rpm, 40 * units.deg)
        .add_sample(2000 * units.rpm, 45 * units.deg)
        .add_sample(3000 * units.rpm, 45 * units.deg)
        .add_sample(4000 * units.rpm, 45 * units.deg)
        .add_sample(5000 * units.rpm, 45 * units.deg)
        .add_sample(6000 * units.rpm, 45 * units.deg)
        .add_sample(7000 * units.rpm, 45 * units.deg)
        .add_sample(8000 * units.rpm, 45 * units.deg)

    engine.add_ignition_module(
        wr12_5555_distributor(
            wires: wires,
            timing_curve: timing_curve
        )
    )
}

// 2001 Volkswagen W12 Nardo
// some numbers from TDU, some guesses
public node volkswagen_w12_nardo_vehicle {
    alias output __out:
        vehicle(
            mass: 1200 * units.kg,
            drag_coefficient: 0.29,
            cross_sectional_area: 1.8 * (units.m * units.m),
            diff_ratio: 3.52,
            tire_radius: 352 * units.mm, // 275/40ZR19
            rolling_resistance: 500 * units.N
        );
}

public node volkswagen_w12_nardo_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 900 * units.lb_ft
        )
        .add_gear(4.171)
        .add_gear(2.34)
        .add_gear(1.521)
        .add_gear(1.143)
        .add_gear(0.867)
        .add_gear(0.691);
}

public node main {
    set_engine(wr12_5555_engine())
    set_vehicle(volkswagen_w12_nardo_vehicle())
    set_transmission(volkswagen_w12_nardo_transmission())
}

main()