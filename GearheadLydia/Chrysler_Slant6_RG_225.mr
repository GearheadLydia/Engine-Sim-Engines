import "engine_sim.mr"

// really rough - a lot of dimensions are a little hard to find

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

public node chrysler_slant_six_225_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 4600 * units.rpm; // approximate / folklore
    input limiter_duration: 0.1;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0/6.0) * cycle)
            .connect_wire(wires.wire5, (1.0/6.0) * cycle)
            .connect_wire(wires.wire3, (2.0/6.0) * cycle)
            .connect_wire(wires.wire6, (3.0/6.0) * cycle)
            .connect_wire(wires.wire2, (4.0/6.0) * cycle)
            .connect_wire(wires.wire4, (5.0/6.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
}

// https://www.forabodiesonly.com/mopar/threads/slant-six-stock-cam-specs.155070/
public node chrysler_slant_six_225_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 240 * units.deg,
            gamma: 1.0,
            lift: 395 * units.thou,
            steps: 100
        );
}

public node chrysler_slant_six_225_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 236 * units.deg,
            gamma: 1.0,
            lift: 365 * units.thou,
            steps: 100
        );
}

public node chrysler_slant_six_225_camshaft_builder {
    input lobe_profile: chrysler_slant_six_225_lobe_profile_int();
    input ex_lobe_profile: chrysler_slant_six_225_lobe_profile_exh();
    input intake_lobe_profile: lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input lobe_separation: 111.0 * units.deg;
    input intake_lobe_center: lobe_separation;
    input exhaust_lobe_center: lobe_separation;
    input advance: 1.0 * units.deg;
    input base_radius: (1.5/2) * units.inch; // guess

    output intake_cam_0: _intake_cam_0;
    output exhaust_cam_0: _exhaust_cam_0;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_0(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_0(params, lobe_profile: exhaust_lobe_profile)


    label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_0
        .add_lobe(rot360 + intake_lobe_center)
        .add_lobe(rot360 + intake_lobe_center + 4 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 2 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 5 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 1 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 3 * rot120)

    _exhaust_cam_0
        .add_lobe(rot360 - exhaust_lobe_center)
        .add_lobe(rot360 - exhaust_lobe_center + 4 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 5 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot120)
}

public node chrysler_slant_six_225_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 60 * units.cc; // nominally 54cc; https://www.slantsix.org/forum/viewtopic.php?t=3563
    input flip_display: false;

    alias output __out: head;

    // http://www.users.interport.net/s/r/srweiss/tablehdc.htm#Chrysler
    function intake_flow(100 * units.thou)
    intake_flow
        .add_flow_sample(  0,   0)
        .add_flow_sample(100,  49)
        .add_flow_sample(200,  98)
        .add_flow_sample(300, 131)
        .add_flow_sample(400, 152)
        .add_flow_sample(500, 163)

    function exhaust_flow(100 * units.thou)
    exhaust_flow
        .add_flow_sample(  0,   0)
        .add_flow_sample(100,  38)
        .add_flow_sample(200,  79)
        .add_flow_sample(300, 106)
        .add_flow_sample(400, 119)
        .add_flow_sample(500, 124)

        
    cylinder_head head(
        chamber_volume: chamber_volume,
        // borrowed dimensions from AMC/Jeep for now
        intake_runner_cross_section_area:  (1.625 * units.inch * 1.375 * units.inch),                    // rectangular
        intake_runner_volume:              (1.625 * units.inch * 1.375 * units.inch)                    * 3 * units.inch,
        exhaust_runner_cross_section_area: (1.375 * units.inch * 1.250 * units.inch * constants.pi / 4), // elliptical
        exhaust_runner_volume:             (1.375 * units.inch * 1.250 * units.inch * constants.pi / 4) * 3 * units.inch,

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}


public node chrysler_slant_six_225_engine {
    alias output __out: engine;

    engine engine(
        name: "Chrysler Slant Six RG 225",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 900 * units.rpm,
        redline: 4500 * units.rpm,
        throttle_gamma: 1.0,
        hf_gain: 0.1,
        jitter: 0.5
    )
    
    label lean_angle(30 * units.deg)

    label bore(3.400 * units.inch)
    label bore_spacing(3.988 * units.inch) // average of 3.98" for 1-2, 3-4, 5-6; and 4.00" for 2-3 and 4-5
    label stroke(4.125 * units.inch)
    label rod_length(6.700 * units.inch)
    label rod_mass(650 * units.g)
    label piston_mass(450 * units.g)
    label compression_height(1.8 * units.inch) // not sure if spec is correct, approximate guess based on known measurements
    label deck_height((10.68 * units.inch) + (40 * units.thou)) // not sure about gasket
    label piston_dome(0 * units.cc) // for ~8.5:1 CR
    label crank_mass(90 * units.lb)
    label flywheel_mass(20 * units.lb)
    label flywheel_radius(6 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 15 * units.kg, radius: 8.0 * units.cm)
    )

    wires wires()

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia: crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 90.0 * units.deg + lean_angle
    )

    rod_journal rj1(angle:  0  * units.deg)
    rod_journal rj2(angle: 120 * units.deg)
    rod_journal rj3(angle: 240 * units.deg)
    rod_journal rj4(angle: 240 * units.deg)
    rod_journal rj5(angle: 120 * units.deg)
    rod_journal rj6(angle:  0  * units.deg)
    
    c0
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)
        .add_rod_journal(rj5)
        .add_rod_journal(rj6)

    piston_parameters piston_params(
        mass: piston_mass,
        blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    intake intake(
        plenum_volume: 2.0 * units.L,
        plenum_cross_section_area: 100.0 * units.cm2,
        intake_flow_rate: k_carb(400.0),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.995,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(180.0),
        runner_length: 8.0 * units.inch,
        velocity_decay: 0.1
    )
    
    label exhaust_radius((3.0 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(400.0),
        primary_tube_length: 9.0 * units.inch,
        primary_flow_rate: k_carb(170.0),
        velocity_decay: 1.0, //0.5
        collector_cross_section_area: circle_area(exhaust_radius),
        length: 2700 * units.mm,
        volume: 2700 * units.mm * circle_area(exhaust_radius)
    )

    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.01, impulse_response: ir_lib.default_0)
    
    label pipe_length_1(2.7 * bore_spacing)
    label pipe_length_2(1.7 * bore_spacing)
    label pipe_length_3(0.7 * bore_spacing)
    label pipe_length_4(0.0 * bore_spacing)
    label pipe_length_5(1.0 * bore_spacing)
    label pipe_length_6(2.0 * bore_spacing)
 
    cylinder_bank b0(bank_params, angle: 0 * units.deg + lean_angle)
    b0
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire1,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_1,
            sound_attenuation: 1.0
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire2,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_2,
            sound_attenuation: 1.0
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire3,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_3,
            sound_attenuation: 1.0
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire4,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_4,
            sound_attenuation: 1.0
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj5,
            ignition_wire: wires.wire5,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_5,
            sound_attenuation: 1.0
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj6,
            ignition_wire: wires.wire6,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_6,
            sound_attenuation: 1.0
        )

    engine
        .add_cylinder_bank(b0)

    engine.add_crankshaft(c0)

    chrysler_slant_six_225_camshaft_builder camshaft(
        lobe_profile: chrysler_slant_six_225_lobe_profile_int(),
        ex_lobe_profile: chrysler_slant_six_225_lobe_profile_exh()
    )

    b0.set_cylinder_head (
        chrysler_slant_six_225_head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0,
            flip_display: false // exhaust is on the right
        )
    )

    // not correct, but produces approximately correct horsepower and torque
    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 12 * units.deg)
        .add_sample(1000 * units.rpm, 17 * units.deg)
        .add_sample(2000 * units.rpm, 19 * units.deg)
        .add_sample(3000 * units.rpm, 21 * units.deg)
        .add_sample(4000 * units.rpm, 22 * units.deg)
        .add_sample(5000 * units.rpm, 22 * units.deg)
        .add_sample(6000 * units.rpm, 22 * units.deg)

    engine.add_ignition_module(
        chrysler_slant_six_225_distributor(
            wires: wires,
            timing_curve: timing_curve
        ))
}

// 1970 Dodge Challenger 225 Six (E-Body)
public node dodge_challenger_225_six_vehicle {
    alias output __out:
        vehicle(
            mass: 3006 * units.lb,
            drag_coefficient: 0.48,
            cross_sectional_area: 2.07 * (units.m) * (units.m),
            diff_ratio: 3.23,
            tire_radius: 324 * units.mm, // E78-14 (~= 195/75R14)
            rolling_resistance: 3006 * units.lb * 9.81 * (195.0/275.0) * 0.01
        );
}

public node dodge_challenger_225_six_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 300 * units.lb_ft
        )
        .add_gear(3.08)
        .add_gear(1.76)
        .add_gear(1.00);
}

public node main {
    set_engine(chrysler_slant_six_225_engine())
    set_vehicle(dodge_challenger_225_six_vehicle())
    set_transmission(dodge_challenger_225_six_transmission())
}

main()