import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

public node bmw_s14b23_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 7250 * units.rpm;
	input limiter_duration: 0.1;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0/4.0) * cycle)
            .connect_wire(wires.wire3, (1.0/4.0) * cycle)
			.connect_wire(wires.wire4, (2.0/4.0) * cycle)
			.connect_wire(wires.wire2, (3.0/4.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
}

private node add_sym_sample {
    input angle;
    input lift;
    input this;
    alias output __out: this;

    this.add_sample(angle * units.deg, lift * units.thou)
    this.add_sample(-angle * units.deg, lift * units.thou)
}

public node bmw_s14b23_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 248.0 * units.deg,
            gamma: 1.0,
            lift: 10.18 * units.mm,
            steps: 100
        );
}

public node bmw_s14b23_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 248.0 * units.deg,
            gamma: 1.0,
            lift: 10.18 * units.mm,
            steps: 100
        );
}

public node bmw_s14b23_camshaft_builder {
    input lobe_profile: bmw_s14b23_lobe_profile_int();
	input ex_lobe_profile: bmw_s14b23_lobe_profile_exh();
    input intake_lobe_profile: lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input intake_lobe_center: 115.0 * units.deg;  // guess
    input exhaust_lobe_center: 115.0 * units.deg; // guess
    input advance: 0.0 * units.deg;
    input base_radius: 16.75 * units.mm;

    output intake_cam_0: _intake_cam_0;
    output exhaust_cam_0: _exhaust_cam_0;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_0(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_0(params, lobe_profile: exhaust_lobe_profile)


    label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_0
        .add_lobe(rot360 + intake_lobe_center + 0 * rot180)
		.add_lobe(rot360 + intake_lobe_center + 3 * rot180)
		.add_lobe(rot360 + intake_lobe_center + 1 * rot180)
		.add_lobe(rot360 + intake_lobe_center + 2 * rot180)

	_exhaust_cam_0
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot180)
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot180)
		.add_lobe(rot360 - exhaust_lobe_center + 1 * rot180)
		.add_lobe(rot360 - exhaust_lobe_center + 2 * rot180)
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.mm, k_28inH2O(flow))
}

public node bmw_s14b23_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 43.0 * units.cc;
    input flip_display: false;
	
	input flow_attenuation_in: 1.0;
	input flow_attenuation_ex: 1.0;
    input lift_scale: 1.0;
    alias output __out: head;

    function intake_flow(1.0 * units.mm)
    intake_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_in)
        .add_flow_sample( 1 * lift_scale,  30 * flow_attenuation_in)
        .add_flow_sample( 2 * lift_scale,  60 * flow_attenuation_in)
        .add_flow_sample( 3 * lift_scale,  90 * flow_attenuation_in)
        .add_flow_sample( 4 * lift_scale, 125 * flow_attenuation_in)
        .add_flow_sample( 5 * lift_scale, 160 * flow_attenuation_in)
        .add_flow_sample( 6 * lift_scale, 195 * flow_attenuation_in)
        .add_flow_sample( 7 * lift_scale, 210 * flow_attenuation_in)
        .add_flow_sample( 8 * lift_scale, 235 * flow_attenuation_in)
        .add_flow_sample( 9 * lift_scale, 270 * flow_attenuation_in)
        .add_flow_sample(10 * lift_scale, 305 * flow_attenuation_in)
        .add_flow_sample(11 * lift_scale, 320 * flow_attenuation_in)
        .add_flow_sample(12 * lift_scale, 335 * flow_attenuation_in)
        .add_flow_sample(13 * lift_scale, 335 * flow_attenuation_in)
        .add_flow_sample(14 * lift_scale, 335 * flow_attenuation_in)

    function exhaust_flow(1.0 * units.mm)
    exhaust_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_ex)
        .add_flow_sample( 1 * lift_scale,  30 * flow_attenuation_ex)
        .add_flow_sample( 2 * lift_scale,  55 * flow_attenuation_ex)
        .add_flow_sample( 3 * lift_scale,  85 * flow_attenuation_ex)
        .add_flow_sample( 4 * lift_scale, 115 * flow_attenuation_ex)
        .add_flow_sample( 5 * lift_scale, 140 * flow_attenuation_ex)
        .add_flow_sample( 6 * lift_scale, 160 * flow_attenuation_ex)
        .add_flow_sample( 7 * lift_scale, 180 * flow_attenuation_ex)
        .add_flow_sample( 8 * lift_scale, 205 * flow_attenuation_ex)
        .add_flow_sample( 9 * lift_scale, 220 * flow_attenuation_ex)
        .add_flow_sample(10 * lift_scale, 240 * flow_attenuation_ex)
        .add_flow_sample(11 * lift_scale, 260 * flow_attenuation_ex)
        .add_flow_sample(12 * lift_scale, 280 * flow_attenuation_ex)
        .add_flow_sample(13 * lift_scale, 280 * flow_attenuation_ex)
        .add_flow_sample(14 * lift_scale, 280 * flow_attenuation_ex)

		
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.1 * units.inch) * 3 * units.inch,
        intake_runner_cross_section_area: circle_area(1.1 * units.inch),
        exhaust_runner_volume: circle_area(0.6 * units.inch) * 6 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.6 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}

public node bmw_s14b23_itb {
    input throttlebody_cfm: 350;
    input idle_flow_rate_cfm: 0.001;
    input idle_throttle_plate_position: 0.999;
    input throttle_gamma: 4.0;

    alias output __out: intake;

    intake intake(
        plenum_volume: (circle_area(1.5 * units.inch) * 9 * units.inch) / 4,
        plenum_cross_section_area: circle_area(1.5 * units.inch),
        intake_flow_rate: k_carb(throttlebody_cfm),
        idle_flow_rate: k_carb(idle_flow_rate_cfm),
        idle_throttle_plate_position: idle_throttle_plate_position,
        throttle_gamma: throttle_gamma,
        runner_flow_rate: k_carb(300.0),
        runner_length: 4 * units.inch,
        velocity_decay: 0.1
    )
}

// one source:
// https://metricmechanic.com/wp-content/uploads/2016/07/M3-Engine-Booklet.pdf

public node S14B23 {
    alias output __out: engine;

    engine engine(
        name: "BMW S14B23",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 1200 * units.rpm,
        redline: 7000 * units.rpm,
		hf_gain: 0.08,
		jitter: 0.6,
        throttle_gamma: 1.0
    )
	
	label lean_angle(30 * units.deg)

    label bore(93.4 * units.mm)
	label bore_spacing(100.0 * units.mm)
    label stroke(84.0 * units.mm)
    label rod_length(144.0 * units.mm)
    label rod_mass(450 * units.g)
	label piston_mass(350 * units.g)
    label compression_height(32.0 * units.mm)
	label deck_height((217.3 + 0.2) * units.mm)
	label piston_dome(-23.1 * units.cc) // aftermarket, probably incorrect
    label crank_mass(4.0 * units.kg)
    label flywheel_mass(7.0 * units.kg)
    label flywheel_radius(5.0 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 15 * units.kg, radius: 8.0 * units.cm)
    )

    wires wires()

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 10.0 * units.lb_ft,
        moment_of_inertia: crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 90.0 * units.deg + lean_angle
    )

    rod_journal rj0(angle:  0  * units.deg)
    rod_journal rj1(angle: 180 * units.deg)
    rod_journal rj2(angle: 180 * units.deg)
    rod_journal rj3(angle:  0  * units.deg)
	
    c0
        .add_rod_journal(rj0)
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)

    piston_parameters piston_params(
        mass: piston_mass,
        //blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    bmw_s14b23_itb itb1()
	bmw_s14b23_itb itb2()
	bmw_s14b23_itb itb3()
	bmw_s14b23_itb itb4()
	
	label exhaust_radius((3.5 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(400.0),
        primary_tube_length: bore_spacing * 1.5,
        primary_flow_rate: k_carb(150.0),
        velocity_decay: 1.0, //0.5
		collector_cross_section_area: circle_area(exhaust_radius),
		length: 2000 * units.mm,
		volume: 2000 * units.mm * circle_area(exhaust_radius)
    )
	
    exhaust_system exhaust(
		es_params,
		audio_volume: 1.0 * 0.001,
		impulse_response: ir_lib.default_0
	)
 
    cylinder_bank b0(bank_params, angle: 0 * units.deg + lean_angle)
    b0
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            ignition_wire: wires.wire1,
            intake: itb1,
            exhaust_system: exhaust,
			primary_length: bore_spacing * 1.2,
			sound_attenuation: 0.64
        )
		.add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire2,
            intake: itb2,
            exhaust_system: exhaust,
			primary_length: bore_spacing * 0.4,
			sound_attenuation: 1.0
        )
		.add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire3,
            intake: itb3,
            exhaust_system: exhaust,
			primary_length: bore_spacing * 0.8,
			sound_attenuation: 0.8
        )
		.add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire4,
            intake: itb4,
            exhaust_system: exhaust,
			primary_length: bore_spacing * 0,
			sound_attenuation: 0.8
        )

    engine
        .add_cylinder_bank(b0)

    engine.add_crankshaft(c0)

    bmw_s14b23_camshaft_builder camshaft(
	    lobe_profile: bmw_s14b23_lobe_profile_int(),
		ex_lobe_profile: bmw_s14b23_lobe_profile_exh()
	)

    b0.set_cylinder_head (
        bmw_s14b23_head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0,
			flip_display: true
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 20 * units.deg)
        .add_sample(1000 * units.rpm, 55 * units.deg)
        .add_sample(2000 * units.rpm, 60 * units.deg)
        .add_sample(3000 * units.rpm, 60 * units.deg)
        .add_sample(4000 * units.rpm, 60 * units.deg)
        .add_sample(5000 * units.rpm, 60 * units.deg)
        .add_sample(6000 * units.rpm, 60 * units.deg)
        .add_sample(7000 * units.rpm, 60 * units.deg)
        .add_sample(8000 * units.rpm, 60 * units.deg)
        .add_sample(9000 * units.rpm, 60 * units.deg)

    engine.add_ignition_module(
        bmw_s14b23_distributor(
            wires: wires,
            timing_curve: timing_curve,
            rev_limit: 7250 * units.rpm
        ))
}

// 1987 BMW M3 Coupe (E30)
public node bmw_m3_e30_vehicle {
    alias output __out:
        vehicle(
            mass: 1240 * units.kg,
            drag_coefficient: 0.33,
            cross_sectional_area: 1.91 * (units.m * units.m),
            diff_ratio: 4.1,
            tire_radius: 313.5 * units.mm, // 205/55VR15
            rolling_resistance: 420 * units.N
        );
}

public node getrag_260_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 500 * units.lb_ft
        )
        .add_gear(3.83)
        .add_gear(2.20)
        .add_gear(1.40)
        .add_gear(1.00)
        .add_gear(0.81);
}

public node main {
	set_engine(S14B23())
    set_vehicle(bmw_m3_e30_vehicle())
    set_transmission(getrag_260_transmission())
}

main()