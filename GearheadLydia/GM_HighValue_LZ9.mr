import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

// firing order: 165432

public node gm_highvalue_lz9_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 6400 * units.rpm;
    input limiter_duration: 0.08;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0 / 6.0) * cycle)
            .connect_wire(wires.wire2, (1.0 / 6.0) * cycle)
            .connect_wire(wires.wire3, (2.0 / 6.0) * cycle)
            .connect_wire(wires.wire4, (3.0 / 6.0) * cycle)
            .connect_wire(wires.wire5, (4.0 / 6.0) * cycle)
            .connect_wire(wires.wire6, (5.0 / 6.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
}

// https://www.60degreev6.com/vb5/forum/pushrod-engines/pushrod-performance/27999-3900-rwd/page3
public node gm_highvalue_lz9_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 194 * units.deg,
            gamma: 1.0,
            lift: 272.0 * units.thou * 1.7,
            steps: 100
        );
}

public node gm_highvalue_lz9_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 195 * units.deg,
            gamma: 1.0,
            lift: 286.0 * units.thou * 1.7,
            steps: 100
        );
}

public node gm_highvalue_lz9_camshaft_builder {
    input in_lobe_profile: gm_highvalue_lz9_lobe_profile_int();
    input ex_lobe_profile: gm_highvalue_lz9_lobe_profile_exh();
    input intake_lobe_profile: in_lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input lobe_separation: 114.75 * units.deg;
    input intake_lobe_center: lobe_separation;
    input exhaust_lobe_center: lobe_separation;
    input advance: 5.75 * units.deg;
    input base_radius: 1.4 / 2 * units.inch; // guess

    output intake_cam_L: _intake_cam_L;
    output exhaust_cam_L: _exhaust_cam_L;
    output intake_cam_R: _intake_cam_R;
    output exhaust_cam_R: _exhaust_cam_R;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_L(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_L(params, lobe_profile: exhaust_lobe_profile)

    camshaft _intake_cam_R(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_R(params, lobe_profile: exhaust_lobe_profile)


    label rot30(30 * units.deg)
    label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)
    
    _intake_cam_L
        .add_lobe(rot360 + intake_lobe_center + 0 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 2 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 4 * rot120)

    _exhaust_cam_L
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 4 * rot120)

    _intake_cam_R
        .add_lobe(rot360 + intake_lobe_center + 1 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 3 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 5 * rot120)

    _exhaust_cam_R
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 5 * rot120)
}

public node gm_highvalue_lz9_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 52.0 * units.cc; // assumption: same as 60/X V6 2.8
    input flip_display: false;
    
    alias output __out: head;

    // https://www.fiero.nl/forum/Archives/Archive-000001/HTML/20110502-2-107582.html
    function intake_flow(50 * units.thou)
    intake_flow
        .add_flow_sample(  0,   0.0)
        .add_flow_sample( 50,  36.8)
        .add_flow_sample(100,  72.1)
        .add_flow_sample(150, 104.5)
        .add_flow_sample(200, 138.6)
        .add_flow_sample(250, 172.3)
        .add_flow_sample(300, 201.1)
        .add_flow_sample(350, 223.4)
        .add_flow_sample(400, 240.3)
        .add_flow_sample(450, 248.7)
        .add_flow_sample(500, 255.6)
        .add_flow_sample(550, 259.9)
        .add_flow_sample(600, 262.5)

    function exhaust_flow(50 * units.thou)
    exhaust_flow
        .add_flow_sample(  0,   0.0)
        .add_flow_sample( 50,  26.6)
        .add_flow_sample(100,  57.1)
        .add_flow_sample(150,  87.3)
        .add_flow_sample(200, 108.7)
        .add_flow_sample(250, 130.1)
        .add_flow_sample(300, 145.0)
        .add_flow_sample(350, 155.5)
        .add_flow_sample(400, 163.5)
        .add_flow_sample(450, 168.7)
        .add_flow_sample(500, 172.0)
        .add_flow_sample(550, 174.0)
        .add_flow_sample(600, 176.3)
        
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.0 * units.inch) * 4 * units.inch,
        intake_runner_cross_section_area: circle_area(1.0 * units.inch),
        exhaust_runner_volume: circle_area(0.7 * units.inch) * 4 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.7 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}

// https://www.fiero.nl/forum/Archives/Archive-000001/HTML/20090219-2-079360.html
// https://realfierotech.com/viewtopic.php?t=18142
// https://www.60degreev6.com/vb5/articles/-2006-present-pushrod-vvt-aa/424703-3900-engine-specifications
public node gm_highvalue_lz9_v6_engine {
    alias output __out: engine;

    wires wires()

    engine engine(
        name: "GM High Value LZ9",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 1000 * units.rpm,
        redline: 6000 * units.rpm,
        throttle_gamma: 1.0
    )

    label bore(99.0 * units.mm)
    label bore_spacing(4.4 * units.inch)
    label stroke(84.0 * units.mm)
    label rod_length(148.2 * units.mm) // not sure if 150.0 or 148.2
    label rod_mass(550 * units.g)
    label compression_height(33.0 * units.mm)
    label piston_mass(500 * units.g)
    label piston_dome(-11.4 * units.cc) // for 9.8:1 CR
    label deck_height((224.0 + 0.5) * units.mm) // not sure about gasket
    label crank_mass(75 * units.lb)
    label flywheel_mass(30 * units.lb)
    label flywheel_radius(6 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 20 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia:
            crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 120.0 * units.deg // 30 degrees CCW from vertical
    )

    rod_journal rj1(angle:   0 * units.deg)
    rod_journal rj2(angle:  60 * units.deg)
    rod_journal rj3(angle: 240 * units.deg)
    rod_journal rj4(angle: 300 * units.deg)
    rod_journal rj5(angle: 120 * units.deg)
    rod_journal rj6(angle: 180 * units.deg)

    c0
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)
        .add_rod_journal(rj5)
        .add_rod_journal(rj6)


    piston_parameters piston_params(
        mass: piston_mass,
        blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0 * units.mm,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )
    
    // bore center lines meet 3.0mm below crankshaft center line
    // this only changes deck height by about 0.005mm
    label bore_offset_x(0.75 * units.mm)      // 1.5 * sin(30); also 3.0 * sin(30) * sin(30)
    label bore_offset_y(1.2993801 * units.mm) // 1.5 * cos(30); also 3.0 * sin(30) * cos(30)

    intake intake(
        plenum_volume: 1.5 * units.L,
        plenum_cross_section_area: 100 * units.cm2,
        intake_flow_rate: k_carb(700.0),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.9965,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(400.0),
        runner_length: 7.0 * units.inch,
        velocity_decay: 0.1
    )

    label exhaust_radius((3.0 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(600.0),
        primary_tube_length: 7.0 * units.inch,
        primary_flow_rate: k_carb(250.0),
        velocity_decay: 1.0, //0.5
        collector_cross_section_area: circle_area(exhaust_radius) * 2,
        length: 2600 * units.mm,
        volume: (2000 * units.mm * circle_area(exhaust_radius)) + (600 * units.mm * circle_area(exhaust_radius) * 2)
    )

    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.1, impulse_response: ir_lib.default_0)
    
    label pipe_length_1(bore_spacing * 1.0)
    label pipe_length_2(bore_spacing * 0.0)
    label pipe_length_3(bore_spacing * 0.3)
    
    label exhaust_stagger(8 * units.inch)

    cylinder_bank bL(bank_params, angle: 30 * units.deg, position_x: -bore_offset_x, position_y: -bore_offset_y) // L (first, rear)
    bL
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire1,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_1
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire3,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_2
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj5,
            ignition_wire: wires.wire5,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_3
        )

    cylinder_bank bR(bank_params, angle: -30.0 * units.deg, position_x: bore_offset_x, position_y: -bore_offset_y) // R (second, front)
    bR
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire2,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_1 + exhaust_stagger
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire4,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_2 + exhaust_stagger
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj6,
            ignition_wire: wires.wire6,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_3 + exhaust_stagger
        )

    engine
        .add_cylinder_bank(bL)
        .add_cylinder_bank(bR)

    engine.add_crankshaft(c0)

    gm_highvalue_lz9_camshaft_builder camshaft(
        in_lobe_profile: gm_highvalue_lz9_lobe_profile_int(),
        ex_lobe_profile: gm_highvalue_lz9_lobe_profile_exh()
    )

    bL.set_cylinder_head (
        gm_highvalue_lz9_head(
            intake_camshaft: camshaft.intake_cam_L,
            exhaust_camshaft: camshaft.exhaust_cam_L,
            flip_display: true
        )
    )
    bR.set_cylinder_head (
        gm_highvalue_lz9_head(
            intake_camshaft: camshaft.intake_cam_R,
            exhaust_camshaft: camshaft.exhaust_cam_R,
            flip_display: false
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 23 * units.deg)
        .add_sample(1000 * units.rpm, 33 * units.deg)
        .add_sample(2000 * units.rpm, 33 * units.deg)
        .add_sample(3000 * units.rpm, 33 * units.deg)
        .add_sample(4000 * units.rpm, 33 * units.deg)
        .add_sample(5000 * units.rpm, 33 * units.deg)
        .add_sample(6000 * units.rpm, 33 * units.deg)
        .add_sample(7000 * units.rpm, 33 * units.deg)

    engine.add_ignition_module(
        gm_highvalue_lz9_distributor(
            wires: wires,
            timing_curve: timing_curve
        )
    )
}

// 2006 Pontiac G6 GTP Coupe
public node pontiac_g6_gtp_coupe_vehicle {
    alias output __out:
        vehicle(
            mass: 3468 * units.lb,
            drag_coefficient: 0.3,
            cross_sectional_area: 2.15 * (units.m) * (units.m),
            diff_ratio: 3.55,
            tire_radius: 341.5 * units.mm, // P225/50R18
            rolling_resistance: 3468 * units.lb * 9.81 * (225.0/275.0) * 0.01
        );
}

public node gm_f40_mt2_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 500 * units.lb_ft
        )
        .add_gear(3.77)
        .add_gear(2.04)
        .add_gear(1.32)
        .add_gear(0.955)
        .add_gear(0.755)
        .add_gear(0.623);
}

public node main {
    set_engine(gm_highvalue_lz9_v6_engine())
    set_vehicle(pontiac_g6_gtp_coupe_vehicle())
    set_transmission(gm_f40_mt2_transmission())
}

main()