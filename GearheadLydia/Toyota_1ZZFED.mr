import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

public node toyota_1zzfed_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 7050 * units.rpm;
	input limiter_duration: 0.06;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0/4.0) * cycle)
            .connect_wire(wires.wire3, (1.0/4.0) * cycle)
			.connect_wire(wires.wire4, (2.0/4.0) * cycle)
			.connect_wire(wires.wire2, (3.0/4.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
}

private node add_sym_sample {
    input angle;
    input lift;
    input this;
    alias output __out: this;

    this.add_sample(angle * units.deg, lift * units.thou)
    this.add_sample(-angle * units.deg, lift * units.thou)
}

// cam specs: https://www.spyderchat.com/threads/stock-valve-timing-data.24965/
public node toyota_1zzfed_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 240 * units.deg,
            gamma: 1.0,
            lift: 9.3 * units.mm,
            steps: 100
        );
}

public node toyota_1zzfed_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 224 * units.deg,
            gamma: 1.0,
            lift: 8.4 * units.mm,
            steps: 100
        );
}

public node toyota_1zzfed_camshaft_builder {
    input lobe_profile: toyota_1zzfed_lobe_profile_int();
	input ex_lobe_profile: toyota_1zzfed_lobe_profile_exh();
    input intake_lobe_profile: lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input lobe_separation: 110.0 * units.deg;
    input intake_lobe_center: 111.73 * units.deg;
    input exhaust_lobe_center: 109.56 * units.deg;
    input advance: -1.085 * units.deg;
    input base_radius: 16.0 * units.mm;

    output intake_cam_0: _intake_cam_0;
    output exhaust_cam_0: _exhaust_cam_0;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_0(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_0(params, lobe_profile: exhaust_lobe_profile)


    label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_0
        .add_lobe(rot360 + intake_lobe_center + 0 * rot180)
		.add_lobe(rot360 + intake_lobe_center + 3 * rot180)
		.add_lobe(rot360 + intake_lobe_center + 1 * rot180)
		.add_lobe(rot360 + intake_lobe_center + 2 * rot180)

	_exhaust_cam_0
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot180)
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot180)
		.add_lobe(rot360 - exhaust_lobe_center + 1 * rot180)
		.add_lobe(rot360 - exhaust_lobe_center + 2 * rot180)
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.mm, k_28inH2O(flow))
}

public node toyota_1zzfed_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 35.0 * units.cc;
    input flip_display: false;
	
	input flow_attenuation_in: 1.0;
	input flow_attenuation_ex: 1.0;
    input lift_scale: 1.0;
    alias output __out: head;

    function intake_flow(1.0 * units.mm)
    intake_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_in)
        .add_flow_sample( 1 * lift_scale,  30 * flow_attenuation_in)
        .add_flow_sample( 2 * lift_scale,  60 * flow_attenuation_in)
        .add_flow_sample( 3 * lift_scale,  90 * flow_attenuation_in)
        .add_flow_sample( 4 * lift_scale, 125 * flow_attenuation_in)
        .add_flow_sample( 5 * lift_scale, 160 * flow_attenuation_in)
        .add_flow_sample( 6 * lift_scale, 195 * flow_attenuation_in)
        .add_flow_sample( 7 * lift_scale, 210 * flow_attenuation_in)
        .add_flow_sample( 8 * lift_scale, 235 * flow_attenuation_in)
        .add_flow_sample( 9 * lift_scale, 270 * flow_attenuation_in)
        .add_flow_sample(10 * lift_scale, 305 * flow_attenuation_in)
        .add_flow_sample(11 * lift_scale, 320 * flow_attenuation_in)
        .add_flow_sample(12 * lift_scale, 335 * flow_attenuation_in)
        .add_flow_sample(13 * lift_scale, 335 * flow_attenuation_in)
        .add_flow_sample(14 * lift_scale, 335 * flow_attenuation_in)

    function exhaust_flow(1.0 * units.mm)
    exhaust_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_ex)
        .add_flow_sample( 1 * lift_scale,  30 * flow_attenuation_ex)
        .add_flow_sample( 2 * lift_scale,  55 * flow_attenuation_ex)
        .add_flow_sample( 3 * lift_scale,  85 * flow_attenuation_ex)
        .add_flow_sample( 4 * lift_scale, 115 * flow_attenuation_ex)
        .add_flow_sample( 5 * lift_scale, 140 * flow_attenuation_ex)
        .add_flow_sample( 6 * lift_scale, 160 * flow_attenuation_ex)
        .add_flow_sample( 7 * lift_scale, 180 * flow_attenuation_ex)
        .add_flow_sample( 8 * lift_scale, 205 * flow_attenuation_ex)
        .add_flow_sample( 9 * lift_scale, 220 * flow_attenuation_ex)
        .add_flow_sample(10 * lift_scale, 240 * flow_attenuation_ex)
        .add_flow_sample(11 * lift_scale, 260 * flow_attenuation_ex)
        .add_flow_sample(12 * lift_scale, 280 * flow_attenuation_ex)
        .add_flow_sample(13 * lift_scale, 280 * flow_attenuation_ex)
        .add_flow_sample(14 * lift_scale, 280 * flow_attenuation_ex)

		
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.18 * units.inch) * 6 * units.inch,
        intake_runner_cross_section_area: circle_area(1.18 * units.inch),
        exhaust_runner_volume: circle_area(0.6 * units.inch) * 8 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.6 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}

public node toyota_1zzfed_engine {
    alias output __out: engine;

    engine engine(
        name: "Toyota 1ZZ-FED",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 1200 * units.rpm,
        redline: 6800 * units.rpm,
		hf_gain: 0.05,
		jitter: 0.55,
        throttle_gamma: 2.0
    )
	
	label lean_angle(10 * units.deg)

    label bore(79.0 * units.mm)
    label bore_spacing(87.5 * units.mm)
    label stroke(91.5 * units.mm)
    label rod_length(146.6 * units.mm)
    label rod_mass(350 * units.g)
	label piston_mass(347 * units.g)
    label compression_height(31.9 * units.mm)
	label deck_height((224.5 + 0.7) * units.mm) // deck height is an educated guess from a Carrillo aftermarket piston spec
	label piston_dome(-10.0 * units.cc) // works out to 10.0:1 compression ratio
    label crank_mass(6.0 * units.kg)
    label flywheel_mass(12.0 * units.kg)
    label flywheel_radius(6.0 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 15 * units.kg, radius: 8.0 * units.cm)
    )

    wires wires()

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 10.0 * units.lb_ft,
        moment_of_inertia: crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 90.0 * units.deg + lean_angle
    )

    rod_journal rj0(angle:  0  * units.deg)
    rod_journal rj1(angle: 180 * units.deg)
    rod_journal rj2(angle: 180 * units.deg)
    rod_journal rj3(angle:  0  * units.deg)
	
    c0
        .add_rod_journal(rj0)
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)

    piston_parameters piston_params(
        mass: piston_mass,
        //blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    intake intake(
        plenum_volume: 1.0 * units.L,
        plenum_cross_section_area: 50.0 * units.cm2,
        intake_flow_rate: k_carb(300.0),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.996,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(250.0),
        runner_length: 5.0 * units.inch,
        velocity_decay: 0.1
    )
	
	label exhaust_radius_celica((3.5 * units.inch)/2)
	label exhaust_radius_mr2((3.5 * units.inch)/2)

    exhaust_system_parameters es_params_celica(
        outlet_flow_rate: k_carb(300.0),
        primary_tube_length: 12.5 * units.inch,
        primary_flow_rate: k_carb(220.0),
        velocity_decay: 1.0, //0.5
		collector_cross_section_area: circle_area(exhaust_radius_celica),
		length: 2500 * units.mm,
		volume: 2500 * units.mm * circle_area(exhaust_radius_celica)
    )

    exhaust_system_parameters es_params_mr2(
        outlet_flow_rate: k_carb(300.0),
        primary_tube_length: 12.5 * units.inch,
        primary_flow_rate: k_carb(220.0),
        velocity_decay: 1.0, //0.5
		collector_cross_section_area: circle_area(exhaust_radius_mr2),
		length: 500 * units.mm,
		volume: 500 * units.mm * circle_area(exhaust_radius_mr2)
    )
	
    // exhaust_system exhaust(es_params_celica, audio_volume: 1.0 * 0.01, impulse_response: ir_lib.default_0)
	exhaust_system exhaust(es_params_mr2, audio_volume: 1.0 * 0.002, impulse_response: ir_lib.default_0)
 
    cylinder_bank b0(bank_params, angle: 0 * units.deg + lean_angle)
    b0
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            ignition_wire: wires.wire1,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: 2.5 * units.inch,
			sound_attenuation: 1.0
        )
		.add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire2,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: 1.7 * units.inch,
			sound_attenuation: 1.0
        )
		.add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire3,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: 0.0 * units.inch,
			sound_attenuation: 1.0
        )
		.add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire4,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: 1.0 * units.inch,
			sound_attenuation: 1.0
        )

    engine
        .add_cylinder_bank(b0)

    engine.add_crankshaft(c0)

    toyota_1zzfed_camshaft_builder camshaft(
	    lobe_profile: toyota_1zzfed_lobe_profile_int(),
		ex_lobe_profile: toyota_1zzfed_lobe_profile_exh()
	)

    b0.set_cylinder_head (
        toyota_1zzfed_head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0,
			flip_display: true
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 30 * units.deg)
        .add_sample(1000 * units.rpm, 56 * units.deg)
        .add_sample(2000 * units.rpm, 59 * units.deg)
        .add_sample(3000 * units.rpm, 60 * units.deg)
        .add_sample(4000 * units.rpm, 59 * units.deg)
        .add_sample(5000 * units.rpm, 57 * units.deg)
        .add_sample(6000 * units.rpm, 54 * units.deg)
        .add_sample(7000 * units.rpm, 50 * units.deg)
        .add_sample(8000 * units.rpm, 48 * units.deg)

    engine.add_ignition_module(
        toyota_1zzfed_distributor(
            wires: wires,
            timing_curve: timing_curve
        ))
}

// 2004 Toyota MR2 Spyder (ZZW30)
public node toyota_mr2_spyder_zzw30_vehicle {
    alias output __out:
        vehicle(
            mass: 2195 * units.lb,
            drag_coefficient: 0.353,
            cross_sectional_area: 1.9 * (units.m * units.m), // my estimate; <= 2.1 in any case
            diff_ratio: 4.529,
            tire_radius: 299.5 * units.mm, // 215/45R16
            rolling_resistance: 430 * units.N
        );
}

// 2004 Toyota Celica GT (ZZT230)
public node toyota_celica_zzt230_vehicle {
    alias output __out:
        vehicle(
            mass: 2425 * units.lb,
            drag_coefficient: 0.32,
            cross_sectional_area: 1.92 * (units.m * units.m), // my estimate
            diff_ratio: 4.529,
            tire_radius: 307.5 * units.mm, // 195/60R15
            rolling_resistance: 400 * units.N
        );
}

// Celica GT and MR2 Spyder (US & pre-2003 JP) - final drive: 4.529
public node toyota_c56_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 500 * units.lb_ft
        )
        .add_gear(3.166)
        .add_gear(1.904)
        .add_gear(1.392)
        .add_gear(1.031)
        .add_gear(0.815);
}

// MR2 Spyder (US SMT & JP manual post-2003) - final drive: 4.312
public node toyota_c63_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 500 * units.lb_ft
        )
        .add_gear(3.166)
        .add_gear(1.904)
        .add_gear(1.392)
        .add_gear(1.031)
        .add_gear(0.815)
        .add_gear(0.725);
}

public node main {
	set_engine(toyota_1zzfed_engine())
    set_vehicle(toyota_mr2_spyder_zzw30_vehicle())
    set_transmission(toyota_c56_transmission())
}

main()