import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

// firing order: 18436572

public node chrysler_hemi_57_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 6200 * units.rpm;
    input limiter_duration: 0.09;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0 / 8.0) * cycle)
            .connect_wire(wires.wire8, (1.0 / 8.0) * cycle)
            .connect_wire(wires.wire4, (2.0 / 8.0) * cycle)
            .connect_wire(wires.wire3, (3.0 / 8.0) * cycle)
            .connect_wire(wires.wire6, (4.0 / 8.0) * cycle)
            .connect_wire(wires.wire5, (5.0 / 8.0) * cycle)
            .connect_wire(wires.wire7, (6.0 / 8.0) * cycle)
            .connect_wire(wires.wire2, (7.0 / 8.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
    output wire7: ignition_wire();
    output wire8: ignition_wire();
}


// https://www.lxforums.com/threads/factory-5-7l-cam-specs.56378/
// https://www.dodgetalk.com/threads/hemi-cam-specs-stock-vs-6-1.77378/

public node chrysler_hemi_57_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 228 * units.deg, // adv. 260
            gamma: 1.0,
            lift: 472 * units.thou,
            steps: 100
        );
}

public node chrysler_hemi_57_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 234 * units.deg, // adv. 268
            gamma: 1.0,
            lift: 460 * units.thou,
            steps: 100
        );
}

public node chrysler_hemi_57_camshaft_builder {
    input in_lobe_profile: chrysler_hemi_57_lobe_profile_int();
    input ex_lobe_profile: chrysler_hemi_57_lobe_profile_exh();
    input intake_lobe_profile: in_lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input lobe_separation: 113.0 * units.deg;
    input intake_lobe_center: lobe_separation;
    input exhaust_lobe_center: lobe_separation;
    input advance: 10 * units.deg;
    input base_radius: 1.50 / 2 * units.inch;

    output intake_cam_R: _intake_cam_R;
    output exhaust_cam_R: _exhaust_cam_R;
    output intake_cam_L: _intake_cam_L;
    output exhaust_cam_L: _exhaust_cam_L;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_R(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_R(params, lobe_profile: exhaust_lobe_profile)

    camshaft _intake_cam_L(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_L(params, lobe_profile: exhaust_lobe_profile)


    label rot30(30 * units.deg)
    label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_R
        .add_lobe(rot360 + intake_lobe_center + 0 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 3 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 5 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 6 * rot90)

    _intake_cam_L
        .add_lobe(rot360 + intake_lobe_center + 7 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 2 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 4 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 1 * rot90)

    _exhaust_cam_R
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 5 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 6 * rot90)

    _exhaust_cam_L
        .add_lobe(rot360 - exhaust_lobe_center + 7 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 4 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot90)
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.thou, k_28inH2O(flow))
}

public node chrysler_hemi_57_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 84.5 * units.cc;
    input flip_display: false;
    
    input flow_attenuation_in: 0.8;
    input flow_attenuation_ex: 0.8;
    input lift_scale: 50.0;
    alias output __out: head;

    function intake_flow(50 * units.thou)
    intake_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_in)
        .add_flow_sample( 1 * lift_scale,  25 * flow_attenuation_in)
        .add_flow_sample( 2 * lift_scale,  76 * flow_attenuation_in)
        .add_flow_sample( 3 * lift_scale, 100 * flow_attenuation_in)
        .add_flow_sample( 4 * lift_scale, 146 * flow_attenuation_in)
        .add_flow_sample( 5 * lift_scale, 175 * flow_attenuation_in)
        .add_flow_sample( 6 * lift_scale, 212 * flow_attenuation_in)
        .add_flow_sample( 7 * lift_scale, 230 * flow_attenuation_in)
        .add_flow_sample( 8 * lift_scale, 255 * flow_attenuation_in)
        .add_flow_sample( 9 * lift_scale, 275 * flow_attenuation_in)
        .add_flow_sample(10 * lift_scale, 294 * flow_attenuation_in)
        .add_flow_sample(11 * lift_scale, 300 * flow_attenuation_in)
        .add_flow_sample(12 * lift_scale, 314 * flow_attenuation_in)

    function exhaust_flow(50 * units.thou)
    exhaust_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_ex)
        .add_flow_sample( 1 * lift_scale,  25 * flow_attenuation_ex)
        .add_flow_sample( 2 * lift_scale,  70 * flow_attenuation_ex)
        .add_flow_sample( 3 * lift_scale, 100 * flow_attenuation_ex)
        .add_flow_sample( 4 * lift_scale, 132 * flow_attenuation_ex)
        .add_flow_sample( 5 * lift_scale, 140 * flow_attenuation_ex)
        .add_flow_sample( 6 * lift_scale, 156 * flow_attenuation_ex)
        .add_flow_sample( 7 * lift_scale, 170 * flow_attenuation_ex)
        .add_flow_sample( 8 * lift_scale, 181 * flow_attenuation_ex)
        .add_flow_sample( 9 * lift_scale, 191 * flow_attenuation_ex)
        .add_flow_sample(10 * lift_scale, 207 * flow_attenuation_ex)
        .add_flow_sample(11 * lift_scale, 214 * flow_attenuation_ex)
        .add_flow_sample(12 * lift_scale, 228 * flow_attenuation_ex)
        .add_flow_sample(13 * lift_scale, 228 * flow_attenuation_ex)
        .add_flow_sample(14 * lift_scale, 228 * flow_attenuation_ex)
        
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.0 * units.inch) * 4 * units.inch,
        intake_runner_cross_section_area: circle_area(1.0 * units.inch),
        exhaust_runner_volume: circle_area(0.7 * units.inch) * 4 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.7 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}


public node chrysler_hemi_57 {
    alias output __out: engine;

    wires wires()

    engine engine(
        name: "Chrysler Hemi",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 1200 * units.rpm,
        redline: 6000 * units.rpm,
        throttle_gamma: 1.0,
        jitter: 0.5,
        hf_gain: 0.03
    )


    // source: https://www.onallcylinders.com/2021/02/01/mopar-chrysler-gen-iii-5-7l-hemi-engine-guide-bore-stroke-cylinder-heads-cam-specs-more/
    label bore(3.92 * units.inch)
    label bore_spacing(4.460 * units.inch)
    label stroke(3.58 * units.inch)
    label rod_length(6.242 * units.inch)
    label rod_mass(464 * units.g)
    label compression_height(1.21 * units.inch)
    label piston_mass(444 * units.g)
    label piston_dome(11.80 * units.cc)
    label deck_height((9.25 * units.inch) + (40 * units.thou))
    label crank_mass(57 * units.lb)
    label flywheel_mass(15 * units.lb)
    label flywheel_radius(6 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 20 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia:
            crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 45.0 * units.deg // 45 degrees back from right side
    )

    rod_journal rj1(angle:   0 * units.deg)
    rod_journal rj2(angle: 270 * units.deg)
    rod_journal rj3(angle:  90 * units.deg)
    rod_journal rj4(angle: 180 * units.deg)

    c0
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)


    piston_parameters piston_params(
        mass: piston_mass,
        //blowby: 0,
        compression_height: compression_height,
        wrist_pin_position: 0 * units.mm,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    intake intake(
        plenum_volume: 1.0 * units.L,
        plenum_cross_section_area: 50.0 * units.cm2,
        intake_flow_rate: k_carb(650),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.996,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(350.0),
        runner_length: 8.0 * units.inch,
        velocity_decay: 0.1
    )
    
    label exhaust_radius((3.5 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(600.0),
        primary_tube_length: 16.0 * units.inch,
        primary_flow_rate: k_carb(350.0),
        velocity_decay: 1.0, //0.5
        collector_cross_section_area: circle_area(exhaust_radius) * 2,
        length: 2800 * units.mm,
        volume: 2800 * units.mm * circle_area(exhaust_radius) * 2
    )

    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.05, impulse_response: ir_lib.default_0)
    
    label pipe_length_1(3.0 * bore_spacing)
    label pipe_length_2(2.0 * bore_spacing)
    label pipe_length_3(1.0 * bore_spacing)
    label pipe_length_4(0.0 * bore_spacing)
    
    label exhaust_stagger(2.0 * units.inch)

    cylinder_bank bR(bank_params, angle: -45.0 * units.deg) // R (first)
    bR
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire1,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_1 + exhaust_stagger,
            sound_attenuation: 0.6
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire3,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_2 + exhaust_stagger,
            sound_attenuation: 0.6
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire5,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_3 + exhaust_stagger,
            sound_attenuation: 0.6
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire7,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_4 + exhaust_stagger,
            sound_attenuation: 0.6
        )

    cylinder_bank bL(bank_params, angle: 45.0 * units.deg) // L (second)
    bL
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire2,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_1
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire4,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_2
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire6,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_3
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire8,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_4
        )

    engine
        .add_cylinder_bank(bR)
        .add_cylinder_bank(bL)

    engine.add_crankshaft(c0)

    chrysler_hemi_57_camshaft_builder camshaft(
        in_lobe_profile: chrysler_hemi_57_lobe_profile_int(),
        ex_lobe_profile: chrysler_hemi_57_lobe_profile_exh()
    )

    bR.set_cylinder_head (
        chrysler_hemi_57_head(
            intake_camshaft: camshaft.intake_cam_R,
            exhaust_camshaft: camshaft.exhaust_cam_R,
            flip_display: false
        )
    )
    bL.set_cylinder_head (
        chrysler_hemi_57_head(
            intake_camshaft: camshaft.intake_cam_L,
            exhaust_camshaft: camshaft.exhaust_cam_L,
            flip_display: true
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 35 * units.deg)
        .add_sample(1000 * units.rpm, 40 * units.deg)
        .add_sample(2000 * units.rpm, 40 * units.deg)
        .add_sample(3000 * units.rpm, 40 * units.deg)
        .add_sample(4000 * units.rpm, 40 * units.deg)
        .add_sample(5000 * units.rpm, 40 * units.deg)
        .add_sample(6000 * units.rpm, 40 * units.deg)
        .add_sample(7000 * units.rpm, 40 * units.deg)

    engine.add_ignition_module(
        chrysler_hemi_57_distributor(
            wires: wires,
            timing_curve: timing_curve
        )
    )
}

// 2006 Dodge Charger R/T (LX)
public node dodge_charger_rt_lx_vehicle {
    alias output __out:
        vehicle(
            mass: 4101 * units.lb,
            drag_coefficient: 0.345,
            cross_sectional_area: 2.32 * (units.m) * (units.m),
            diff_ratio: 2.82,
            tire_radius: 363 * units.mm, // 225/60R18H
            rolling_resistance: 4101 * units.lb * 9.81 * (225.0/275.0) * 0.01
        );
}

public node mercedes_5g_tronic_w5a580_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 700 * units.lb_ft
        )
        .add_gear(3.595)
        .add_gear(2.186)
        .add_gear(1.405)
        .add_gear(1.000)
        .add_gear(0.831);
}

public node main {
    set_engine(chrysler_hemi_57())
    set_vehicle(dodge_charger_rt_lx_vehicle())
    set_transmission(mercedes_5g_tronic_w5a580_transmission())
}

main()