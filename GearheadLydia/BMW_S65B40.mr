import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

// firing order: 15487263
// in GM layout:  18736524

public node bmw_s65b40_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 8400 * units.rpm;
    input limiter_duration: 0.09;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0 / 8.0) * cycle)
            .connect_wire(wires.wire5, (1.0 / 8.0) * cycle)
            .connect_wire(wires.wire4, (2.0 / 8.0) * cycle)
            .connect_wire(wires.wire8, (3.0 / 8.0) * cycle)
            .connect_wire(wires.wire7, (4.0 / 8.0) * cycle)
            .connect_wire(wires.wire2, (5.0 / 8.0) * cycle)
            .connect_wire(wires.wire6, (6.0 / 8.0) * cycle)
            .connect_wire(wires.wire3, (7.0 / 8.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
    output wire7: ignition_wire();
    output wire8: ignition_wire();
}

public node bmw_s65b40_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 251.0 * units.deg, //adv. 281
            gamma: 1.0,
            lift: 12.0 * units.mm,
            steps: 100
        );
}

public node bmw_s65b40_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 245.5 * units.deg, //adv. 275.5
            gamma: 1.0,
            lift: 12.0 * units.mm,
            steps: 100
        );
}


// https://ia801005.us.archive.org/11/items/BMWTechnicalTrainingDocuments/ST709%204th%20Generation%20M3%20Complete%20Vehicle/03_S65B40%2520Engine.pdf
// BMW timing data:
// intake cam:  72-130 degrees ATDC
// exhaust cam: 81-129 degrees BTDC
// not sure where other data came from (Schrick?)
public node bmw_s65b40_camshaft_builder {
    input in_lobe_profile: bmw_s65b40_lobe_profile_int();
    input ex_lobe_profile: bmw_s65b40_lobe_profile_exh();
    input intake_lobe_profile: in_lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    //input intake_lobe_center: lobe_separation;
    //input exhaust_lobe_center: lobe_separation;
    input intake_lobe_center: 101 * units.deg;
    input exhaust_lobe_center: 105 * units.deg;
    //input advance: -8.375 * units.deg;
    input advance: 0 * units.deg;
    input base_radius: 15.0 * units.mm;

    output intake_cam_L: _intake_cam_L;
    output intake_cam_R: _intake_cam_R;
    output exhaust_cam_L: _exhaust_cam_L;
    output exhaust_cam_R: _exhaust_cam_R;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_L(params, lobe_profile: intake_lobe_profile)
    camshaft _intake_cam_R(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_L(params, lobe_profile: exhaust_lobe_profile)
    camshaft _exhaust_cam_R(params, lobe_profile: exhaust_lobe_profile)

    label rot90(90 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_L
        .add_lobe(rot360 + intake_lobe_center + 0 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 5 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 7 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 2 * rot90)

    _exhaust_cam_L
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 5 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 7 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot90)

    _intake_cam_R
        .add_lobe(rot360 + intake_lobe_center + 1 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 6 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 4 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 3 * rot90)

    _exhaust_cam_R
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 6 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 4 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot90)
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.mm, k_28inH2O(flow))
}

public node bmw_s65b40_head {
    input chamber_volume: 51.0 * units.cc;
    input intake_camshaft;
    input exhaust_camshaft;
    input flip_display: false;
    
    input flow_attenuation_in: 0.9;
    input flow_attenuation_ex: 0.9;
    input lift_scale: 1.27;
    alias output __out: head;

    function intake_flow(1.27 * units.mm)
    intake_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_in)
        .add_flow_sample( 1 * lift_scale,  30 * flow_attenuation_in)
        .add_flow_sample( 2 * lift_scale,  60 * flow_attenuation_in)
        .add_flow_sample( 3 * lift_scale,  90 * flow_attenuation_in)
        .add_flow_sample( 4 * lift_scale, 125 * flow_attenuation_in)
        .add_flow_sample( 5 * lift_scale, 160 * flow_attenuation_in)
        .add_flow_sample( 6 * lift_scale, 195 * flow_attenuation_in)
        .add_flow_sample( 7 * lift_scale, 210 * flow_attenuation_in)
        .add_flow_sample( 8 * lift_scale, 235 * flow_attenuation_in)
        .add_flow_sample( 9 * lift_scale, 270 * flow_attenuation_in)
        .add_flow_sample(10 * lift_scale, 305 * flow_attenuation_in)
        .add_flow_sample(11 * lift_scale, 320 * flow_attenuation_in)
        .add_flow_sample(12 * lift_scale, 335 * flow_attenuation_in)
        .add_flow_sample(13 * lift_scale, 335 * flow_attenuation_in)
        .add_flow_sample(14 * lift_scale, 335 * flow_attenuation_in)

    function exhaust_flow(1.27 * units.mm)
    exhaust_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_ex)
        .add_flow_sample( 1 * lift_scale,  30 * flow_attenuation_ex)
        .add_flow_sample( 2 * lift_scale,  55 * flow_attenuation_ex)
        .add_flow_sample( 3 * lift_scale,  85 * flow_attenuation_ex)
        .add_flow_sample( 4 * lift_scale, 115 * flow_attenuation_ex)
        .add_flow_sample( 5 * lift_scale, 140 * flow_attenuation_ex)
        .add_flow_sample( 6 * lift_scale, 160 * flow_attenuation_ex)
        .add_flow_sample( 7 * lift_scale, 180 * flow_attenuation_ex)
        .add_flow_sample( 8 * lift_scale, 205 * flow_attenuation_ex)
        .add_flow_sample( 9 * lift_scale, 220 * flow_attenuation_ex)
        .add_flow_sample(10 * lift_scale, 240 * flow_attenuation_ex)
        .add_flow_sample(11 * lift_scale, 260 * flow_attenuation_ex)
        .add_flow_sample(12 * lift_scale, 280 * flow_attenuation_ex)
        .add_flow_sample(13 * lift_scale, 280 * flow_attenuation_ex)
        .add_flow_sample(14 * lift_scale, 280 * flow_attenuation_ex)

    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.0 * units.inch) * 4 * units.inch,
        intake_runner_cross_section_area: circle_area(1.0 * units.inch),
        exhaust_runner_volume: circle_area(0.65 * units.inch) * 6 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.65 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}

public node bmw_s65b40_engine {
    alias output __out: engine;

    wires wires()

    label bore(92.0 * units.mm)
    label bore_spacing(98.0 * units.mm)
    label stroke(75.2 * units.mm)
    label rod_length(140.72 * units.mm)
    label rod_mass(450 * units.g)
    label piston_mass(487 * units.g)
    label compression_height(27.51 * units.mm)
    label piston_dome(4.65 * units.cc)
    label deck_height((206.5 + 1) * units.mm)
    label crank_mass(60 * units.lb)
    label flywheel_mass(20 * units.lb)
    label flywheel_radius(5.5 * units.inch)

    engine engine(
        name: "BMW S65B40",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 1200 * units.rpm,
        redline: 8000 * units.rpm,
        throttle_gamma: 1.0
    )

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 15 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia: crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 135.0 * units.deg // 45 degrees CCW from vertical
    )

    // S65B40 has mirror-chiral crankshaft
    rod_journal rj1(angle:   0 * units.deg)
    rod_journal rj2(angle:  90 * units.deg)
    rod_journal rj3(angle: -90 * units.deg)
    rod_journal rj4(angle: 180 * units.deg)

    c0
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)

    piston_parameters piston_params(
        mass: piston_mass,
        blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )
    
    intake intake(
        plenum_volume: (circle_area(3.5 * units.inch) * 4 * units.inch),
        plenum_cross_section_area: circle_area(3.5 * units.inch),
        intake_flow_rate: k_carb(900.0),
        idle_flow_rate: k_carb(0.0001),
        idle_throttle_plate_position: 0.9983,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(350.0),
        runner_length: 6.0 * units.inch,
        velocity_decay: 0.1
    )
    
    label exhaust_radius((4.0 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(900.0),
        primary_tube_length: 4.0 * bore_spacing,
        primary_flow_rate: k_carb(400.0),
        velocity_decay: 0.8,
        collector_cross_section_area: circle_area(exhaust_radius) * 2,
        length: 2700 * units.mm,
        volume: (2700 * units.mm * circle_area(exhaust_radius)) * 2
    )

    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.05, impulse_response: ir_lib.default_0)

    label pipe_length_1(0.5 * bore_spacing)
    label pipe_length_2(0.0 * bore_spacing)
    label pipe_length_3(1.0 * bore_spacing)
    label pipe_length_4(0.0 * bore_spacing)
    
    label exhaust_stagger(0.6 * bore_spacing)

    cylinder_bank bR(bank_params, angle: -45 * units.deg) // R (first)
    bR
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            intake: intake,
            exhaust_system: exhaust,
            ignition_wire: wires.wire5,
            primary_length: pipe_length_1 + exhaust_stagger,
            sound_attenuation: 0.9 * 0.8
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            intake: intake,
            exhaust_system: exhaust,
            ignition_wire: wires.wire6,
            primary_length: pipe_length_2 + exhaust_stagger,
            sound_attenuation: 0.9
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            intake: intake,
            exhaust_system: exhaust,
            ignition_wire: wires.wire7,
            primary_length: pipe_length_3 + exhaust_stagger,
            sound_attenuation: 0.9 * 0.8
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            intake: intake,
            exhaust_system: exhaust,
            ignition_wire: wires.wire8,
            primary_length: pipe_length_4 + exhaust_stagger,
            sound_attenuation: 0.9
        )

    cylinder_bank bL(bank_params, angle: 45.0 * units.deg) // L (second)
    bL
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            intake: intake,
            exhaust_system: exhaust,
            ignition_wire: wires.wire1,
            primary_length: pipe_length_1,
            sound_attenuation: 0.8
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            intake: intake,
            exhaust_system: exhaust,
            ignition_wire: wires.wire2,
            primary_length: pipe_length_2
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            intake: intake,
            exhaust_system: exhaust,
            ignition_wire: wires.wire3,
            primary_length: pipe_length_3,
            sound_attenuation: 0.8
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            intake: intake,
            exhaust_system: exhaust,
            ignition_wire: wires.wire4,
            primary_length: pipe_length_4
        )

    engine
        .add_cylinder_bank(bL)
        .add_cylinder_bank(bR)

    engine.add_crankshaft(c0)

    bmw_s65b40_camshaft_builder camshaft()

    bL.set_cylinder_head (
        bmw_s65b40_head(
            intake_camshaft: camshaft.intake_cam_L,
            exhaust_camshaft: camshaft.exhaust_cam_L,
            flip_display: true
        )
    )
    bR.set_cylinder_head (
        bmw_s65b40_head(
            intake_camshaft: camshaft.intake_cam_R,
            exhaust_camshaft: camshaft.exhaust_cam_R
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 60 * units.deg)
        .add_sample(1000 * units.rpm, 65 * units.deg)
        .add_sample(2000 * units.rpm, 65 * units.deg)
        .add_sample(3000 * units.rpm, 65 * units.deg)
        .add_sample(4000 * units.rpm, 65 * units.deg)
        .add_sample(5000 * units.rpm, 65 * units.deg)
        .add_sample(6000 * units.rpm, 65 * units.deg)
        .add_sample(7000 * units.rpm, 65 * units.deg)
        .add_sample(8000 * units.rpm, 65 * units.deg)
        .add_sample(9000 * units.rpm, 65 * units.deg)

    engine.add_ignition_module(
        bmw_s65b40_distributor(
            wires: wires,
            timing_curve: timing_curve
        )
    )
}

// 2010 BMW M3 Coupe (E92)
public node bmw_m3_e92_vehicle {
    alias output __out:
        vehicle(
            mass: 3649 * units.lb,
            drag_coefficient: 0.31,
            cross_sectional_area: 2.17 * (units.m * units.m),
            diff_ratio: 3.850,
            tire_radius: 334 * units.mm, // 265/35ZR19
            rolling_resistance: 500 * units.N
        );
}

public node zf_gs6_53bz_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 900 * units.lb_ft
        )
        .add_gear(4.055)
        .add_gear(2.396)
        .add_gear(1.582)
        .add_gear(1.192)
        .add_gear(1.000)
        .add_gear(0.872);
}

public node main {
    set_engine(bmw_s65b40_engine())
    set_vehicle(bmw_m3_e92_vehicle())
    set_transmission(zf_gs6_53bz_transmission())
}

main()