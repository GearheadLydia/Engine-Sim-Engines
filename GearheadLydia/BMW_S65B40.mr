import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

// firing order: 15487263
// standardized:  18736524

public node bmw_s65b40_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 8400 * units.rpm;
    input limiter_duration: 0.09;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0 / 8.0) * cycle)
            .connect_wire(wires.wire5, (1.0 / 8.0) * cycle)
            .connect_wire(wires.wire4, (2.0 / 8.0) * cycle)
            .connect_wire(wires.wire8, (3.0 / 8.0) * cycle)
            .connect_wire(wires.wire7, (4.0 / 8.0) * cycle)
            .connect_wire(wires.wire2, (5.0 / 8.0) * cycle)
            .connect_wire(wires.wire6, (6.0 / 8.0) * cycle)
            .connect_wire(wires.wire3, (7.0 / 8.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
    output wire7: ignition_wire();
    output wire8: ignition_wire();
}

public node bmw_s65b40_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 251.0 * units.deg, //adv. 281
            gamma: 1.0,
            lift: 12.0 * units.mm,
            steps: 100
        );
}

public node bmw_s65b40_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 245.5 * units.deg, //adv. 275.5
            gamma: 1.0,
            lift: 12.0 * units.mm,
            steps: 100
        );
}

public node bmw_s65b40_camshaft_builder {
    input in_lobe_profile: bmw_s65b40_lobe_profile_int();
    input ex_lobe_profile: bmw_s65b40_lobe_profile_exh();
    input intake_lobe_profile: in_lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input lobe_separation: 132.625 * units.deg;
    //input intake_lobe_center: lobe_separation;
    //input exhaust_lobe_center: lobe_separation;
    input intake_lobe_center: 140.5 * units.deg;
    input exhaust_lobe_center: 123.75 * units.deg;
    //input advance: -8.375 * units.deg;
    input advance: 0 * units.deg;
    input base_radius: 15.0 * units.mm;

    output intake_cam_0: _intake_cam_0;
    output intake_cam_1: _intake_cam_1;
    output exhaust_cam_0: _exhaust_cam_0;
    output exhaust_cam_1: _exhaust_cam_1;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_0(params, lobe_profile: intake_lobe_profile)
    camshaft _intake_cam_1(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_0(params, lobe_profile: exhaust_lobe_profile)
    camshaft _exhaust_cam_1(params, lobe_profile: exhaust_lobe_profile)

    label rot90(90 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_0
        .add_lobe(rot360 + intake_lobe_center + 1 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 6 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 4 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 3 * rot90)

    _exhaust_cam_0
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 6 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 4 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot90)

    _intake_cam_1
        .add_lobe(rot360 + intake_lobe_center + 0 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 5 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 7 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 2 * rot90)

    _exhaust_cam_1
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 5 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 7 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot90)
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.mm, k_28inH2O(flow))
}

public node bmw_s65b40_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input flip_display: false;
    
    input flow_attenuation_in: 1.0;
    input flow_attenuation_ex: 1.0;
    input lift_scale: 1.27;
    alias output __out: head;

    function intake_flow(1.0 * units.mm)
    intake_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_in)
        .add_flow_sample( 1 * lift_scale,  30 * flow_attenuation_in)
        .add_flow_sample( 2 * lift_scale,  60 * flow_attenuation_in)
        .add_flow_sample( 3 * lift_scale,  90 * flow_attenuation_in)
        .add_flow_sample( 4 * lift_scale, 125 * flow_attenuation_in)
        .add_flow_sample( 5 * lift_scale, 160 * flow_attenuation_in)
        .add_flow_sample( 6 * lift_scale, 195 * flow_attenuation_in)
        .add_flow_sample( 7 * lift_scale, 210 * flow_attenuation_in)
        .add_flow_sample( 8 * lift_scale, 235 * flow_attenuation_in)
        .add_flow_sample( 9 * lift_scale, 270 * flow_attenuation_in)
        .add_flow_sample(10 * lift_scale, 305 * flow_attenuation_in)
        .add_flow_sample(11 * lift_scale, 320 * flow_attenuation_in)
        .add_flow_sample(12 * lift_scale, 335 * flow_attenuation_in)
        .add_flow_sample(13 * lift_scale, 335 * flow_attenuation_in)
        .add_flow_sample(14 * lift_scale, 335 * flow_attenuation_in)

    function exhaust_flow(1.0 * units.mm)
    exhaust_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_ex)
        .add_flow_sample( 1 * lift_scale,  30 * flow_attenuation_ex)
        .add_flow_sample( 2 * lift_scale,  55 * flow_attenuation_ex)
        .add_flow_sample( 3 * lift_scale,  85 * flow_attenuation_ex)
        .add_flow_sample( 4 * lift_scale, 115 * flow_attenuation_ex)
        .add_flow_sample( 5 * lift_scale, 140 * flow_attenuation_ex)
        .add_flow_sample( 6 * lift_scale, 160 * flow_attenuation_ex)
        .add_flow_sample( 7 * lift_scale, 180 * flow_attenuation_ex)
        .add_flow_sample( 8 * lift_scale, 205 * flow_attenuation_ex)
        .add_flow_sample( 9 * lift_scale, 220 * flow_attenuation_ex)
        .add_flow_sample(10 * lift_scale, 240 * flow_attenuation_ex)
        .add_flow_sample(11 * lift_scale, 260 * flow_attenuation_ex)
        .add_flow_sample(12 * lift_scale, 280 * flow_attenuation_ex)
        .add_flow_sample(13 * lift_scale, 280 * flow_attenuation_ex)
        .add_flow_sample(14 * lift_scale, 280 * flow_attenuation_ex)

    cylinder_head head(
        chamber_volume: 51.0 * units.cc,
        intake_runner_volume: circle_area(1.0 * units.inch) * 4 * units.inch,
        intake_runner_cross_section_area: circle_area(1.0 * units.inch),
        exhaust_runner_volume: circle_area(0.75 * units.inch) * 6 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.75 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}



public node bmw_s65b40_itb {
    input throttlebody_cfm: 250;
    input idle_flow_rate_cfm: 0.0001;
    input idle_throttle_plate_position: 0.9987;
    input throttle_gamma: 8.0;

    alias output __out: intake;

    intake intake(
        plenum_volume: (circle_area(4 * units.inch) * 4 * units.inch) / 8,
        plenum_cross_section_area: circle_area(4 * units.inch) / 8,
        intake_flow_rate: k_carb(throttlebody_cfm),
        idle_flow_rate: k_carb(idle_flow_rate_cfm),
        idle_throttle_plate_position: idle_throttle_plate_position,
        throttle_gamma: throttle_gamma,
        runner_flow_rate: k_carb(350.0),
        runner_length: 6 * units.inch,
        velocity_decay: 0.1
    )
}

public node S65B40 {
    alias output __out: engine;

    wires wires()

    label bore(92.0 * units.mm)
    label stroke(75.2 * units.mm)
    label rod_length(140.72 * units.mm)
    label rod_mass(450 * units.g)
    label piston_mass(487 * units.g)
    label compression_height(27.51 * units.mm)
    label piston_dome(4.65 * units.cc)
    label deck_height((206.5 + 1) * units.mm)
    label crank_mass(60 * units.lb)
    label flywheel_mass(20 * units.lb)
    label flywheel_radius(5.5 * units.inch)

    engine engine(
        name: "BMW S65B40",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 1000 * units.rpm,
        redline: 8000 * units.rpm,
        throttle_gamma: 1.0
    )

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 15 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia: crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 135.0 * units.deg // 45 degrees CCW from vertical
    )

    // S65B40 has mirror-chiral crankshaft
    rod_journal rj0(angle: 0.0)
    rod_journal rj1(angle: 90.0 * units.deg)
    rod_journal rj2(angle: -90.0 * units.deg)
    rod_journal rj3(angle: 180.0 * units.deg)

    c0
        .add_rod_journal(rj0)
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)

    piston_parameters piston_params(
        mass: piston_mass,
        blowby: 0,
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    bmw_s65b40_itb itb1()
    bmw_s65b40_itb itb2()
    bmw_s65b40_itb itb3()
    bmw_s65b40_itb itb4()
    bmw_s65b40_itb itb5()
    bmw_s65b40_itb itb6()
    bmw_s65b40_itb itb7()
    bmw_s65b40_itb itb8()

    label exhaust_radius((4.0 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(700.0),
        primary_tube_length: 7.5 * units.inch,
        primary_flow_rate: k_carb(400.0),
        velocity_decay: 1.0, //0.5
        collector_cross_section_area: circle_area(exhaust_radius) * 2,
        length: 2700 * units.mm,
        volume: (2700 * units.mm * circle_area(exhaust_radius)) * 2
    )

    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.1, impulse_response: ir_lib.default_0)

    label pipe_length_1(2.25 * units.inch)
    label pipe_length_2(2.25 * units.inch)
    label pipe_length_3(0.0 * units.inch)
    label pipe_length_4(0.0 * units.inch)
    
    label exhaust_stagger(35 * units.mm)
    label exhaust_volume_L(1.0)
    label exhaust_volume_R(0.25)

    cylinder_bank b0(bank_params, angle: -45 * units.deg) // R (first)
    b0
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.05)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            intake: itb5,
            exhaust_system: exhaust,
            ignition_wire: wires.wire5,
            primary_length: pipe_length_1 + exhaust_stagger,
            sound_attenuation: exhaust_volume_R * 1.6
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.05)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            intake: itb6,
            exhaust_system: exhaust,
            ignition_wire: wires.wire6,
            primary_length: pipe_length_2 + exhaust_stagger,
            sound_attenuation: exhaust_volume_R
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.05)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            intake: itb7,
            exhaust_system: exhaust,
            ignition_wire: wires.wire7,
            primary_length: pipe_length_3 + exhaust_stagger,
            sound_attenuation: exhaust_volume_R * 1.3
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.05)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            intake: itb8,
            exhaust_system: exhaust,
            ignition_wire: wires.wire8,
            primary_length: pipe_length_4 + exhaust_stagger,
            sound_attenuation: exhaust_volume_R * 1.3 * 1.6
        )

    cylinder_bank b1(bank_params, angle: 45.0 * units.deg) // L (second)
    b1
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.05)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            intake: itb1,
            exhaust_system: exhaust,
            ignition_wire: wires.wire1,
            primary_length: pipe_length_1,
            sound_attenuation: exhaust_volume_L / 1.3
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.05)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            intake: itb2,
            exhaust_system: exhaust,
            ignition_wire: wires.wire2,
            primary_length: pipe_length_2,
            sound_attenuation: exhaust_volume_L / 1.6
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.05)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            intake: itb3,
            exhaust_system: exhaust,
            ignition_wire: wires.wire3,
            primary_length: pipe_length_3,
            sound_attenuation: exhaust_volume_L / 1.3 / 1.6
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.05)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            intake: itb4,
            exhaust_system: exhaust,
            ignition_wire: wires.wire4,
            primary_length: pipe_length_4,
            sound_attenuation: exhaust_volume_L
        )

    engine
        .add_cylinder_bank(b1)
        .add_cylinder_bank(b0)

    engine.add_crankshaft(c0)

    bmw_s65b40_camshaft_builder camshaft()

    b0.set_cylinder_head (
        bmw_s65b40_head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0
        )
    )
    b1.set_cylinder_head (
        bmw_s65b40_head(
            intake_camshaft: camshaft.intake_cam_1,
            exhaust_camshaft: camshaft.exhaust_cam_1,
            flip_display: true
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 80 * units.deg)
        .add_sample(1000 * units.rpm, 85 * units.deg)
        .add_sample(2000 * units.rpm, 85 * units.deg)
        .add_sample(3000 * units.rpm, 85 * units.deg)
        .add_sample(4000 * units.rpm, 85 * units.deg)
        .add_sample(5000 * units.rpm, 85 * units.deg)
        .add_sample(6000 * units.rpm, 84 * units.deg)
        .add_sample(7000 * units.rpm, 82 * units.deg)
        .add_sample(8000 * units.rpm, 80 * units.deg)
        .add_sample(9000 * units.rpm, 80 * units.deg)

    engine.add_ignition_module(
        bmw_s65b40_distributor(
            wires: wires,
            timing_curve: timing_curve
        )
    )
}

// 2010 BMW M3 Coupe (E92)
public node bmw_m3_e92_vehicle {
    alias output __out:
        vehicle(
            mass: 3649 * units.lb,
            drag_coefficient: 0.31,
            cross_sectional_area: 2.17 * (units.m * units.m),
            diff_ratio: 3.850,
            tire_radius: 334 * units.mm, // 265/35ZR19
            rolling_resistance: 500 * units.N
        );
}

public node zf_gs6_53bz_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 900 * units.lb_ft
        )
        .add_gear(4.055)
        .add_gear(2.396)
        .add_gear(1.582)
        .add_gear(1.192)
        .add_gear(1.000)
        .add_gear(0.872);
}

public node main {
    set_engine(S65B40())
    set_vehicle(bmw_m3_e92_vehicle())
    set_transmission(zf_gs6_53bz_transmission())
}

main()