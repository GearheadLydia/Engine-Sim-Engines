import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

public node honda_j35a8_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 6850 * units.rpm;
    input limiter_duration: 0.1;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0/6.0) * cycle)
            .connect_wire(wires.wire4, (1.0/6.0) * cycle)
            .connect_wire(wires.wire2, (2.0/6.0) * cycle)
            .connect_wire(wires.wire5, (3.0/6.0) * cycle)
            .connect_wire(wires.wire3, (4.0/6.0) * cycle)
            .connect_wire(wires.wire6, (5.0/6.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
}

label base_circle(27 * units.mm) // this figure is a guess

// https://i1263.photobucket.com/albums/ii622/Yungone501/0929295a70df14fb0b5699a68ef1e40f.jpg
// durations are guesses

public node honda_j35a8_lobe_profile_int_lo {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 210 * units.deg,
            gamma: 1.0,
            lift: 35.112 * units.mm - base_circle,
            steps: 100
        );
}

public node honda_j35a8_lobe_profile_int_hi {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 235 * units.deg,
            gamma: 1.0,
            lift: 36.394 * units.mm - base_circle,
            steps: 100
        );
}

public node honda_j35a8_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 230 * units.deg,
            gamma: 1.0,
            lift: 36.389 * units.mm - base_circle,
            steps: 100
        );
}

public node honda_j35a8_camshaft_builder {
    input in_lobe_profile_lo: honda_j35a8_lobe_profile_int_lo();
    input in_lobe_profile_hi: honda_j35a8_lobe_profile_int_hi();
    input ex_lobe_profile: honda_j35a8_lobe_profile_exh();
    input lobe_separation: 110.0 * units.deg;
    input intake_lobe_center_lo: lobe_separation + (18 * units.deg);
    input intake_lobe_center_hi: lobe_separation;
    input exhaust_lobe_center: lobe_separation;
    input advance: 0 * units.deg;
    input advance_in_lo: advance;
    input advance_in_hi: advance;
    input advance_ex: advance;
    input base_radius: base_circle / 2;

    output intake_cam_0_lo: _intake_cam_0_lo;
    output intake_cam_1_lo: _intake_cam_1_lo;
    output intake_cam_0_hi: _intake_cam_0_hi;
    output intake_cam_1_hi: _intake_cam_1_hi;
    output exhaust_cam_0: _exhaust_cam_0;
    output exhaust_cam_1: _exhaust_cam_1;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_0_lo(params, lobe_profile: in_lobe_profile_lo, advance: advance_in_lo)
    camshaft _intake_cam_0_hi(params, lobe_profile: in_lobe_profile_hi, advance: advance_in_hi)
    camshaft _exhaust_cam_0(params, lobe_profile: ex_lobe_profile, advance: advance_ex)

    camshaft _intake_cam_1_lo(params, lobe_profile: in_lobe_profile_lo, advance: advance_in_lo)
    camshaft _intake_cam_1_hi(params, lobe_profile: in_lobe_profile_hi, advance: advance_in_hi)
    camshaft _exhaust_cam_1(params, lobe_profile: ex_lobe_profile, advance: advance_ex)


    label rot30(30 * units.deg)
    label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_0_lo
        .add_lobe(rot360 + intake_lobe_center_lo + (0 * rot120))
        .add_lobe(rot360 + intake_lobe_center_lo + (2 * rot120))
        .add_lobe(rot360 + intake_lobe_center_lo + (4 * rot120))

    _intake_cam_1_lo
        .add_lobe(rot360 + intake_lobe_center_lo + (1 * rot120))
        .add_lobe(rot360 + intake_lobe_center_lo + (3 * rot120))
        .add_lobe(rot360 + intake_lobe_center_lo + (5 * rot120))

    _intake_cam_0_hi
        .add_lobe(rot360 + intake_lobe_center_hi + (0 * rot120))
        .add_lobe(rot360 + intake_lobe_center_hi + (2 * rot120))
        .add_lobe(rot360 + intake_lobe_center_hi + (4 * rot120))

    _intake_cam_1_hi
        .add_lobe(rot360 + intake_lobe_center_hi + (1 * rot120))
        .add_lobe(rot360 + intake_lobe_center_hi + (3 * rot120))
        .add_lobe(rot360 + intake_lobe_center_hi + (5 * rot120))

    _exhaust_cam_0
        .add_lobe(rot360 - exhaust_lobe_center + (0 * rot120))
        .add_lobe(rot360 - exhaust_lobe_center + (2 * rot120))
        .add_lobe(rot360 - exhaust_lobe_center + (4 * rot120))

    _exhaust_cam_1
        .add_lobe(rot360 - exhaust_lobe_center + (1 * rot120))
        .add_lobe(rot360 - exhaust_lobe_center + (3 * rot120))
        .add_lobe(rot360 - exhaust_lobe_center + (5 * rot120))
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.mm, k_28inH2O(flow))
}

public node honda_j35a8_head {
    input cam_in_lo;
    input cam_in_hi;
    input cam_ex;
    input chamber_volume: 53.0 * units.cc;
    input flip_display: false;
    
    input flow_attenuation: 1.0;
    input lift_scale: 1.05; // sound purposes
    alias output __out: head;

    function intake_flow(1.0 * units.mm)
    intake_flow
        .add_flow_sample(0 * lift_scale, 0 * flow_attenuation)
        .add_flow_sample(1 * lift_scale, 30 * flow_attenuation)
        .add_flow_sample(2 * lift_scale, 60 * flow_attenuation)
        .add_flow_sample(3 * lift_scale, 90 * flow_attenuation)
        .add_flow_sample(4 * lift_scale, 125 * flow_attenuation)
        .add_flow_sample(5 * lift_scale, 160 * flow_attenuation)
        .add_flow_sample(6 * lift_scale, 195 * flow_attenuation)
        .add_flow_sample(7 * lift_scale, 210 * flow_attenuation)
        .add_flow_sample(8 * lift_scale, 235 * flow_attenuation)
        .add_flow_sample(9 * lift_scale, 270 * flow_attenuation)
        .add_flow_sample(10 * lift_scale, 305 * flow_attenuation)
        .add_flow_sample(11 * lift_scale, 320 * flow_attenuation)
        .add_flow_sample(12 * lift_scale, 335 * flow_attenuation)

    function exhaust_flow(1.0 * units.mm)
    exhaust_flow
        .add_flow_sample(0 * lift_scale, 0 * flow_attenuation)
        .add_flow_sample(1 * lift_scale, 30 * flow_attenuation)
        .add_flow_sample(2 * lift_scale, 55 * flow_attenuation)
        .add_flow_sample(3 * lift_scale, 85 * flow_attenuation)
        .add_flow_sample(4 * lift_scale, 115 * flow_attenuation)
        .add_flow_sample(5 * lift_scale, 140 * flow_attenuation)
        .add_flow_sample(6 * lift_scale, 160 * flow_attenuation)
        .add_flow_sample(7 * lift_scale, 180 * flow_attenuation)
        .add_flow_sample(8 * lift_scale, 205 * flow_attenuation)
        .add_flow_sample(9 * lift_scale, 220 * flow_attenuation)
        .add_flow_sample(10 * lift_scale, 240 * flow_attenuation)
        .add_flow_sample(11 * lift_scale, 260 * flow_attenuation)
        .add_flow_sample(12 * lift_scale, 280 * flow_attenuation)

        
    generic_cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.0 * units.inch) * 5 * units.inch,
        intake_runner_cross_section_area: circle_area(1.0 * units.inch),
        exhaust_runner_volume: circle_area(0.7 * units.inch) * 5 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.7 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        valvetrain: vtec_valvetrain(
            vtec_intake_camshaft: cam_in_hi,
            vtec_exhaust_camshaft: cam_ex,
            intake_camshaft: cam_in_lo,
            exhaust_camshaft: cam_ex,
            min_rpm: 4400 * units.rpm
        ),
        flip_display: flip_display
    )
}


public node j35a8 {
    alias output __out: engine;

    wires wires()

    engine engine(
        name: "Honda J35A8",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 1000 * units.rpm,
        redline: 6500 * units.rpm,
        throttle_gamma: 1.0
    )

    label bore(89.0 * units.mm)
    label bore_spacing(98.0 * units.mm)
    label stroke(93.0 * units.mm)
    label rod_length(158.5 * units.mm)
    label rod_mass(550 * units.g)
    label piston_mass(450 * units.g)
    label compression_height(30.0 * units.mm)
    label deck_height((235.0 + 0.7) * units.mm)
    label piston_dome(0.0 * units.cc) // for 11.0:1 CR
    label crank_mass(85 * units.lb)
    label flywheel_mass(20 * units.lb)
    label flywheel_radius(6 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 15 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia: crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 120.0 * units.deg // +30 degrees
    )

    rod_journal rj0(angle: 0 * units.deg)
    rod_journal rj1(angle: 60 * units.deg)
    rod_journal rj2(angle: 240 * units.deg)
    rod_journal rj3(angle: 300 * units.deg)
    rod_journal rj4(angle: 120 * units.deg)
    rod_journal rj5(angle: 180 * units.deg)

    c0
        .add_rod_journal(rj0)
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)
        .add_rod_journal(rj5)


    piston_parameters piston_params(
        mass: piston_mass,
        //blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    intake intake (
        plenum_volume: 2.0 * units.L,
        plenum_cross_section_area: 100.0 * units.cm2,
        intake_flow_rate: k_carb(450.0),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.996,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(280.0),
        runner_length: 8.0 * units.inch,
        velocity_decay: 0.1
    )

    label exhaust_radius((3.0 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(450.0),
        primary_tube_length: 5.0 * units.inch,
        primary_flow_rate: k_carb(250.0),
        velocity_decay: 1.0, //0.5,
        collector_cross_section_area: circle_area(exhaust_radius) * 2,
        length: 2700 * units.mm,
        volume: (2200 * units.mm * circle_area(exhaust_radius)) + (500 * units.mm * circle_area(exhaust_radius) * 2)
    )

    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.05, impulse_response: ir_lib.default_0)
    
    label exhaust_scale(0.8)
    
    label pipe_length_1(1.0 * bore_spacing * exhaust_scale)
    label pipe_length_2(0.0 * bore_spacing * exhaust_scale)
    label pipe_length_3(0.9 * bore_spacing * exhaust_scale)
    
    label exhaust_stagger(2.5 * units.inch * exhaust_scale)

    cylinder_bank b0(bank_params, angle: 30.0 * units.deg) // L (first) - rear
    b0
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            ignition_wire: wires.wire1,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_1,
            sound_attenuation: 1.0 * 0.9
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire2,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_2,
            sound_attenuation: 0.8 * 0.9
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire3,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_3,
            sound_attenuation: 1.0 * 0.9
        )

    cylinder_bank b1(bank_params, angle: -30.0 * units.deg) // R (second) - front
    b1
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire4,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_1 + exhaust_stagger,
            sound_attenuation: 1.0
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire5,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_2 + exhaust_stagger,
            sound_attenuation: 0.8
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj5,
            ignition_wire: wires.wire6,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_3 + exhaust_stagger,
            sound_attenuation: 1.0
        )

    engine
        .add_cylinder_bank(b0)
        .add_cylinder_bank(b1)

    engine.add_crankshaft(c0)

    honda_j35a8_camshaft_builder camshaft()

    b0.set_cylinder_head (
        honda_j35a8_head(
            cam_in_lo: camshaft.intake_cam_0_lo,
            cam_in_hi: camshaft.intake_cam_0_hi,
            cam_ex: camshaft.exhaust_cam_0,
            flip_display: true
        )
    )
    b1.set_cylinder_head (
        honda_j35a8_head(
            cam_in_lo: camshaft.intake_cam_1_lo,
            cam_in_hi: camshaft.intake_cam_1_hi,
            cam_ex: camshaft.exhaust_cam_1,
            flip_display: false
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 38 * units.deg)
        .add_sample(1000 * units.rpm, 48 * units.deg)
        .add_sample(2000 * units.rpm, 48 * units.deg)
        .add_sample(3000 * units.rpm, 48 * units.deg)
        .add_sample(4000 * units.rpm, 48 * units.deg)
        .add_sample(5000 * units.rpm, 48 * units.deg)
        .add_sample(6000 * units.rpm, 48 * units.deg)
        .add_sample(7000 * units.rpm, 48 * units.deg)
        .add_sample(8000 * units.rpm, 48 * units.deg)

    engine.add_ignition_module(
        honda_j35a8_distributor(
            wires: wires,
            timing_curve: timing_curve
        )
    )
}

// 2008 Acura TL Type-S (UA7)
public node acura_tl_ua7_vehicle {
    alias output __out:
        vehicle(
            mass: 1614 * units.kg,
            drag_coefficient: 0.3,
            cross_sectional_area: 2.19 * (units.m * units.m),
            diff_ratio: 3.285,
            tire_radius: 321.5 * units.mm, // 235/45R17
            rolling_resistance: 1614 * units.kg * 9.81 * (235.0/275.0) * 0.01
        );
}

public node acura_tl_ua7_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 500 * units.lb_ft
        )
        .add_gear(3.933)
        .add_gear(2.478)
        .add_gear(1.700)
        .add_gear(1.250)
        .add_gear(0.976)
        .add_gear(0.771);
}

public node main {
    set_engine(j35a8())
    set_vehicle(acura_tl_ua7_vehicle())
    set_transmission(acura_tl_ua7_transmission())
}

main()