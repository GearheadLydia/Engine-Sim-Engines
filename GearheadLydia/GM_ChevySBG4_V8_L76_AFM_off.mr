import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

// firing order: 18726543

public node chevrolet_l76_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 6200 * units.rpm;
    input limiter_duration: 0.08;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0 / 8.0) * cycle)
            .connect_wire(wires.wire8, (1.0 / 8.0) * cycle)
            .connect_wire(wires.wire7, (2.0 / 8.0) * cycle)
            .connect_wire(wires.wire2, (3.0 / 8.0) * cycle)
            .connect_wire(wires.wire6, (4.0 / 8.0) * cycle)
            .connect_wire(wires.wire5, (5.0 / 8.0) * cycle)
            .connect_wire(wires.wire4, (6.0 / 8.0) * cycle)
            .connect_wire(wires.wire3, (7.0 / 8.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
    output wire7: ignition_wire();
    output wire8: ignition_wire();
}


// https://www.onallcylinders.com/2018/02/02/l76-engine-specs-performance-bore-stroke-cylinder-heads-cam-specs/

public node chevrolet_l76_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 196 * units.deg,
            gamma: 1.0,
            lift: 467 * units.thou,
            steps: 100
        );
}

public node chevrolet_l76_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 208 * units.deg,
            gamma: 1.0,
            lift: 479 * units.thou,
            steps: 100
        );
}

public node chevrolet_l76_camshaft_builder {
    input in_lobe_profile: chevrolet_l76_lobe_profile_int();
    input ex_lobe_profile: chevrolet_l76_lobe_profile_exh();
    input intake_lobe_profile: in_lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input lobe_separation: 116.0 * units.deg;
    input intake_lobe_center: lobe_separation;
    input exhaust_lobe_center: lobe_separation;
    input advance: -3.0 * units.deg; // not 100% sure about this
    input base_radius: 1.462 / 2 * units.inch;

    output intake_cam_R: _intake_cam_R;
    output exhaust_cam_R: _exhaust_cam_R;
    output intake_cam_L: _intake_cam_L;
    output exhaust_cam_L: _exhaust_cam_L;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_R(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_R(params, lobe_profile: exhaust_lobe_profile)

    camshaft _intake_cam_L(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_L(params, lobe_profile: exhaust_lobe_profile)


    label rot30(30 * units.deg)
    label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_R
        .add_lobe(rot360 + intake_lobe_center + 0 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 7 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 5 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 2 * rot90)

    _intake_cam_L
        .add_lobe(rot360 + intake_lobe_center + 3 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 6 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 4 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 1 * rot90)

    _exhaust_cam_R
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 7 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 5 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot90)

    _exhaust_cam_L
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 6 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 4 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot90)
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.thou, k_28inH2O(flow))
}

public node chevrolet_l76_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 70.0 * units.cc;
    input flip_display: false;
    
    input flow_attenuation_in: 1.0;
    input flow_attenuation_ex: 1.0;
    input lift_scale: 50.0;
    alias output __out: head;

    function intake_flow(50 * units.thou)
    intake_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_in)
        .add_flow_sample( 1 * lift_scale,  25 * flow_attenuation_in)
        .add_flow_sample( 2 * lift_scale,  76 * flow_attenuation_in)
        .add_flow_sample( 3 * lift_scale, 100 * flow_attenuation_in)
        .add_flow_sample( 4 * lift_scale, 146 * flow_attenuation_in)
        .add_flow_sample( 5 * lift_scale, 175 * flow_attenuation_in)
        .add_flow_sample( 6 * lift_scale, 212 * flow_attenuation_in)
        .add_flow_sample( 7 * lift_scale, 230 * flow_attenuation_in)
        .add_flow_sample( 8 * lift_scale, 255 * flow_attenuation_in)
        .add_flow_sample( 9 * lift_scale, 275 * flow_attenuation_in)
        .add_flow_sample(10 * lift_scale, 294 * flow_attenuation_in)
        .add_flow_sample(11 * lift_scale, 300 * flow_attenuation_in)
        .add_flow_sample(12 * lift_scale, 314 * flow_attenuation_in)
        .add_flow_sample(13 * lift_scale, 314 * flow_attenuation_in)
        .add_flow_sample(14 * lift_scale, 314 * flow_attenuation_in)

    function exhaust_flow(50 * units.thou)
    exhaust_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_ex)
        .add_flow_sample( 1 * lift_scale,  25 * flow_attenuation_ex)
        .add_flow_sample( 2 * lift_scale,  70 * flow_attenuation_ex)
        .add_flow_sample( 3 * lift_scale, 100 * flow_attenuation_ex)
        .add_flow_sample( 4 * lift_scale, 132 * flow_attenuation_ex)
        .add_flow_sample( 5 * lift_scale, 140 * flow_attenuation_ex)
        .add_flow_sample( 6 * lift_scale, 156 * flow_attenuation_ex)
        .add_flow_sample( 7 * lift_scale, 170 * flow_attenuation_ex)
        .add_flow_sample( 8 * lift_scale, 181 * flow_attenuation_ex)
        .add_flow_sample( 9 * lift_scale, 191 * flow_attenuation_ex)
        .add_flow_sample(10 * lift_scale, 207 * flow_attenuation_ex)
        .add_flow_sample(11 * lift_scale, 214 * flow_attenuation_ex)
        .add_flow_sample(12 * lift_scale, 228 * flow_attenuation_ex)
        .add_flow_sample(13 * lift_scale, 228 * flow_attenuation_ex)
        .add_flow_sample(14 * lift_scale, 228 * flow_attenuation_ex)
        
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.43 * units.inch) * 8 * units.inch,
        intake_runner_cross_section_area: circle_area(1.43 * units.inch),
        exhaust_runner_volume: circle_area(0.8 * units.inch) * 4 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.8 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}


public node chevrolet_l76 {
    alias output __out: engine;

    wires wires()

    engine engine(
        name: "Chevrolet L76 (AFM off)",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 1200 * units.rpm,
        redline: 6000 * units.rpm,
        throttle_gamma: 1.0,
        // jitter: 0.7,
        // hf_gain: 0.15
        jitter: 0.6,
        hf_gain: 0.1
        // jitter: 0.1,
        // hf_gain: 0.1
    )


    // source: https://www.onallcylinders.com/2018/02/02/l76-engine-specs-performance-bore-stroke-cylinder-heads-cam-specs/
    label bore(4.000 * units.inch)
    label bore_spacing(4.4 * units.inch)
    label stroke(3.622 * units.inch)
    label rod_length(6.098 * units.inch)
    label rod_mass(464 * units.g)
    label compression_height(1.34 * units.inch)
    label piston_mass(482 * units.g)
    label piston_dome(0.00 * units.cc)
    label deck_height(9.240 * units.inch)
    label crank_mass(57 * units.lb)
    label flywheel_mass(15 * units.lb)
    label flywheel_radius(6 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 20 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia:
            crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 45.0 * units.deg // 45 degrees back from right side
    )

    rod_journal rj1(angle:   0 * units.deg)
    rod_journal rj2(angle: 270 * units.deg)
    rod_journal rj3(angle:  90 * units.deg)
    rod_journal rj4(angle: 180 * units.deg)

    c0
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)


    piston_parameters piston_params(
        mass: piston_mass,
        blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0 * units.mm,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    intake intake(
        plenum_volume: 0.3 * units.L,
        plenum_cross_section_area: circle_area(45 * units.mm),
        intake_flow_rate: k_carb(550),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.994,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(420.0),
        runner_length: 12.0 * units.inch,
        velocity_decay: 0.1
    )
    
    label exhaust_radius((4.0 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(400.0),
        primary_tube_length: 20.0 * units.inch,
        primary_flow_rate: k_carb(350.0),
        velocity_decay: 1.0, //0.5
        collector_cross_section_area: circle_area(exhaust_radius),
        length: 2900 * units.mm,
        volume: 2900 * units.mm * circle_area(exhaust_radius)
    )

    exhaust_system exhaustL(es_params, audio_volume: 1.0 * 0.05, impulse_response: ir_lib.default_0)
    exhaust_system exhaustR(es_params, audio_volume: 1.0 * 0.05, impulse_response: ir_lib.default_0)
    
    // lower grade LS headers //
    label pipe_length_1(bore_spacing * 2.2)
    label pipe_length_2(bore_spacing * 1.2)
    label pipe_length_3(bore_spacing * 0.2)
    label pipe_length_4(bore_spacing * 0.0)
    label pipe_volume_1(0.6)
    label pipe_volume_2(0.8)
    label pipe_volume_3(1.0)
    label pipe_volume_4(1.0)
    
    // stock Z06 headers //
    // label pipe_length_1(bore_spacing * 1.2)
    // label pipe_length_2(bore_spacing * 0.8)
    // label pipe_length_3(bore_spacing * 0.75)
    // label pipe_length_4(bore_spacing * 1.15)
    // label pipe_volume_1(0.8)
    // label pipe_volume_2(1.0)
    // label pipe_volume_3(1.0)
    // label pipe_volume_4(0.8)
    
    // Kooks headers
    // label pipe_length_1(bore_spacing * 1.0)
    // label pipe_length_2(bore_spacing * 1.3)
    // label pipe_length_3(bore_spacing * 1.0)
    // label pipe_length_4(bore_spacing * 0.7)
    // label pipe_volume_1(0.95)
    // label pipe_volume_2(0.90)
    // label pipe_volume_3(0.95)
    // label pipe_volume_4(1.00)
    
    label exhaust_stagger(80 * units.mm)

    cylinder_bank bR(bank_params, angle: -45.0 * units.deg) // R (first)
    bR
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire1,
            intake: intake,
            exhaust_system: exhaustR,
            primary_length: pipe_length_1 + exhaust_stagger,
            sound_attenuation: pipe_volume_1
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire3,
            intake: intake,
            exhaust_system: exhaustR,
            primary_length: pipe_length_2 + exhaust_stagger,
            sound_attenuation: pipe_volume_2
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire5,
            intake: intake,
            exhaust_system: exhaustR,
            primary_length: pipe_length_3 + exhaust_stagger,
            sound_attenuation: pipe_volume_3
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire7,
            intake: intake,
            exhaust_system: exhaustR,
            primary_length: pipe_length_4 + exhaust_stagger,
            sound_attenuation: pipe_volume_4
        )

    cylinder_bank bL(bank_params, angle: 45.0 * units.deg) // L (second)
    bL
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire2,
            intake: intake,
            exhaust_system: exhaustL,
            primary_length: pipe_length_1,
            sound_attenuation: pipe_volume_1
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire4,
            intake: intake,
            exhaust_system: exhaustL,
            primary_length: pipe_length_2,
            sound_attenuation: pipe_volume_2
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire6,
            intake: intake,
            exhaust_system: exhaustL,
            primary_length: pipe_length_3,
            sound_attenuation: pipe_volume_3
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire8,
            intake: intake,
            exhaust_system: exhaustL,
            primary_length: pipe_length_4,
            sound_attenuation: pipe_volume_4
        )

    engine
        .add_cylinder_bank(bR)
        .add_cylinder_bank(bL)

    engine.add_crankshaft(c0)

    chevrolet_l76_camshaft_builder camshaft(
        in_lobe_profile: chevrolet_l76_lobe_profile_int(),
        ex_lobe_profile: chevrolet_l76_lobe_profile_exh()
    )

    bR.set_cylinder_head (
        chevrolet_l76_head(
            intake_camshaft: camshaft.intake_cam_R,
            exhaust_camshaft: camshaft.exhaust_cam_R,
            flip_display: false
        )
    )
    bL.set_cylinder_head (
        chevrolet_l76_head(
            intake_camshaft: camshaft.intake_cam_L,
            exhaust_camshaft: camshaft.exhaust_cam_L,
            flip_display: true
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 25 * units.deg)
        .add_sample(1000 * units.rpm, 30 * units.deg)
        .add_sample(2000 * units.rpm, 34 * units.deg)
        .add_sample(3000 * units.rpm, 37 * units.deg)
        .add_sample(4000 * units.rpm, 40 * units.deg)
        .add_sample(5000 * units.rpm, 44 * units.deg)
        .add_sample(6000 * units.rpm, 48 * units.deg)
        .add_sample(7000 * units.rpm, 52 * units.deg)
        .add_sample(8000 * units.rpm, 55 * units.deg)

    engine.add_ignition_module(
        chevrolet_l76_distributor(
            wires: wires,
            timing_curve: timing_curve
        )
    )
}

// 2009 Pontiac G8 GT (VE)
public node pontiac_g8_gt_ve_vehicle {
    alias output __out:
        vehicle(
            mass: 3995 * units.lb,
            drag_coefficient: 0.3,
            cross_sectional_area: 2.31 * (units.m) * (units.m),
            diff_ratio: 2.92,
            tire_radius: 339 * units.mm, // 245/45R18
            rolling_resistance: 450 * units.N
        );
}

public node gm_6l80_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 1000 * units.lb_ft
        )
        .add_gear(4.027)
        .add_gear(2.364)
        .add_gear(1.522)
        .add_gear(1.152)
        .add_gear(0.852)
        .add_gear(0.667);
}

public node main {
    set_engine(chevrolet_l76())
    set_vehicle(pontiac_g8_gt_ve_vehicle())
    set_transmission(gm_6l80_transmission())
}

main()