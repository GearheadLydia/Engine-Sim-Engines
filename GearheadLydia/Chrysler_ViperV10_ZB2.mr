import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

label rot18(18 * units.deg)

// firing order: 1-10-9-4-3-6-5-8-7-2

public node dodge_v10_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 6250 * units.rpm;
	input limiter_duration: 0.08;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire01, (0.0/10.0) * cycle)
            .connect_wire(wires.wire10, (1.0/10.0) * cycle - rot18)
			.connect_wire(wires.wire09, (2.0/10.0) * cycle)
            .connect_wire(wires.wire04, (3.0/10.0) * cycle - rot18)
            .connect_wire(wires.wire03, (4.0/10.0) * cycle)
			.connect_wire(wires.wire06, (5.0/10.0) * cycle - rot18)
            .connect_wire(wires.wire05, (6.0/10.0) * cycle)
            .connect_wire(wires.wire08, (7.0/10.0) * cycle - rot18)
			.connect_wire(wires.wire07, (8.0/10.0) * cycle)
            .connect_wire(wires.wire02, (9.0/10.0) * cycle - rot18);
}

private node wires {
    output wire01: ignition_wire();
    output wire02: ignition_wire();
    output wire03: ignition_wire();
    output wire04: ignition_wire();
    output wire05: ignition_wire();
    output wire06: ignition_wire();
    output wire07: ignition_wire();
    output wire08: ignition_wire();
    output wire09: ignition_wire();
    output wire10: ignition_wire();
}

// rocker ratio: 1.5 for SR I, 1.6 for SR II, 1.7 for ZB I

// cam specs inaccurate because VVT introduced for ZB II / VX

public node dodge_v10_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 220 * units.deg,
            gamma: 1.0,
            lift: 321 * 1.7 * units.thou,
            steps: 100
        );
}

public node dodge_v10_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 230 * units.deg,
            gamma: 1.0,
            lift: 321 * 1.7 * units.thou,
            steps: 100
        );
}

public node dodge_v10_camshaft_builder {
    input in_lobe_profile: dodge_v10_lobe_profile_int();
    input ex_lobe_profile: dodge_v10_lobe_profile_exh();
    input intake_lobe_profile: in_lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input lobe_separation: 114.0 * units.deg;
    input intake_lobe_center: lobe_separation;
    input exhaust_lobe_center: lobe_separation;
    input advance: 3.0 * units.deg;
    input base_radius: 0.75 * units.inch;

    output intake_cam_0: _intake_cam_0;
    output intake_cam_1: _intake_cam_1;
    output exhaust_cam_0: _exhaust_cam_0;
    output exhaust_cam_1: _exhaust_cam_1;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_0(params, lobe_profile: intake_lobe_profile)
    camshaft _intake_cam_1(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_0(params, lobe_profile: exhaust_lobe_profile)
    camshaft _exhaust_cam_1(params, lobe_profile: exhaust_lobe_profile)

	label rot30(30 * units.deg)
	label rot54(54 * units.deg)
	label rot60(60 * units.deg)
	label rot72(72 * units.deg)
    label rot90(90 * units.deg)
	label rot120(120 * units.deg)
    label rot360(360 * units.deg)
	
    _intake_cam_0
        .add_lobe(rot360 + intake_lobe_center + 0 * rot72) //  #1
        .add_lobe(rot360 + intake_lobe_center + 4 * rot72) //  #3
        .add_lobe(rot360 + intake_lobe_center + 6 * rot72) //  #5
        .add_lobe(rot360 + intake_lobe_center + 8 * rot72) //  #7
        .add_lobe(rot360 + intake_lobe_center + 2 * rot72) //  #9

    _intake_cam_1
        .add_lobe(rot360 + intake_lobe_center + 9 * rot72 - rot18) //  #2
        .add_lobe(rot360 + intake_lobe_center + 3 * rot72 - rot18) //  #4
        .add_lobe(rot360 + intake_lobe_center + 5 * rot72 - rot18) //  #6
        .add_lobe(rot360 + intake_lobe_center + 7 * rot72 - rot18) //  #8
        .add_lobe(rot360 + intake_lobe_center + 1 * rot72 - rot18) // #10

    _exhaust_cam_0
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot72)
        .add_lobe(rot360 - exhaust_lobe_center + 4 * rot72)
        .add_lobe(rot360 - exhaust_lobe_center + 6 * rot72)
        .add_lobe(rot360 - exhaust_lobe_center + 8 * rot72)
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot72)

    _exhaust_cam_1
        .add_lobe(rot360 - exhaust_lobe_center + 9 * rot72 - rot18)
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot72 - rot18)
        .add_lobe(rot360 - exhaust_lobe_center + 5 * rot72 - rot18)
        .add_lobe(rot360 - exhaust_lobe_center + 7 * rot72 - rot18)
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot72 - rot18)
}

public node dodge_v10_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 105.0 * units.cc;
    input flip_display: false;
	
	input flow_attenuation_in: 1.02;
	input flow_attenuation_ex: 1.02;
    input lift_scale: 50.0;
    alias output __out: head;

    function intake_flow(50 * units.thou)
    intake_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_in)
        .add_flow_sample( 1 * lift_scale,  25 * flow_attenuation_in)
        .add_flow_sample( 2 * lift_scale,  76 * flow_attenuation_in)
        .add_flow_sample( 3 * lift_scale, 100 * flow_attenuation_in)
        .add_flow_sample( 4 * lift_scale, 146 * flow_attenuation_in)
        .add_flow_sample( 5 * lift_scale, 175 * flow_attenuation_in)
        .add_flow_sample( 6 * lift_scale, 212 * flow_attenuation_in)
        .add_flow_sample( 7 * lift_scale, 230 * flow_attenuation_in)
        .add_flow_sample( 8 * lift_scale, 255 * flow_attenuation_in)
        .add_flow_sample( 9 * lift_scale, 275 * flow_attenuation_in)
        .add_flow_sample(10 * lift_scale, 294 * flow_attenuation_in)
        .add_flow_sample(11 * lift_scale, 300 * flow_attenuation_in)
        .add_flow_sample(12 * lift_scale, 314 * flow_attenuation_in)
        .add_flow_sample(13 * lift_scale, 314 * flow_attenuation_in)
        .add_flow_sample(14 * lift_scale, 314 * flow_attenuation_in)

    function exhaust_flow(50 * units.thou)
    exhaust_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_ex)
        .add_flow_sample( 1 * lift_scale,  25 * flow_attenuation_ex)
        .add_flow_sample( 2 * lift_scale,  70 * flow_attenuation_ex)
        .add_flow_sample( 3 * lift_scale, 100 * flow_attenuation_ex)
        .add_flow_sample( 4 * lift_scale, 132 * flow_attenuation_ex)
        .add_flow_sample( 5 * lift_scale, 140 * flow_attenuation_ex)
        .add_flow_sample( 6 * lift_scale, 156 * flow_attenuation_ex)
        .add_flow_sample( 7 * lift_scale, 170 * flow_attenuation_ex)
        .add_flow_sample( 8 * lift_scale, 181 * flow_attenuation_ex)
        .add_flow_sample( 9 * lift_scale, 191 * flow_attenuation_ex)
        .add_flow_sample(10 * lift_scale, 207 * flow_attenuation_ex)
        .add_flow_sample(11 * lift_scale, 214 * flow_attenuation_ex)
        .add_flow_sample(12 * lift_scale, 228 * flow_attenuation_ex)
        .add_flow_sample(13 * lift_scale, 228 * flow_attenuation_ex)
        .add_flow_sample(14 * lift_scale, 228 * flow_attenuation_ex)
		
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.2 * units.inch) * 2 * units.inch,
        intake_runner_cross_section_area: circle_area(1.2 * units.inch),
        exhaust_runner_volume: circle_area(0.85 * units.inch) * 2 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.85 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}

public node viper_v10 {
    alias output __out: engine;

    wires wires()

    engine engine(
        name: "Dodge Viper V10 (ZB II)",
        starter_torque: 200 * units.lb_ft,
		starter_speed: 900 * units.rpm,
        redline: 6000 * units.rpm,
        fuel: fuel(
            max_turbulence_effect: 4.0,
            burning_efficiency_randomness: 0.2,
            max_burning_efficiency: 0.68),
        throttle_gamma: 1.0,
		simulation_frequency: 7500
    )

    label bore(4.055 * units.inch)
	label bore_spacing(4.460 * units.inch)
    label stroke(3.960 * units.inch)
    label rod_length(6.135 * units.inch)
    label rod_mass(785 * units.g)
    label compression_height(1.220 * units.inch)
	label piston_mass(435 * units.g)
	label piston_dome(0.00 * units.cc)
	label deck_height(9.480 * units.inch)
    label crank_mass(75 * units.lb)
    label flywheel_mass(50 * units.lb)
    label flywheel_radius(6 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 20 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia:
            crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 45.0 * units.deg // 45 degrees back from right side
    )

    rod_journal rj0(angle:  0  * units.deg)
    rod_journal rj1(angle: 288 * units.deg)
    rod_journal rj2(angle: 72 * units.deg)
	rod_journal rj3(angle: 216 * units.deg)
    rod_journal rj4(angle: 144 * units.deg)

    c0
        .add_rod_journal(rj0)
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)

    piston_parameters piston_params(
        mass: piston_mass,
        //blowby: 0,
        compression_height: compression_height,
        wrist_pin_position: 0 * units.mm,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    intake intake(
        plenum_volume: 4.0 * units.L,
        plenum_cross_section_area: 200.0 * units.cm2,
        intake_flow_rate: k_carb(1650.0),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.997,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(800.0),
        runner_length: 6.0 * units.inch,
        velocity_decay: 0.1
    )
	
	label exhaust_radius((3.0 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(775.0),
        primary_tube_length: 20.0 * units.inch,
        primary_flow_rate: k_carb(600.0),
        velocity_decay: 1.0, //0.5
		collector_cross_section_area: circle_area(exhaust_radius),
		length: 1200 * units.mm,
		volume: (1200 * units.mm * circle_area(exhaust_radius))
    )

    exhaust_system exhaustL(
        es_params,
        audio_volume: 1.0 * 0.04,
        impulse_response: ir_lib.default_0
    )

    exhaust_system exhaustR(
        es_params,
        audio_volume: 1.0 * 0.04,
        impulse_response: ir_lib.default_0
    )
	
	label pipe_length_1(bore_spacing * 0.5)
	label pipe_length_2(bore_spacing * 0.3)
	label pipe_length_3(bore_spacing * 0.05)
	label pipe_length_4(bore_spacing * 0.0)
	label pipe_length_5(bore_spacing * 0.1)
	
	label exhaust_stagger(bore_spacing * 0.2)
	
	label exhaust_volume_R0(0.45)
	label exhaust_volume_R1(0.5)
	label exhaust_volume_L0(0.9)
	label exhaust_volume_L1(1.0)

    cylinder_bank b0(bank_params, angle: -45 * units.deg) // R (first)
    b0
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.15)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            ignition_wire: wires.wire01,
            intake: intake,
            exhaust_system: exhaustR,
			primary_length: pipe_length_1 + exhaust_stagger,
            sound_attenuation: exhaust_volume_R0
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.15)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire03,
            intake: intake,
            exhaust_system: exhaustR,
			primary_length: pipe_length_2 + exhaust_stagger,
			sound_attenuation: exhaust_volume_R1
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.15)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire05,
            intake: intake,
            exhaust_system: exhaustR,
			primary_length: pipe_length_3 + exhaust_stagger,
			sound_attenuation: exhaust_volume_R1
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.15)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire07,
            intake: intake,
            exhaust_system: exhaustR,
			primary_length: pipe_length_4 + exhaust_stagger,
			sound_attenuation: exhaust_volume_R1
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.15)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire09,
            intake: intake,
            exhaust_system: exhaustR,
			primary_length: pipe_length_5 + exhaust_stagger,
			sound_attenuation: exhaust_volume_R0
        )

    cylinder_bank b1(bank_params, angle: 45.0 * units.deg) // L (second)
    b1
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.15)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            ignition_wire: wires.wire02,
            intake: intake,
            exhaust_system: exhaustL,
			primary_length: pipe_length_1,
			sound_attenuation: exhaust_volume_L0
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.15)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire04,
            intake: intake,
            exhaust_system: exhaustL,
			primary_length: pipe_length_2,
			sound_attenuation: exhaust_volume_L1
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.15)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire06,
            intake: intake,
            exhaust_system: exhaustL,
			primary_length: pipe_length_3,
			sound_attenuation: exhaust_volume_L1
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.15)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire08,
            intake: intake,
            exhaust_system: exhaustL,
			primary_length: pipe_length_4,
			sound_attenuation: exhaust_volume_L1
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.15)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire10,
            intake: intake,
            exhaust_system: exhaustL,
			primary_length: pipe_length_5,
			sound_attenuation: exhaust_volume_L0
        )

    engine
        .add_cylinder_bank(b0)
        .add_cylinder_bank(b1)

    engine.add_crankshaft(c0)

    dodge_v10_camshaft_builder camshaft(
		in_lobe_profile: dodge_v10_lobe_profile_int(),
		ex_lobe_profile: dodge_v10_lobe_profile_exh()
	)

    b0.set_cylinder_head (
        dodge_v10_head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0,
            flip_display: false
        )
    )
    b1.set_cylinder_head (
        dodge_v10_head(
            intake_camshaft: camshaft.intake_cam_1,
            exhaust_camshaft: camshaft.exhaust_cam_1,
            flip_display: true
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 15 * units.deg)
        .add_sample(1000 * units.rpm, 42 * units.deg)
        .add_sample(2000 * units.rpm, 49 * units.deg)
        .add_sample(3000 * units.rpm, 50 * units.deg)
        .add_sample(4000 * units.rpm, 50 * units.deg)
        .add_sample(5000 * units.rpm, 50 * units.deg)
        .add_sample(6000 * units.rpm, 50 * units.deg)
		.add_sample(7000 * units.rpm, 50 * units.deg)

    engine.add_ignition_module(
        dodge_v10_distributor(
            wires: wires,
            timing_curve: timing_curve,
            rev_limit: 6250 * units.rpm
        )
    )
}

// 2008 Dodge Viper SRT-10 Coupe (ZB II)
public node dodge_viper_srt10_zb_vehicle {
    alias output __out:
        vehicle(
            mass: 3455 * units.lb,
            drag_coefficient: 0.38,
            cross_sectional_area: 1.79 * (units.m * units.m),
            diff_ratio: 3.07,
            tire_radius: 344 * units.mm, // P345/30ZR19
            rolling_resistance: 600 * units.N
        );
}

public node tremec_t56_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 700 * units.lb_ft
        )
        .add_gear(2.66)
        .add_gear(1.78)
        .add_gear(1.3)
        .add_gear(1.00)
        .add_gear(0.74)
        .add_gear(0.5);
}

public node main {
    set_engine(viper_v10())
    set_vehicle(dodge_viper_srt10_zb_vehicle())
    set_transmission(tremec_t56_transmission())
}

main()