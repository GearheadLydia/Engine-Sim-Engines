import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

// firing order: 18436572

public node gmc_v6_478m_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 3750 * units.rpm;
	input limiter_duration: 0.11;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0 / 6.0) * cycle)
            .connect_wire(wires.wire6, (1.0 / 6.0) * cycle)
            .connect_wire(wires.wire5, (2.0 / 6.0) * cycle)
            .connect_wire(wires.wire4, (3.0 / 6.0) * cycle)
            .connect_wire(wires.wire3, (4.0 / 6.0) * cycle)
            .connect_wire(wires.wire2, (5.0 / 6.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
}

// all cam specs are guesses
public node gmc_v6_478m_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 220 * units.deg,
            gamma: 1.0,
            lift: 350 * 1.5 * units.thou,
            steps: 100
        );
}

public node gmc_v6_478m_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 220 * units.deg,
            gamma: 1.0,
            lift: 350 * 1.5 * units.thou,
            steps: 100
        );
}

public node gmc_v6_478m_camshaft_builder {
    input in_lobe_profile: gmc_v6_478m_lobe_profile_int();
	input ex_lobe_profile: gmc_v6_478m_lobe_profile_exh();
    input intake_lobe_profile: in_lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input lobe_separation: 120.0 * units.deg;
    input intake_lobe_center: lobe_separation;
    input exhaust_lobe_center: lobe_separation;
    input advance: 0.0 * units.deg;
    input base_radius: 1.8 / 2 * units.inch;

    output intake_cam_0: _intake_cam_0;
    output exhaust_cam_0: _exhaust_cam_0;
    output intake_cam_1: _intake_cam_1;
    output exhaust_cam_1: _exhaust_cam_1;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_0(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_0(params, lobe_profile: exhaust_lobe_profile)

    camshaft _intake_cam_1(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_1(params, lobe_profile: exhaust_lobe_profile)


    label rot30(30 * units.deg)
	label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)
	
    _intake_cam_0
        .add_lobe(rot360 + intake_lobe_center + 0 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 4 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 2 * rot120)

    _exhaust_cam_0
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 4 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot120)

    _intake_cam_1
        .add_lobe(rot360 + intake_lobe_center + 5 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 3 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 1 * rot120)

    _exhaust_cam_1
        .add_lobe(rot360 - exhaust_lobe_center + 5 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot120)
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.mm, k_28inH2O(flow))
}

public node gmc_v6_478m_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 85.0 * units.cc; // guess based on Cadillac; Olds; Chrysler RB
    input flip_display: false;
	
	input flow_attenuation_in: 1.1;
	input flow_attenuation_ex: 1.1;
    input lift_scale: 1.0;
    alias output __out: head;

    function intake_flow(1.0 * units.mm)
    intake_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_in)
        .add_flow_sample( 1 * lift_scale,  25 * flow_attenuation_in)
        .add_flow_sample( 2 * lift_scale,  76 * flow_attenuation_in)
        .add_flow_sample( 3 * lift_scale, 100 * flow_attenuation_in)
        .add_flow_sample( 4 * lift_scale, 146 * flow_attenuation_in)
        .add_flow_sample( 5 * lift_scale, 175 * flow_attenuation_in)
        .add_flow_sample( 6 * lift_scale, 212 * flow_attenuation_in)
        .add_flow_sample( 7 * lift_scale, 230 * flow_attenuation_in)
        .add_flow_sample( 8 * lift_scale, 255 * flow_attenuation_in)
        .add_flow_sample( 9 * lift_scale, 275 * flow_attenuation_in)
        .add_flow_sample(10 * lift_scale, 294 * flow_attenuation_in)
        .add_flow_sample(11 * lift_scale, 300 * flow_attenuation_in)
        .add_flow_sample(12 * lift_scale, 314 * flow_attenuation_in)
        .add_flow_sample(13 * lift_scale, 314 * flow_attenuation_in)
        .add_flow_sample(14 * lift_scale, 314 * flow_attenuation_in)

    function exhaust_flow(1.0 * units.mm)
    exhaust_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_ex)
        .add_flow_sample( 1 * lift_scale,  25 * flow_attenuation_ex)
        .add_flow_sample( 2 * lift_scale,  70 * flow_attenuation_ex)
        .add_flow_sample( 3 * lift_scale, 100 * flow_attenuation_ex)
        .add_flow_sample( 4 * lift_scale, 132 * flow_attenuation_ex)
        .add_flow_sample( 5 * lift_scale, 140 * flow_attenuation_ex)
        .add_flow_sample( 6 * lift_scale, 156 * flow_attenuation_ex)
        .add_flow_sample( 7 * lift_scale, 170 * flow_attenuation_ex)
        .add_flow_sample( 8 * lift_scale, 181 * flow_attenuation_ex)
        .add_flow_sample( 9 * lift_scale, 191 * flow_attenuation_ex)
        .add_flow_sample(10 * lift_scale, 207 * flow_attenuation_ex)
        .add_flow_sample(11 * lift_scale, 214 * flow_attenuation_ex)
        .add_flow_sample(12 * lift_scale, 228 * flow_attenuation_ex)
        .add_flow_sample(13 * lift_scale, 228 * flow_attenuation_ex)
        .add_flow_sample(14 * lift_scale, 228 * flow_attenuation_ex)
		
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.4 * units.inch) * 5 * units.inch,
        intake_runner_cross_section_area: circle_area(1.4 * units.inch),
        exhaust_runner_volume: circle_area(1.0 * units.inch) * 5 * units.inch,
        exhaust_runner_cross_section_area: circle_area(1.0 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}


public node gmc_v6_478m_engine {
    alias output __out: engine;

    wires wires()

    engine engine(
        name: "GMC Big Block V6",
        starter_torque: 250 * units.lb_ft,
		starter_speed: 500 * units.rpm,
        redline: 3500 * units.rpm,
        throttle_gamma: 1.0,
		hf_gain: 0.05,
		jitter: 0.5
    )

	// https://bobistheoilguy.com/forums/threads/oil-recommend-1961-gmc-305d-v6.165855/page-2
	// https://bangshift.com/forum/forum/bangshift/general-discussion/13963-478-cu-in-v6/page3
    label bore(5.125 * units.inch)
	label bore_spacing(7.24 * units.inch) // visual estimate from photographs
    label stroke(3.86 * units.inch)
    label rod_length(7.185 * units.inch) // correct?
    label rod_mass(900 * units.g)
    label compression_height(2.200 * units.inch) // guess; 0.285" deck clearance
	label piston_mass(600 * units.g)
	label piston_dome(-3.00 * units.cc) // for 7.5:1 CR
	label deck_height((11.600 * units.inch) + (48 * units.thou)) // gasket probably correct, deck height numbers vary
    label crank_mass(120 * units.lb)
    label flywheel_mass(50 * units.lb)
    label flywheel_radius(9 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 20 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia:
            crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 60.0 * units.deg // -30 degrees from top
    )

    rod_journal rj0(angle: -60 * units.deg)
    rod_journal rj1(angle:  0  * units.deg)
    rod_journal rj2(angle:  60 * units.deg)
    rod_journal rj3(angle: 120 * units.deg)
    rod_journal rj4(angle: 180 * units.deg)
    rod_journal rj5(angle: 240 * units.deg)

    c0
        .add_rod_journal(rj0)
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)
        .add_rod_journal(rj5)


    piston_parameters piston_params(
        mass: piston_mass,
        //blowby: 0,
        compression_height: compression_height,
        wrist_pin_position: 0 * units.mm,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    intake intake(
        plenum_volume: 1.0 * units.L,
        plenum_cross_section_area: circle_area(60 * units.mm),
        intake_flow_rate: k_carb(350),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.991,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(800.0),
        runner_length: 12.0 * units.inch,
        velocity_decay: 0.1
    )

	label exhaust_radius((2.5 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(270.0),
        primary_tube_length: 8.0 * units.inch,
        primary_flow_rate: k_carb(550.0),
        velocity_decay: 1.0, //0.5
		collector_cross_section_area: circle_area(exhaust_radius) * 2,
		length: 3000 * units.mm,
		volume: (2500 * units.mm * circle_area(exhaust_radius)) + (500 * units.mm * circle_area(exhaust_radius) * 2)
    )

    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.01, impulse_response: ir_lib.default_0)
	
	label pipe_length_1(bore_spacing * 2)
	label pipe_length_2(bore_spacing * 1)
	label pipe_length_3(bore_spacing * 0)
	
	label exhaust_stagger(bore_spacing * 0.5)
	label exhaust_wobble(bore_spacing * 0.4)

    cylinder_bank b1(bank_params, angle: 30 * units.deg) // L (first)
    b1
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.6)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            ignition_wire: wires.wire2,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: pipe_length_1 + exhaust_stagger
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.4)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire4,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: pipe_length_2 + exhaust_stagger + exhaust_wobble
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.6)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire6,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: pipe_length_3 + exhaust_stagger
        )

    cylinder_bank b0(bank_params, angle: -30 * units.deg) // R (second)
    b0
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.6)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire1,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: pipe_length_1
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.4)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire3,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: pipe_length_2 - exhaust_wobble
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.6)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj5,
            ignition_wire: wires.wire5,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: pipe_length_3
        )

    engine
        .add_cylinder_bank(b0)
        .add_cylinder_bank(b1)

    engine.add_crankshaft(c0)

    gmc_v6_478m_camshaft_builder camshaft(
	    in_lobe_profile: gmc_v6_478m_lobe_profile_int(),
		ex_lobe_profile: gmc_v6_478m_lobe_profile_exh()
	)

    b0.set_cylinder_head (
        gmc_v6_478m_head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0,
			flip_display: false
        )
    )
    b1.set_cylinder_head (
        gmc_v6_478m_head(
            intake_camshaft: camshaft.intake_cam_1,
            exhaust_camshaft: camshaft.exhaust_cam_1,
            flip_display: true
        )
    )

	// only power figures I found were net anyway
    function timing_curve_net(1000 * units.rpm)
    timing_curve_net
        .add_sample(0000 * units.rpm, 48 * units.deg)
        .add_sample(1000 * units.rpm, 53 * units.deg)
        .add_sample(2000 * units.rpm, 53 * units.deg)
        .add_sample(3000 * units.rpm, 53 * units.deg)
        .add_sample(4000 * units.rpm, 53 * units.deg)

    engine.add_ignition_module(
        gmc_v6_478m_distributor(
            wires: wires,
            timing_curve: timing_curve_net
        )
    )
}

// 1966 GMC 2500 truck
// most specs very estimated
// https://www.xr793.com/wp-content/uploads/2021/08/1966-Chevrolet-50-to-80-Truck.pdf
// http://www.6066gmcguy.com/GMCID1.html
// http://www.6066gmcguy.com/DriveAxle.html
public node gmc_2500_pickup_vehicle {
    alias output __out:
        vehicle(
            mass: (3925+560) * units.lb,
            drag_coefficient: 0.6,
            cross_sectional_area: 4.3 * (units.m) * (units.m),
            diff_ratio: 4.57,
            tire_radius: 400 * units.mm, // guess
            rolling_resistance: (3925+560) * units.lb * 9.81 * (200.0/275.0) * 0.01
        );
}

public node muncie_sm420_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 600 * units.lb_ft
        )
        .add_gear(7.06)
        .add_gear(3.58)
        .add_gear(1.71)
        .add_gear(1.00);
}

public node muncie_sm465_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 600 * units.lb_ft
        )
        .add_gear(6.54)
        .add_gear(3.57)
        .add_gear(1.70)
        .add_gear(1.00);
}

public node main {
    set_engine(gmc_v6_478m_engine())
    set_vehicle(gmc_2500_pickup_vehicle())
    set_transmission(muncie_sm420_transmission())
}

main()