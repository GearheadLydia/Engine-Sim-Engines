import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

public node toyota_2jz_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 7200 * units.rpm;
    input limiter_duration: 0.08;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0/6.0) * cycle)
            .connect_wire(wires.wire5, (1.0/6.0) * cycle)
            .connect_wire(wires.wire3, (2.0/6.0) * cycle)
            .connect_wire(wires.wire6, (3.0/6.0) * cycle)
            .connect_wire(wires.wire2, (4.0/6.0) * cycle)
            .connect_wire(wires.wire4, (5.0/6.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
}

private node add_sym_sample {
    input angle;
    input lift;
    input this;
    alias output __out: this;

    this.add_sample(angle * units.deg, lift * units.thou)
    this.add_sample(-angle * units.deg, lift * units.thou)
}

// https://www.eatsleepboost.lt/tech-info/jz-tech-info/1jz-2jz-cam-specs/
public node toyota_2jz_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 200 * units.deg, // technically at 1mm; 233 adv
            gamma: 1.0,
            lift: 8.25 * units.mm,
            steps: 100
        );
}

public node toyota_2jz_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 205 * units.deg, // technically at 1mm; 233 adv
            gamma: 1.0,
            lift: 8.25 * units.mm,
            steps: 100
        );
}

// lobe separation is a guess
public node toyota_2jz_camshaft_builder {
    input lobe_profile: toyota_2jz_lobe_profile_int();
    input ex_lobe_profile: toyota_2jz_lobe_profile_exh();
    input intake_lobe_profile: lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input intake_lobe_center: 120.0 * units.deg;
    input exhaust_lobe_center: 120.0 * units.deg;
    input advance: 0.0 * units.deg;
    input base_radius: 16.0 * units.mm;

    output intake_cam_0: _intake_cam_0;
    output exhaust_cam_0: _exhaust_cam_0;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_0(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_0(params, lobe_profile: exhaust_lobe_profile)


    label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_0
        .add_lobe(rot360 + intake_lobe_center)
        .add_lobe(rot360 + intake_lobe_center + 4 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 2 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 5 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 1 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 3 * rot120)

    _exhaust_cam_0
        .add_lobe(rot360 - exhaust_lobe_center)
        .add_lobe(rot360 - exhaust_lobe_center + 4 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 5 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot120)
}

public node toyota_2jz_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 44.5 * units.cc;
    input flip_display: false;
    
    alias output __out: head;
    
    function intake_flow(50 * units.thou)
    intake_flow
        .add_flow_sample(  0,   0)
        .add_flow_sample( 50,  44) // interp
        .add_flow_sample(100,  82)
        .add_flow_sample(150, 130)
        .add_flow_sample(200, 163)
        .add_flow_sample(250, 191)
        .add_flow_sample(300, 211)
        .add_flow_sample(350, 220)
        .add_flow_sample(400, 224)
        .add_flow_sample(450, 228)
        .add_flow_sample(500, 230) // interp
        .add_flow_sample(550, 232)
        .add_flow_sample(600, 233) // interp

    function exhaust_flow(50 * units.thou)
    exhaust_flow
        .add_flow_sample(  0,   0)
        .add_flow_sample( 50,  32) // interp
        .add_flow_sample(100,  64)
        .add_flow_sample(150,  97)
        .add_flow_sample(200, 126)
        .add_flow_sample(250, 135)
        .add_flow_sample(300, 142)
        .add_flow_sample(350, 144)
        .add_flow_sample(400, 147)
        .add_flow_sample(450, 150)
        .add_flow_sample(500, 152)
        .add_flow_sample(550, 152)
        .add_flow_sample(600, 151)

        
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume:              circle_area(33.6 * units.mm / 2) * 2 * (2 * units.inch),
        intake_runner_cross_section_area:  circle_area(33.6 * units.mm / 2) * 2,
        exhaust_runner_volume:             circle_area(29.0 * units.mm / 2) * 2 * (2 * units.inch),
        exhaust_runner_cross_section_area: circle_area(29.0 * units.mm / 2) * 2,

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}

// https://www.supraforums.com/threads/all-piston-and-rod-weights-here.595763/
public node toyota_2jzgte_engine {
    alias output __out: engine;

    engine engine(
        name: "Toyota 2JZ-GTE",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 1200 * units.rpm,
        redline: 6800 * units.rpm,
        fuel: fuel(max_burning_efficiency: 0.95), // free power to supplant non-available turbo
        throttle_gamma: 1.0
    )
    
    label lean_angle(0 * units.deg)

    label bore(86.0 * units.mm)
    label stroke(86.0 * units.mm)
    label rod_length(142.0 * units.mm)
    label rod_mass(800 * units.g)
    label piston_mass((407+132) * units.g)
    label compression_height(34.0 * units.mm)
    label piston_dome(-14.2 * units.cc)
    label deck_height((219.0 + 1.3) * units.mm)
    label crank_mass(59.5 * units.lb)
    label flywheel_mass(20 * units.lb)
    label flywheel_radius(6 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 15 * units.kg, radius: 8.0 * units.cm)
    )

    wires wires()

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia: crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 90 * units.deg + lean_angle
    )

    rod_journal rj1(angle:  0  * units.deg)
    rod_journal rj2(angle: 120 * units.deg)
    rod_journal rj3(angle: 240 * units.deg)
    rod_journal rj4(angle: 240 * units.deg)
    rod_journal rj5(angle: 120 * units.deg)
    rod_journal rj6(angle:  0  * units.deg)
    
    c0
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)
        .add_rod_journal(rj5)
        .add_rod_journal(rj6)

    piston_parameters piston_params(
        mass: piston_mass,
        //blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    intake intake(
        plenum_volume: 4.0 * units.L,
        plenum_cross_section_area: 50 * units.cm2,
        intake_flow_rate: k_carb(450.0),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.9955,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(220.0),
        runner_length: 5.0 * units.inch,
        velocity_decay: 0.1
    )
    
    label exhaust_radius((3.5 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(450.0),
        primary_tube_length: 2.0 * units.inch,
        primary_flow_rate: k_carb(380.0),
        velocity_decay: 1.5,
        collector_cross_section_area: circle_area(exhaust_radius),
        length: 2800 * units.mm,
        volume: 2800 * units.mm * circle_area(exhaust_radius)
    )

    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.1, impulse_response: ir_lib.default_0)
 
    cylinder_bank b0(bank_params, angle: 0 * units.deg + lean_angle)
    b0
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire1,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: 3.2 * units.inch
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire2,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: 0.4 * units.inch,
            sound_attenuation: 0.9
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire3,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: 2.8 * units.inch
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire4,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: 2.4 * units.inch
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj5,
            ignition_wire: wires.wire5,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: 0.0 * units.inch,
            sound_attenuation: 0.9
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.3)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj6,
            ignition_wire: wires.wire6,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: 2.8 * units.inch
        )

    engine
        .add_cylinder_bank(b0)

    engine.add_crankshaft(c0)

    toyota_2jz_camshaft_builder camshaft(
        lobe_profile: toyota_2jz_lobe_profile_int(),
        ex_lobe_profile: toyota_2jz_lobe_profile_exh()
    )

    b0.set_cylinder_head (
        toyota_2jz_head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0,
            flip_display: true
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 55 * units.deg)
        .add_sample(1000 * units.rpm, 68 * units.deg)
        .add_sample(2000 * units.rpm, 70 * units.deg)
        .add_sample(3000 * units.rpm, 68 * units.deg)
        .add_sample(4000 * units.rpm, 63 * units.deg)
        .add_sample(5000 * units.rpm, 58 * units.deg)
        .add_sample(6000 * units.rpm, 53 * units.deg)
        .add_sample(7000 * units.rpm, 46 * units.deg)
        .add_sample(8000 * units.rpm, 46 * units.deg)

    engine.add_ignition_module(
        toyota_2jz_distributor(
            wires: wires,
            timing_curve: timing_curve
        ))
}

// 1997 Toyota Supra Turbo
public node toyota_supra_turbo_jza80_vehicle {
    alias output __out:
        vehicle(
            mass: 1590 * units.kg,
            drag_coefficient: 0.34,
            cross_sectional_area: 2.02 * (units.m * units.m), // fastestlaps.com
            diff_ratio: 3.133, // USDM; JDM: 3.266
            tire_radius: 317.5 * units.mm, // 255/40R17
            rolling_resistance: 500 * units.N
        );
}

public node toyota_v160_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 1200 * units.lb_ft
        )
        .add_gear(3.827)
        .add_gear(2.360)
        .add_gear(1.685)
        .add_gear(1.312)
        .add_gear(1.000)
        .add_gear(0.793);
}

public node toyota_v161_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 1200 * units.lb_ft
        )
        .add_gear(3.724)
        .add_gear(2.246)
        .add_gear(1.541)
        .add_gear(1.205)
        .add_gear(1.000)
        .add_gear(0.818);
}

public node main {
    set_engine(toyota_2jzgte_engine())
    set_vehicle(toyota_supra_turbo_jza80_vehicle())
    set_transmission(toyota_v160_transmission())
}

main()