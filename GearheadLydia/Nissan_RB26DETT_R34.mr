import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

public node nissan_rb26_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 8250 * units.rpm;
    input limiter_duration: 0.08;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0/6.0) * cycle)
            .connect_wire(wires.wire5, (1.0/6.0) * cycle)
            .connect_wire(wires.wire3, (2.0/6.0) * cycle)
            .connect_wire(wires.wire6, (3.0/6.0) * cycle)
            .connect_wire(wires.wire2, (4.0/6.0) * cycle)
            .connect_wire(wires.wire4, (5.0/6.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
}

public node nissan_rb26_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 240 * units.deg,
            gamma: 1.0,
            lift: 8.58 * units.mm,
            steps: 100
        );
}

public node nissan_rb26_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 236 * units.deg,
            gamma: 1.0,
            lift: 8.28 * units.mm,
            steps: 100
        );
}

// BNR32: 113 in / 125 ex
// BCNR33: 113 in / 120 ex
// BNR34: 117 in / 121 ex

public node nissan_camshaft_builder {
    input in_lobe_profile: nissan_rb26_lobe_profile_int();
    input ex_lobe_profile: nissan_rb26_lobe_profile_exh();
    input intake_lobe_profile: in_lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input intake_lobe_center: 117.0 * units.deg;
    input exhaust_lobe_center: 121.0 * units.deg;
    input advance: 0.0 * units.deg;
    input base_radius: 16.0 * units.mm;

    output intake_cam_0: _intake_cam_0;
    output exhaust_cam_0: _exhaust_cam_0;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_0(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_0(params, lobe_profile: exhaust_lobe_profile)


    label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_0
        .add_lobe(rot360 + intake_lobe_center)
        .add_lobe(rot360 + intake_lobe_center + 4 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 2 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 5 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 1 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 3 * rot120)

    _exhaust_cam_0
        .add_lobe(rot360 - exhaust_lobe_center)
        .add_lobe(rot360 - exhaust_lobe_center + 4 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 5 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot120)
}

label throttlebody_radius(20 * units.mm)

// intake valve diameter:  35.60mm
// exhaust valve diameter: 31.15mm
// throttle body diameter: 45   mm

public node nissan_rb26_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 64.5 * units.cc;
    input flip_display: false;
    
    alias output __out: head;
    
    // https://dsportmag.com/the-cars/part-11-ported-and-flowed-part-2-gt-r/2/
    // https://dsportmag.com/wp-content/uploads/022-003-ProjectCars-ProjectGTR-11-FlowChart.jpg
    function intake_flow(50 * units.thou)
    intake_flow
        .add_flow_sample(  0,   0.0)
        .add_flow_sample( 50,  43.7)
        .add_flow_sample(100,  87.8)
        .add_flow_sample(150, 125.7)
        .add_flow_sample(200, 164.5)
        .add_flow_sample(250, 195.0)
        .add_flow_sample(300, 217.7)
        .add_flow_sample(350, 231.6)
        .add_flow_sample(400, 243.1)
        .add_flow_sample(450, 248.7)

    function exhaust_flow(50 * units.thou)
    exhaust_flow
        .add_flow_sample(  0,   0.0)
        .add_flow_sample( 50,  35.0)
        .add_flow_sample(100,  73.6)
        .add_flow_sample(150, 105.9)
        .add_flow_sample(200, 127.4)
        .add_flow_sample(250, 138.0)
        .add_flow_sample(300, 142.3)
        .add_flow_sample(350, 145.4)
        .add_flow_sample(400, 147.0)
        .add_flow_sample(450, 148.4)

        
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume:              circle_area(35.60 * units.mm / 2) * 2 * (2 * units.inch),
        intake_runner_cross_section_area:  circle_area(35.60 * units.mm / 2) * 2,
        exhaust_runner_volume:             circle_area(31.15 * units.mm / 2) * 2 * (2 * units.inch),
        exhaust_runner_cross_section_area: circle_area(31.15 * units.mm / 2) * 2,

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}

// deprecated
public node nissan_rb26_itb {
    input throttlebody_cfm: 200.0;
    input idle_flow_rate_cfm: 0.001;
    input idle_throttle_plate_position: 0.99975;
    input throttle_gamma: 6.0;

    alias output __out: intake;

    intake intake(
        plenum_volume: circle_area(45 * units.mm / 2) * (2 * units.inch),
        plenum_cross_section_area: circle_area(45 * units.mm / 2),
        intake_flow_rate: k_carb(throttlebody_cfm),
        idle_flow_rate: k_carb(idle_flow_rate_cfm),
        idle_throttle_plate_position: idle_throttle_plate_position,
        throttle_gamma: throttle_gamma,
        runner_flow_rate: k_carb(220.0),
        runner_length: 2.0 * units.inch,
        velocity_decay: 0.1
    )
}

// https://tomeiusa.com/tool/stock_engine_data.html
public node nissan_rb26dett_engine {
    alias output __out: engine;

    engine engine(
        name: "Nissan RB26DETT",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 1200 * units.rpm,
        redline: 8000 * units.rpm,
        fuel: fuel(max_burning_efficiency: 1.30), // free power to simulate unavailable turbo
        throttle_gamma: 1.0
    )
    
    label lean_angle(-10 * units.deg)

    label bore(86.0 * units.mm)
    label bore_spacing(96.5 * units.mm)
    label stroke(73.7 * units.mm)
    label rod_length(121.49 * units.mm)
    label rod_mass(495 * units.g)            // various
    label piston_mass(372 * units.g)         // Mahle
    label compression_height(30.00 * units.mm)
    label deck_height((188.5 + 1.2) * units.mm)
    label piston_dome(15.5 * units.cc)
    label crank_mass(23.5 * units.kg)        // Tomei
    label flywheel_mass(8.5 * units.kg)      // JUN
    label flywheel_radius(7.25 * units.inch) // Tenaci

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 15 * units.kg, radius: 8.0 * units.cm)
    )

    wires wires()

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia: crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 90 * units.deg + lean_angle
    )

    rod_journal rj1(angle:  0  * units.deg)
    rod_journal rj2(angle: 120 * units.deg)
    rod_journal rj3(angle: 240 * units.deg)
    rod_journal rj4(angle: 240 * units.deg)
    rod_journal rj5(angle: 120 * units.deg)
    rod_journal rj6(angle:  0  * units.deg)
    
    c0
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)
        .add_rod_journal(rj5)
        .add_rod_journal(rj6)

    piston_parameters piston_params(
        mass: piston_mass,
        blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    // https://www.tf-works.com/nismo-intake-manifold-rb26dett-skyline-gt-r-bnr32-bcnr33-bnr34/
    intake intake(
        plenum_volume: 3.4 * units.L,
        plenum_cross_section_area: 58.7 * units.cm2, // guesstimate: 3.4L divided by (bore spacing * 6)
        intake_flow_rate: k_carb(800.0),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.9987,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(220.0),
        runner_length: 48.5 * units.mm,
        velocity_decay: 0.1
    )
    
    //nissan_rb26_itb intake1()
    //nissan_rb26_itb intake2()
    //nissan_rb26_itb intake3()
    //nissan_rb26_itb intake4()
    //nissan_rb26_itb intake5()
    //nissan_rb26_itb intake6()
    
    label exhaust_radius((4.0 * units.inch)/2)
    
    // sedan: 259cm + 50cm midpipe; "15-20cm difference"
    // https://www.sau.com.au/forums/topic/327048-exhaust-system-lengths-on-r34-gtt-sedan/
    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(600.0),
        primary_tube_length: 2.0 * units.inch,
        primary_flow_rate: k_carb(380.0),
        velocity_decay: 1.3,
        collector_cross_section_area: circle_area(exhaust_radius) * 2,
        length: (245 + 50) * units.cm,
        volume: (245 * units.cm * circle_area(exhaust_radius)) + (50 * units.cm * circle_area(exhaust_radius) * 2)
    )

    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.05, impulse_response: ir_lib.default_0)
    
    label exhaust_scale(0.5)
    
    label manifold_length_1(0.9 * bore_spacing * exhaust_scale)
    label manifold_length_2(0.0 * bore_spacing * exhaust_scale)
    label manifold_length_3(0.7 * bore_spacing * exhaust_scale)
    
    label front_pipe_difference(6 * units.inch * exhaust_scale)
 
    cylinder_bank b0(bank_params, angle: 0 * units.deg + lean_angle)
    b0
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire1,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: manifold_length_1 + front_pipe_difference,
            sound_attenuation: 1.0
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire2,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: manifold_length_2 + front_pipe_difference,
            sound_attenuation: 0.9
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire3,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: manifold_length_3 + front_pipe_difference,
            sound_attenuation: 1.0
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire4,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: manifold_length_1,
            sound_attenuation: 1.0 * 0.85
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj5,
            ignition_wire: wires.wire5,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: manifold_length_2,
            sound_attenuation: 0.9 * 0.85
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj6,
            ignition_wire: wires.wire6,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: manifold_length_3,
            sound_attenuation: 1.0 * 0.85
        )

    engine
        .add_cylinder_bank(b0)

    engine.add_crankshaft(c0)

    nissan_camshaft_builder camshaft(
        in_lobe_profile: nissan_rb26_lobe_profile_int(),
        ex_lobe_profile: nissan_rb26_lobe_profile_exh()
    )

    b0.set_cylinder_head (
        nissan_rb26_head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 50 * units.deg)
        .add_sample(1000 * units.rpm, 65 * units.deg)
        .add_sample(2000 * units.rpm, 70 * units.deg)
        .add_sample(3000 * units.rpm, 73 * units.deg)
        .add_sample(4000 * units.rpm, 70 * units.deg)
        .add_sample(5000 * units.rpm, 64 * units.deg)
        .add_sample(6000 * units.rpm, 56 * units.deg)
        .add_sample(7000 * units.rpm, 49 * units.deg)
        .add_sample(8000 * units.rpm, 44 * units.deg)
        .add_sample(9000 * units.rpm, 44 * units.deg)

    engine.add_ignition_module(
        nissan_rb26_distributor(
            wires: wires,
            timing_curve: timing_curve
        ))
}

// 1999 Nissan Skyline GT-R V-Spec (BNR34)
public node nissan_skyline_gtr_bnr34_vehicle {
    alias output __out:
        vehicle(
            mass: 1580 * units.kg,
            drag_coefficient: 0.34,
            cross_sectional_area: 2.4276 * (units.m * units.m),
            diff_ratio: 3.545,
            tire_radius: 326.4 * units.mm, // 245/40R18
            rolling_resistance: 1580 * units.kg * 9.81 * (245.0/275.0) * 0.01
        );
}

// https://www.gtr.co.uk/threads/power-limits-of-stock-clutches-and-gears.172765/
public node getrag_233_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 650 * units.lb_ft // observed
        )
        .add_gear(3.827)
        .add_gear(2.360)
        .add_gear(1.685)
        .add_gear(1.312)
        .add_gear(1.000)
        .add_gear(0.793);
}

public node main {
    set_engine(nissan_rb26dett_engine())
    set_vehicle(nissan_skyline_gtr_bnr34_vehicle())
    set_transmission(getrag_233_transmission())
}

main()