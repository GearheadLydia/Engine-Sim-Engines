import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

// firing order: 18726543

public node chevrolet_ls2_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 6500 * units.rpm;
    input limiter_duration: 0.09;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0 / 8.0) * cycle)
            .connect_wire(wires.wire8, (1.0 / 8.0) * cycle)
            .connect_wire(wires.wire7, (2.0 / 8.0) * cycle)
            .connect_wire(wires.wire2, (3.0 / 8.0) * cycle)
            .connect_wire(wires.wire6, (4.0 / 8.0) * cycle)
            .connect_wire(wires.wire5, (5.0 / 8.0) * cycle)
            .connect_wire(wires.wire4, (6.0 / 8.0) * cycle)
            .connect_wire(wires.wire3, (7.0 / 8.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
    output wire7: ignition_wire();
    output wire8: ignition_wire();
}


// https://www.onallcylinders.com/2017/12/07/ls2-car-engine-specs-performance-bore-stroke-cylinder-heads-cam-specs-more/

public node chevrolet_ls2_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 204 * units.deg,
            gamma: 1.0,
            lift: 520 * units.thou,
            steps: 100
        );
}

public node chevrolet_ls2_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 213 * units.deg,
            gamma: 1.0,
            lift: 521 * units.thou,
            steps: 100
        );
}

public node chevrolet_ls2_camshaft_builder {
    input in_lobe_profile: chevrolet_ls2_lobe_profile_int();
    input ex_lobe_profile: chevrolet_ls2_lobe_profile_exh();
    input intake_lobe_profile: in_lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input lobe_separation: 116.0 * units.deg;
    input intake_lobe_center: lobe_separation;
    input exhaust_lobe_center: lobe_separation;
    input advance: -4.0 * units.deg;
    input base_radius: 1.462 / 2 * units.inch;

    output intake_cam_R: _intake_cam_R;
    output exhaust_cam_R: _exhaust_cam_R;
    output intake_cam_L: _intake_cam_L;
    output exhaust_cam_L: _exhaust_cam_L;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_R(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_R(params, lobe_profile: exhaust_lobe_profile)

    camshaft _intake_cam_L(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_L(params, lobe_profile: exhaust_lobe_profile)


    label rot30(30 * units.deg)
    label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_R
        .add_lobe(rot360 + intake_lobe_center + 0 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 7 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 5 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 2 * rot90)

    _intake_cam_L
        .add_lobe(rot360 + intake_lobe_center + 3 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 6 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 4 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 1 * rot90)

    _exhaust_cam_R
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 7 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 5 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot90)

    _exhaust_cam_L
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 6 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 4 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot90)
}

public node chevrolet_ls2_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 65.0 * units.cc;
    input flip_display: false;
    
    alias output __out: head;
    
    // https://sites.google.com/site/sloppywiki/everything-ls/lsx-head-cast-numbers-flow-swap-info
    function intake_flow(100 * units.thou)
    intake_flow
        .add_flow_sample(  0,   0)
        .add_flow_sample(100,  67)
        .add_flow_sample(200, 126)
        .add_flow_sample(300, 184)
        .add_flow_sample(400, 224)
        .add_flow_sample(500, 251)
        .add_flow_sample(600, 257)

    function exhaust_flow(100 * units.thou)
    exhaust_flow
        .add_flow_sample(  0,   0)
        .add_flow_sample(100,  57)
        .add_flow_sample(200, 108)
        .add_flow_sample(300, 143)
        .add_flow_sample(400, 163)
        .add_flow_sample(500, 176)
        .add_flow_sample(600, 183)
        
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume:  210 * units.cc,
        intake_runner_cross_section_area:  circle_area(2.00 * units.inch / 2),
        exhaust_runner_volume:  75 * units.cc,
        exhaust_runner_cross_section_area: circle_area(1.55 * units.inch / 2),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}


public node chevrolet_ls2 {
    alias output __out: engine;

    wires wires()

    engine engine(
        name: "Chevrolet LS2",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 1000 * units.rpm,
        redline: 6000 * units.rpm,
        throttle_gamma: 1.0,
        // jitter: 0.7,
        // hf_gain: 0.15
        jitter: 0.6,
        hf_gain: 0.1
        // jitter: 0.1,
        // hf_gain: 0.1
    )


    // source: https://help.summitracing.com/app/answers/detail/a_id/4874/~/chevy-ls2-engine-specs
    label bore(4.000 * units.inch)
    label bore_spacing(4.4 * units.inch)
    label stroke(3.622 * units.inch)
    label rod_length(6.098 * units.inch)
    label rod_mass(464 * units.g)
    label compression_height(1.34 * units.inch)
    label piston_mass(482 * units.g)
    label piston_dome(0.00 * units.cc)
    label deck_height(9.240 * units.inch)
    label crank_mass(57 * units.lb)
    label flywheel_mass(15 * units.lb)
    label flywheel_radius(6 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 20 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia:
            crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 45.0 * units.deg // 45 degrees back from right side
    )

    rod_journal rj1(angle:   0 * units.deg)
    rod_journal rj2(angle: 270 * units.deg)
    rod_journal rj3(angle:  90 * units.deg)
    rod_journal rj4(angle: 180 * units.deg)

    c0
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)


    piston_parameters piston_params(
        mass: piston_mass,
        blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0 * units.mm,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    intake intake(
        plenum_volume: 0.3 * units.L,
        plenum_cross_section_area: circle_area(45 * units.mm),
        intake_flow_rate: k_carb(750.0),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.996,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(250.0),
        runner_length: 8.0 * units.inch,
        velocity_decay: 0.1
    )
    
    label exhaust_radius((4.0 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(600.0),
        primary_tube_length: 10.0 * units.inch,
        primary_flow_rate: k_carb(300.0),
        velocity_decay: 1.0, //0.5
        collector_cross_section_area: circle_area(exhaust_radius),
        length: 2600 * units.mm,
        volume: 2600 * units.mm * circle_area(exhaust_radius)
    )

    exhaust_system exhaustL(es_params, audio_volume: 1.0 * 0.01, impulse_response: ir_lib.default_0)
    exhaust_system exhaustR(es_params, audio_volume: 1.0 * 0.01, impulse_response: ir_lib.default_0)
    
    // lower grade LS headers //
    // label pipe_length_1(bore_spacing * 2.2)
    // label pipe_length_2(bore_spacing * 1.2)
    // label pipe_length_3(bore_spacing * 0.2)
    // label pipe_length_4(bore_spacing * 0.0)
    // label pipe_volume_1(0.6)
    // label pipe_volume_2(0.8)
    // label pipe_volume_3(1.0)
    // label pipe_volume_4(1.0)
    
    // GTO LS2 headers //
    // label pipe_length_1(bore_spacing * 0.0)
    // label pipe_length_2(bore_spacing * 0.1)
    // label pipe_length_3(bore_spacing * 0.2)
    // label pipe_length_4(bore_spacing * 0.3)
    // label pipe_volume_1(1.0)
    // label pipe_volume_2(1.0)
    // label pipe_volume_3(1.0)
    // label pipe_volume_4(1.0)
    
    // stock Z06 headers //
    label pipe_length_1(bore_spacing * 1.2)
    label pipe_length_2(bore_spacing * 0.8)
    label pipe_length_3(bore_spacing * 0.75)
    label pipe_length_4(bore_spacing * 1.15)
    label pipe_volume_1(0.8)
    label pipe_volume_2(1.0)
    label pipe_volume_3(1.0)
    label pipe_volume_4(0.8)
    
    // Kooks headers
    // label pipe_length_1(bore_spacing * 1.0)
    // label pipe_length_2(bore_spacing * 1.3)
    // label pipe_length_3(bore_spacing * 1.0)
    // label pipe_length_4(bore_spacing * 0.7)
    // label pipe_volume_1(0.95)
    // label pipe_volume_2(0.90)
    // label pipe_volume_3(0.95)
    // label pipe_volume_4(1.00)
    
    label exhaust_stagger(80 * units.mm)

    cylinder_bank bR(bank_params, angle: -45.0 * units.deg) // R (first)
    bR
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire1,
            intake: intake,
            exhaust_system: exhaustR,
            primary_length: pipe_length_1 + exhaust_stagger,
            sound_attenuation: pipe_volume_1 * 0.5
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire3,
            intake: intake,
            exhaust_system: exhaustR,
            primary_length: pipe_length_2 + exhaust_stagger,
            sound_attenuation: pipe_volume_2 * 0.5
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire5,
            intake: intake,
            exhaust_system: exhaustR,
            primary_length: pipe_length_3 + exhaust_stagger,
            sound_attenuation: pipe_volume_3
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire7,
            intake: intake,
            exhaust_system: exhaustR,
            primary_length: pipe_length_4 + exhaust_stagger,
            sound_attenuation: pipe_volume_4
        )

    cylinder_bank bL(bank_params, angle: 45.0 * units.deg) // L (second)
    bL
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire2,
            intake: intake,
            exhaust_system: exhaustL,
            primary_length: pipe_length_1,
            sound_attenuation: pipe_volume_1 * 0.5
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire4,
            intake: intake,
            exhaust_system: exhaustL,
            primary_length: pipe_length_2,
            sound_attenuation: pipe_volume_2
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire6,
            intake: intake,
            exhaust_system: exhaustL,
            primary_length: pipe_length_3,
            sound_attenuation: pipe_volume_3 * 0.5
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire8,
            intake: intake,
            exhaust_system: exhaustL,
            primary_length: pipe_length_4,
            sound_attenuation: pipe_volume_4
        )

    engine
        .add_cylinder_bank(bR)
        .add_cylinder_bank(bL)

    engine.add_crankshaft(c0)

    chevrolet_ls2_camshaft_builder camshaft(
        in_lobe_profile: chevrolet_ls2_lobe_profile_int(),
        ex_lobe_profile: chevrolet_ls2_lobe_profile_exh()
    )

    bR.set_cylinder_head (
        chevrolet_ls2_head(
            intake_camshaft: camshaft.intake_cam_R,
            exhaust_camshaft: camshaft.exhaust_cam_R,
            flip_display: false
        )
    )
    bL.set_cylinder_head (
        chevrolet_ls2_head(
            intake_camshaft: camshaft.intake_cam_L,
            exhaust_camshaft: camshaft.exhaust_cam_L,
            flip_display: true
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 27 * units.deg)
        .add_sample(1000 * units.rpm, 33 * units.deg)
        .add_sample(2000 * units.rpm, 36 * units.deg)
        .add_sample(3000 * units.rpm, 39 * units.deg)
        .add_sample(4000 * units.rpm, 42 * units.deg)
        .add_sample(5000 * units.rpm, 44 * units.deg)
        .add_sample(6000 * units.rpm, 46 * units.deg)
        .add_sample(7000 * units.rpm, 46 * units.deg)

    engine.add_ignition_module(
        chevrolet_ls2_distributor(
            wires: wires,
            timing_curve: timing_curve
        )
    )
}

// 2009 Chevrolet Corvette Z51 (C6)
public node chevrolet_corvette_z51_c6_vehicle {
    alias output __out:
        vehicle(
            mass: 3216 * units.lb,
            drag_coefficient: 0.286,
            cross_sectional_area: 1.9 * (units.m) * (units.m),
            diff_ratio: 3.42,
            tire_radius: 341.5 * units.mm, // 285/35ZR19
            rolling_resistance: 650 * units.N
        );
}

// 2006 Pontiac GTO 6.0 (VZ)
public node pontiac_gto_60_vz_vehicle {
    alias output __out:
        vehicle(
            mass: 3724 * units.lb,
            drag_coefficient: 0.32,
            cross_sectional_area: 2.13 * (units.m) * (units.m),
            diff_ratio: 3.46,
            tire_radius: 326.5 * units.mm, // 245/45ZR17
            rolling_resistance: 500 * units.N
        );
}

public node tremec_t56_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 1200 * units.lb_ft
        )
        .add_gear(2.97)
        .add_gear(2.07)
        .add_gear(1.43)
        .add_gear(1.00)
        .add_gear(0.71)
        .add_gear(0.57);
}

public node main {
    set_engine(chevrolet_ls2())
    set_vehicle(chevrolet_corvette_z51_c6_vehicle())
    set_transmission(tremec_t56_transmission())
}

main()