import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

// firing order: 15426378
// in GM layout: 21843657

public node ford_fe_v8_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 6200 * units.rpm;
	input limiter_duration: 0.11;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0 / 8.0) * cycle)
            .connect_wire(wires.wire5, (1.0 / 8.0) * cycle)
            .connect_wire(wires.wire4, (2.0 / 8.0) * cycle)
            .connect_wire(wires.wire2, (3.0 / 8.0) * cycle)
            .connect_wire(wires.wire6, (4.0 / 8.0) * cycle)
            .connect_wire(wires.wire3, (5.0 / 8.0) * cycle)
            .connect_wire(wires.wire7, (6.0 / 8.0) * cycle)
            .connect_wire(wires.wire8, (7.0 / 8.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
    output wire7: ignition_wire();
    output wire8: ignition_wire();
}

// https://www.fordfe.com/factory-spec-s-for-the-427-r-code-camshaft-t62981.html
// https://www.mercurystuff.com/fe-camshaft-specs.html
// http://www.clubcobra.com/forums/fe-talk/73793-427-so-cam-specs.html
// https://fepower.net/simplemachinesforum/index.php?topic=6936.0
public node ford_fe_v8_427_cobra_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 228 * units.deg,
            gamma: 1.0,
            lift: 600 * units.thou,
            steps: 100
        );
}

public node ford_fe_v8_427_cobra_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 228 * units.deg,
            gamma: 1.0,
            lift: 600 * units.thou,
            steps: 100
        );
}

public node ford_fe_v8_camshaft_builder {
    input in_lobe_profile: ford_fe_v8_427_cobra_lobe_profile_int();
	input ex_lobe_profile: ford_fe_v8_427_cobra_lobe_profile_exh();
    input intake_lobe_profile: in_lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input lobe_separation: 114.5 * units.deg;
    input intake_lobe_center: lobe_separation;
    input exhaust_lobe_center: lobe_separation;
    input advance: 5.0 * units.deg;
    input base_radius: 1.5 / 2 * units.inch; // guess

    output intake_cam_0: _intake_cam_0;
    output exhaust_cam_0: _exhaust_cam_0;
    output intake_cam_1: _intake_cam_1;
    output exhaust_cam_1: _exhaust_cam_1;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_0(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_0(params, lobe_profile: exhaust_lobe_profile)

    camshaft _intake_cam_1(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_1(params, lobe_profile: exhaust_lobe_profile)


    label rot30(30 * units.deg)
	label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_0
        .add_lobe(rot360 + intake_lobe_center + 0 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 3 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 5 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 2 * rot90)

    _intake_cam_1
        .add_lobe(rot360 + intake_lobe_center + 1 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 4 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 6 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 7 * rot90)

    _exhaust_cam_0
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 5 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot90)

    _exhaust_cam_1
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 4 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 6 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 7 * rot90)
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.mm, k_28inH2O(flow))
}

// https://www.erareplicas.com/427man/engine/fe_specs.htm
public node ford_fe_v8_427_cobra_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 74.3 * units.cc; // specs vary a lot
    input flip_display: false;
	
	input flow_attenuation_in: 0.8;
	input flow_attenuation_ex: 0.8;
    input lift_scale: 1.0;
    alias output __out: head;

    function intake_flow(1 * units.mm)
    intake_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_in)
        .add_flow_sample( 1 * lift_scale,  25 * flow_attenuation_in)
        .add_flow_sample( 2 * lift_scale,  76 * flow_attenuation_in)
        .add_flow_sample( 3 * lift_scale, 100 * flow_attenuation_in)
        .add_flow_sample( 4 * lift_scale, 146 * flow_attenuation_in)
        .add_flow_sample( 5 * lift_scale, 175 * flow_attenuation_in)
        .add_flow_sample( 6 * lift_scale, 212 * flow_attenuation_in)
        .add_flow_sample( 7 * lift_scale, 230 * flow_attenuation_in)
        .add_flow_sample( 8 * lift_scale, 255 * flow_attenuation_in)
        .add_flow_sample( 9 * lift_scale, 275 * flow_attenuation_in)
        .add_flow_sample(10 * lift_scale, 294 * flow_attenuation_in)
        .add_flow_sample(11 * lift_scale, 300 * flow_attenuation_in)
        .add_flow_sample(12 * lift_scale, 314 * flow_attenuation_in)

    function exhaust_flow(1 * units.mm)
    exhaust_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_ex)
        .add_flow_sample( 1 * lift_scale,  25 * flow_attenuation_ex)
        .add_flow_sample( 2 * lift_scale,  70 * flow_attenuation_ex)
        .add_flow_sample( 3 * lift_scale, 100 * flow_attenuation_ex)
        .add_flow_sample( 4 * lift_scale, 132 * flow_attenuation_ex)
        .add_flow_sample( 5 * lift_scale, 140 * flow_attenuation_ex)
        .add_flow_sample( 6 * lift_scale, 156 * flow_attenuation_ex)
        .add_flow_sample( 7 * lift_scale, 170 * flow_attenuation_ex)
        .add_flow_sample( 8 * lift_scale, 181 * flow_attenuation_ex)
        .add_flow_sample( 9 * lift_scale, 191 * flow_attenuation_ex)
        .add_flow_sample(10 * lift_scale, 207 * flow_attenuation_ex)
        .add_flow_sample(11 * lift_scale, 214 * flow_attenuation_ex)
        .add_flow_sample(12 * lift_scale, 228 * flow_attenuation_ex)
        .add_flow_sample(13 * lift_scale, 228 * flow_attenuation_ex)
        .add_flow_sample(14 * lift_scale, 228 * flow_attenuation_ex)
		
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.0 * units.inch) * 4 * units.inch,
        intake_runner_cross_section_area: circle_area(1.0 * units.inch),
        exhaust_runner_volume: circle_area(0.7 * units.inch) * 4 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.7 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}


public node ford_fe_v8_427_cobra_engine {
    alias output __out: engine;

    wires wires()

    engine engine(
        name: "Ford FE V8 Shelby Cobra",
        starter_torque: 150 * units.lb_ft,
		starter_speed: 800 * units.rpm,
        redline: 6000 * units.rpm,
        throttle_gamma: 1.0
    )


    label bore(4.2328 * units.inch)
	label bore_spacing(4.630 * units.inch)
    label stroke(3.784 * units.inch)
    label rod_length(6.489 * units.inch)
    label rod_mass(650 * units.g)
    label compression_height(1.752 * units.inch)
	label piston_mass(500 * units.g)
	label piston_dome(16.5 * units.cc) // for 12.5:1 CR
	label deck_height((10.170 * units.inch) + (41 * units.thou))
    label crank_mass(75 * units.lb)
    label flywheel_mass(40 * units.lb)
    label flywheel_radius(7 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 20 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia:
            crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 135.0 * units.deg // 45 degrees CCW from vertical
    )

    rod_journal rj0(angle:   0.0 * units.deg)
    rod_journal rj1(angle: -90.0 * units.deg)
    rod_journal rj2(angle:  90.0 * units.deg)
    rod_journal rj3(angle: 180.0 * units.deg)

    c0
        .add_rod_journal(rj0)
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)


    piston_parameters piston_params(
        mass: piston_mass,
        //blowby: 0,
        compression_height: compression_height,
        wrist_pin_position: 0 * units.mm,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    intake intake(
        plenum_volume: 1.0 * units.L,
        plenum_cross_section_area: 50.0 * units.cm2,
        intake_flow_rate: k_carb(750), // actual spec: 750cfm
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.9965,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(280.0),
        runner_length: 8.0 * units.inch,
        velocity_decay: 0.1
    )
	
	label exhaust_radius((3.0 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(700.0),
        primary_tube_length: 10.0 * units.inch,
        primary_flow_rate: k_carb(450.0),
        velocity_decay: 1.0, //0.5
		collector_cross_section_area: circle_area(exhaust_radius) * 2,
		length: 2200 * units.mm,
		volume: 2200 * units.mm * circle_area(exhaust_radius) * 2
    )

    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.02, impulse_response: ir_lib.default_0)
	
	label pipe_length_1(1.6 * bore_spacing)
	label pipe_length_2(0.8 * bore_spacing)
	label pipe_length_3(0.0 * bore_spacing)
	label pipe_length_4(0.8 * bore_spacing)
	
	label exhaust_stagger(0.3 * bore_spacing)

    cylinder_bank b0(bank_params, angle: 45.0 * units.deg) // L (first)
    b0
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            ignition_wire: wires.wire1,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: pipe_length_1 + exhaust_stagger
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire2,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: pipe_length_2 + exhaust_stagger
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire3,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: pipe_length_3 + exhaust_stagger
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire4,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: pipe_length_4 + exhaust_stagger
        )

    cylinder_bank b1(bank_params, angle: -45.0 * units.deg) // R (second)
    b1
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            ignition_wire: wires.wire5,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: pipe_length_1
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire6,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: pipe_length_2
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire7,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: pipe_length_3
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire8,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: pipe_length_4
        )

    engine
        .add_cylinder_bank(b0)
        .add_cylinder_bank(b1)

    engine.add_crankshaft(c0)

    ford_fe_v8_camshaft_builder camshaft(
	    in_lobe_profile: ford_fe_v8_427_cobra_lobe_profile_int(),
		ex_lobe_profile: ford_fe_v8_427_cobra_lobe_profile_exh()
	)

    b0.set_cylinder_head (
        ford_fe_v8_427_cobra_head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0,
			flip_display: true
        )
    )
    b1.set_cylinder_head (
        ford_fe_v8_427_cobra_head(
            intake_camshaft: camshaft.intake_cam_1,
            exhaust_camshaft: camshaft.exhaust_cam_1,
            flip_display: false
        )
    )

    function timing_curve_gross(1000 * units.rpm)
    timing_curve_gross
        .add_sample(0000 * units.rpm, 25 * units.deg)
        .add_sample(1000 * units.rpm, 30 * units.deg)
        .add_sample(2000 * units.rpm, 30 * units.deg)
        .add_sample(3000 * units.rpm, 30 * units.deg)
        .add_sample(4000 * units.rpm, 30 * units.deg)
        .add_sample(5000 * units.rpm, 30 * units.deg)
        .add_sample(6000 * units.rpm, 30 * units.deg)

	// guess; approximately 15% reduction in horsepower
    function timing_curve_net(1000 * units.rpm)
    timing_curve_net
        .add_sample(0000 * units.rpm, 17.8 * units.deg)
        .add_sample(1000 * units.rpm, 22.8 * units.deg)
        .add_sample(2000 * units.rpm, 22.8 * units.deg)
        .add_sample(3000 * units.rpm, 22.8 * units.deg)
        .add_sample(4000 * units.rpm, 22.8 * units.deg)
        .add_sample(5000 * units.rpm, 22.8 * units.deg)
        .add_sample(6000 * units.rpm, 22.8 * units.deg)

    engine.add_ignition_module(
        ford_fe_v8_distributor(
            wires: wires,
            timing_curve: timing_curve_net
        )
    )
}

// 1965 Shelby Cobra 427 (MkII)
public node shelby_cobra_427_mkii_vehicle {
    alias output __out:
        vehicle(
            mass: 2359 * units.lb,
            drag_coefficient: 0.5,
            cross_sectional_area: 1.78 * (units.m) * (units.m),
            diff_ratio: 3.54,
            tire_radius: 331.5 * units.mm, // 8.15-15
            rolling_resistance: 2359 * units.lb * 9.81 * (210.0/275.0) * 0.01
        );
}

public node muncie_m21_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 800 * units.lb_ft // uprated probably
        )
        .add_gear(2.20)
        .add_gear(1.64)
        .add_gear(1.28)
        .add_gear(1.00);
}

public node main {
    set_engine(ford_fe_v8_427_cobra_engine())
    set_vehicle(shelby_cobra_427_mkii_vehicle())
    set_transmission(muncie_m21_transmission())
}

main()