import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

public node corvair_turboair6_27_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 5750 * units.rpm;
    input limiter_duration: 0.12;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0/6.0) * cycle)
            .connect_wire(wires.wire4, (1.0/6.0) * cycle)
            .connect_wire(wires.wire5, (2.0/6.0) * cycle)
            .connect_wire(wires.wire2, (3.0/6.0) * cycle)
            .connect_wire(wires.wire3, (4.0/6.0) * cycle)
            .connect_wire(wires.wire6, (5.0/6.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
}

// http://www.n56ml.com/corvair/specs.html
// https://corvaircenter.com/forum/viewtopic.php?t=71584
public node corvair_turboair6_27_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 208 * units.deg,
            gamma: 1.0,
            lift: 280 * units.thou * 1.56,
            steps: 100
        );
}

public node corvair_turboair6_27_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 220 * units.deg,
            gamma: 1.0,
            lift: 265 * units.thou * 1.56,
            steps: 100
        );
}

public node corvair_turboair6_27_camshaft_builder {
    input in_lobe_profile: corvair_turboair6_27_lobe_profile_int();
    input ex_lobe_profile: corvair_turboair6_27_lobe_profile_exh();
    input lobe_separation: 112.0 * units.deg;
    input intake_lobe_center: lobe_separation;
    input exhaust_lobe_center: lobe_separation;
    input advance: -4.0 * units.deg;
    input base_radius: 15 * units.mm;

    output intake_cam_R: _intake_cam_R;
    output exhaust_cam_R: _exhaust_cam_R;
    output intake_cam_L: _intake_cam_L;
    output exhaust_cam_L: _exhaust_cam_L;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_R(params, lobe_profile: in_lobe_profile, advance: advance)
    camshaft _exhaust_cam_R(params, lobe_profile: ex_lobe_profile, advance: advance)

    camshaft _intake_cam_L(params, lobe_profile: in_lobe_profile, advance: advance)
    camshaft _exhaust_cam_L(params, lobe_profile: ex_lobe_profile, advance: advance)


    label rot30(30 * units.deg)
    label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_R
        .add_lobe(rot360 + intake_lobe_center + (0 * rot120))
        .add_lobe(rot360 + intake_lobe_center + (4 * rot120))
        .add_lobe(rot360 + intake_lobe_center + (2 * rot120))

    _intake_cam_L
        .add_lobe(rot360 + intake_lobe_center + (3 * rot120))
        .add_lobe(rot360 + intake_lobe_center + (1 * rot120))
        .add_lobe(rot360 + intake_lobe_center + (5 * rot120))

    _exhaust_cam_R
        .add_lobe(rot360 - exhaust_lobe_center + (0 * rot120))
        .add_lobe(rot360 - exhaust_lobe_center + (4 * rot120))
        .add_lobe(rot360 - exhaust_lobe_center + (2 * rot120))

    _exhaust_cam_L
        .add_lobe(rot360 - exhaust_lobe_center + (3 * rot120))
        .add_lobe(rot360 - exhaust_lobe_center + (1 * rot120))
        .add_lobe(rot360 - exhaust_lobe_center + (5 * rot120))
}

// http://autoxer.skiblack.com/raycams.html
public node corvair_turboair6_27_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 49.0 * units.cc; // real apparently
    input flip_display: false;
    
    input flow_attenuation_in: 1.0;
    input flow_attenuation_ex: 1.0;
    input lift_scale: 50.0;
    alias output __out: head;

    // https://corvaircenter.com/forum/viewtopic.php?t=862222
    function intake_flow(50 * units.thou)
    intake_flow
        .add_flow_sample(  0,   0)
        .add_flow_sample( 50,  29)
        .add_flow_sample(100,  57)
        .add_flow_sample(150,  88)
        .add_flow_sample(200, 113)
        .add_flow_sample(250, 140)
        .add_flow_sample(300, 159)
        .add_flow_sample(350, 175)
        .add_flow_sample(400, 191)
        .add_flow_sample(450, 201)
        .add_flow_sample(500, 206)
        .add_flow_sample(550, 206)
        .add_flow_sample(600, 207)

    function exhaust_flow(50 * units.thou)
    exhaust_flow
        .add_flow_sample(  0,   0)
        .add_flow_sample( 50,  20)
        .add_flow_sample(100,  47)
        .add_flow_sample(150,  66)
        .add_flow_sample(200,  83)
        .add_flow_sample(250,  98)
        .add_flow_sample(300, 112)
        .add_flow_sample(350, 121)
        .add_flow_sample(400, 128)
        .add_flow_sample(450, 132)
        .add_flow_sample(500, 137)
        .add_flow_sample(550, 140)
        .add_flow_sample(600, 143)

        
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(0.95 * units.inch) * 4 * units.inch,
        intake_runner_cross_section_area: circle_area(0.95 * units.inch),
        exhaust_runner_volume: circle_area(0.55 * units.inch) * 3 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.55 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}

// https://corvaircenter.com/forum/viewtopic.php?t=68014
// https://corvaircenter.com/forum/viewtopic.php?t=552184
public node corvair_turboair6_27_engine {
    alias output __out: engine;

    wires wires()

    label bore(3.4375 * units.inch)
    label stroke(2.94 * units.inch)
    label rod_length(4.72 * units.inch) // Pauter
    label compression_height(1.590 * units.inch)
    label chamber_volume(49.0 * units.cc) // 
    label deck_clearance((22 + 32) * units.thou)  // like Porsches, this is in fact how these engines are measured; second number is gasket
    
    label crank_mass(80 * units.lb)
    label rod_mass(550 * units.g)
    label piston_mass(500 * units.g)
    label flywheel_mass(15 * units.lb)
    label flywheel_radius(6 * units.inch)
    
    label compression_ratio(9.25)
    label clearance_volume(deck_clearance*bore*bore*(constants.pi/4.0))
    label piston_dome(-((bore * bore * stroke * (constants.pi/4.0))/(compression_ratio-1.0)) + (chamber_volume + clearance_volume))
    
    engine engine(
        name: "Chevrolet Turbo-Air 6",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 900 * units.rpm,
        redline: 5500 * units.rpm,
        throttle_gamma: 1.5
    )

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 15 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia: crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 0.0 * units.deg
    )

    rod_journal rj1(angle:  0  * units.deg)
    rod_journal rj2(angle: 180 * units.deg)
    rod_journal rj3(angle: 120 * units.deg)
    rod_journal rj4(angle: 300 * units.deg)
    rod_journal rj5(angle: 240 * units.deg)
    rod_journal rj6(angle:  60 * units.deg)

    c0
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)
        .add_rod_journal(rj5)
        .add_rod_journal(rj6)


    piston_parameters piston_params(
        mass: piston_mass,
        blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: stroke / 2 + rod_length + compression_height + deck_clearance
    )

    intake intake(
        plenum_volume: 1.0 * units.L,
        plenum_cross_section_area: 100.0 * units.cm2,
        intake_flow_rate: k_carb(300),
        idle_flow_rate: k_carb(0.0001),
        idle_throttle_plate_position: 0.994,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(150),
        runner_length: 8.0 * units.inch,
        velocity_decay: 0.1
    )
    
    label exhaust_radius((2.75 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(300),
        primary_tube_length: 5.0 * units.inch,
        primary_flow_rate: k_carb(135),
        velocity_decay: 1.0, //0.5
        collector_cross_section_area: circle_area(exhaust_radius) * 2,
        length: 500 * units.mm,
        volume: (500 * units.mm * circle_area(exhaust_radius) * 2)
    )

    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.001, impulse_response: ir_lib.default_0)
    
    label pipe_length_1( 0.0 * units.inch)
    label pipe_length_2( 3.0 * units.inch)
    label pipe_length_3( 6.0 * units.inch)
    
    label exhaust_stagger( 2.0 * units.inch)

    cylinder_bank bR(bank_params, angle: -90.0 * units.deg) // R (first)
    bR
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire1,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_1 + exhaust_stagger,
            sound_attenuation: 1.0
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire3,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_2 + exhaust_stagger,
            sound_attenuation: 1.0
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj5,
            ignition_wire: wires.wire5,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_3 + exhaust_stagger,
            sound_attenuation: 1.0
        )

    cylinder_bank bL(bank_params, angle: 90.0 * units.deg) // L (second)
    bL
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire2,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_1,
            sound_attenuation: 0.9
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire4,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_2,
            sound_attenuation: 0.9
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj6,
            ignition_wire: wires.wire6,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_3,
            sound_attenuation: 0.9
        )

    engine
        .add_cylinder_bank(bR)
        .add_cylinder_bank(bL)

    engine.add_crankshaft(c0)

    corvair_turboair6_27_camshaft_builder camshaft()

    bR.set_cylinder_head (
        corvair_turboair6_27_head(
            intake_camshaft: camshaft.intake_cam_R,
            exhaust_camshaft: camshaft.exhaust_cam_R,
            chamber_volume: chamber_volume,
            flip_display: false
        )
    )
    bL.set_cylinder_head (
        corvair_turboair6_27_head(
            intake_camshaft: camshaft.intake_cam_L,
            exhaust_camshaft: camshaft.exhaust_cam_L,
            chamber_volume: chamber_volume,
            flip_display: true
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 28 * units.deg)
        .add_sample(1000 * units.rpm, 38 * units.deg)
        .add_sample(2000 * units.rpm, 38 * units.deg)
        .add_sample(3000 * units.rpm, 38 * units.deg)
        .add_sample(4000 * units.rpm, 38 * units.deg)
        .add_sample(5000 * units.rpm, 38 * units.deg)
        .add_sample(6000 * units.rpm, 38 * units.deg)

    engine.add_ignition_module(
        corvair_turboair6_27_distributor(
            wires: wires,
            timing_curve: timing_curve
        )
    )
}

// 1965 Chevrolet Corvair Monza Sport Coupe 110 manual
// thanks as always to Automobile-Catalog
public node chevrolet_corvair_monza_sport_coupe_110_vehicle {
    alias output __out:
        vehicle(
            mass: 2524 * units.lb,
            drag_coefficient: 0.45,
            cross_sectional_area: 1.91 * (units.m * units.m),
            diff_ratio: 3.55,
            tire_radius: 312 * units.mm, // 6.50-13 (90)
            rolling_resistance: 2524 * units.lb * 9.81 * (165.0/275.0) * 0.01
        );
}

public node chevrolet_corvair_monza_sport_coupe_110_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 300 * units.lb_ft
        )
        .add_gear(3.20)
        .add_gear(2.19)
        .add_gear(1.44)
        .add_gear(1.00);
}

public node main {
    set_engine(corvair_turboair6_27_engine())
    set_vehicle(chevrolet_corvair_monza_sport_coupe_110_vehicle())
    set_transmission(chevrolet_corvair_monza_sport_coupe_110_transmission())
}

main()