import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

// almost completely guesswork

label cycle(2 * 360 * units.deg)

public node hyundai_omega_g8aa_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 6200 * units.rpm; // guess
    input limiter_duration: 0.08;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0 / 8.0) * cycle)
            .connect_wire(wires.wire2, (1.0 / 8.0) * cycle)
            .connect_wire(wires.wire7, (2.0 / 8.0) * cycle)
            .connect_wire(wires.wire8, (3.0 / 8.0) * cycle)
            .connect_wire(wires.wire4, (4.0 / 8.0) * cycle)
            .connect_wire(wires.wire5, (5.0 / 8.0) * cycle)
            .connect_wire(wires.wire6, (6.0 / 8.0) * cycle)
            .connect_wire(wires.wire3, (7.0 / 8.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
    output wire7: ignition_wire();
    output wire8: ignition_wire();
}

// cam specs are guesses
public node hyundai_omega_g8aa_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 200 * units.deg,
            gamma: 1.0,
            lift: 9.5 * units.mm,
            steps: 100
        );
}

public node hyundai_omega_g8aa_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 200 * units.deg,
            gamma: 1.0,
            lift: 9.5 * units.mm,
            steps: 100
        );
}

public node hyundai_omega_g8aa_camshaft_builder {
    input in_lobe_profile: hyundai_omega_g8aa_lobe_profile_int();
    input ex_lobe_profile: hyundai_omega_g8aa_lobe_profile_exh();
    input intake_lobe_profile: in_lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input intake_lobe_center: 110 * units.deg;
    input exhaust_lobe_center: 110 * units.deg;
    input advance: 0.0 * units.deg;
    input base_radius: 16 * units.mm;

    output intake_cam_L: _intake_cam_L;
    output exhaust_cam_L: _exhaust_cam_L;
    output intake_cam_R: _intake_cam_R;
    output exhaust_cam_R: _exhaust_cam_R;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_L(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_L(params, lobe_profile: exhaust_lobe_profile)

    camshaft _intake_cam_R(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_R(params, lobe_profile: exhaust_lobe_profile)


    label rot30(30 * units.deg)
    label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_L
        .add_lobe(rot360 + intake_lobe_center + 0 * rot90) // #1
        .add_lobe(rot360 + intake_lobe_center + 7 * rot90) // #3
        .add_lobe(rot360 + intake_lobe_center + 5 * rot90) // #5
        .add_lobe(rot360 + intake_lobe_center + 2 * rot90) // #7

    _intake_cam_R
        .add_lobe(rot360 + intake_lobe_center + 1 * rot90) // #2
        .add_lobe(rot360 + intake_lobe_center + 4 * rot90) // #4
        .add_lobe(rot360 + intake_lobe_center + 6 * rot90) // #6
        .add_lobe(rot360 + intake_lobe_center + 3 * rot90) // #8

    _exhaust_cam_L
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 7 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 5 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot90)

    _exhaust_cam_R
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 4 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 6 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot90)
}

public node hyundai_omega_g8aa_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 45.0 * units.cc; // guess
    input flip_display: false;
    
    // guesses
    input flow_intake: 233.0;  //  intake valve size: 35.5mm
    input flow_exhaust: 200.0; // exhaust valve size: 30.5mm
    input lift_max: 9.0 * units.mm;
    alias output __out: head;

    function intake_flow(lift_max / 5.0)
    intake_flow
        .add_sample(0.0 * lift_max, k_28inH2O(0.00 * flow_intake))
        .add_sample(0.2 * lift_max, k_28inH2O(0.32 * flow_intake))
        .add_sample(0.4 * lift_max, k_28inH2O(0.63 * flow_intake))
        .add_sample(0.6 * lift_max, k_28inH2O(0.83 * flow_intake))
        .add_sample(0.8 * lift_max, k_28inH2O(0.95 * flow_intake))
        .add_sample(1.0 * lift_max, k_28inH2O(1.00 * flow_intake))
        .add_sample(1.2 * lift_max, k_28inH2O(1.02 * flow_intake))

    function exhaust_flow(lift_max / 5.0)
    exhaust_flow
        .add_sample(0.0 * lift_max, k_28inH2O(0.00 * flow_exhaust))
        .add_sample(0.2 * lift_max, k_28inH2O(0.32 * flow_exhaust))
        .add_sample(0.4 * lift_max, k_28inH2O(0.63 * flow_exhaust))
        .add_sample(0.6 * lift_max, k_28inH2O(0.83 * flow_exhaust))
        .add_sample(0.8 * lift_max, k_28inH2O(0.95 * flow_exhaust))
        .add_sample(1.0 * lift_max, k_28inH2O(1.00 * flow_exhaust))
        .add_sample(1.2 * lift_max, k_28inH2O(1.02 * flow_exhaust))

        
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.1 * units.inch) * 5 * units.inch,
        intake_runner_cross_section_area: circle_area(1.1 * units.inch),
        exhaust_runner_volume: circle_area(0.7 * units.inch) * 3 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.7 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}

// https://web.archive.org/web/20090427092556/http://www.sae.org/automag/globalview_03-00/04.htm
public node hyundai_omega_g8aa_engine {
    alias output __out: engine;

    wires wires()

    engine engine(
        name: "Hyundai G8AA (Mitsubishi 8A80)",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 1000 * units.rpm,
        redline: 6000 * units.rpm, // guess
        throttle_gamma: 1.0
    )

    label bore(86.0 * units.mm)
    label bore_spacing(95.0 * units.mm)
    label stroke(96.8 * units.mm)
    label rod_length(160.0 * units.mm) // guess/irrelevant
    label compression_height(30.0 * units.mm) // guess/irrelevant
    label piston_dome(0.0 * units.cc) // guess/irrelevant
    label chamber_volume(45.0 * units.cc) // guess/irrelevant
    
    label rod_mass(600 * units.g)
    label piston_mass(450 * units.g)
    label crank_mass(35.0 * units.kg)
    label flywheel_mass(10.0 * units.kg)
    label flywheel_radius(5.0 * units.inch)

    label compression_ratio(10.7)
    label deck_clearance((((bore * bore / 4.0 * constants.pi * stroke)/(compression_ratio-1.0)) - chamber_volume + piston_dome) / (bore * bore / 4.0 * constants.pi))

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 15 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia: crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 135.0 * units.deg // +45 degrees
    )

    rod_journal rj1(angle:   0 * units.deg)
    rod_journal rj2(angle: 270 * units.deg)
    rod_journal rj3(angle:  90 * units.deg)
    rod_journal rj4(angle: 180 * units.deg)

    c0
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)


    piston_parameters piston_params(
        mass: piston_mass,
        blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: stroke / 2 + rod_length + compression_height + deck_clearance
    )

    intake intake (
        plenum_volume: 1.0 * units.L,
        plenum_cross_section_area: 40.0 * units.cm2,
        intake_flow_rate: k_carb(500.0),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.995,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(300.0),
        runner_length: 9.0 * units.inch,
        velocity_decay: 0.1
    )
    
    label exhaust_radius((3.0 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(500.0),
        primary_tube_length: 10.0 * units.inch,
        primary_flow_rate: k_carb(200.0),
        velocity_decay: 1.0, //0.5
        collector_cross_section_area: circle_area(exhaust_radius) * 2,
        length: 2600 * units.mm,
        volume: (2000 * units.mm * circle_area(exhaust_radius)) + (600 * units.mm * circle_area(exhaust_radius) * 2)
    )

    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.08, impulse_response: ir_lib.default_0)
    
    label pipe_length_1(5.0 * units.inch)
    label pipe_length_2(1.0 * units.inch)
    label pipe_length_3(0.0 * units.inch)
    label pipe_length_4(4.0 * units.inch)
    
    label exhaust_stagger(8.0 * units.inch)

    cylinder_bank bL(bank_params, angle: 45.0 * units.deg) // L (first, front)
    bL
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire1,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_1 + exhaust_stagger
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire3,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_2 + exhaust_stagger
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire5,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_3 + exhaust_stagger
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire7,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_4 + exhaust_stagger
        )
        
    cylinder_bank bR(bank_params, angle: -45.0 * units.deg) // R (second, rear)
    bR
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire2,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_1
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire4,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_2
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire6,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_3
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire8,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: pipe_length_4
        )

    engine
        .add_cylinder_bank(bL)
        .add_cylinder_bank(bR)

    engine.add_crankshaft(c0)

    hyundai_omega_g8aa_camshaft_builder camshaft(
        in_lobe_profile: hyundai_omega_g8aa_lobe_profile_int(),
        ex_lobe_profile: hyundai_omega_g8aa_lobe_profile_exh()
    )

    bL.set_cylinder_head (
        hyundai_omega_g8aa_head(
            intake_camshaft: camshaft.intake_cam_L,
            exhaust_camshaft: camshaft.exhaust_cam_L,
            chamber_volume: chamber_volume,
            flip_display: true
        )
    )
    bR.set_cylinder_head (
        hyundai_omega_g8aa_head(
            intake_camshaft: camshaft.intake_cam_R,
            exhaust_camshaft: camshaft.exhaust_cam_R,
            chamber_volume: chamber_volume,
            flip_display: false
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 13 * units.deg)
        .add_sample(1000 * units.rpm, 23 * units.deg)
        .add_sample(2000 * units.rpm, 23 * units.deg)
        .add_sample(3000 * units.rpm, 23 * units.deg)
        .add_sample(4000 * units.rpm, 23 * units.deg)
        .add_sample(5000 * units.rpm, 23 * units.deg)
        .add_sample(6000 * units.rpm, 23 * units.deg)
        .add_sample(7000 * units.rpm, 23 * units.deg)

    engine.add_ignition_module(
        hyundai_omega_g8aa_distributor(
            wires: wires,
            timing_curve: timing_curve
        )
    )
}

// 1999 Hyundai Equus Limousine VL 450 (LZ/YJ)
// Mitsubishi equivalent: Mitsubishi Dignity (S43A)
// thanks as always to Automobile Catalog
public node hyundai_equus_vl_450_lz_yj_vehicle {
    alias output __out:
        vehicle(
            mass: 2150 * units.kg,
            drag_coefficient: 0.32,
            cross_sectional_area: 2.29 * (units.m * units.m),
            diff_ratio: 2.824,
            tire_radius: 338 * units.mm, // 225/60R16V
            rolling_resistance: 2150 * units.kg * 9.81 * (225.0/275.0) * 0.01
        );
}

public node mitsubishi_invecs_ii_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 500 * units.lb_ft
        )
        .add_gear(3.84)
        .add_gear(2.14)
        .add_gear(1.44)
        .add_gear(1.02)
        .add_gear(0.71);
}

public node main {
    set_engine(hyundai_omega_g8aa_engine())
    set_vehicle(hyundai_equus_vl_450_lz_yj_vehicle())
    set_transmission(mitsubishi_invecs_ii_transmission())
}

main()