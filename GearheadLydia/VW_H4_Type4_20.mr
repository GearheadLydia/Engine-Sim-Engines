import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

public node vw_boxer_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 5600 * units.rpm;
    input limiter_duration: 0.13;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire2, (0.0/4.0) * cycle)
            .connect_wire(wires.wire1, (1.0/4.0) * cycle)
            .connect_wire(wires.wire4, (2.0/4.0) * cycle)
            .connect_wire(wires.wire3, (3.0/4.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
}

// https://shoptalkforums.com/viewtopic.php?t=2771
public node vw_boxer_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 234 * units.deg,
            gamma: 2.5,
            lift: 393 * units.thou,
            steps: 100
        );
}

public node vw_boxer_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 226.5 * units.deg,
            gamma: 2.5,
            lift: 368 * units.thou,
            steps: 100
        );
}

public node vw_boxer_camshaft_builder {
    input lobe_profile: vw_boxer_lobe_profile_int();
    input ex_lobe_profile: vw_boxer_lobe_profile_exh();
    input intake_lobe_profile: lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input lobe_separation: 107.25 * units.deg;
    input intake_lobe_center: lobe_separation;
    input exhaust_lobe_center: lobe_separation;
    input advance: 2.25 * units.deg;
    input base_radius: (1.0 * units.inch) / 2;

    output intake_cam_0: _intake_cam_0;
    output exhaust_cam_0: _exhaust_cam_0;
    output intake_cam_1: _intake_cam_1;
    output exhaust_cam_1: _exhaust_cam_1;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_0(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_0(params, lobe_profile: exhaust_lobe_profile)
    camshaft _intake_cam_1(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_1(params, lobe_profile: exhaust_lobe_profile)


    label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_0
        .add_lobe(rot360 + intake_lobe_center + 0 * rot180) // #2
        .add_lobe(rot360 + intake_lobe_center + 1 * rot180) // #1

    _exhaust_cam_0
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot180) // #2
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot180) // #1

    _intake_cam_1
        .add_lobe(rot360 + intake_lobe_center + 2 * rot180) // #4
        .add_lobe(rot360 + intake_lobe_center + 3 * rot180) // #3

    _exhaust_cam_1
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot180) // #4
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot180) // #3
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.mm, k_28inH2O(flow))
}

public node vw_boxer_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 64.0 * units.cc;
    input flip_display: false;
    
    input flow_attenuation_in: 0.9;
    input flow_attenuation_ex: 0.9;
    input lift_scale: 1.0;
    alias output __out: head;

    function intake_flow(1.0 * units.mm) // 1.34 rocker
    intake_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_in)
        .add_flow_sample( 1 * lift_scale,  30 * flow_attenuation_in)
        .add_flow_sample( 2 * lift_scale,  60 * flow_attenuation_in)
        .add_flow_sample( 3 * lift_scale,  90 * flow_attenuation_in)
        .add_flow_sample( 4 * lift_scale, 125 * flow_attenuation_in)
        .add_flow_sample( 5 * lift_scale, 160 * flow_attenuation_in)
        .add_flow_sample( 6 * lift_scale, 195 * flow_attenuation_in)
        .add_flow_sample( 7 * lift_scale, 210 * flow_attenuation_in)
        .add_flow_sample( 8 * lift_scale, 235 * flow_attenuation_in)
        .add_flow_sample( 9 * lift_scale, 270 * flow_attenuation_in)
        .add_flow_sample(10 * lift_scale, 305 * flow_attenuation_in)
        .add_flow_sample(11 * lift_scale, 320 * flow_attenuation_in)
        .add_flow_sample(12 * lift_scale, 335 * flow_attenuation_in)

    function exhaust_flow(1.0 * units.mm) // 1.29 rocker
    exhaust_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_ex)
        .add_flow_sample( 1 * lift_scale,  30 * flow_attenuation_ex)
        .add_flow_sample( 2 * lift_scale,  55 * flow_attenuation_ex)
        .add_flow_sample( 3 * lift_scale,  85 * flow_attenuation_ex)
        .add_flow_sample( 4 * lift_scale, 115 * flow_attenuation_ex)
        .add_flow_sample( 5 * lift_scale, 140 * flow_attenuation_ex)
        .add_flow_sample( 6 * lift_scale, 160 * flow_attenuation_ex)
        .add_flow_sample( 7 * lift_scale, 180 * flow_attenuation_ex)
        .add_flow_sample( 8 * lift_scale, 205 * flow_attenuation_ex)
        .add_flow_sample( 9 * lift_scale, 220 * flow_attenuation_ex)
        .add_flow_sample(10 * lift_scale, 240 * flow_attenuation_ex)
        .add_flow_sample(11 * lift_scale, 260 * flow_attenuation_ex)
        .add_flow_sample(12 * lift_scale, 280 * flow_attenuation_ex)

        
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(0.8 * units.inch) * 6 * units.inch,
        intake_runner_cross_section_area: circle_area(0.8 * units.inch),
        exhaust_runner_volume: circle_area(0.6 * units.inch) * 6 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.6 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}

// https://shoptalkforums.com/viewtopic.php?t=141693
// VW and Porsche aircooled engines seem to be measured in deck clearance, not true deck height
// https://www.thesamba.com/vw/forum/archive/index.php/o-t--t-643150--.html
public node vw_boxer_type4_20_engine {
    alias output __out: engine;

    engine engine(
        name: "Volkswagen Type 4",
        starter_torque: 50 * units.lb_ft,
        starter_speed: 800 * units.rpm,
        redline: 5500 * units.rpm,
        throttle_gamma: 1.0
    )

    label bore(94.0 * units.mm)
    label stroke(71.0 * units.mm)
    label rod_length(131.0 * units.mm)
    label rod_mass(600 * units.g)
    label piston_mass(450 * units.g)
    label compression_height(34.6 * units.mm)
    label deck_clearance((1.0 + 0.9) * units.mm) // VW / Porsche boxers measured with deck clearance instead
    label piston_dome(7.0 * units.cc) // calculated for 8.0:1 compression
    label crank_mass(3.0 * units.kg)
    label flywheel_mass(15 * units.lb)
    label flywheel_radius(6.0 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 15 * units.kg, radius: 8.0 * units.cm)
    )

    wires wires()

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 2.0 * units.lb_ft,
        moment_of_inertia: crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 0.0 * units.deg
    )

    rod_journal rj1(angle:  0  * units.deg)
    rod_journal rj2(angle: 180 * units.deg)
    rod_journal rj3(angle: 180 * units.deg)
    rod_journal rj4(angle:  0  * units.deg)

    c0
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)

    piston_parameters piston_params(
        mass: piston_mass,
        //blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: stroke / 2 + rod_length + compression_height + deck_clearance
    )

    intake intake(
        plenum_volume: 2.0 * units.L,
        plenum_cross_section_area: 100.0 * units.cm2,
        intake_flow_rate: k_carb(230.0),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.996,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(150.0),
        runner_length: 8.0 * units.inch,
        velocity_decay: 0.1
    )

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(230.0),
        primary_tube_length: 6.0 * units.inch,
        primary_flow_rate: k_carb(150.0),
        velocity_decay: 1.0, //0.5
        collector_cross_section_area: circle_area((2.5 * units.inch) / 2),
        length: 600 * units.mm,
        volume: 600 * units.mm * circle_area((2.5 * units.inch) / 2)
    )

    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.01, impulse_response: ir_lib.default_0)

    cylinder_bank b0(bank_params, angle: -90.0 * units.deg) // R (first)
    b0
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire2,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: 16 * units.inch
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire1,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: 12 * units.inch
        )

    cylinder_bank b1(bank_params, angle: 90.0 * units.deg) // L (second)
    b1
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire4,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: 4 * units.inch
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire3,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: 0 * units.inch
        )

    engine
        .add_cylinder_bank(b0)
        .add_cylinder_bank(b1)

    engine.add_crankshaft(c0)

    vw_boxer_camshaft_builder camshaft()

    b0.set_cylinder_head (
        vw_boxer_head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0,
            flip_display: false
        )
    )

    b1.set_cylinder_head (
        vw_boxer_head(
            intake_camshaft: camshaft.intake_cam_1,
            exhaust_camshaft: camshaft.exhaust_cam_1,
            flip_display: true
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample( 0000 * units.rpm, 25 * units.deg)
        .add_sample( 1000 * units.rpm, 33 * units.deg)
        .add_sample( 2000 * units.rpm, 35 * units.deg)
        .add_sample( 3000 * units.rpm, 35 * units.deg)
        .add_sample( 4000 * units.rpm, 35 * units.deg)
        .add_sample( 5000 * units.rpm, 35 * units.deg)
        .add_sample( 6000 * units.rpm, 35 * units.deg)

    engine.add_ignition_module(
        vw_boxer_distributor(
            wires: wires,
            timing_curve: timing_curve
        ))
}

// 1973 Porsche 914 2.0
public node porsche_914_20_vehicle {
    alias output __out:
        vehicle(
            mass: 970 * units.kg,
            drag_coefficient: 0.36,
            cross_sectional_area: 1.68 * (units.m * units.m),
            diff_ratio: 4.429,
            tire_radius: 323.5 * units.mm, // 165HR15
            rolling_resistance: 970 * units.kg * 9.81 * (171.0/275.0) * 0.01
        );
}

public node porsche_914_20_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 400 * units.lb_ft
        )
        .add_gear(3.091)
        .add_gear(1.889)
        .add_gear(1.261)
        .add_gear(0.926)
        .add_gear(0.710);
}

public node main {
    set_engine(vw_boxer_type4_20_engine())
    set_vehicle(porsche_914_20_vehicle())
    set_transmission(porsche_914_20_transmission())
}

main()