import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

// firing order: 15374826
// in GM layout: 27638145

public node ferrari_f105ab_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 7900 * units.rpm;
    input limiter_duration: 0.1;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0 / 8.0) * cycle)
            .connect_wire(wires.wire5, (1.0 / 8.0) * cycle)
            .connect_wire(wires.wire3, (2.0 / 8.0) * cycle)
            .connect_wire(wires.wire7, (3.0 / 8.0) * cycle)
            .connect_wire(wires.wire4, (4.0 / 8.0) * cycle)
            .connect_wire(wires.wire8, (5.0 / 8.0) * cycle)
            .connect_wire(wires.wire2, (6.0 / 8.0) * cycle)
            .connect_wire(wires.wire6, (7.0 / 8.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
    output wire7: ignition_wire();
    output wire8: ignition_wire();
}

// https://www.ferrarichat.com/discus/messages/256120/251131.html?1054254530
// http://webcamshafts.com/pages_vehicles/automobile/ferrari/815.html
public node ferrari_f105ab_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 222 * units.deg,
            gamma: 1.0,
            lift: 8.75 * units.mm,
            steps: 100
        );
}

public node ferrari_f105ab_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 216 * units.deg,
            gamma: 1.0,
            lift: 7.80 * units.mm,
            steps: 100
        );
}

public node ferrari_f105ab_camshaft_builder {
    input in_lobe_profile: ferrari_f105ab_lobe_profile_int();
    input ex_lobe_profile: ferrari_f105ab_lobe_profile_exh();
    input intake_lobe_profile: in_lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input intake_lobe_center: 115.0 * units.deg;  // guess
    input exhaust_lobe_center: 115.0 * units.deg; // guess
    input advance: 0.0 * units.deg;
    input base_radius: 15 * units.mm;

    output intake_cam_L: _intake_cam_L;
    output intake_cam_R: _intake_cam_R;
    output exhaust_cam_L: _exhaust_cam_L;
    output exhaust_cam_R: _exhaust_cam_R;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_L(params, lobe_profile: intake_lobe_profile)
    camshaft _intake_cam_R(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_L(params, lobe_profile: exhaust_lobe_profile)
    camshaft _exhaust_cam_R(params, lobe_profile: exhaust_lobe_profile)

    label rot90(90 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_R
        .add_lobe(rot360 + intake_lobe_center + 5 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 3 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 7 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 1 * rot90)

    _intake_cam_L
        .add_lobe(rot360 + intake_lobe_center + 0 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 6 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 2 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 4 * rot90)

    _exhaust_cam_R
        .add_lobe(rot360 - exhaust_lobe_center + 5 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 7 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot90)

    _exhaust_cam_L
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 6 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 4 * rot90)
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.mm, k_28inH2O(flow))
}

public node ferrari_f105ab_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 30.0 * units.cc; // guess
    input flip_display: false;
    
    input flow_attenuation_in: 0.95;
    input flow_attenuation_ex: 1.0;
    input lift_scale: 1.0;
    alias output __out: head;

    function intake_flow(1.0 * units.mm)
    intake_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_in)
        .add_flow_sample( 1 * lift_scale,  30 * flow_attenuation_in)
        .add_flow_sample( 2 * lift_scale,  60 * flow_attenuation_in)
        .add_flow_sample( 3 * lift_scale,  90 * flow_attenuation_in)
        .add_flow_sample( 4 * lift_scale, 125 * flow_attenuation_in)
        .add_flow_sample( 5 * lift_scale, 160 * flow_attenuation_in)
        .add_flow_sample( 6 * lift_scale, 195 * flow_attenuation_in)
        .add_flow_sample( 7 * lift_scale, 210 * flow_attenuation_in)
        .add_flow_sample( 8 * lift_scale, 235 * flow_attenuation_in)
        .add_flow_sample( 9 * lift_scale, 270 * flow_attenuation_in)
        .add_flow_sample(10 * lift_scale, 305 * flow_attenuation_in)
        .add_flow_sample(11 * lift_scale, 320 * flow_attenuation_in)
        .add_flow_sample(12 * lift_scale, 335 * flow_attenuation_in)

    function exhaust_flow(1.0 * units.mm)
    exhaust_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_ex)
        .add_flow_sample( 1 * lift_scale,  30 * flow_attenuation_ex)
        .add_flow_sample( 2 * lift_scale,  55 * flow_attenuation_ex)
        .add_flow_sample( 3 * lift_scale,  85 * flow_attenuation_ex)
        .add_flow_sample( 4 * lift_scale, 115 * flow_attenuation_ex)
        .add_flow_sample( 5 * lift_scale, 140 * flow_attenuation_ex)
        .add_flow_sample( 6 * lift_scale, 160 * flow_attenuation_ex)
        .add_flow_sample( 7 * lift_scale, 180 * flow_attenuation_ex)
        .add_flow_sample( 8 * lift_scale, 205 * flow_attenuation_ex)
        .add_flow_sample( 9 * lift_scale, 220 * flow_attenuation_ex)
        .add_flow_sample(10 * lift_scale, 240 * flow_attenuation_ex)
        .add_flow_sample(11 * lift_scale, 260 * flow_attenuation_ex)
        .add_flow_sample(12 * lift_scale, 280 * flow_attenuation_ex)

    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.05 * units.inch) * 4 * units.inch,
        intake_runner_cross_section_area: circle_area(1.05 * units.inch),
        exhaust_runner_volume: circle_area(0.7 * units.inch) * 4 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.7 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}

public node ferrari_f105ab_engine {
    alias output __out: engine;

    wires wires()

    engine engine(
        name: "Ferrari F105AB",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 1200 * units.rpm,
        redline: 7500 * units.rpm, // 7800
        throttle_gamma: 1.0
    )

    label bore(81.0 * units.mm)
    label stroke(71.0 * units.mm)
    label rod_length(137.0 * units.mm)
    label rod_mass(575 * units.g)
    label piston_mass(450 * units.g)
    label compression_height(30.70 * units.mm)
    label piston_dome(-1.7 * units.cc) // for 9.2:1 compression
    label deck_height((204.2 + 1.5) * units.mm) // real, apparently
    label crank_mass(70 * units.lb)
    label flywheel_mass(20 * units.lb)
    label flywheel_radius(6 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 20 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia:
            crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 135.0 * units.deg // 45 degrees
    )

    rod_journal rj1(angle:  0  * units.deg)
    rod_journal rj2(angle: 180 * units.deg)
    rod_journal rj3(angle: 180 * units.deg)
    rod_journal rj4(angle:  0  * units.deg)

    c0
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)

    // piston specs are guesses
    piston_parameters piston_params(
        mass: piston_mass,
        blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0 * units.mm,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    intake intakeL(
        plenum_volume: 2.0 * units.L,
        plenum_cross_section_area: 100.0 * units.cm2,
        intake_flow_rate: k_carb(300),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.9977,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(300.0),
        runner_length: 6.5 * units.inch,
        velocity_decay: 0.1
    )

    intake intakeR(
        plenum_volume: 2.0 * units.L,
        plenum_cross_section_area: 100.0 * units.cm2,
        intake_flow_rate: k_carb(300),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.9977,
        throttle_gamma: 2.0,
        runner_flow_rate: k_carb(300.0),
        runner_length: 6.5 * units.inch,
        velocity_decay: 0.1
    )
    
    label exhaust_radius((3.0 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(600.0),
        primary_tube_length: 12.0 * units.inch,
        primary_flow_rate: k_carb(400.0),
        velocity_decay: 1.0,
        collector_cross_section_area: circle_area(exhaust_radius) * 2,
        length: 700 * units.mm,
        volume: (200 * units.mm * circle_area(exhaust_radius) * 2) + (600 * units.mm * circle_area(3.0 * units.inch))
    )

    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.002, impulse_response: ir_lib.default_0)
    
    label pipe_length_1( 0 * units.mm)
    label pipe_length_2(20 * units.mm)
    label pipe_length_3(40 * units.mm)
    label pipe_length_4(60 * units.mm)
    
    label exhaust_stagger(60 * units.mm)
    
    label exh_vol_L(0.9)
    label exh_vol_R(1.0)

    cylinder_bank bL(bank_params, angle: 45.0 * units.deg) // L (first, rear)
    bL
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire1,
            intake: intakeL,
            exhaust_system: exhaust,
            primary_length: pipe_length_4,
            sound_attenuation: exh_vol_L * 1.0
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire2,
            intake: intakeL,
            exhaust_system: exhaust,
            primary_length: pipe_length_3,
            sound_attenuation: exh_vol_L * 0.95
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire3,
            intake: intakeL,
            exhaust_system: exhaust,
            primary_length: pipe_length_2,
            sound_attenuation: exh_vol_L * 0.9
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire4,
            intake: intakeL,
            exhaust_system: exhaust,
            primary_length: pipe_length_1,
            sound_attenuation: exh_vol_L * 0.85
        )

    cylinder_bank bR(bank_params, angle: -45 * units.deg) // R (second, front)
    bR
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire8,
            intake: intakeR,
            exhaust_system: exhaust,
            primary_length: pipe_length_1 + exhaust_stagger,
            sound_attenuation: exh_vol_R * 0.85
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire7,
            intake: intakeR,
            exhaust_system: exhaust,
            primary_length: pipe_length_2 + exhaust_stagger,
            sound_attenuation: exh_vol_R * 0.9
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire6,
            intake: intakeR,
            exhaust_system: exhaust,
            primary_length: pipe_length_3 + exhaust_stagger,
            sound_attenuation: exh_vol_R * 0.95
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire5,
            intake: intakeR,
            exhaust_system: exhaust,
            primary_length: pipe_length_4 + exhaust_stagger,
            sound_attenuation: exh_vol_R * 1.0
        )

    engine
        .add_cylinder_bank(bL)
        .add_cylinder_bank(bR)

    engine.add_crankshaft(c0)

    ferrari_f105ab_camshaft_builder camshaft(
        in_lobe_profile: ferrari_f105ab_lobe_profile_int(),
        ex_lobe_profile: ferrari_f105ab_lobe_profile_exh()
    )

    bL.set_cylinder_head (
        ferrari_f105ab_head(
            intake_camshaft: camshaft.intake_cam_L,
            exhaust_camshaft: camshaft.exhaust_cam_L,
            flip_display: true
        )
    )
    bR.set_cylinder_head (
        ferrari_f105ab_head(
            intake_camshaft: camshaft.intake_cam_R,
            exhaust_camshaft: camshaft.exhaust_cam_R,
            flip_display: false
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 45 * units.deg)
        .add_sample(1000 * units.rpm, 55 * units.deg)
        .add_sample(2000 * units.rpm, 55 * units.deg)
        .add_sample(3000 * units.rpm, 55 * units.deg)
        .add_sample(4000 * units.rpm, 55 * units.deg)
        .add_sample(5000 * units.rpm, 55 * units.deg)
        .add_sample(6000 * units.rpm, 55 * units.deg)
        .add_sample(7000 * units.rpm, 55 * units.deg)
        .add_sample(8000 * units.rpm, 55 * units.deg)

    engine.add_ignition_module(
        ferrari_f105ab_distributor(
            wires: wires,
            timing_curve: timing_curve
        )
    )
}

// 1984 Ferrari 308 GTB Quattrovalvole
public node ferrari_308_gtb_qv_vehicle {
    alias output __out:
        vehicle(
            mass: 1330 * units.kg,
            drag_coefficient: 0.4,
            cross_sectional_area: 1.6 * (units.m * units.m),
            diff_ratio: 3.824,
            tire_radius: 316 * units.mm, // 225/50VR16
            rolling_resistance: 1330 * units.kg * 9.81 * (215.0/275.0) * 0.01 // front tires are 205
        );
}

public node ferrari_308_gtb_qv_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 400 * units.lb_ft
        )
        .add_gear(3.418)
        .add_gear(2.353)
        .add_gear(1.693)
        .add_gear(1.244)
        .add_gear(0.918);
}

public node main {
    set_engine(ferrari_f105ab_engine())
    set_vehicle(ferrari_308_gtb_qv_vehicle())
    set_transmission(ferrari_308_gtb_qv_transmission())
}

main()