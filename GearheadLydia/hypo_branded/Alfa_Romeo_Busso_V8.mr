import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

// firing order: 15726348
// in standard:  54368721

public node alfa_romeo_busso_30_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 6700 * units.rpm;
    input limiter_duration: 0.11;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0 / 8.0) * cycle)
            .connect_wire(wires.wire5, (1.0 / 8.0) * cycle)
            .connect_wire(wires.wire7, (2.0 / 8.0) * cycle)
            .connect_wire(wires.wire2, (3.0 / 8.0) * cycle)
            .connect_wire(wires.wire6, (4.0 / 8.0) * cycle)
            .connect_wire(wires.wire3, (5.0 / 8.0) * cycle)
            .connect_wire(wires.wire4, (6.0 / 8.0) * cycle)
            .connect_wire(wires.wire8, (7.0 / 8.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
    output wire7: ignition_wire();
    output wire8: ignition_wire();
}

// https://www.alfaowner.com/threads/camshaft-specifications.994617/
// 3.0 V6
// Cam Lift Duration Open Close Lobe Center
// Inlet 9.3mm 215 1 BTDC 32 ABDC 109
// Exhaust 9.3mm 215 37 BBDC 2 ATDC 109
// 3.2 V6 GTA
// Inlet 9.3mm 223 1 ATDC 44 ABDC 113
// Exhaust 9.3mm 215 36 BBDC 3 ATDC 110
// 3.2 V6 GTA2 (Popular grind in South Africa)
// Inlet 9.3mm 231 9 BTDC??? 40 ABDC??? 109???
// Exhaust 9.3mm 223 41 BBDC??? 6 ATDC??? 109???

public node alfa_romeo_busso_30_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 213 * units.deg,
            gamma: 1.0,
            lift: 9.3 * units.mm,
            steps: 100
        );
}

public node alfa_romeo_busso_30_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 219 * units.deg,
            gamma: 1.0,
            lift: 9.3 * units.mm,
            steps: 100
        );
}

public node alfa_romeo_busso_30_camshaft_builder {
    input in_lobe_profile: alfa_romeo_busso_30_lobe_profile_int();
    input ex_lobe_profile: alfa_romeo_busso_30_lobe_profile_exh();
    input intake_lobe_profile: in_lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input lobe_separation: 106.5 * units.deg;
    input intake_lobe_center: lobe_separation;
    input exhaust_lobe_center: lobe_separation;
    input advance: 1.0 * units.deg;
    input base_radius: 15 * units.mm;

    output intake_cam_0: _intake_cam_0;
    output exhaust_cam_0: _exhaust_cam_0;
    output intake_cam_1: _intake_cam_1;
    output exhaust_cam_1: _exhaust_cam_1;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_0(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_0(params, lobe_profile: exhaust_lobe_profile)

    camshaft _intake_cam_1(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_1(params, lobe_profile: exhaust_lobe_profile)


    label rot30(30 * units.deg)
    label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    // firing order: 15726348
    _intake_cam_0
        .add_lobe(rot360 + intake_lobe_center + 0 * rot90) // #1
        .add_lobe(rot360 + intake_lobe_center + 3 * rot90) // #2
        .add_lobe(rot360 + intake_lobe_center + 5 * rot90) // #3
        .add_lobe(rot360 + intake_lobe_center + 6 * rot90) // #4

    _intake_cam_1
        .add_lobe(rot360 + intake_lobe_center + 1 * rot90) // #5
        .add_lobe(rot360 + intake_lobe_center + 4 * rot90) // #6
        .add_lobe(rot360 + intake_lobe_center + 2 * rot90) // #7
        .add_lobe(rot360 + intake_lobe_center + 7 * rot90) // #8

    _exhaust_cam_0
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 5 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 6 * rot90)

    _exhaust_cam_1
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 4 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 7 * rot90)
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.thou, k_28inH2O(flow))
}

public node alfa_romeo_busso_30_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 54.976 * units.cc;
    input flip_display: false;
    
    input flow_attenuation: 0.8;
    input lift_scale: 50.0;
    alias output __out: head;

    function intake_flow(1.15 * units.mm)
    intake_flow
        .add_flow_sample(0 * lift_scale, 0 * flow_attenuation)
        .add_flow_sample(1 * lift_scale, 30 * flow_attenuation)
        .add_flow_sample(2 * lift_scale, 60 * flow_attenuation)
        .add_flow_sample(3 * lift_scale, 90 * flow_attenuation)
        .add_flow_sample(4 * lift_scale, 125 * flow_attenuation)
        .add_flow_sample(5 * lift_scale, 160 * flow_attenuation)
        .add_flow_sample(6 * lift_scale, 195 * flow_attenuation)
        .add_flow_sample(7 * lift_scale, 210 * flow_attenuation)
        .add_flow_sample(8 * lift_scale, 235 * flow_attenuation)
        .add_flow_sample(9 * lift_scale, 270 * flow_attenuation)
        .add_flow_sample(10 * lift_scale, 305 * flow_attenuation)
        .add_flow_sample(11 * lift_scale, 320 * flow_attenuation)
        .add_flow_sample(12 * lift_scale, 335 * flow_attenuation)

    function exhaust_flow(1.15 * units.mm)
    exhaust_flow
        .add_flow_sample(0 * lift_scale, 0 * flow_attenuation)
        .add_flow_sample(1 * lift_scale, 30 * flow_attenuation)
        .add_flow_sample(2 * lift_scale, 55 * flow_attenuation)
        .add_flow_sample(3 * lift_scale, 85 * flow_attenuation)
        .add_flow_sample(4 * lift_scale, 115 * flow_attenuation)
        .add_flow_sample(5 * lift_scale, 140 * flow_attenuation)
        .add_flow_sample(6 * lift_scale, 160 * flow_attenuation)
        .add_flow_sample(7 * lift_scale, 180 * flow_attenuation)
        .add_flow_sample(8 * lift_scale, 205 * flow_attenuation)
        .add_flow_sample(9 * lift_scale, 220 * flow_attenuation)
        .add_flow_sample(10 * lift_scale, 240 * flow_attenuation)
        .add_flow_sample(11 * lift_scale, 260 * flow_attenuation)
        .add_flow_sample(12 * lift_scale, 280 * flow_attenuation)

        
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(0.9 * units.inch) * 4 * units.inch,
        intake_runner_cross_section_area: circle_area(0.9 * units.inch), // actual runner about 0.8" x 7.5"
        exhaust_runner_volume: circle_area(0.9 * units.inch) * 3 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.9 * units.inch), // actual runner about 0.8" x 15"

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}


public node alfa_romeo_busso_30 {
    alias output __out: engine;

    wires wires()

    engine engine(
        name: "Alfa Romeo Busso",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 900 * units.rpm,
        redline: 6500 * units.rpm,
        throttle_gamma: 2.0,
        hf_gain: 0.05,
        jitter: 0.5
    )

    label bore(93.0 * units.mm)
    label stroke(72.6 * units.mm)
    label rod_length(131.1 * units.mm)
    label rod_mass(434.0 * units.g)
    label piston_mass(400 * units.g)
    label compression_height(32 * units.mm) // 30 for 3.2
    label deck_height((203.0 * units.mm) + (0.3 * units.mm)) // calculated, approximate
    label piston_dome(26 * units.cc) // calculated, approximate
    label crank_mass(45 * units.lb)
    label flywheel_mass(15 * units.lb)
    label flywheel_radius(6 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 15 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia: crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 120.0 * units.deg
    )

    rod_journal rj1(angle:  0.0  * units.deg)
    rod_journal rj2(angle: -90.0 * units.deg)
    rod_journal rj3(angle:  90.0 * units.deg)
    rod_journal rj4(angle: 180.0 * units.deg)

    c0
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)

    piston_parameters piston_params(
        mass: piston_mass,
        //blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    intake intake (
        plenum_volume: 2.0 * units.L,
        plenum_cross_section_area: 100.0 * units.cm2,
        intake_flow_rate: k_carb(400.0),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.995,
        throttle_gamma: 2.0,
        runner_flow_rate: k_carb(300.0),
        runner_length: 4.0 * units.inch,
        velocity_decay: 0.1
    )
    
    label exhaust_radius((3.0 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(800.0),
        primary_tube_length: 5.0 * units.inch,
        primary_flow_rate: k_carb(750.0),
        velocity_decay: 1.0, //0.5
        collector_cross_section_area: circle_area(exhaust_radius) * 2,
        length: 800 * units.mm,
        volume: (500 * units.mm * circle_area(exhaust_radius)) + (300 * units.mm * circle_area(exhaust_radius) * 2)
    )

    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.01, impulse_response: ir_lib.default_0)
    
    label pipe_length_1(3 * units.inch)
    label pipe_length_2(0 * units.inch)
    label pipe_length_3(1 * units.inch)
    label pipe_length_4(4 * units.inch)
    
    label exhaust_stagger(1.2 * units.inch)

    cylinder_bank b0(bank_params, angle: 45.0 * units.deg) // L (first)
    b0
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            intake: intake,
            exhaust_system: exhaust,
            ignition_wire: wires.wire1,
            primary_length: pipe_length_1 + exhaust_stagger
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            intake: intake,
            exhaust_system: exhaust,
            ignition_wire: wires.wire2,
            primary_length: pipe_length_2 + exhaust_stagger
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            intake: intake,
            exhaust_system: exhaust,
            ignition_wire: wires.wire3,
            primary_length: pipe_length_3 + exhaust_stagger
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            intake: intake,
            exhaust_system: exhaust,
            ignition_wire: wires.wire4,
            primary_length: pipe_length_4 + exhaust_stagger
        )

    cylinder_bank b1(bank_params, angle: -45 * units.deg) // R (second)
    b1
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            intake: intake,
            exhaust_system: exhaust,
            ignition_wire: wires.wire5,
            primary_length: pipe_length_1
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            intake: intake,
            exhaust_system: exhaust,
            ignition_wire: wires.wire6,
            primary_length: pipe_length_2
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            intake: intake,
            exhaust_system: exhaust,
            ignition_wire: wires.wire7,
            primary_length: pipe_length_3
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            intake: intake,
            exhaust_system: exhaust,
            ignition_wire: wires.wire8,
            primary_length: pipe_length_4
        )

    engine
        .add_cylinder_bank(b0)
        .add_cylinder_bank(b1)

    engine.add_crankshaft(c0)

    alfa_romeo_busso_30_camshaft_builder camshaft(
        in_lobe_profile: alfa_romeo_busso_30_lobe_profile_int(),
        ex_lobe_profile: alfa_romeo_busso_30_lobe_profile_exh()
    )

    b0.set_cylinder_head (
        alfa_romeo_busso_30_head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0,
            flip_display: true
        )
    )
    b1.set_cylinder_head (
        alfa_romeo_busso_30_head(
            intake_camshaft: camshaft.intake_cam_1,
            exhaust_camshaft: camshaft.exhaust_cam_1,
            flip_display: false
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 20 * units.deg)
        .add_sample(1000 * units.rpm, 50 * units.deg)
        .add_sample(2000 * units.rpm, 55 * units.deg)
        .add_sample(3000 * units.rpm, 60 * units.deg)
        .add_sample(4000 * units.rpm, 65 * units.deg)
        .add_sample(5000 * units.rpm, 70 * units.deg)
        .add_sample(6000 * units.rpm, 75 * units.deg)
        .add_sample(7000 * units.rpm, 75 * units.deg)
        .add_sample(8000 * units.rpm, 75 * units.deg)
        .add_sample(9000 * units.rpm, 75 * units.deg)

    engine.add_ignition_module(
        alfa_romeo_busso_30_distributor(
            wires: wires,
            timing_curve: timing_curve,
            rev_limit: 6700 * units.rpm
        )
    )
}

// 1993 Alfa Romeo SZ
public node alfa_romeo_sprint_zagato_vehicle {
    alias output __out:
        vehicle(
            mass: 2778 * units.lb,
            drag_coefficient: 0.3,
            cross_sectional_area: 1.88 * (units.m * units.m),
            diff_ratio: 3.909,
            tire_radius: 316 * units.mm, // 225/50R16
            rolling_resistance: 500 * units.N
        );
}

public node alfa_romeo_sprint_zagato_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 500 * units.lb_ft
        )
        .add_gear(2.875)
        .add_gear(1.720)
        .add_gear(1.226)
        .add_gear(0.946)
        .add_gear(0.780);
}

public node main {
    set_engine(alfa_romeo_busso_30())
    set_vehicle(alfa_romeo_sprint_zagato_vehicle())
    set_transmission(alfa_romeo_sprint_zagato_transmission())
}

main()