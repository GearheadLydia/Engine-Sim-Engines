import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

public node chrysler_egv6_sohc_egg_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 6750 * units.rpm;
	input limiter_duration: 0.08;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0 / 6.0) * cycle)
            .connect_wire(wires.wire2, (1.0 / 6.0) * cycle)
            .connect_wire(wires.wire3, (2.0 / 6.0) * cycle)
            .connect_wire(wires.wire4, (3.0 / 6.0) * cycle)
            .connect_wire(wires.wire5, (4.0 / 6.0) * cycle)
            .connect_wire(wires.wire6, (5.0 / 6.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
}

// cam specs are very much guesses
public node chrysler_egv6_sohc_egg_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 205 * units.deg,
            gamma: 1.0,
            lift: 375 * units.thou,
            steps: 100
        );
}

public node chrysler_egv6_sohc_egg_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 205 * units.deg,
            gamma: 1.0,
            lift: 375 * units.thou,
            steps: 100
        );
}

public node chrysler_egv6_sohc_egg_camshaft_builder {
    input in_lobe_profile: chrysler_egv6_sohc_egg_lobe_profile_int();
	input ex_lobe_profile: chrysler_egv6_sohc_egg_lobe_profile_exh();
    input intake_lobe_profile: in_lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input lobe_separation: 115.0 * units.deg;
    input intake_lobe_center: lobe_separation;
    input exhaust_lobe_center: lobe_separation;
    input advance: 0.0 * units.deg;
    input base_radius: 1.2 / 2 * units.inch;

    output intake_cam_0: _intake_cam_0;
    output exhaust_cam_0: _exhaust_cam_0;
    output intake_cam_1: _intake_cam_1;
    output exhaust_cam_1: _exhaust_cam_1;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_0(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_0(params, lobe_profile: exhaust_lobe_profile)

    camshaft _intake_cam_1(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_1(params, lobe_profile: exhaust_lobe_profile)


    label rot30(30 * units.deg)
	label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)
	
    _intake_cam_0
        .add_lobe(rot360 + intake_lobe_center + 0 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 2 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 4 * rot120)

    _exhaust_cam_0
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 4 * rot120)

    _intake_cam_1
        .add_lobe(rot360 + intake_lobe_center + 1 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 3 * rot120)
        .add_lobe(rot360 + intake_lobe_center + 5 * rot120)

    _exhaust_cam_1
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot120)
        .add_lobe(rot360 - exhaust_lobe_center + 5 * rot120)
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.mm, k_28inH2O(flow))
}

public node chrysler_egv6_sohc_egg_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 52.0 * units.cc; // should be correct
    input flip_display: false;
	
	input flow_attenuation_in: 0.85;
	input flow_attenuation_ex: 0.9;
    input lift_scale: 1.0;
    alias output __out: head;

    function intake_flow(1 * units.mm)
    intake_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_in)
        .add_flow_sample( 1 * lift_scale,  25 * flow_attenuation_in)
        .add_flow_sample( 2 * lift_scale,  76 * flow_attenuation_in)
        .add_flow_sample( 3 * lift_scale, 100 * flow_attenuation_in)
        .add_flow_sample( 4 * lift_scale, 146 * flow_attenuation_in)
        .add_flow_sample( 5 * lift_scale, 175 * flow_attenuation_in)
        .add_flow_sample( 6 * lift_scale, 212 * flow_attenuation_in)
        .add_flow_sample( 7 * lift_scale, 230 * flow_attenuation_in)
        .add_flow_sample( 8 * lift_scale, 255 * flow_attenuation_in)
        .add_flow_sample( 9 * lift_scale, 275 * flow_attenuation_in)
        .add_flow_sample(10 * lift_scale, 294 * flow_attenuation_in)
        .add_flow_sample(11 * lift_scale, 300 * flow_attenuation_in)
        .add_flow_sample(12 * lift_scale, 314 * flow_attenuation_in)
        .add_flow_sample(13 * lift_scale, 314 * flow_attenuation_in)
        .add_flow_sample(14 * lift_scale, 314 * flow_attenuation_in)

    function exhaust_flow(1 * units.mm)
    exhaust_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_ex)
        .add_flow_sample( 1 * lift_scale,  25 * flow_attenuation_ex)
        .add_flow_sample( 2 * lift_scale,  70 * flow_attenuation_ex)
        .add_flow_sample( 3 * lift_scale, 100 * flow_attenuation_ex)
        .add_flow_sample( 4 * lift_scale, 132 * flow_attenuation_ex)
        .add_flow_sample( 5 * lift_scale, 140 * flow_attenuation_ex)
        .add_flow_sample( 6 * lift_scale, 156 * flow_attenuation_ex)
        .add_flow_sample( 7 * lift_scale, 170 * flow_attenuation_ex)
        .add_flow_sample( 8 * lift_scale, 181 * flow_attenuation_ex)
        .add_flow_sample( 9 * lift_scale, 191 * flow_attenuation_ex)
        .add_flow_sample(10 * lift_scale, 207 * flow_attenuation_ex)
        .add_flow_sample(11 * lift_scale, 214 * flow_attenuation_ex)
        .add_flow_sample(12 * lift_scale, 228 * flow_attenuation_ex)
        .add_flow_sample(13 * lift_scale, 228 * flow_attenuation_ex)
        .add_flow_sample(14 * lift_scale, 228 * flow_attenuation_ex)
		
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.0 * units.inch) * 6 * units.inch,
        intake_runner_cross_section_area: circle_area(1.0 * units.inch),
        exhaust_runner_volume: circle_area(0.7 * units.inch) * 4 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.7 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}

// https://www.dodgeintrepid.net/threads/more-engine-info-3-5-3-3.67452/
// https://dacomsa.com/dacomsastorefront/dacomsa/en/All-lines/MORESA-PISTONS/PISTON/p/1110+STD
// https://www.brakeandfrontend.com/ohc-engine-repairs-and-service-on-chrysler-s-3-5l-v6/
public node chrysler_egv6_sohc_egg_engine {
    alias output __out: engine;

    wires wires()

    engine engine(
        name: "Chrysler V6 EGG",
        starter_torque: 150 * units.lb_ft,
		starter_speed: 1200 * units.rpm,
        redline: 6500 * units.rpm,
        throttle_gamma: 1.0
    )

    label bore(96 * units.mm)
	label bore_spacing(4.45 * units.inch)
    label stroke(81 * units.mm)
    label rod_length(5.945 * units.inch)
    label rod_mass(464 * units.g)
    label compression_height(1.49 * units.inch)
	label piston_mass(482 * units.g)
	label piston_dome(-1.0 * units.cc) // for 10.4:1 compression; also very close to bowl dimensions
	label deck_height((229.0 + 1.58) * units.mm) // not sure on gasket
    label crank_mass(57 * units.lb)
    label flywheel_mass(45 * units.lb)
    label flywheel_radius(6 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 20 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia:
            crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 120.0 * units.deg // 30 degrees from top
    )

    rod_journal rj0(angle:   0 * units.deg)
    rod_journal rj1(angle:  60 * units.deg)
    rod_journal rj2(angle: 240 * units.deg)
    rod_journal rj3(angle: 300 * units.deg)
    rod_journal rj4(angle: 120 * units.deg)
    rod_journal rj5(angle: 180 * units.deg)

    c0
        .add_rod_journal(rj0)
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)
        .add_rod_journal(rj5)


    piston_parameters piston_params(
        mass: piston_mass,
        //blowby: 0,
        compression_height: compression_height,
        wrist_pin_position: 0 * units.mm,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    intake intake(
        plenum_volume: 1.0 * units.L,
        plenum_cross_section_area: circle_area(60 * units.mm),
        intake_flow_rate: k_carb(600),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.996,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(250.0),
        runner_length: 6.0 * units.inch,
        velocity_decay: 0.1
    )

	label exhaust_radius((3.0 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(600.0),
        primary_tube_length: 8.0 * units.inch,
        primary_flow_rate: k_carb(250.0),
        velocity_decay: 1.0, //0.5
		collector_cross_section_area: circle_area(exhaust_radius) * 2,
		length: 2500 * units.mm,
		volume: (2000 * units.mm * circle_area(exhaust_radius)) + (500 * units.mm * circle_area(exhaust_radius) * 2)
    )

    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.01, impulse_response: ir_lib.default_0)
	
	label pipe_length_1(bore_spacing * 1.0)
	label pipe_length_2(bore_spacing * 0.0)
	label pipe_length_3(bore_spacing * 0.5)
	
	label exhaust_stagger(bore_spacing * 0.3)

    cylinder_bank b0(bank_params, angle: 30 * units.deg) // L (first)
    b0
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            ignition_wire: wires.wire1,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: pipe_length_1 + exhaust_stagger,
			sound_attenuation: 0.5 * 0.8
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire3,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: pipe_length_2 + exhaust_stagger,
			sound_attenuation: 1.0 * 0.8
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire5,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: pipe_length_3 + exhaust_stagger,
			sound_attenuation: 0.8 * 0.8
        )

    cylinder_bank b1(bank_params, angle: -30 * units.deg) // R (second)
    b1
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire2,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: pipe_length_1,
			sound_attenuation: 0.5
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire4,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: pipe_length_2,
			sound_attenuation: 1.0
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj5,
            ignition_wire: wires.wire6,
            intake: intake,
            exhaust_system: exhaust,
			primary_length: pipe_length_3,
			sound_attenuation: 0.8
        )

    engine
        .add_cylinder_bank(b0)
        .add_cylinder_bank(b1)

    engine.add_crankshaft(c0)

    chrysler_egv6_sohc_egg_camshaft_builder camshaft(
	    in_lobe_profile: chrysler_egv6_sohc_egg_lobe_profile_int(),
		ex_lobe_profile: chrysler_egv6_sohc_egg_lobe_profile_exh()
	)

    b0.set_cylinder_head (
        chrysler_egv6_sohc_egg_head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0,
			flip_display: true
        )
    )
    b1.set_cylinder_head (
        chrysler_egv6_sohc_egg_head(
            intake_camshaft: camshaft.intake_cam_1,
            exhaust_camshaft: camshaft.exhaust_cam_1,
            flip_display: false
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 40 * units.deg)
        .add_sample(1000 * units.rpm, 50 * units.deg)
        .add_sample(2000 * units.rpm, 50 * units.deg)
        .add_sample(3000 * units.rpm, 50 * units.deg)
        .add_sample(4000 * units.rpm, 50 * units.deg)
        .add_sample(5000 * units.rpm, 50 * units.deg)
        .add_sample(6000 * units.rpm, 50 * units.deg)
		.add_sample(7000 * units.rpm, 50 * units.deg)

    engine.add_ignition_module(
        chrysler_egv6_sohc_egg_distributor(
            wires: wires,
            timing_curve: timing_curve
        )
    )
}

// 1998 Plymouth Prowler
public node plymouth_prowler_vehicle {
    alias output __out:
        vehicle(
            mass: 2833 * units.lb,
            drag_coefficient: 0.5,
            cross_sectional_area: 2.07 * (units.m) * (units.m),
            diff_ratio: 3.89,
            tire_radius: 387.5 * units.mm, // 295/45R20
            rolling_resistance: 500 * units.N
        );
}

// 2002 Dodge Intrepid R/T (LH)
public node dodge_intrepid_rt_lh_vehicle {
    alias output __out:
        vehicle(
            mass: 3549 * units.lb,
            drag_coefficient: 0.3,
            cross_sectional_area: 2.24 * (units.m) * (units.m),
            diff_ratio: 3.66,
            tire_radius: 339 * units.mm, // 225/55VR17
            rolling_resistance: 420 * units.N
        );
}

// 2009 Dodge Challenger SE (LC)
public node dodge_challenger_se_vehicle {
    alias output __out:
        vehicle(
            mass: 3721 * units.lb,
            drag_coefficient: 0.35,
            cross_sectional_area: 2.35 * (units.m) * (units.m),
            diff_ratio: 3.64,
            tire_radius: 355.5 * units.mm, // 215/65VR17
            rolling_resistance: 420 * units.N
        );
}

// 42LE and 42RLE (RWD for LX platform cars) have the same ratios
public node chrysler_42le_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 500 * units.lb_ft
        )
        .add_gear(2.84)
        .add_gear(1.57)
        .add_gear(1.00)
        .add_gear(0.69);
}

public node main {
    set_engine(chrysler_egv6_sohc_egg_engine())
    set_vehicle(plymouth_prowler_vehicle())
    set_transmission(chrysler_42le_transmission())
}

main()