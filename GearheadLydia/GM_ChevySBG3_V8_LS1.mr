import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

// firing order: 18726543

public node chevrolet_ls1_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 6500 * units.rpm;
    input limiter_duration: 0.09;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0 / 8.0) * cycle)
            .connect_wire(wires.wire8, (1.0 / 8.0) * cycle)
            .connect_wire(wires.wire7, (2.0 / 8.0) * cycle)
            .connect_wire(wires.wire2, (3.0 / 8.0) * cycle)
            .connect_wire(wires.wire6, (4.0 / 8.0) * cycle)
            .connect_wire(wires.wire5, (5.0 / 8.0) * cycle)
            .connect_wire(wires.wire4, (6.0 / 8.0) * cycle)
            .connect_wire(wires.wire3, (7.0 / 8.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
    output wire7: ignition_wire();
    output wire8: ignition_wire();
}


// https://www.onallcylinders.com/2017/10/12/ls1-engine-specs-performance-bore-stroke-cylinder-heads-cam-specs/

// 1999 Camaro cams
public node chevrolet_ls1_1999_fbody_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 198 * units.deg,
            gamma: 1.0,
            lift: 498 * units.thou,
            steps: 100
        );
}

public node chevrolet_ls1_1999_fbody_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 209 * units.deg,
            gamma: 1.0,
            lift: 497 * units.thou,
            steps: 100
        );
}

// 2002 all model cams
public node chevrolet_ls1_2002_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 196 * units.deg,
            gamma: 1.0,
            lift: 467 * units.thou,
            steps: 100
        );
}

public node chevrolet_ls1_2002_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 208 * units.deg,
            gamma: 1.0,
            lift: 479 * units.thou,
            steps: 100
        );
}

label chevrolet_ls1_1999_fbody_lobe_separation(119.5 * units.deg)
label chevrolet_ls1_1999_fbody_advance(-2.5 * units.deg)
label chevrolet_ls1_2002_lobe_separation(116.0 * units.deg)
label chevrolet_ls1_2002_advance(-1.0 * units.deg) // guess

public node chevrolet_ls1_camshaft_builder {
    input in_lobe_profile: chevrolet_ls1_1999_fbody_lobe_profile_int();
    input ex_lobe_profile: chevrolet_ls1_1999_fbody_lobe_profile_exh();
    input intake_lobe_profile: in_lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input lobe_separation: chevrolet_ls1_1999_fbody_lobe_separation;
    input intake_lobe_center: lobe_separation;
    input exhaust_lobe_center: lobe_separation;
    input advance: chevrolet_ls1_1999_fbody_advance;
    input base_radius: 1.462 / 2 * units.inch;

    output intake_cam_0: _intake_cam_0;
    output exhaust_cam_0: _exhaust_cam_0;
    output intake_cam_1: _intake_cam_1;
    output exhaust_cam_1: _exhaust_cam_1;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_0(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_0(params, lobe_profile: exhaust_lobe_profile)

    camshaft _intake_cam_1(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_1(params, lobe_profile: exhaust_lobe_profile)


    label rot30(30 * units.deg)
    label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_0
        .add_lobe(rot360 + intake_lobe_center + 0 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 7 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 5 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 2 * rot90)

    _intake_cam_1
        .add_lobe(rot360 + intake_lobe_center + 3 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 6 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 4 * rot90)
        .add_lobe(rot360 + intake_lobe_center + 1 * rot90)

    _exhaust_cam_0
        .add_lobe(rot360 - exhaust_lobe_center + 0 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 7 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 5 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 2 * rot90)

    _exhaust_cam_1
        .add_lobe(rot360 - exhaust_lobe_center + 3 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 6 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 4 * rot90)
        .add_lobe(rot360 - exhaust_lobe_center + 1 * rot90)
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.thou, k_28inH2O(flow))
}

public node chevrolet_ls1_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 67.0 * units.cc;
    input flip_display: false;
    
    alias output __out: head;
    
    // https://www.motortrend.com/how-to/hrdp-1012-ultimate-chevrolet-ls-cylinder-head-test/
    function intake_flow(50 * units.thou)
    intake_flow
        .add_flow_sample(  0,   0)
        .add_flow_sample( 50,  32) // interp
        .add_flow_sample(100,  67)
        .add_flow_sample(150, 100) // interp
        .add_flow_sample(200, 136)
        .add_flow_sample(250, 170) // interp
        .add_flow_sample(300, 196)
        .add_flow_sample(350, 210) // interp
        .add_flow_sample(400, 217)
        .add_flow_sample(450, 221) // interp
        .add_flow_sample(500, 224)
        .add_flow_sample(550, 227) // interp
        .add_flow_sample(600, 230)

    function exhaust_flow(50 * units.thou)
    exhaust_flow
        .add_flow_sample(  0,   0)
        .add_flow_sample( 50,  28) // interp
        .add_flow_sample(100,  53)
        .add_flow_sample(150,  80) // interp
        .add_flow_sample(200, 105)
        .add_flow_sample(250, 124) // interp
        .add_flow_sample(300, 141)
        .add_flow_sample(350, 154) // interp
        .add_flow_sample(400, 165)
        .add_flow_sample(450, 179) // interp
        .add_flow_sample(500, 193)
        .add_flow_sample(550, 204) // interp
        .add_flow_sample(600, 211)
        
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume:  205 * units.cc,
        intake_runner_cross_section_area:  circle_area(2.00 * units.inch / 2),
        exhaust_runner_volume:  70 * units.cc,
        exhaust_runner_cross_section_area: circle_area(1.55 * units.inch / 2),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}


public node chevrolet_ls1_engine {
    alias output __out: engine;

    wires wires()

    engine engine(
        name: "Chevrolet LS1",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 1000 * units.rpm,
        redline: 6000 * units.rpm,
        throttle_gamma: 1.0,
        // jitter: 0.7,
        // hf_gain: 0.15
        jitter: 0.6,
        hf_gain: 0.1
        // jitter: 0.1,
        // hf_gain: 0.1
    )


    // source: https://help.summitracing.com/app/answers/detail/a_id/4813/~/chevy-ls1-engine-specs
    label bore(3.898 * units.inch)
    label bore_spacing(4.4 * units.inch)
    label stroke(3.622 * units.inch)
    label rod_length(6.098 * units.inch)
    label rod_mass(464 * units.g)
    label compression_height(1.314 * units.inch)
    label piston_mass(482 * units.g)
    label piston_dome(-4.00 * units.cc)
    label deck_height(9.240 * units.inch)
    label crank_mass(57 * units.lb)
    label flywheel_mass(57 * units.lb)
    label flywheel_radius(6 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 20 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia:
            crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 45.0 * units.deg // 45 degrees back from right side
    )

    rod_journal rj0(angle: 0.0)
    rod_journal rj1(angle: -90.0 * units.deg)
    rod_journal rj2(angle: 90.0 * units.deg)
    rod_journal rj3(angle: 180.0 * units.deg)

    c0
        .add_rod_journal(rj0)
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)


    piston_parameters piston_params(
        mass: piston_mass,
        //blowby: 0,
        compression_height: compression_height,
        wrist_pin_position: 0 * units.mm,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    intake intake(
        plenum_volume: 0.3 * units.L,
        plenum_cross_section_area: circle_area(45 * units.mm),
        intake_flow_rate: k_carb(750),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.996,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(250.0),
        runner_length: 11.0 * units.inch,
        velocity_decay: 0.1
    )
    
    label exhaust_radius((4.0 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(400.0),
        primary_tube_length: 2.0 * units.inch,
        primary_flow_rate: k_carb(250.0),
        velocity_decay: 0.3,
        collector_cross_section_area: circle_area(exhaust_radius),
        length: 2600 * units.mm,
        volume: 2600 * units.mm * circle_area(exhaust_radius)
    )

    exhaust_system exhaustL(es_params, audio_volume: 1.0 * 0.005, impulse_response: ir_lib.default_0)
    exhaust_system exhaustR(es_params, audio_volume: 1.0 * 0.005, impulse_response: ir_lib.default_0)
    
    // lower grade LS headers //
    label pipe_length_1(bore_spacing * 2.2)
    label pipe_length_2(bore_spacing * 1.2)
    label pipe_length_3(bore_spacing * 0.2)
    label pipe_length_4(bore_spacing * 0.0)
    label pipe_volume_1(0.6)
    label pipe_volume_2(0.8)
    label pipe_volume_3(1.0)
    label pipe_volume_4(1.0)
    
    // stock Z06 headers //
    // label pipe_length_1(bore_spacing * 0.6)
    // label pipe_length_2(bore_spacing * 0.1)
    // label pipe_length_3(bore_spacing * 0.0)
    // label pipe_length_4(bore_spacing * 0.5)
    // label pipe_volume_1(0.9)
    // label pipe_volume_2(1.0)
    // label pipe_volume_3(1.0)
    // label pipe_volume_4(0.9)
    
    label exhaust_stagger(40 * units.mm)

    cylinder_bank b0(bank_params, angle: -45.0 * units.deg) // R (first)
    b0
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            ignition_wire: wires.wire1,
            intake: intake,
            exhaust_system: exhaustR,
            primary_length: pipe_length_1 + exhaust_stagger,
            sound_attenuation: pipe_volume_1 * 0.9
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire3,
            intake: intake,
            exhaust_system: exhaustR,
            primary_length: pipe_length_2 + exhaust_stagger,
            sound_attenuation: pipe_volume_2 * 0.9
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire5,
            intake: intake,
            exhaust_system: exhaustR,
            primary_length: pipe_length_3 + exhaust_stagger,
            sound_attenuation: pipe_volume_3 * 0.9
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire7,
            intake: intake,
            exhaust_system: exhaustR,
            primary_length: pipe_length_4 + exhaust_stagger,
            sound_attenuation: pipe_volume_4 * 0.9
        )

    cylinder_bank b1(bank_params, angle: 45.0 * units.deg) // L (second)
    b1
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            ignition_wire: wires.wire2,
            intake: intake,
            exhaust_system: exhaustL,
            primary_length: pipe_length_1,
            sound_attenuation: pipe_volume_1
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire4,
            intake: intake,
            exhaust_system: exhaustL,
            primary_length: pipe_length_2,
            sound_attenuation: pipe_volume_2
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire6,
            intake: intake,
            exhaust_system: exhaustL,
            primary_length: pipe_length_3,
            sound_attenuation: pipe_volume_3
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire8,
            intake: intake,
            exhaust_system: exhaustL,
            primary_length: pipe_length_4,
            sound_attenuation: pipe_volume_4
        )

    engine
        .add_cylinder_bank(b0)
        .add_cylinder_bank(b1)

    engine.add_crankshaft(c0)

    chevrolet_ls1_camshaft_builder camshaft(
        in_lobe_profile: chevrolet_ls1_1999_fbody_lobe_profile_int(),
        ex_lobe_profile: chevrolet_ls1_1999_fbody_lobe_profile_exh()
    )

    b0.set_cylinder_head (
        chevrolet_ls1_head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0,
            flip_display: false
        )
    )
    b1.set_cylinder_head (
        chevrolet_ls1_head(
            intake_camshaft: camshaft.intake_cam_1,
            exhaust_camshaft: camshaft.exhaust_cam_1,
            flip_display: true
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 20 * units.deg)
        .add_sample(1000 * units.rpm, 27 * units.deg)
        .add_sample(2000 * units.rpm, 31 * units.deg)
        .add_sample(3000 * units.rpm, 34 * units.deg)
        .add_sample(4000 * units.rpm, 37 * units.deg)
        .add_sample(5000 * units.rpm, 40 * units.deg)
        .add_sample(6000 * units.rpm, 41 * units.deg)
        .add_sample(7000 * units.rpm, 41 * units.deg)

    engine.add_ignition_module(
        chevrolet_ls1_distributor(
            wires: wires,
            timing_curve: timing_curve
        )
    )
}

// 2004 Chevrolet Corvette Coupe (C5)
public node chevrolet_corvette_coupe_c5_vehicle {
    alias output __out:
        vehicle(
            mass: 3225 * units.lb,
            drag_coefficient: 0.29,
            cross_sectional_area: 1.88 * (units.m) * (units.m),
            diff_ratio: 3.42,
            tire_radius: 339.0 * units.mm, // 275/40ZR18
            rolling_resistance: 580 * units.N
        );
}

// 2004 Pontiac GTO 5.7 (V2)
public node pontiac_gto_57_v2_vehicle {
    alias output __out:
        vehicle(
            mass: 3726 * units.lb,
            drag_coefficient: 0.32,
            cross_sectional_area: 2.13 * (units.m) * (units.m),
            diff_ratio: 3.46,
            tire_radius: 329.0 * units.mm, // 225/50ZR17
            rolling_resistance: 450 * units.N
        );
}

// 1998-2002 Chevrolet Camaro SS
public node chevrolet_camaro_fbody_vehicle {
    alias output __out:
        vehicle(
            mass: 3410 * units.lb,
            drag_coefficient: 0.34,
            cross_sectional_area: 2.03 * (units.m) * (units.m),
            diff_ratio: 3.42,
            tire_radius: 326.5 * units.mm, // 275/40ZR17
            rolling_resistance: 500 * units.N
        );
}

public node tremec_t56_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 1000 * units.lb_ft
        )
        .add_gear(2.97)
        .add_gear(2.07)
        .add_gear(1.43)
        .add_gear(1.00)
        .add_gear(0.84)
        .add_gear(0.56);
}

public node tremec_t56_camaro_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 1000 * units.lb_ft
        )
        .add_gear(2.66)
        .add_gear(1.78)
        .add_gear(1.30)
        .add_gear(1.00)
        .add_gear(0.74)
        .add_gear(0.50);
}

public node main {
    set_engine(chevrolet_ls1_engine())
    set_vehicle(chevrolet_camaro_fbody_vehicle())
    set_transmission(tremec_t56_camaro_transmission())
}

main()