import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

public node toyota_2zzge_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 8500 * units.rpm; // 8200 in Celica / 8500 in Elise
    input limiter_duration: 0.04; // flavor: 0.07 in Celica / 0.04 in Elise
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, (0.0/4.0) * cycle)
            .connect_wire(wires.wire3, (1.0/4.0) * cycle)
            .connect_wire(wires.wire4, (2.0/4.0) * cycle)
            .connect_wire(wires.wire2, (3.0/4.0) * cycle);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
}

private node add_sym_sample {
    input angle;
    input lift;
    input this;
    alias output __out: this;

    this.add_sample(angle * units.deg, lift * units.thou)
    this.add_sample(-angle * units.deg, lift * units.thou)
}


// cam specs are approximate

public node toyota_2zzge_lobe_profile_int_lo {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 228.0 * units.deg,
            gamma: 1.0,
            lift: 7.25 * units.mm,
            steps: 100
        );
}

public node toyota_2zzge_lobe_profile_exh_lo {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 228.0 * units.deg,
            gamma: 1.0,
            lift: 7.25 * units.mm,
            steps: 100
        );
}

public node toyota_2zzge_lobe_profile_int_hi {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 292.0 * units.deg,
            gamma: 1.0,
            lift: 11.2 * units.mm,
            steps: 100
        );
}

public node toyota_2zzge_lobe_profile_exh_hi {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 276.0 * units.deg,
            gamma: 1.0,
            lift: 10.0 * units.mm,
            steps: 100
        );
}

public node toyota_2zzge_camshaft_builder {
    input in_lobe_profile_lo: toyota_2zzge_lobe_profile_int_lo();
    input ex_lobe_profile_lo: toyota_2zzge_lobe_profile_exh_lo();
    input in_lobe_profile_hi: toyota_2zzge_lobe_profile_int_hi();
    input ex_lobe_profile_hi: toyota_2zzge_lobe_profile_exh_hi();
    input lobe_separation: 99.0 * units.deg;
    input intake_lobe_center_lo: 125.0 * units.deg; // calculated median: 105.5
    input exhaust_lobe_center_lo: 135.0 * units.deg; // calculated median: 116.5
    input intake_lobe_center_hi: 95.0 * units.deg; // calculated median: 109.5
    input exhaust_lobe_center_hi: 90.0 * units.deg; // calculated median: 102.0
    input advance_in_lo: 0.0 * units.deg;
    input advance_ex_lo: 0.0 * units.deg;
    input advance_in_hi: 0.0 * units.deg;
    input advance_ex_hi: 0.0 * units.deg;
    input base_radius: 15 * units.mm;

    output intake_cam_0_lo: _intake_cam_0_lo;
    output exhaust_cam_0_lo: _exhaust_cam_0_lo;

    output intake_cam_0_hi: _intake_cam_0_hi;
    output exhaust_cam_0_hi: _exhaust_cam_0_hi;

    camshaft_parameters params(
        advance: advance_in_lo,
        base_radius: base_radius
    )

    camshaft _intake_cam_0_lo(params, lobe_profile: in_lobe_profile_lo, advance: advance_in_lo)
    camshaft _exhaust_cam_0_lo(params, lobe_profile: ex_lobe_profile_lo, advance: advance_ex_lo)

    camshaft _intake_cam_0_hi(params, lobe_profile: in_lobe_profile_hi, advance: advance_in_hi)
    camshaft _exhaust_cam_0_hi(params, lobe_profile: ex_lobe_profile_hi, advance: advance_ex_hi)


    label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_0_lo
        .add_lobe(rot360 + intake_lobe_center_lo + 0 * rot180)
        .add_lobe(rot360 + intake_lobe_center_lo + 3 * rot180)
        .add_lobe(rot360 + intake_lobe_center_lo + 1 * rot180)
        .add_lobe(rot360 + intake_lobe_center_lo + 2 * rot180)

    _exhaust_cam_0_lo
        .add_lobe(rot360 - exhaust_lobe_center_lo + 0 * rot180)
        .add_lobe(rot360 - exhaust_lobe_center_lo + 3 * rot180)
        .add_lobe(rot360 - exhaust_lobe_center_lo + 1 * rot180)
        .add_lobe(rot360 - exhaust_lobe_center_lo + 2 * rot180)

    _intake_cam_0_hi
        .add_lobe(rot360 + intake_lobe_center_hi + 0 * rot180)
        .add_lobe(rot360 + intake_lobe_center_hi + 3 * rot180)
        .add_lobe(rot360 + intake_lobe_center_hi + 1 * rot180)
        .add_lobe(rot360 + intake_lobe_center_hi + 2 * rot180)

    _exhaust_cam_0_hi
        .add_lobe(rot360 - exhaust_lobe_center_hi + 0 * rot180)
        .add_lobe(rot360 - exhaust_lobe_center_hi + 3 * rot180)
        .add_lobe(rot360 - exhaust_lobe_center_hi + 1 * rot180)
        .add_lobe(rot360 - exhaust_lobe_center_hi + 2 * rot180)
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.thou, k_28inH2O(flow))
}

public node toyota_2zzge_head {
    input cam_in_lo;
    input cam_ex_lo;
    input cam_in_hi;
    input cam_ex_hi;
    input chamber_volume: 25.0 * units.cc; // guess
    input flip_display: false;
    
    alias output __out: head;

    // http://www.users.interport.net/s/r/srweiss/tablehdc.htm#Rover
    function intake_flow(50 * units.thou)
    intake_flow
        .add_flow_sample(  0,   0)
        .add_flow_sample( 50,  34) // interp
        .add_flow_sample(100,  81)
        .add_flow_sample(150, 121)
        .add_flow_sample(200, 158)
        .add_flow_sample(250, 187)
        .add_flow_sample(300, 209)
        .add_flow_sample(350, 227)
        .add_flow_sample(400, 237)
        .add_flow_sample(450, 245)
        .add_flow_sample(500, 252)

    function exhaust_flow(50 * units.thou)
    exhaust_flow
        .add_flow_sample(  0,   0)
        .add_flow_sample( 50,  27) // interp
        .add_flow_sample(100,  67)
        .add_flow_sample(150, 101)
        .add_flow_sample(200, 126)
        .add_flow_sample(250, 139)
        .add_flow_sample(300, 153)
        .add_flow_sample(350, 158)
        .add_flow_sample(400, 164)
        .add_flow_sample(450, 167)
        .add_flow_sample(500, 173)

        
    generic_cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.0 * units.inch) * 5 * units.inch,
        intake_runner_cross_section_area: circle_area(1.0 * units.inch),
        exhaust_runner_volume: circle_area(0.6 * units.inch) * 5 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.6 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        valvetrain: vtec_valvetrain(
            vtec_intake_camshaft: cam_in_hi,
            vtec_exhaust_camshaft: cam_ex_hi,
            intake_camshaft: cam_in_lo,
            exhaust_camshaft: cam_ex_lo,
            min_rpm: 6000 * units.rpm
        ),
        flip_display: flip_display
    )
}

public node toyota_2zzge_engine {
    alias output __out: engine;

    engine engine(
        name: "Toyota 2ZZ-GE",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 1200 * units.rpm,
        redline: 8000 * units.rpm,
        hf_gain: 0.05,
        jitter: 0.55,
        throttle_gamma: 2.0
    )
    
    label lean_angle(10 * units.deg)

    label bore(82.0 * units.mm)
    label bore_spacing(87.5 * units.mm)
    label stroke(85.0 * units.mm)
    label rod_length(137.99 * units.mm)
    label rod_mass(350 * units.g)
    label piston_mass(298 * units.g) // Mahle aftermarket
    label compression_height(31.20 * units.mm)
    label deck_height((211.75 + 0.5) * units.mm)
    label piston_dome(-12.0 * units.cc) // Mahle aftermarket
    label crank_mass(4.0 * units.kg)
    label flywheel_mass(5.0 * units.kg)
    label flywheel_radius(5.0 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 15 * units.kg, radius: 8.0 * units.cm)
    )

    wires wires()

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 10.0 * units.lb_ft,
        moment_of_inertia: crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 90.0 * units.deg + lean_angle
    )

    rod_journal rj1(angle:  0  * units.deg)
    rod_journal rj2(angle: 180 * units.deg)
    rod_journal rj3(angle: 180 * units.deg)
    rod_journal rj4(angle:  0  * units.deg)
    
    c0
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)

    piston_parameters piston_params(
        mass: piston_mass,
        //blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    intake intake(
        plenum_volume: 1.0 * units.L,
        plenum_cross_section_area: 50.0 * units.cm2,
        intake_flow_rate: k_carb(600.0),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.997,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(250.0),
        runner_length: 5.0 * units.inch,
        velocity_decay: 0.1
    )
    
    label exhaust_radius_celica((4.5 * units.inch)/2)
    label exhaust_radius_elise((4.5 * units.inch)/2)

    exhaust_system_parameters es_params_toyota_celica(
        outlet_flow_rate: k_carb(400.0),
        primary_tube_length: 12.5 * units.inch,
        primary_flow_rate: k_carb(200.0),
        velocity_decay: 1.0, //0.5
        collector_cross_section_area: circle_area(exhaust_radius_celica),
        length: 2500 * units.mm,
        volume: 2500 * units.mm * circle_area(exhaust_radius_celica)
    )

    exhaust_system_parameters es_params_lotus_elise(
        outlet_flow_rate: k_carb(400.0),
        primary_tube_length: 15.5 * units.inch,
        primary_flow_rate: k_carb(200.0),
        velocity_decay: 1.0, //0.5
        collector_cross_section_area: circle_area(exhaust_radius_elise),
        length: 500 * units.mm,
        volume: 500 * units.mm * circle_area(exhaust_radius_elise)
    )
    
    // exhaust_system exhaust(es_params_toyota_celica, audio_volume: 1.0 * 0.01, impulse_response: ir_lib.default_0)
    exhaust_system exhaust(es_params_lotus_elise, audio_volume: 1.0 * 0.002, impulse_response: ir_lib.default_0)
 
    cylinder_bank b0(bank_params, angle: 0 * units.deg + lean_angle)
    b0
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire1,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: bore_spacing * 0.4,
            sound_attenuation: 1.0
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire2,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: bore_spacing * 0.38,
            sound_attenuation: 1.0
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire3,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: bore_spacing * 0,
            sound_attenuation: 1.02
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire4,
            intake: intake,
            exhaust_system: exhaust,
            primary_length: bore_spacing * 0.4,
            sound_attenuation: 1.0
        )

    engine
        .add_cylinder_bank(b0)

    engine.add_crankshaft(c0)

    toyota_2zzge_camshaft_builder camshaft()

    b0.set_cylinder_head (
        toyota_2zzge_head(
            cam_in_lo: camshaft.intake_cam_0_lo,
            cam_in_hi: camshaft.intake_cam_0_hi,
            cam_ex_lo: camshaft.exhaust_cam_0_lo,
            cam_ex_hi: camshaft.exhaust_cam_0_hi,
            flip_display: true
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 20 * units.deg)
        .add_sample(1000 * units.rpm, 50 * units.deg)
        .add_sample(2000 * units.rpm, 55 * units.deg)
        .add_sample(3000 * units.rpm, 55 * units.deg)
        .add_sample(4000 * units.rpm, 55 * units.deg)
        .add_sample(5000 * units.rpm, 55 * units.deg)
        .add_sample(6000 * units.rpm, 55 * units.deg)
        .add_sample(7000 * units.rpm, 55 * units.deg)
        .add_sample(8000 * units.rpm, 55 * units.deg)
        .add_sample(9000 * units.rpm, 55 * units.deg)

    engine.add_ignition_module(
        toyota_2zzge_distributor(
            wires: wires,
            timing_curve: timing_curve
        ))
}

// 2004 Lotus Elise 111R (type 111 - Europe)
public node lotus_elise_111r_vehicle {
    alias output __out:
        vehicle(
            mass: 1900 * units.lb,
            drag_coefficient: 0.4,
            cross_sectional_area: 1.63 * (units.m * units.m), // my estimate
            diff_ratio: 4.529,
            tire_radius: 317.5 * units.mm, // 225/45R17
            rolling_resistance: 430 * units.N
        );
}

// 2004 Toyota Celica GT-S (ZZT231)
public node toyota_celica_zzt231_vehicle {
    alias output __out:
        vehicle(
            mass: 2500 * units.lb,
            drag_coefficient: 0.32,
            cross_sectional_area: 1.92 * (units.m * units.m), // my estimate
            diff_ratio: 4.529,
            tire_radius: 312 * units.mm, // 205/50R16
            rolling_resistance: 400 * units.N
        );
}

// Lotus & most markets Celica
// US Celica uses C60 with taller 0.725 sixth gear
public node toyota_c64_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 500 * units.lb_ft
        )
        .add_gear(3.166)
        .add_gear(2.050)
        .add_gear(1.481)
        .add_gear(1.166)
        .add_gear(0.916)
        .add_gear(0.815);
}

public node main {
    set_engine(toyota_2zzge_engine())
    set_vehicle(lotus_elise_111r_vehicle())
    set_transmission(toyota_c64_transmission())
}

main()