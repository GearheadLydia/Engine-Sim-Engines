import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

label rot30(30 * units.deg)

// firing order: 1-12-5-8-3-10-6-7-2-11-4-9

public node ford_modular_v12_gt90_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 6800 * units.rpm;
    input limiter_duration: 0.05;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire01, ( 0.0/12.0) * cycle)
            .connect_wire(wires.wire12, ( 1.0/12.0) * cycle + rot30)
            .connect_wire(wires.wire05, ( 2.0/12.0) * cycle)
            .connect_wire(wires.wire08, ( 3.0/12.0) * cycle + rot30)
            .connect_wire(wires.wire03, ( 4.0/12.0) * cycle)
            .connect_wire(wires.wire10, ( 5.0/12.0) * cycle + rot30)
            .connect_wire(wires.wire06, ( 6.0/12.0) * cycle)
            .connect_wire(wires.wire07, ( 7.0/12.0) * cycle + rot30)
            .connect_wire(wires.wire02, ( 8.0/12.0) * cycle)
            .connect_wire(wires.wire11, ( 9.0/12.0) * cycle + rot30)
            .connect_wire(wires.wire04, (10.0/12.0) * cycle)
            .connect_wire(wires.wire09, (11.0/12.0) * cycle + rot30);
}

private node wires {
    output wire01: ignition_wire();
    output wire02: ignition_wire();
    output wire03: ignition_wire();
    output wire04: ignition_wire();
    output wire05: ignition_wire();
    output wire06: ignition_wire();
    output wire07: ignition_wire();
    output wire08: ignition_wire();
    output wire09: ignition_wire();
    output wire10: ignition_wire();
    output wire11: ignition_wire();
    output wire12: ignition_wire();
}

public node ford_modular_v12_gt90_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 224 * units.deg,
            gamma: 1.3,
            lift: 10.5 * units.mm,
            steps: 100
        );
}

public node ford_modular_v12_gt90_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 220 * units.deg,
            gamma: 1.3,
            lift: 10.5 * units.mm,
            steps: 100
        );
}

public node ford_modular_v12_gt90_camshaft_builder {
    input in_lobe_profile: ford_modular_v12_gt90_lobe_profile_int();
    input ex_lobe_profile: ford_modular_v12_gt90_lobe_profile_exh();
    input intake_lobe_profile: in_lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input intake_lobe_center: 115.0 * units.deg;
    input exhaust_lobe_center: 115.0 * units.deg;
    input advance: 15.0 * units.deg;
    input base_radius: 15 * units.mm;

    output intake_cam_0: _intake_cam_0;
    output exhaust_cam_0: _exhaust_cam_0;
    output intake_cam_1: _intake_cam_1;
    output exhaust_cam_1: _exhaust_cam_1;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_0(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_0(params, lobe_profile: exhaust_lobe_profile)

    camshaft _intake_cam_1(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_1(params, lobe_profile: exhaust_lobe_profile)


    //label rot30(30 * units.deg)
    label rot60(60 * units.deg)
    label rot72(72 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

    _intake_cam_0
        .add_lobe(rot360 + intake_lobe_center +  0 * rot60) // # 1
        .add_lobe(rot360 + intake_lobe_center +  8 * rot60) // # 2
        .add_lobe(rot360 + intake_lobe_center +  4 * rot60) // # 3
        .add_lobe(rot360 + intake_lobe_center + 10 * rot60) // # 4
        .add_lobe(rot360 + intake_lobe_center +  2 * rot60) // # 5
        .add_lobe(rot360 + intake_lobe_center +  6 * rot60) // # 6

    _intake_cam_1
        .add_lobe(rot360 + intake_lobe_center +  7 * rot60 + rot30) // # 7
        .add_lobe(rot360 + intake_lobe_center +  3 * rot60 + rot30) // # 8
        .add_lobe(rot360 + intake_lobe_center + 11 * rot60 + rot30) // # 9
        .add_lobe(rot360 + intake_lobe_center +  5 * rot60 + rot30) // # 10
        .add_lobe(rot360 + intake_lobe_center +  9 * rot60 + rot30) // # 11
        .add_lobe(rot360 + intake_lobe_center +  1 * rot60 + rot30) // # 12

    _exhaust_cam_0
        .add_lobe(rot360 - exhaust_lobe_center +  0 * rot60) // # 1
        .add_lobe(rot360 - exhaust_lobe_center +  8 * rot60) // # 2
        .add_lobe(rot360 - exhaust_lobe_center +  4 * rot60) // # 3
        .add_lobe(rot360 - exhaust_lobe_center + 10 * rot60) // # 4
        .add_lobe(rot360 - exhaust_lobe_center +  2 * rot60) // # 5
        .add_lobe(rot360 - exhaust_lobe_center +  6 * rot60) // # 6

    _exhaust_cam_1
        .add_lobe(rot360 - exhaust_lobe_center +  7 * rot60 + rot30) // # 7
        .add_lobe(rot360 - exhaust_lobe_center +  3 * rot60 + rot30) // # 8
        .add_lobe(rot360 - exhaust_lobe_center + 11 * rot60 + rot30) // # 9
        .add_lobe(rot360 - exhaust_lobe_center +  5 * rot60 + rot30) // # 10
        .add_lobe(rot360 - exhaust_lobe_center +  9 * rot60 + rot30) // # 11
        .add_lobe(rot360 - exhaust_lobe_center +  1 * rot60 + rot30) // # 12
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.mm, k_28inH2O(flow))
}

public node ford_modular_v12_gt90_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 57.0 * units.cc;
    input flip_display: false;
    
    input flow_attenuation: 1.0;
    input lift_scale: 1.0;
    alias output __out: head;

    function intake_flow(1.0 * units.mm)
    intake_flow
        .add_flow_sample(0 * lift_scale, 0 * flow_attenuation)
        .add_flow_sample(1 * lift_scale, 30 * flow_attenuation)
        .add_flow_sample(2 * lift_scale, 60 * flow_attenuation)
        .add_flow_sample(3 * lift_scale, 90 * flow_attenuation)
        .add_flow_sample(4 * lift_scale, 125 * flow_attenuation)
        .add_flow_sample(5 * lift_scale, 160 * flow_attenuation)
        .add_flow_sample(6 * lift_scale, 195 * flow_attenuation)
        .add_flow_sample(7 * lift_scale, 210 * flow_attenuation)
        .add_flow_sample(8 * lift_scale, 235 * flow_attenuation)
        .add_flow_sample(9 * lift_scale, 270 * flow_attenuation)
        .add_flow_sample(10 * lift_scale, 305 * flow_attenuation)
        .add_flow_sample(11 * lift_scale, 320 * flow_attenuation)
        .add_flow_sample(12 * lift_scale, 335 * flow_attenuation)

    function exhaust_flow(1.0 * units.mm)
    exhaust_flow
        .add_flow_sample(0 * lift_scale, 0 * flow_attenuation)
        .add_flow_sample(1 * lift_scale, 30 * flow_attenuation)
        .add_flow_sample(2 * lift_scale, 55 * flow_attenuation)
        .add_flow_sample(3 * lift_scale, 85 * flow_attenuation)
        .add_flow_sample(4 * lift_scale, 115 * flow_attenuation)
        .add_flow_sample(5 * lift_scale, 140 * flow_attenuation)
        .add_flow_sample(6 * lift_scale, 160 * flow_attenuation)
        .add_flow_sample(7 * lift_scale, 180 * flow_attenuation)
        .add_flow_sample(8 * lift_scale, 205 * flow_attenuation)
        .add_flow_sample(9 * lift_scale, 220 * flow_attenuation)
        .add_flow_sample(10 * lift_scale, 240 * flow_attenuation)
        .add_flow_sample(11 * lift_scale, 260 * flow_attenuation)
        .add_flow_sample(12 * lift_scale, 280 * flow_attenuation)

        
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.15 * units.inch) * 5 * units.inch,
        intake_runner_cross_section_area: circle_area(1.15 * units.inch),
        exhaust_runner_volume: circle_area(0.7 * units.inch) * 5 * units.inch,
        exhaust_runner_cross_section_area: circle_area(0.7 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}


public node ford_modular_v12_engine {
    alias output __out: engine;

    wires wires()

    engine engine(
        name: "Ford Modular V12 GT90",
        starter_torque: 150 * units.lb_ft,
        starter_speed: 1200 * units.rpm,
        redline: 6500 * units.rpm,
        fuel: fuel(max_burning_efficiency: 1.15),
        throttle_gamma: 1.0,
        simulation_frequency: 6000,
        jitter: 0.75,
        hf_gain: 0.05
    )

    // https://fordauthority.com/fmc/ford-motor-company-engines/ford-modular-family/ford-4-6l-engine/
    label bore(90.22 * units.mm)
    label bore_spacing(100.0 * units.mm)
    label stroke(77.2 * units.mm)
    label rod_length(157.1 * units.mm)
    label rod_mass(618 * units.g)
    label compression_height(31.01 * units.mm)
    label piston_mass(361 * units.g)
    label piston_dome(4.451 * units.cc)
    label deck_height((227.0 + 0.2) * units.mm)
    label crank_mass(70 * units.lb)
    label flywheel_mass(45 * units.lb)
    label flywheel_radius(6 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 20 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia:
            crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 135.0 * units.deg // -45 degrees
    )

    rod_journal rj1(angle:  0  * units.deg)
    rod_journal rj2(angle: 120 * units.deg)
    rod_journal rj3(angle: 240 * units.deg)
    rod_journal rj4(angle: 240 * units.deg)
    rod_journal rj5(angle: 120 * units.deg)
    rod_journal rj6(angle:  0  * units.deg)

    c0
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)
        .add_rod_journal(rj5)
        .add_rod_journal(rj6)


    piston_parameters piston_params(
        mass: piston_mass,
        //blowby: 0,
        compression_height: compression_height,
        wrist_pin_position: 0 * units.mm,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    intake intakeL(
        plenum_volume: 1.0 * units.L,
        plenum_cross_section_area: 50.0 * units.cm2,
        intake_flow_rate: k_carb(400),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.996,
        throttle_gamma: 2.0,
        runner_flow_rate: k_carb(400.0),
        runner_length: 8.5 * units.inch,
        velocity_decay: 0.1
    )

    intake intakeR(
        plenum_volume: 1.0 * units.L,
        plenum_cross_section_area: 50.0 * units.cm2,
        intake_flow_rate: k_carb(400),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.996,
        throttle_gamma: 2.0,
        runner_flow_rate: k_carb(400.0),
        runner_length: 8.5 * units.inch,
        velocity_decay: 0.1
    )

    label exhaust_radius((4.0 * units.inch)/2)

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(600.0),
        primary_tube_length: 10.0 * units.inch,
        primary_flow_rate: k_carb(450.0),
        velocity_decay: 1.0, //0.5
        collector_cross_section_area: circle_area(exhaust_radius) * 4,
        length: 480 * units.mm,
        volume: 480 * units.mm * circle_area(exhaust_radius) * 4
    )

    exhaust_system exhaust(es_params, audio_volume: 1.0 * 0.001, impulse_response: ir_lib.default_0)

    label pipe_length_1(2.3 * bore_spacing)
    label pipe_length_2(1.5 * bore_spacing)
    label pipe_length_3(2.7 * bore_spacing)
    label pipe_length_4(0.8 * bore_spacing)
    label pipe_length_5(0.0 * bore_spacing)
    label pipe_length_6(1.2 * bore_spacing)
    
    label exhaust_offset_LR(0.75 * bore_spacing)

    cylinder_bank b0(bank_params, angle: 45.0 * units.deg) // L (first)
    b0
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.5)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire01,
            intake: intakeL,
            exhaust_system: exhaust,
            primary_length: pipe_length_1 + exhaust_offset_LR
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.3)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire02,
            intake: intakeL,
            exhaust_system: exhaust,
            primary_length: pipe_length_2 + exhaust_offset_LR
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire03,
            intake: intakeL,
            exhaust_system: exhaust,
            primary_length: pipe_length_3 + exhaust_offset_LR
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.3)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire04,
            intake: intakeL,
            exhaust_system: exhaust,
            primary_length: pipe_length_4 + exhaust_offset_LR
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.5)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj5,
            ignition_wire: wires.wire05,
            intake: intakeL,
            exhaust_system: exhaust,
            primary_length: pipe_length_5 + exhaust_offset_LR
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.5)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj6,
            ignition_wire: wires.wire06,
            intake: intakeL,
            exhaust_system: exhaust,
            primary_length: pipe_length_6 + exhaust_offset_LR
        )

    cylinder_bank b1(bank_params, angle: -45.0 * units.deg) // R (second)
    b1
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.5)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire07,
            intake: intakeR,
            exhaust_system: exhaust,
            primary_length: pipe_length_1
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.3)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire08,
            intake: intakeR,
            exhaust_system: exhaust,
            primary_length: pipe_length_2
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire09,
            intake: intakeR,
            exhaust_system: exhaust,
            primary_length: pipe_length_3
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.3)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire10,
            intake: intakeR,
            exhaust_system: exhaust,
            primary_length: pipe_length_4
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.5)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj5,
            ignition_wire: wires.wire11,
            intake: intakeR,
            exhaust_system: exhaust,
            primary_length: pipe_length_5
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.5)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj6,
            ignition_wire: wires.wire12,
            intake: intakeR,
            exhaust_system: exhaust,
            primary_length: pipe_length_6
        )

    engine
        .add_cylinder_bank(b0)
        .add_cylinder_bank(b1)

    engine.add_crankshaft(c0)

    ford_modular_v12_gt90_camshaft_builder camshaft(
        in_lobe_profile: ford_modular_v12_gt90_lobe_profile_int(),
        ex_lobe_profile: ford_modular_v12_gt90_lobe_profile_exh()
    )

    b0.set_cylinder_head (
        ford_modular_v12_gt90_head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0,
            flip_display: true
        )
    )
    b1.set_cylinder_head (
        ford_modular_v12_gt90_head(
            intake_camshaft: camshaft.intake_cam_1,
            exhaust_camshaft: camshaft.exhaust_cam_1,
            flip_display: false
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 45 * units.deg)
        .add_sample(1000 * units.rpm, 60 * units.deg)
        .add_sample(2000 * units.rpm, 65 * units.deg)
        .add_sample(3000 * units.rpm, 65 * units.deg)
        .add_sample(4000 * units.rpm, 65 * units.deg)
        .add_sample(5000 * units.rpm, 65 * units.deg)
        .add_sample(6000 * units.rpm, 65 * units.deg)
        .add_sample(7000 * units.rpm, 65 * units.deg)
        .add_sample(8000 * units.rpm, 65 * units.deg)

    engine.add_ignition_module(
        ford_modular_v12_gt90_distributor(
            wires: wires,
            timing_curve: timing_curve
        )
    )
}

// 1995 Ford GT90 Concept
public node ford_gt90_concept_vehicle {
    alias output __out:
        vehicle(
            mass: 3200 * units.lb,
            drag_coefficient: 0.32,
            cross_sectional_area: 2.2 * (units.m * units.m), // guesstimate
            diff_ratio: 2.88, // guess - XJ220
            tire_radius: 348 * units.mm, // 355/30R19
            rolling_resistance: 650 * units.N
        );
}

public node ffd_ricardo_5spd_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 1200 * units.lb_ft
        )
        .add_gear(3.00)
        .add_gear(1.95)
        .add_gear(1.42)
        .add_gear(1.09)
        .add_gear(0.85);
}

public node main {
    set_engine(ford_modular_v12_engine())
    set_vehicle(ford_gt90_concept_vehicle())
    set_transmission(ffd_ricardo_5spd_transmission())
}

main()