import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

label cycle(2 * 360 * units.deg)

// firing order: 1-14-5-16-7-12-3-10-6-9-8-13-4-15-2-11

public node cadillac_ls_v16_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 6500 * units.rpm;
	input limiter_duration: 0.08;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire01, ( 0.0 / 16.0) * cycle)
            .connect_wire(wires.wire14, ( 1.0 / 16.0) * cycle)
            .connect_wire(wires.wire05, ( 2.0 / 16.0) * cycle)
            .connect_wire(wires.wire16, ( 3.0 / 16.0) * cycle)
            .connect_wire(wires.wire07, ( 4.0 / 16.0) * cycle)
            .connect_wire(wires.wire12, ( 5.0 / 16.0) * cycle)
            .connect_wire(wires.wire03, ( 6.0 / 16.0) * cycle)
            .connect_wire(wires.wire10, ( 7.0 / 16.0) * cycle)
            .connect_wire(wires.wire06, ( 8.0 / 16.0) * cycle)
            .connect_wire(wires.wire09, ( 9.0 / 16.0) * cycle)
            .connect_wire(wires.wire08, (10.0 / 16.0) * cycle)
            .connect_wire(wires.wire13, (11.0 / 16.0) * cycle)
            .connect_wire(wires.wire04, (12.0 / 16.0) * cycle)
            .connect_wire(wires.wire15, (13.0 / 16.0) * cycle)
            .connect_wire(wires.wire02, (14.0 / 16.0) * cycle)
            .connect_wire(wires.wire11, (15.0 / 16.0) * cycle);
}

private node wires {
    output wire01: ignition_wire();
    output wire02: ignition_wire();
    output wire03: ignition_wire();
    output wire04: ignition_wire();
    output wire05: ignition_wire();
    output wire06: ignition_wire();
    output wire07: ignition_wire();
    output wire08: ignition_wire();
    output wire09: ignition_wire();
    output wire10: ignition_wire();
    output wire11: ignition_wire();
    output wire12: ignition_wire();
    output wire13: ignition_wire();
    output wire14: ignition_wire();
    output wire15: ignition_wire();
    output wire16: ignition_wire();
}


// https://www.onallcylinders.com/2018/01/18/ls7-engine-specs-performance-bore-stroke-cylinder-heads-cam-specs/

public node cadillac_ls_v16_lobe_profile_int {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 204 * units.deg,
            gamma: 1.0,
            lift: 521 * units.thou,
            steps: 100
        );
}

public node cadillac_ls_v16_lobe_profile_exh {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: 213 * units.deg,
            gamma: 1.0,
            lift: 520 * units.thou,
            steps: 100
        );
}

public node cadillac_ls_v16_camshaft_builder {
    input in_lobe_profile: cadillac_ls_v16_lobe_profile_int();
	input ex_lobe_profile: cadillac_ls_v16_lobe_profile_exh();
    input intake_lobe_profile: in_lobe_profile;
    input exhaust_lobe_profile: ex_lobe_profile;
    input lobe_separation: 116.0 * units.deg;
    input intake_lobe_center: lobe_separation;
    input exhaust_lobe_center: lobe_separation;
    input advance: -4.0 * units.deg;
    input base_radius: 1.462 / 2 * units.inch;

    output intake_cam_0: _intake_cam_0;
    output exhaust_cam_0: _exhaust_cam_0;
    output intake_cam_1: _intake_cam_1;
    output exhaust_cam_1: _exhaust_cam_1;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam_0(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_0(params, lobe_profile: exhaust_lobe_profile)

    camshaft _intake_cam_1(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_1(params, lobe_profile: exhaust_lobe_profile)


    label rot30(30 * units.deg)
    label rot45(45 * units.deg)
	label rot60(60 * units.deg)
    label rot90(90 * units.deg)
    label rot120(120 * units.deg)
    label rot180(180 * units.deg)
    label rot360(360 * units.deg)

	// firing order: 1-14-8-13-7-12-3-10-6-9-5-16-4-15-2-11

    _intake_cam_0
        .add_lobe(rot360 + intake_lobe_center +  0 * rot45) //  #1
        .add_lobe(rot360 + intake_lobe_center +  6 * rot45) //  #3
        .add_lobe(rot360 + intake_lobe_center +  2 * rot45) //  #5
        .add_lobe(rot360 + intake_lobe_center +  4 * rot45) //  #7
        .add_lobe(rot360 + intake_lobe_center +  9 * rot45) //  #9
        .add_lobe(rot360 + intake_lobe_center + 15 * rot45) // #11
        .add_lobe(rot360 + intake_lobe_center + 11 * rot45) // #13
        .add_lobe(rot360 + intake_lobe_center + 13 * rot45) // #15

    _intake_cam_1
        .add_lobe(rot360 + intake_lobe_center + 14 * rot45) //  #2
        .add_lobe(rot360 + intake_lobe_center + 12 * rot45) //  #4
        .add_lobe(rot360 + intake_lobe_center +  8 * rot45) //  #6
        .add_lobe(rot360 + intake_lobe_center + 10 * rot45) //  #8
        .add_lobe(rot360 + intake_lobe_center +  7 * rot45) // #10
        .add_lobe(rot360 + intake_lobe_center +  5 * rot45) // #12
        .add_lobe(rot360 + intake_lobe_center +  1 * rot45) // #14
        .add_lobe(rot360 + intake_lobe_center +  3 * rot45) // #16

    _exhaust_cam_0
        .add_lobe(rot360 - exhaust_lobe_center +  0 * rot45) //  #1
        .add_lobe(rot360 - exhaust_lobe_center +  6 * rot45) //  #3
        .add_lobe(rot360 - exhaust_lobe_center +  2 * rot45) //  #5
        .add_lobe(rot360 - exhaust_lobe_center +  4 * rot45) //  #7
        .add_lobe(rot360 - exhaust_lobe_center +  9 * rot45) //  #9
        .add_lobe(rot360 - exhaust_lobe_center + 15 * rot45) // #11
        .add_lobe(rot360 - exhaust_lobe_center + 11 * rot45) // #13
        .add_lobe(rot360 - exhaust_lobe_center + 13 * rot45) // #15

    _exhaust_cam_1
        .add_lobe(rot360 - exhaust_lobe_center + 14 * rot45) //  #2
        .add_lobe(rot360 - exhaust_lobe_center + 12 * rot45) //  #4
        .add_lobe(rot360 - exhaust_lobe_center +  8 * rot45) //  #6
        .add_lobe(rot360 - exhaust_lobe_center + 10 * rot45) //  #8
        .add_lobe(rot360 - exhaust_lobe_center +  7 * rot45) // #10
        .add_lobe(rot360 - exhaust_lobe_center +  5 * rot45) // #12
        .add_lobe(rot360 - exhaust_lobe_center +  1 * rot45) // #14
        .add_lobe(rot360 - exhaust_lobe_center +  3 * rot45) // #16
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.thou, k_28inH2O(flow))
}

public node cadillac_ls_v16_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 70.0 * units.cc;
    input flip_display: false;
	
	input flow_attenuation_in: 1.0;
	input flow_attenuation_ex: 1.0;
    input lift_scale: 50.0;
    alias output __out: head;

    function intake_flow(50 * units.thou)
    intake_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_in)
        .add_flow_sample( 1 * lift_scale,  25 * flow_attenuation_in)
        .add_flow_sample( 2 * lift_scale,  76 * flow_attenuation_in)
        .add_flow_sample( 3 * lift_scale, 100 * flow_attenuation_in)
        .add_flow_sample( 4 * lift_scale, 146 * flow_attenuation_in)
        .add_flow_sample( 5 * lift_scale, 175 * flow_attenuation_in)
        .add_flow_sample( 6 * lift_scale, 212 * flow_attenuation_in)
        .add_flow_sample( 7 * lift_scale, 230 * flow_attenuation_in)
        .add_flow_sample( 8 * lift_scale, 255 * flow_attenuation_in)
        .add_flow_sample( 9 * lift_scale, 275 * flow_attenuation_in)
        .add_flow_sample(10 * lift_scale, 294 * flow_attenuation_in)
        .add_flow_sample(11 * lift_scale, 300 * flow_attenuation_in)
        .add_flow_sample(12 * lift_scale, 314 * flow_attenuation_in)
        .add_flow_sample(13 * lift_scale, 314 * flow_attenuation_in)
        .add_flow_sample(14 * lift_scale, 314 * flow_attenuation_in)

    function exhaust_flow(50 * units.thou)
    exhaust_flow
        .add_flow_sample( 0 * lift_scale,   0 * flow_attenuation_ex)
        .add_flow_sample( 1 * lift_scale,  25 * flow_attenuation_ex)
        .add_flow_sample( 2 * lift_scale,  70 * flow_attenuation_ex)
        .add_flow_sample( 3 * lift_scale, 100 * flow_attenuation_ex)
        .add_flow_sample( 4 * lift_scale, 132 * flow_attenuation_ex)
        .add_flow_sample( 5 * lift_scale, 140 * flow_attenuation_ex)
        .add_flow_sample( 6 * lift_scale, 156 * flow_attenuation_ex)
        .add_flow_sample( 7 * lift_scale, 170 * flow_attenuation_ex)
        .add_flow_sample( 8 * lift_scale, 181 * flow_attenuation_ex)
        .add_flow_sample( 9 * lift_scale, 191 * flow_attenuation_ex)
        .add_flow_sample(10 * lift_scale, 207 * flow_attenuation_ex)
        .add_flow_sample(11 * lift_scale, 214 * flow_attenuation_ex)
        .add_flow_sample(12 * lift_scale, 228 * flow_attenuation_ex)
        .add_flow_sample(13 * lift_scale, 228 * flow_attenuation_ex)
        .add_flow_sample(14 * lift_scale, 228 * flow_attenuation_ex)
		
    cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: circle_area(1.45 * units.inch) * 8 * units.inch,
        intake_runner_cross_section_area: circle_area(1.45 * units.inch),
        exhaust_runner_volume: circle_area(1.0 * units.inch) * 4 * units.inch,
        exhaust_runner_cross_section_area: circle_area(1.0 * units.inch),

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}


public node cadillac_ls_v16 {
    alias output __out: engine;

    wires wires()

    engine engine(
        name: "Cadillac Sixteen",
        starter_torque: 300 * units.lb_ft,
		starter_speed: 600 * units.rpm,
        redline: 6250 * units.rpm,
        throttle_gamma: 2.0,
		simulation_frequency: 4500,
		jitter: 0.7,
		hf_gain: 0.15
    )


	// source: https://help.summitracing.com/app/answers/detail/a_id/4882/~/chevy-ls7-engine-specs
    label bore(4.134 * units.inch)
	label bore_spacing(4.4 * units.inch)
    label stroke(3.860 * units.inch)
    label rod_length(6.207 * units.inch)
    label rod_mass(464 * units.g)
    label compression_height(1.17 * units.inch)
	label piston_mass(482 * units.g)
	label piston_dome(7.00 * units.cc)
	label deck_height(9.240 * units.inch)
    label crank_mass(57 * units.lb)
    label flywheel_mass(57 * units.lb)
    label flywheel_radius(6 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 20 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia:
            crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 45.0 * units.deg // 45 degrees back from right side
    )

    rod_journal rj1(angle:   0.0 * units.deg)
    rod_journal rj2(angle: -90.0 * units.deg)
    rod_journal rj3(angle:  90.0 * units.deg)
    rod_journal rj4(angle: 180.0 * units.deg)

    rod_journal rj5(angle:  45.0 * units.deg)
    rod_journal rj6(angle: -45.0 * units.deg)
    rod_journal rj7(angle: 135.0 * units.deg)
    rod_journal rj8(angle: 225.0 * units.deg)

    c0
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)
        .add_rod_journal(rj5)
        .add_rod_journal(rj6)
        .add_rod_journal(rj7)
        .add_rod_journal(rj8)


    piston_parameters piston_params(
        mass: piston_mass,
        //blowby: 0,
        compression_height: compression_height,
        wrist_pin_position: 0 * units.mm,
        displacement: piston_dome
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: deck_height
    )

    intake intake(
        plenum_volume: 0.3 * units.L,
        plenum_cross_section_area: circle_area(45 * units.mm),
        intake_flow_rate: k_carb(1250),
        idle_flow_rate: k_carb(0.001),
        idle_throttle_plate_position: 0.995,
        throttle_gamma: 1.0,
        runner_flow_rate: k_carb(850.0),
        runner_length: 12.0 * units.inch,
        velocity_decay: 0.1
    )

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(900.0),
        primary_tube_length: 20.0 * units.inch,
        primary_flow_rate: k_carb(750.0),
        velocity_decay: 1.0, //0.5
        volume: 30.0 * units.L
    )

    exhaust_system exhaustL(es_params, audio_volume: 1.0 * 0.01, impulse_response: ir_lib.default_0, length: 500 * units.mm)
    exhaust_system exhaustR(es_params, audio_volume: 1.0 * 0.01, impulse_response: ir_lib.default_0, length: 550 * units.mm)

	label exhaust_stagger(0.2 * bore_spacing)

    cylinder_bank b0(bank_params, angle: -45.0 * units.deg) // R (first)
    b0
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire01,
            intake: intake,
            exhaust_system: exhaustR,
			primary_length: (bore_spacing * 7.5) + exhaust_stagger
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire03,
            intake: intake,
            exhaust_system: exhaustR,
			primary_length: (bore_spacing * 6.5) + exhaust_stagger
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire05,
            intake: intake,
            exhaust_system: exhaustR,
			primary_length: (bore_spacing * 5.5) + exhaust_stagger
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire07,
            intake: intake,
            exhaust_system: exhaustR,
			primary_length: (bore_spacing * 4.5) + exhaust_stagger
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj5,
            ignition_wire: wires.wire09,
            intake: intake,
            exhaust_system: exhaustR,
			primary_length: (bore_spacing * 3.5) + exhaust_stagger
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj6,
            ignition_wire: wires.wire11,
            intake: intake,
            exhaust_system: exhaustR,
			primary_length: (bore_spacing * 2.5) + exhaust_stagger
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj7,
            ignition_wire: wires.wire13,
            intake: intake,
            exhaust_system: exhaustR,
			primary_length: (bore_spacing * 2.0) + exhaust_stagger
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj8,
            ignition_wire: wires.wire15,
            intake: intake,
            exhaust_system: exhaustR,
			primary_length: (bore_spacing * 1.5) + exhaust_stagger
        )

    cylinder_bank b1(bank_params, angle: 45.0 * units.deg) // L (second)
    b1
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            ignition_wire: wires.wire02,
            intake: intake,
            exhaust_system: exhaustL,
			primary_length: bore_spacing * 7.5
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            ignition_wire: wires.wire04,
            intake: intake,
            exhaust_system: exhaustL,
			primary_length: bore_spacing * 6.5
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            ignition_wire: wires.wire06,
            intake: intake,
            exhaust_system: exhaustL,
			primary_length: bore_spacing * 5.5
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            ignition_wire: wires.wire08,
            intake: intake,
            exhaust_system: exhaustL,
			primary_length: bore_spacing * 4.5
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj5,
            ignition_wire: wires.wire10,
            intake: intake,
            exhaust_system: exhaustL,
			primary_length: bore_spacing * 3.5
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj6,
            ignition_wire: wires.wire12,
            intake: intake,
            exhaust_system: exhaustL,
			primary_length: bore_spacing * 2.5
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj7,
            ignition_wire: wires.wire14,
            intake: intake,
            exhaust_system: exhaustL,
			primary_length: bore_spacing * 2.0
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj8,
            ignition_wire: wires.wire16,
            intake: intake,
            exhaust_system: exhaustL,
			primary_length: bore_spacing * 1.5
        )

    engine
        .add_cylinder_bank(b0)
        .add_cylinder_bank(b1)

    engine.add_crankshaft(c0)

    cadillac_ls_v16_camshaft_builder camshaft(
	    in_lobe_profile: cadillac_ls_v16_lobe_profile_int(),
		ex_lobe_profile: cadillac_ls_v16_lobe_profile_exh()
	)

    b0.set_cylinder_head (
        cadillac_ls_v16_head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0,
			flip_display: false
        )
    )
    b1.set_cylinder_head (
        cadillac_ls_v16_head(
            intake_camshaft: camshaft.intake_cam_1,
            exhaust_camshaft: camshaft.exhaust_cam_1,
            flip_display: true
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 15 * units.deg)
        .add_sample(1000 * units.rpm, 25 * units.deg)
        .add_sample(2000 * units.rpm, 29 * units.deg)
        .add_sample(3000 * units.rpm, 33 * units.deg)
        .add_sample(4000 * units.rpm, 39 * units.deg)
        .add_sample(5000 * units.rpm, 42 * units.deg)
        .add_sample(6000 * units.rpm, 45 * units.deg)
		.add_sample(7000 * units.rpm, 45 * units.deg)

    engine.add_ignition_module(
        cadillac_ls_v16_distributor(
            wires: wires,
            timing_curve: timing_curve,
            rev_limit: 6500 * units.rpm
        )
    )
}

// 2003 Cadillac Sixteen Concept
public node cadillac_sixteen_concept_vehicle {
    alias output __out:
        vehicle(
            mass: 5005 * units.lb,
            drag_coefficient: 0.35,
            cross_sectional_area: 2.6 * (units.m) * (units.m),
            diff_ratio: 3.8,
            tire_radius: 410.21 * units.mm, // 265/40ZR24
            rolling_resistance: 600 * units.N
        );
}

public node gm_4l85e_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 1300 * units.lb_ft
        )
        .add_gear(2.482)
        .add_gear(1.482)
        .add_gear(1.000)
        .add_gear(0.750);
}

public node main {
    set_engine(cadillac_ls_v16())
    set_vehicle(cadillac_sixteen_concept_vehicle())
    set_transmission(gm_4l85e_transmission())
}

main()